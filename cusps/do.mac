;<SOURCES>DO.MAC;6    19-JUN-75 05:40:13    EDIT BY HEDBERG
;<SOURCES>DO.MAC;5    10-JUN-75 08:19:01    EDIT BY HEDBERG
;<SOURCES>DO.MAC;4    28-MAY-75 16:18:54    EDIT BY HEDBERG
;Fixed bug in insurance of LF at end of text
;<SOURCES>DO.MAC;3    12-MAY-75 11:03:53    EDIT BY HEDBERG
;Cannot change file's type if not owner
;<SOURCES>DO.MAC;2    11-MAY-75 17:13:27    EDIT BY HEDBERG
;Handles both coded and ASCII files
;<SOURCES>DO.MAC;1     3-MAY-75 10:12:44    EDIT BY HEDBERG
;Made error formats less annoying

	TITLE	DO
	SEARCH	STENEX

P==17
CR==15
LF==12
TAB==11
EOL==37
BUFLEN==^D132			;MAXIMUM INPUT STRING
NPDL==20			;PDL SIZE
PAGE==3				;WHERE TO PUT THE TEXT
FDBSIZ==11			;INDEX OF BYTE SIZE
FDBCNT==12			;INDEX OF BYTE COUNT
FDBUSW==24			;INDEX INTO FDB OF USER SETTABLE WORD
TYP==15				;AC WITH THE TYPE
CODED==1
ASCII==0

;	*************************************
;	*  AC15 IS THE FILE TYPE INDICATOR  *
;	*************************************

OPDEF	CALL	[PUSHJ P,0]
OPDEF	RET	[POPJ  P,0]

	LOC	140

START:
ENTVEC:	JRST	DO
	JRST	WHAT
	JRST	DONE
	JRST	TODAY
	JRST	SWITCH
	JRST	BUG		;ADD TO BUG LIST ON <OPERATOR>
	JRST	BUGS		;LIST BUG LIST 
	JRST	FIXED		;REMOVE FROM BUGLIST
	SUBTTL	DO - ADDS LINE TO LINE ITEMIZED LIST

BUG:	MOVEI	2,[[ASCIZ /OPERATOR/] ;DEFAULT TO <OPERATOR>*.BUG
		   [ASCIZ /BUGS/]]
	JRST	.+2		;AND ACT LIKE DO

DO:	MOVEI	2,[-1			;DEFAULT TO <LOGIN>*.LIST
		   [ASCIZ /LIST/]]

	RESET
	MOVE	P,[IOWD NPDL,PDL]
	MOVEI	1,GERR
	MOVEM	1,JFNEX			;SET JFN ERROR MESSAGE

	SETZ	1,
	CALL	GETJFN
	CALL	MAPIN

	MOVE	1,[POINT 7,PAGE*1000]
ALOOP:	ILDB	2,1
	JUMPN	2,.-1

ATHERE:	BKJFN
	  CALL	HUH
	MOVEM	1,TPNT	;SAVE THE POINTER TO ENCRYPT
	SETO	2,
	HRLZI	3,(1B3!1B6!1B8!1B10!1B17)
	ODTIM
	MOVEI	2," "
	IDPB	2,1
	IDPB	2,1
	MOVEI	2,BUFLEN	
	MOVEI	3,0
	PSTIN

	MOVEI	2,CR
	DPB	2,1
	MOVEI	2,LF
	IDPB	2,1		;REPLACE TERMINATOR W/ CRLF

	MOVE	1,TPNT
	CAIE	TYP,ASCII	;DON'T DECODE IF ASCII
	CALL	CRYPT

	CALL	MAPOUT

	RESET			;FINISHED.
	HALTF
	JRST	.-1
	SUBTTL	TODAY - USES TODAY AS THE FILE NAME

TODAY:	RESET
	MOVE	P,[IOWD NPDL,PDL]

	SETO	2,
	SETZ	4,
	ODCNV			;RETURNS TODAY'S NUMBER IN RH3
	MOVE	2,[POINT 7,0]	;GET A FAKE POINTER
	HRR	2,DAYTAB(3)	;POINT IT IN THE RIGHT DIRECTION
	MOVEM	2,EJFN+4	;AND PUT IT IN THE TABLE

	MOVEI	1,DERR
	MOVEM	1,JFNEX		;DIFFERENT ERROR MESSAGE FOR DAYS

	MOVE	2,[377777,,377777]	;NO IN/OUT JFNS
	MOVEM	2,EJFN+1	;PUT THEM IN THE TABLE TOO

	HRLZI	1,(1B2)		;OLD FILE
	MOVEI	2,[-1		;SAY DEFAULT TO <LOGIN>*.LIST
		   [ASCIZ /LIST/]]
	CALL	GETJFN
	JRST	WHAT1		;LINK UP WITH WHAT
	SUBTTL	WHAT - LISTS THE LIST

BUGS:	MOVEI	2,[[ASCIZ /OPERATOR/]
		   [ASCIZ /BUGS/]]
	JRST	.+2

WHAT:	MOVEI	2,[-1		;DEFAULT TO <LOGIN>*.LIST
		   [ASCIZ /LIST/]]
	RESET
	MOVE	P,[IOWD NPDL,PDL]
	MOVEI	1,GERR
	MOVEM	1,JFNEX			;SET JFN ERROR MESSAGE
	
	HRLZI	1,(1B2)		;OLD FILE ONLY
	CALL	GETJFN
	
WHAT1:	CALL	MAPIN	;MAPS PAGE 0 OF LJFN INTO PAGE 3
	SKIPN	3000
	 JRST	NULFIL		;ZERO FILE

	HRROI	1,[ASCIZ /
For /]
	PSOUT

	MOVEI	1,101
	SETO	2,
	SETO	3,
	ODTIM

	HRROI	1,[ASCIZ \ [Coded]\]
	CAIE	TYP,CODED	;PRINT THE PROPER MESSAGE
	HRROI	1,[ASCIZ \ [ASCII]\]
	PSOUT

	MOVEI	1,EOL
	PBOUT

	SETZM	NLINE#
	MOVE	4,[POINT 7,PAGE*1000]

LINOUT:	ILDB	1,4
	JUMPE	1,WHDONE
	MOVE	1,4
	BKJFN
	 CALL	HUH
	MOVE	4,1
	AOS	2,NLINE
	MOVEI	1,101
	MOVE	3,[1B2!2B17!12]
	NOUT
	 CALL	HUH
	HRROI	1,[ASCIZ /) /]
	PSOUT

	MOVE	2,[POINT 7,BUFFER]
LINE1:	ILDB	1,4
	JUMPN	1,LINE2
	MOVEI	1,CR
	IDPB	1,2
	MOVEI	1,LF
LINE2:	IDPB	1,2
	CAIE	1,LF
	JRST	LINE1
	SETZ	1,
	IDPB	1,2
	MOVE	1,[POINT 7,BUFFER]
	CAIE	TYP,ASCII	;DON'T CODE IF ASCII
	CALL	CRYPT
	HRROI	1,BUFFER
	PSOUT
	JRST	LINOUT

WHDONE:	CALL	MAPOUT
	RESET
	MOVEI	1,37
	PBOUT
	HALTF
	JRST	.-1
 	SUBTTL	DONE -- DELETES A LINE

FIXED:	MOVEI	2,[[ASCIZ /OPERATOR/]
		   [ASCIZ /BUGS/]]
	JRST	.+2

DONE:	MOVEI	2,[-1		;DEFAULT TO <LOGIN>*.LIST
		   [ASCIZ /LIST/]]
	RESET
	MOVE	P,[IOWD NPDL,PDL]
	MOVEI	1,GERR
	MOVEM	1,JFNEX			;SET JFN ERROR MESSAGE
	SETZM	NLINES
	
	HRLZI	1,(1B2)		;OLD FILE ONLY
	CALL	GETJFN
	
 	HRROI	1,4
	MOVEI	2,4		;GET THE LINE NUMBER
	MOVEI	3,"-"
	PSTIN
	HRROI	1,4
	MOVEI	3,12
	NIN
	 JRST	NERR0
	SKIPG	2
	 JRST	NERR		;NEGATIVE OR ZERO LINE NUMBER
	MOVEM	2,NUM#
	
	MOVEI	1,100		;IS HE DELETING A RANGE
	BKJFN
	 CALL	HUH		;INTERNAL ERROR
	PBIN
	CAIN	1,37		;IS HE DONE INPUTTING THAT LINE?
	JRST	DONE1		;YES

 	HRROI	1,4
	MOVEI	2,4		;GET THE LINE NUMBER
	SETZ	3,
	PSTIN
	HRROI	1,4
	MOVEI	3,12
	NIN
	 JRST	NERR0
	JUMPLE	2,NERR		;NEGATIVE OR ZERO LINE NUMBER
	SUB	2,NUM
	 JUMPL	2,RERR		;RANGE ERROR
	MOVEM	2,NLINES

DONE1:	CALL	MAPIN

	MOVN	5,NUM		;COUNTER
	MOVN	6,NLINES	;NUMBER OF LINES TO DELETE-1
	MOVE	1,[POINT 7,PAGE*1000]

LINPNT:	MOVEM	1,TPNT		;SAVE THE BEGINNING OF THE CURRENT LINE
	CALL	GTEND		;POINT 1 AT THE CRLF
	AOJL	5,LINPNT	;ARE WE ON THE LINE TO START DELETING
DELPNT:	AOJG	6,TRNSFR	;YES, AND WE'RE ON THE ONE TO TRANSFER
	CALL	GTEND		;GET THE NEXT EOL
	JRST	DELPNT		;CHECK AND SEE IF IT'S THE ONE TO DELETE
TRNSFR:	MOVE	2,TPNT		;OK, READY.  TRANSFR FROM 1 TO 2
	ILDB	3,1
	IDPB	3,2
	JUMPN	3,.-2
	SETZ	3,
REPEAT 5, <
	IDPB	3,2	>
	SETZM	0(2)
	HRLI	1,0(2)
	HRRI	1,1(2)
	BLT	1,<PAGE*1000>+777

DONE2:	HRROI	1,[ASCIZ/

You have deleted the last line, you're free!

/]
	SKIPN	3000	;IS THE BUFFER ZERO?
	PSOUT
	CALL	MAPOUT	;NO, OUTPUT
	RESET
	HALTF
	JRST	.-1
;
GTEND:	PUSH	P,2		;POINTS THE POINTER IN1 TO THE LF
GTEOL1:	ILDB	2,1
	JUMPE	2,GTERR		;GONE OVERBOARD ETC
	CAIE	2,LF		;ARE WE THERE
	JRST	GTEOL1		;NOPE, DO SOME MORE
	POP	P,2
	RET
;
GTERR:	HRROI	1,[ASCIZ /
Gone overboard on line lookup, quitting/]
	PSOUT
	JRST	DONE2
;
	SUBTTL	SWITCH - SWITCHES THE FILE'S TYPE

SWITCH:	RESET
	MOVE	P,[IOWD NPDL,PDL]
	
	HRLZI	1,(1B2)		;OLD FILE ONLY
	MOVEI	2,[-1		;DEFAULT TO <LOGIN>*.LIST
		   [ASCIZ /LIST/]]
	CALL	GETJFN

	HRROI	1,LOGDIR	
	MOVE	2,LJFN
	HRLZI	3,(1B5)		;JUST THE DIRECTORY
	JFNS
	SETZ	1,
	TLO	1,400000	;TRY TO RECOGNIZE
	HRROI	2,LOGDIR
	STDIR			;GET THE DIR NUM OF THE .LIST FILE
	 CALL	HUH
	 CALL	HUH
	HRRZ	1,1
	PUSH	P,1		;SAVE IT
	GJINF
	POP	P,2	
	CAME	1,2		;YOU MUST OWN THE FILE TO SWITCH IT'S TYPE
	JRST	[HRROI	1,[ASCIZ \ You do not own that file.\]
		 JRST	Q8Q	]
	CALL	MAPIN
	HRROI	1,[ASCIZ \The file is currently an \]
	PSOUT
	HRROI	1,[ASCIZ \ASCII file.
Do you want to switch it? \]
	CAIE	TYP,ASCII
	HRROI	1,[ASCIZ \encoded file.
Do you want to switch it?\]
	PSOUT
YNLOOP:	HRROI	1,[ASCIZ \ (Y/N) \]
	PSOUT
	MOVEI	2,525252
	PBIN
	IORI	1,40		;GET A small CHAR.
	CAIN	1,"y"
	MOVEI	2,-1		;INDICATE IF IT'S YES
	CAIN	1,"n"
	JRST	SWTEND		;QUIT IF IT'S NO
	CAIN	2,525252
	JRST	YNLOOP		;LOOP IF IT'S ANYTHING ELSE

	MOVE	1,[POINT 7,PAGE*1000]
SWLOOP:	CALL	CRYPT		;CHANGE A LINE
	ILDB	2,1
	JUMPE	2,SWQUIT	;LOOP IF NOT THE END
	BKJFN
	 CALL	HUH
	JRST	SWLOOP
	
SWQUIT:	TRC	TYP,1		;SWITCH TYPE FLAG

SWDONE:	HRROI	1,[ASCIZ \
File is now \]
	PSOUT
	HRROI	1,[ASCIZ \ASCII.\]
	CAIE	TYP,ASCII
	HRROI	1,[ASCIZ \coded.\]
	PSOUT
	
SWTEND:	CALL	MAPOUT
	RESET
	HALTF
	JRST	.-1
	SUBTTL	MISC
CRYPT:	PUSH	P,2		;ACCEPTS  POINTER IN 1
ELP:	ILDB	2,1		;ENCODES UP TO, BUT NOT INCLUDING
	CAIN	2,CR		;THE CR-LF
	ILDB	2,1
	CAIN	2,LF
	JRST	EEND
	XOR	2,LOGNUM
	CAIE	2,CR
	CAIN	2,LF
	XOR	2,LOGNUM
	DPB	2,1
	JRST	ELP
EEND:	POP	P,2
	RET

MAPIN:	MOVE	1,LJFN	;OPEN THE FILE READ WRITE
	MOVE	2,[7B5!3B20]
	OPENF
	 JRST	OERR
	HRLZ	1,LJFN	;MAP IN PAGE [PAGE]OF LJFN
	HRLI	2,400000
	HRRI	2,PAGE
	HRLZI	3,140000	;READ/WRITE ACCESS
	PMAP
	MOVE	1,[POINT 7,PAGE*1000] ;THIS IS NECESSARY!!!  IT IS THE SAME
				;LITERAL USED AT FIXEOL AND ELSEWHERE
	ILDB	1,1		;GET THE FIRST CHAR
	SKIPE	1		;DONT LOOK FOR EOL'S ON NEW/EMPTY FILES
	CALL	FIXEOL
	RET

			;EOL => CRLF CONVERTER
FIXEOL:	MOVE	1,[POINT 7,PAGE*1000]		;PAGE WITH EOL'S
	MOVE	2,[POINT 7,<PAGE+1>*1000]	;PAGE WITHOUT EOL'S
	SETZ	16,		;16 IS THE CHANGED EOL'S TO CRLF'S FLAG
FIXEO1:	ILDB	3,1
	IDPB	3,2		;TRANSFER A BYTE
	JUMPE	3,FIXEO2	;QUIT IF IT'S NULL
	CAIE	3,EOL		;IS IT AN EOL
	JRST	FIXEO1		;NO, GO ON
	SETO	16,		;YES, SET FLAG
	MOVEI	3,CR		;GET A CR
	DPB	3,2		;CRUSH THE EOL
	MOVEI	3,LF
	IDPB	3,2		;ADD THE LF
	JRST	FIXEO1		;KEEP GOING

FIXEO2:	BKJFN			;POINT 1 AT THE LF (HOPE HOPE)
	 JRST	HUH
	LDB	2,1		;GET IT
	CAIE	2,12		;IS IT A LF?
	JRST	[MOVEI	2,12
		 IDPB	2,1	;IT IS NOW
		 JRST	.+1 ]

	SKIPN	16		;DID WE CONVERT EOL'S?
	RET			;DIDN'T CHANGE, DON'T BLT
	MOVE	1,[XWD <<PAGE+1>*1000>,<PAGE*1000>]	;REPLACE THE BAD
	BLT	1,<<PAGE+1>*1000-1>	;PAGE WITH THE GOOD ONE
	RET

MAPOUT:	MOVE	1,[POINT 7,PAGE*1000]
	SETZ	3,		;COUNT THE NUMBER OF CHARS
MAPO1:	ILDB	2,1
	AOJ	3,
	JUMPN	2,MAPO1
	SOJ	3,
	MOVE	1,LJFN
	HRLI	1,FDBCNT	;INDEX OF BYTE COUNT WORD IN FDB
	SETO	2,
	CHFDB			;SET THE BYTE COUNT

	HRLI	1,FDBUSW
	MOVE	3,TYP		;GET THE TYPE
	CHFDB			;AND PUT IT IN THE FDB
	
	HRLI	1,FDBSIZ	;INDEX OF BYTE SIZE
	HRLZI	2,(77B11) 	;POSITION OF BYTE SIZE
	HRLZI	3,(7B11)  	;THE BYTE SIZE
	CHFDB			;SET THE BYTE SIZE

	SETOM	1	;REMOVE PAGE [PAGE] FROM 
	HRLI	2,400000	;PAGE TABLE
	HRRI	2,PAGE
	PMAP
	MOVE	1,LJFN	;AND CLOSE THE FILE
	CLOSF
	 CALL	HUH
	RET

GETJFN:	MOVEM	1,EJFN		;accepts the flags in 1
	PUSH	P,2		;save default pointers
	SKIPL	(2)		;DEFAULT TO LOGIN DIR?
	 JRST  [MOVE 2,(2)	;NO, GET POINTER TO DEFAULT DIR
		HRRM 2,DEFDIR
		JRST GETJF1]

	GJINF
	MOVE	2,1
	TRZ	1,777600
	MOVEM	1,LOGNUM
	HRROI	1,LOGDIR
	DIRST
	 CALL	HUH
	MOVEI	1,LOGDIR	;SET INTO DEFAULT FIELD
	HRRM	1,DEFDIR

GETJF1: POP P,2			;GET POINTER TO DEFAULTS
	MOVE	2,1(2)		;GET POINTER TO DEFAULT EXT
	HRRM	2,DEFEXT

	MOVEI	1,EJFN		;LONG CALL GTJFN
	SETZ	2,
	GTJFN
	 JRST	@JFNEX		;FIDDLED WITH BY TODAY
	MOVEM	1,LJFN		;JFN OF LIST FILE

	MOVE	2,[1,,FDBUSW]
	MOVEI	3,TYP
	GTFDB			;GET THE FILE'S TYPE INTO TYP
	ANDI	TYP,7		;ONLY THOSE BITS COUNT

	RET

HUH:	PUSH	P,1			;ERROR
	PUSH	P,2
	PUSH	P,3
	HRROI	1,[ASCIZ /
Internal error at /]
	PSOUT
	HRRZ	2,0(P)
	SUBI	2,2
	MOVEI	1,101
	MOVEI	3,8
	NOUT
	JFCL
	HRROI	1,[ASCIZ /
 please SNDMSG to <HEDBERG>/]
	POP	P,3
	POP	P,2
	POP	P,1
	JRST	Q8Q

RERR:	HRROI	1,[ASCIZ /Range error/]
	JRST	Q8Q
NULFIL:	HRROI	1,[ASCIZ /File is empty/]
	JRST	Q8Q
NERR0:	HRROI	1,[ASCIZ /Is that a number?/]
	JRST	Q8Q
NERR:	HRROI	1,[ASCIZ /That number is too small/]
	JRST	Q8Q
DERR:	HRROI	1,[ASCIZ /No file for today/]
	JRST	Q8Q
GERR:	HRROI	1,[ASCIZ /Can not gtjfn that file/]
	JRST	Q8Q
OERR:	HRROI	1,[ASCIZ /Can not open that file/]
Q8Q:	PSOUT
	RESET
	HALTF
	JRST	.-1

	LOC	<.!777>+1
JFNEX:	0
LOGDIR:	BLOCK	8
FILNAM:	BLOCK	8
DAYTAB:	[ASCIZ /MON/]
	[ASCIZ /TUE/]
	[ASCIZ /WED/]
	[ASCIZ /THU/]
	[ASCIZ /FRI/]
	[ASCIZ /SAT/]
	[ASCIZ /SUN/]

NLINES:	0			;NUMBER OF LINES TO DELETE
LOGNUM:	0
LJFN:	0
TPNT:	0
BUFFER:	BLOCK <BUFLEN+4>/5
PDL:	BLOCK NPDL
EJFN:	0			;SET BY GETJFN
	100,,101		;I/O TO/FROM TTY
	0			;DEFAULT SYS DEVICE
DEFDIR:	POINT 7,0		;DEFAULT DIR TO FILLED IN BY GETJFN
	0			;NO DEFAULT NAME
DEFEXT:	POINT 7,0		;DEFAULT EXTENSION TO BE FILLED IN BY GETJFN
	0			;STANDARD PROTECTION
	0			;STANDARD ACCOUNT
	0			;NO DESIRED JFN
	0
	0			;EXTRA ZEROS
	LIT
	VAR

	END START



