;<SYSTEM>PRINTR.MAC;4    27-DEC-74 14:16:07     EDIT BY COWER
;FIX TO DELETE AND EXP NON-LP?. FILES
;<KREMERS>PRINTR.MAC;4    12-NOV-73 23:05:59	EDIT BY KREMERS
;FIX ERROR AT DLOOPI+2.  NUMBER TOO BIG FOR IMMEDIATE OPERAND
;<KREMERS>PRINTR.MAC;3    12-NOV-73 22:44:58	EDIT BY KREMERS
;<KREMERS>PRINTR.MAC;2    12-NOV-73 17:27:48	EDIT BY KREMERS
;<KREMERS>PRINTR.FAI;2    12-NOV-73 16:49:42	EDIT BY KREMERS
;CONVERTED TO MACRO


	TITLE	PRINTR
	MLON			;ALLOW USE OF MULTI-LINE LITERALS
	SEARCH STENEX							;***SRI-AIC***
	SUBTTL	R.S.TOMLINSON  1 FEB 72
;	MODIFIED BY J. BERGER 13 OCTOBER 1973 TO PRINT BANNERS ON HEADER
;	MODIFIED BY J. KREMERS  6 APRIL 1973 TO MAKE LOG FILE OPTIONAL
;	LAST ALTERED BY P. H. LIPMAN	20 JUNE 72	4:30PM
IFNDEF NETF,<NETF==0>	; FLAG ASSEMBLES VERSION FOR NETWORK USE
IFNDEF PRIF,<PRIF==1>	; FLAG CAUSES FILE LENGTH TO ALTER PRIORITY

; ACCUMULATORS

A=1
B=2
C=3
D=4
CNT=5
PAG=6
X=7
PTR=10
HCNT=11	;***SRI-AIC*** JEB
HCNTX1=12	;***SRI-AIC*** JEB
HCNTX2=13	;***SRI-AIC*** JEB
VCNT=14		;***SRI-AIC*** JEB
P=17

; PARAMETERS

NPDL=10		; PDL LENGTH
MAXFIL==50	; MAXIMUM FILES UNDER CONSIDERATION AT ONCE
CHPMIN==^D200	; CHARACTERS/MINUTE TRADE-OFF
LNGCUT==^D120000	; LONG FILE CUTOFF
MAXTIM==15*^D60000
PRIMBG==^D9	; BEGINNING OF PRIME TIME
PRIMND==^D18	; END OF PRIME TIME
WAITDL==^D15000	; TIME TO WAIT WHEN NOTHING TO DO
WINDOW==600000	; LOCATION OF WINDOW
WPAG==600	; WINDOW PAGE NUMBER

; STORAGE


FILERF:	0
STOPTM:	0
FILCNT:	0
PRIMEF:	0
LPTJFN:	0
INJFN:	0
DELSW:	0
PNTDIR:	0

OUTPTR:	0
FLUSHT:	0
PNTR:	0
SAVCNT:	0
STARTM:	0
THSCNT:	0
RETPC:	0

QUEUE:	ASCIZ /LPT/		; **** MAH 7/25/74 ****

OUTBUF:	BLOCK ^D27
PDL:	BLOCK NPDL
FILDAT:	BLOCK	MAXFIL
FILPTR:	BLOCK MAXFIL
FILSTR:	BLOCK MAXFIL*^D10+20
NAMBUF:	BLOCK 10
EXTBUF:	BLOCK 17
BANTXT:	BLOCK 3

START:	JFCL
	GTAD
	JUMPL A,ABORT
	RESET			; RESET THE WORLD
	MOVE A,[SIXBIT /PRINTR/]
	SETNM
	MOVE P,[XWD -NPDL,PDL-1]
	MOVEI A,1
	HRROI B,[ASCIZ /PRINTER/]
	STDIR
	 JFCL
	 SETZ A,
	HRRZM A,PNTDIR
	MOVEI A,400000
	SETOB B,C
	EPCAP
	MOVE A,PNTDIR
	HRROI B,[ASCIZ /LPT/]	;PRINTER DIRECTORY PASSWORD	***SRI-AIC***
	CNDIR
	 JFCL
	MOVEI A,400000
	MOVE B,[XWD LEVTAB,CHNTAB]
	SIR
	EIR
	MOVSI B,(1B11)
	AIC			; FILE ERROR INTERRUPT
	SETZM LPTJFN		; NO LINE PRINTER JFN
	SETZM DELSW		; NO DELETED FILES YET
MLOOP:	IFN PRIF,<
	SETO B,
	SETZ D,
	ODCNV			; GET LOCAL TIME OF DAY
	HRRZ A,D		; SECONDS SINCE MINUTE
	IDIVI A,^D3600		; CONVERT TO HOURS SINCE MIDNIGHT
	HRRZS C			; DAY OF WEEK
	SETOM PRIMEF		; PRIME TIME
	CAILE C,4
	SETZM PRIMEF		; NOT PRIME TIME ON WEEKEND
	CAIL A,PRIMBG		; UNLESS BEFORE
	CAIL A,PRIMND		; OR AFTER PRIME HOURS
	SETZM PRIMEF		; THEN NOT
>
	MOVSI A,(1B2!1B11!1B17)
	HRROI B,[ASCIZ /<PRINTER>*.*;*/]
	GTJFN			; GTJFN FOR ALL FILES
	 JRST WAIT		; APPARENTLY NO FILES TO LIST
	MOVEM A,INJFN		; SAVE
	MOVSI X,-MAXFIL		; PREPARE TO COLLECT INFO ABOUT EACH
	MOVE PTR,[POINT 7,FILSTR]

GLOOP:  HRROI A,NAMBUF		;list only files in active queue
	HRRZ B,INJFN		; **** MAH 7/25/74 *****
	MOVSI C,(1B8)
	JFNS
	MOVE A,QUEUE
	CAME A,NAMBUF
	 JRST GLOOP2

	MOVEM PTR,FILPTR(X)
	HRRZ A,INJFN
	MOVE B,[XWD 25,0]
	MOVEI C,FDB
	GTFDB
	SKIPGE FDBUSW
	 JRST GLOOPN
	IFN PRIF,<
	SKIPE FDBUSW		; IF CONTINUATION
	SKIPN PRIMEF		; AND PRIME TIME
	TDZA A,A		; NO.
	MOVSI A,2		; YES, DEMOTE PRIORITY BY TWO DAYS
	MOVE C,FDBSIZ
	CAML C,[LNGCUT]
	 LSH C,2		; PENALIZE LONG FILES
	IDIVI C,CHPMIN
	ADD A,C
>
	MOVE C,FDBWRT
IFN PRIF,<ADD C,A>
	MOVEM C,FILDAT(X)
	MOVE A,PTR
	HRRZ B,INJFN
	MOVEI C,0
	JFNS			; REMEMBER NAME
	MOVEM A,PTR
	IBP PTR
	AOBJP X,FULL		; STOP IF FULL
	HRRZ A,PTR
	CAIL A,FILSTR+^D10*MAXFIL
	 JRST FULL		; STRING FULL
GLOOPN:	MOVE A,INJFN
	GNJFN
	 JRST FULL1		; NO MORE FILES
	JRST GLOOP
GLOOP2:	MOVE 	A,NAMBUF	;GET NAME
	SETZM	NAMBUF		;ZERO  OUT NAMBUF
	TDZ	A,[000017,,-1]	;MASK OFF LAST 4 CHAR
	CAMN	A,[ASCIZ /LP/]	;IS IT AN LP? FILE?
	JRST	GLOOPN		;YES GET NEXT FILE
	MOVE	A,INJFN		;NO GET RID OF IT
	DELF			;DELETE IT
	JFCL			;IGNORE ERROR RETURN
	AOS	DELSW		;MARK FOR EXPUNGE
	JRST 	START		;START OVER
	

FULL1:	HRRZ A,INJFN
	RLJFN
	 JFCL
FULL:	TRNN X,-1
	 JRST WAIT		; NO FILES FOUND
	MOVNI A,0(X)
	MOVEM A,FILCNT		; SAVE NEG OF NUMBER OF FILES
	HRLZ A,FILCNT
SLOOP:	MOVE B,A		; SORT FILES BY PRIORITY
SLOOP1:	MOVE C,FILDAT(A)
	CAMG C,FILDAT(B)
	 JRST SLOOPX
	EXCH C,FILDAT(B)
	MOVEM C,FILDAT(A)
	MOVE C,FILPTR(A)
	EXCH C,FILPTR(B)
	MOVEM C,FILPTR(A)
SLOOPX:	AOBJN B,SLOOP1
	AOBJN A,SLOOP
	MOVSI A,1
	HRROI B,[ASCIZ /<SYSTEM>SPOOLER.QUEUE;1/]
	GTJFN
	 JRST DLOOPI
	PUSH P,A
	MOVE B,[XWD 70000,100000]
	OPENF
	 JRST [	POP P,A
		RLJFN
		JFCL
		JRST DLOOPI]
	HRLZ X,FILCNT
QLOOP:	MOVE B,FILPTR(X)
	SETZ C,
	SOUT
	MOVEI B,37
	BOUT
	AOBJN X,QLOOP
	CLOSF
	 JFCL
	SUB P,[XWD 1,1]
DLOOPI:	IFN PRIF,<
	TIME
	ADD A,[^D<5*60*1000>]
	MOVEM A,STOPTM		; RECOMPUTE PRIORITIES AFTER 5 MINUTES
>
	HRLZ X,FILCNT
	IFE NETF,<
	SKIPE LPTJFN		; LINE PRINTER OPENED?
	 JRST DLOOP		; YES
	MOVSI A,1
	HRROI B,[ASCIZ /LP1:/]
	GTJFN
	 JRST ABORT		; NO LINE PRINTER
	MOVEM A,LPTJFN
DL1:	MOVE B,[XWD 70000,100000+1B28]
	OPENF
	 JRST [CAIE A,OPNX9		;LPT OFFLINE?
		JRST ABORT		;NO, MUST BE ERROR
		MOVEI A,^D5*^D1000	;YES, WAIT
		DISMS
		MOVE A,LPTJFN
		JRST DL1]		;AND TRY AGAIN
>
DLOOP:	MOVSI A,(1B2!1B17)
	MOVE B,FILPTR(X)
	GTJFN
	 JRST DLOOPN		; DISAPPEARED
	MOVEM A,INJFN
	MOVE B,[XWD 25,0]
	MOVEI C,FDB
	GTFDB
	MOVE B,[XWD 70000,200000]
	OPENF
	 JRST [	MOVE A,INJFN
		RLJFN
		 JFCL
		JRST DLOOPN]	; CAN'T OPEN NOW
	IFE NETF,<
	SETZM NAMBUF  ;BMW/JEB, FIX BUG THAT CAUSED "LPT.MUMBLE" SOMETIMES	***SRI-AIC***
	MOVE A,[POINT 7,NAMBUF]
	HRRZ B,INJFN
	MOVSI C,(1B8)
	JFNS			; FILE NAME TO BUFFER
	MOVE A,NAMBUF
	CAME A,QUEUE		; *** MAH 7/25/74 ****
	 JRST [	MOVE A,[POINT 7,EXTBUF]
		MOVE C,[1B8+1B11+1]
		JFNS
		JRST DLOOP1]
	MOVE A,[POINT 7,EXTBUF]
	MOVSI C,(1B11)
	JFNS
DLOOP1:	HRRZI	CNT,1	;FOR one HEADER PGS ***sumex*** mah   
DLOOP2:	MOVE A,LPTJFN
	MOVEI B,37
	BOUT
	MOVE A,[POINT 7,EXTBUF]
	MOVEI C,1
	ILDB B,A
	AOS C
	JUMPN B,.-2
	MOVEI A,^D127	;LINE WIDTH OF LPT - 5			***SRI-AIC***
	IDIV A,C
	PUSH P,[^D10]
	PUSH P,A
HLOOP:	PUSH P,0(P)		; NAME COUNT
HLOOP1:	MOVE A,LPTJFN
	HRROI B,EXTBUF
	SETZ C,
	SOUT
	MOVEI B," "
	BOUT
	BOUT
	SOSLE 0(P)
	 JRST HLOOP1
	MOVEI B,37
	BOUT
	SUB P,[XWD 1,1]
	SOSLE -1(P)
	 JRST HLOOP

;**********
;**********  THE FOLLOWING SECTION PRODUCES A BANNER IN BIG LETTERS
;**********  ON THE LISTING.  ADDED OCT 73, JEB, ***SRI-AIC***
;**********

	PUSH	P,[1]

	MOVE	C,EXTBUF
	MOVEM	C,BANTXT	;SET UP BANTXT ARRAY FOR BANNER.
	MOVE	C,EXTBUF+1
	AND	C,[XWD 777760,0]
	MOVEM	C,BANTXT+1
	HRRZI	B,24	;SKIP TO NEXT SISXTH PAGE.
	BOUT
	HRRZI	B,22	;TRIPLE SPACE.
	BOUT
	HRRZI	B,37
	BOUT
	BOUT

BANGO:	SETZ	VCNT,	;INITIALIZE VERT. POSITION COUNTER.

NEWH:	MOVE	B,[POINT 7,OUTBUF]
	MOVEM	B,OUTPTR	;CLEAR OUTPUT BUFFER.
	MOVE	B,[POINT 7,BANTXT]

	PUSHJ	P,NEXTCH	;GET FIRST CHAR FROM STRING.
	JRST	VOUT2	;NO STRING??? OH WELL, DON'T TRY TO DO ONE.
	JRST	HLP1A	;SKIP SPACING BEFORE THIS CHAR.

HLP1:	PUSHJ	P,NEXTCH
	JRST	VOUT
	HRRZI	D,40	;DO SPACES BEFORE THIS CHARACTER.
	IDPB	D,OUTPTR
	SKIPN	(P)
	JRST	HLP1A	;THIS IS SMALL-SIZED BANNER, ONLY ONE SPACE.
	IDPB	D,OUTPTR
	IDPB	D,OUTPTR	;THREE SPACES FOR BIG BANNER.
HLP1A:	SUBI	C,40
	LSH	C,1	;MAKE RELATIVE INDEX INTO DATA.
	ADDI	C,CHRDAT  ;MAKE ABSOLUTE INDEX.
	HRRZI	HCNT,10	;INITIALIZE HORIZ POSITION COUNTER.

HLP2:	SOJL	HCNT,HLP1	;IF DONE WITH CHAR, GO DO SPACES.
	MOVE	HCNTX1,HCNT	;GET A COPY TO PLAY WITH.
	LSHC	HCNTX1,-2	;GET HIGH BIT,
	XORI	HCNTX1,1	;TOGGLE IT.
	ADD	HCNTX1,C	;GET REAL INDEX TO WORD WITH BIT WE WANT.
	MOVE	D,0(HCNTX1)
	SETZ	HCNTX1,
	LSHC	HCNTX1,5	;COMPUTE POSITION OF BYTE WE WANT.
	ADD	HCNTX1,VCNT	;COMPUTE POSITION OF BIT WE WANT.
	MOVNS	HCNTX1		;NEGATE FOR RIGHT SHIFT.
	LSH	D,-4(HCNTX1)	;BIT WE WANT IS NOW RIGHTMOST.
	ANDI	D,1
	HRRZI	HCNTX2,40	;SPACE
	CAIN	D,1
	LDB	HCNTX2,B
	IDPB	HCNTX2,OUTPTR
	SKIPE	(P)
	IDPB	HCNTX2,OUTPTR	;PUT CHAR TWICE FOR DOUBLE WIDTH.
	JRST	HLP2

VOUT:	IDPB	C,OUTPTR	;PUT ZERO BYTE AT END OF OUTPUT STRING.
	HRROI	B,OUTBUF
	SOUT
	HRRZI	B,37
	BOUT
	SKIPN	(P)
	JRST	VOUT1
	HRROI	B,OUTBUF
	SETZ	C,	;IN CASE IT WAS CHANGED.
	SOUT		;TWO SOUTS FOR DOUBLE HEIGHT.
	HRRZI	2,37
	BOUT
VOUT1:	AOJ	VCNT,
	CAIG	VCNT,7
	JRST	NEWH
VOUT2:	SKIPN	(P)
	JRST	NXTPAG
	SETZM	(P)
	HRRZI	B,24
	BOUT
	BOUT
	MOVE	A,[POINT 7,BANTXT]
	HRREI	B,-1
	MOVSI	C,(1B7+1B10+1B12+1B17)
	ODTIM		;GET DATE/TIME IN FORMAT DD MON YY HHMM
	MOVE	A,LPTJFN
	JRST	BANGO

; SUBROUTINE CALLED BY PUSHJ FROM BANNER ROUTINE.
; GETS NEXT CHAR FROM BANNER TEXT STRING (PTR IN B).
; SKIP 0 FOR ZERO BYTE, SKIP 1 FOR CHARACTER OK.

NEXTCH:	ILDB	C,B
	JUMPE	C,NXTCH1	;IT'S ZERO, RETURN NO SKIP.
	CAIG	C,140
	CAIGE	C,40	;TEST FOR WITHIN RANGE.
	JRST	NEXTCH	;IT'S NOT, IGNORE IT.
	AOS	(P)	;CHAR IS COOL, INCR RETURN PTR.
NXTCH1:	POPJ	P,

;**********
;**********  END OF SRI-AIC ADDITION FOR BANNERS
;**********

NXTPAG:	SUB	P,[XWD 3,3]	;***SRI-AIC***
	MOVEI B,14
	BOUT
	SOJG	CNT,DLOOP2	;TWO HEADER PGS ***SRI-AIC*** JEB
	SETZ PAG,
>
	IFN NETF,<
	MOVSI A,1
	HRROI B,[ASCIZ /NET:0.BBN-TENEX-7/]
	GTJFN			; GET JFN FOR ICP
	 JRST ABORT		; HOPELESS NOW
	MOVEM A,ICPJFN
	MOVE B,[XWD 400000,200000]
	OPENF
	 JRST ABORT
	BIN
	JUMPE B,ABORT		; REJECTING
	MOVEM B,SOCKET
	CLOSF
	 JFCL
	MOVE A,[POINT 7,EXTBUF]
	HRROI B,[ASCIZ /NET:2.BBN-TENEX-/]
	SETZ C,
	SOUT
	MOVE B,SOCKET
	MOVEI C,10
	NOUT
	 JRST ABORT
	MOVSI A,1
	HRROI B,EXTBUF
	GTJFN
	 JRST ABORT
	MOVEM A,LPTJFN
	MOVSI A,1
	HRROI B,EXTBUF
	GTJFN
	 JRST ABORT
	MOVEM A,ICPJFN
	MOVE B,[XWD 72400,200000]
	OPENF
	 JRST ABORT
	MOVE A,LPTJFN
	MOVE B,[XWD 73400,100000]
	OPENF
	 JRST ABORT
	MOVE B,PNTDIR
	DIRST
	 JRST ABORT
	MOVEI B,":"
	BOUT
	MOVE A,PNTDIR
	MOVEI B,DIRTAB
	MOVE C,[POINT 7,NAMBUF]
	GTDIR
	MOVE A,LPTJFN
	MOVE B,DIRTAB+1
	SETZ C,
	SOUT
	SETZM NAMBUF
	MOVE B,[XWD NAMBUF,NAMBUF+1]
	BLT B,NAMBUF+7
	HRROI B,[ASCIZ /:W:0:7:<PRINTER>/]
	SOUT
	HRRZ B,INJFN
	MOVE C,[BYTE (3)0,0,1,1,1,0,1(1)0,0,0,0,0(5)0,1]
	JFNS
	HRLI A,600000
	CLOSF
	 JRST ABORT
	MOVE A,ICPJFN
	BIN
	GTSTS
	TLNN B,1000
	 JRST ABORT
	CLOSF
	 JRST ABORT
	MOVE A,LPTJFN
	MOVE B,[XWD 72400,100000]
	OPENF
	 JRST ABORT
>
	GTAD
	MOVEM A,STARTM
	MOVE A,INJFN
	MOVE CNT,FDBSIZ
	SUB CNT,FDBUSW
	MOVEM CNT,SAVCNT
	MOVE B,FDBUSW
	IDIVI B,5		; SEPARATE ITO BYTE AND WORD
	IMULI C,7
	SUBI C,^D36
	MOVNS C
	ROT C,-6
	TLO C,700
	MOVEM C,PNTR
	IDIVI B,1000		; SEPARATE INTO WORD AND PAGE
	ADDI C,WINDOW
	HRRM C,PNTR
	MOVEM B,PAG
	MOVE B,FDBUSW
	IDIVI B,5000		; GET BYTE WITHIN PAGE
	MOVNS C
	ADDI C,5000
	MOVEM C,THSCNT
	TIME
	ADD A,[MAXTIM]
	MOVEM A,FLUSHT		; WHEN TO FLUSH DURING PRIME TIME
	MOVEI D,14		; XFER PAGE AT A TIME UNTIL TIME IS UP
	SETZM FILERF
	SETZ C,
	JUMPE CNT,CLOOPE
CLOOP:	HRLZ A,INJFN
	HRR A,PAG
	MOVE B,[XWD 400000,WPAG]
	MOVSI C,100000
	PMAP
	MOVE C,THSCNT
	CAMLE C,CNT
	 MOVE C,CNT
	MOVN B,C
	ADDM B,CNT
ILOOP:	MOVE A,LPTJFN
	MOVE B,PNTR
	SOUT
	SKIPE FILERF
	 JRST CLOOPE
	JUMPN C,[MOVEM B,PNTR
		TIME
		SKIPE PRIMEF
		CAMG A,FLUSHT
		 JRST ILOOP
		JRST CLOOPE]
	AOS PAG
	MOVEI A,5000
	MOVEM A,THSCNT
	HRROI A,WINDOW
	MOVEM A,PNTR
	JUMPN CNT,[TIME
		SUBI A,^D30000
		SKIPE PRIMEF
		CAMG A,FLUSHT		; TIME TO FLUSH
		 JRST CLOOP		; NO
		MOVEI D,12		; YES, GO BY LINES
		SUBI A,^D30000
		CAMG A,FLUSHT
		 JRST CLOOP		; FOR 30 SECONDS
		JRST CLOOPE]		; THEN STOP ANYWAY
CLOOPE:	ADDM C,CNT
	MOVE B,[XWD 400000,WPAG]
	SETZ C,
	SETO A,
	PMAP
	MOVE A,LPTJFN
	SKIPN FILERF
	JUMPN CNT,[MOVEI B,14
		BOUT
		HRROI	B,CONLIS
	SETZ C,
		SOUT
		JRST .+1]
;THE FOLLOWING CODE PUTS MARKERS ON THE PAGE BOUNDARY OF SEVERAL
;PAGES FOR THE PURPOSE OF EASILY SEPARATING LISTINGS.
;IT IS CURRENTLY NOT IN USE.

REPEAT 0,<
	PUSH P,[2]
PLOP1:	MOVEI B,14
	BOUT
	MOVEI B,37
	MOVEI C,^D59						;***SRI-AIC***
	BOUT
	MOVEI B,23
	SOJG C,.-2
	MOVEI D,5
PUNCH:	MOVEI C,^D132	;LINE WIDTH OF LPT			***SRI-AIC***
	MOVEI B,"W"
	BOUT
	SOJG C,.-1
	MOVEI B,15
	BOUT
	MOVEI B,23
	BOUT
	SOJG D,PUNCH
	SOSLE 0(P)
	 JRST PLOP1
	SUB P,[XWD 1,1]
>				;END REPEAT 0 AT PLOP1:-2
	MOVEI B,14
	BOUT
	MOVE A,INJFN
	HRLI A,24
	SETO B,
	MOVE C,FDBSIZ
	SUB C,CNT
	IOR C,FILERF
	CHFDB
	MOVE A,INJFN
	SKIPN CNT
	DELF
	 JFCL
	MOVE A,INJFN
	CLOSF
	 JFCL
	AOS DELSW

IFDEF LOGF,<			;DON'T MAKE SPOOLER.LOG UNLESS NEEDED	***SRI-AIC***
	MOVSI A,1
	HRROI B,[ASCIZ /<SYSTEM>SPOOLER.LOG;1/]
	GTJFN
	 JRST DLOOPN
	PUSH P,A
	MOVE B,[XWD 70000,20000]
	OPENF
	 JRST [	POP P,A
		RLJFN
		 JFCL
		JRST DLOOPN]
	MOVE B,STARTM
	MOVSI C,(1B10!1012)
	ODTIM
	MOVEI B,40
	BOUT
	BOUT
	HRROI B,EXTBUF
	MOVEI C,1000
	MOVEI D,0
	SOUT
	SUBI C,1000-^D15
	MOVEI B,40
	BOUT
	SOJG C,.-1
	MOVE B,SAVCNT
	SUB B,CNT
	MOVE C,[1B2!1B4!7B17+12]
	NOUT
	 MOVE A,0(P)
	HRROI B,[ASCIZ / CHRS/]
	SETZ C,
	SOUT
	GTAD
	HRRZ B,STARTM
	HRRZS A
	PUSH P,A
	SUBM A,B
	CAIGE B,0
	ADDI B,^D24*^D60*^D60
	MOVE A,-1(P)
	FSC B,233
	FDVRI B,(60.0)
	MOVE C,[5B23+2B29+1B4+1B6+1B11]
	FLOUT
	 MOVE A,-1(P)
	HRROI B,[ASCIZ / MINS/]
	SETZ C,
	SOUT
	POP P,B
	MOVE C,FDBWRT
	SUBI B,(C)
	JUMPL B,[ADDI B,^D24*^D60*^D60
		JRST .]
	FSC B,233
	FDVRI B,(60.0)
	MOVE C,[5B23+2B29+1B4+1B6+1B11]
	FLOUT
	 MOVE A,0(P)
	HRROI B,[ASCIZ / DELAY/]
	SETZ C,
	SOUT
	HRROI B,[ASCIZ / FILE ERROR/]
	SKIPN FILERF
	HRROI B,[ASCIZ / CONTINUED/]
	SKIPE CNT
	SOUT
	MOVEI B,37
	BOUT
	POP P,A
	CLOSF
	 JFCL
>				;***SRI-AIC***

DLOOPN:	IFE NETF,<
	TIME
	CAMLE A,STOPTM
	 JRST MLOOP>
	AOBJN X,DLOOP
WAIT:	IFE NETF,<
	SKIPE A,LPTJFN
	 CLOSF
	 JFCL
	SETZM LPTJFN
>
	SKIPN A,PNTDIR
	 JRST WAIT1
	SKIPE DELSW
	 DELDF
	SETZM DELSW
WAIT1:	MOVEI A,WAITDL
	DISMS
	JRST MLOOP

ABORT:	RESET
	MOVEI A,^D60000
	JRST START

LEVTAB:	RETPC
	0
	0

CHNTAB:	REPEAT ^D11,<0>
	XWD 1,ERRINT
	REPEAT ^D36-1-^D11,<0>

ERRINT:	PUSH P,A
	PUSH P,B
	PUSH P,C
	HRROI B,[ASCIZ /
************************************  FILE DATA ERROR  *************************************
/]
	SETZ C,
	SKIPE A,LPTJFN
	SOUT
	SETOM FILERF
	POP P,C
	POP P,B
	POP P,A
	DEBRK

FDB:	BLOCK 12
FDBSIZ:	BLOCK 2
FDBWRT:	BLOCK 10
FDBUSW:	BLOCK 1





;**********
;**********  THE "CHRDAT" TABLE INSERTED FOR BANNERS ON HEADERS.
;**********  ***SRI-AIC*** JEB OCT 1973.
;**********

CHRDAT:	BYTE (8) 0,0,0,0,0,0,0,0		;SPACE
	BYTE (8) 0,0,36,241,36,0,0,0		;EXC PT
	BYTE (8) 0,0,7,0,7,0,0,0		;DBL QUOTE
	BYTE (8) 50,50,376,50,376,50,50,0	;NBR SIGN
	BYTE (8) 40,114,122,377,122,44,0,0	;DLR SIGN
	BYTE (8) 203,103,40,20,10,304,302,0	;PCT SIGN
	BYTE (8) 0,146,231,231,246,100,240,0	;AMPERSAND
	BYTE (8) 0,0,0,7,0,0,0,0		;SNGL QUOTE
	BYTE (8) 0,74,102,201,0,0,0,0		;L PAREN
	BYTE (8) 0,0,0,201,102,74,0,0		;R PAREN
	BYTE (8) 222,124,60,376,60,124,222,0	;ASTERISK
	BYTE (8) 0,10,10,74,10,10,0,0		;PLUS
	BYTE (8) 0,0,200,340,0,0,0,0		;COMMA
	BYTE (8) 0,10,10,10,10,10,0,0		;MINUS
	BYTE (8) 0,0,300,300,0,0,0,0		;DOT
	BYTE (8) 340,160,70,30,14,6,7,0		;FWD SLASH
	BYTE (8) 44,102,201,201,201,201,102,44	;0   (ZERO)
	BYTE (8) 0,202,201,377,200,200,0,0	;1
	BYTE (8) 0,302,241,221,211,206,200,0	;2
	BYTE (8) 0,102,201,211,211,211,166,0	;3
	BYTE (8) 30,24,22,21,377,20,20,0	;4
	BYTE (8) 117,211,211,211,211,211,161,0	;5
	BYTE (8) 174,242,221,221,221,222,140,0	;6
	BYTE (8) 0,3,201,101,41,21,17,0		;7
	BYTE (8) 140,226,211,211,211,226,140,0	;8
	BYTE (8) 146,211,211,211,211,105,76,0	;9
	BYTE (8) 0,0,0,330,330,0,0,0		;COLON
	BYTE (8) 0,230,330,0,0,0,0,0		;SEMICOLON
	BYTE (8) 0,10,24,42,101,0,0,0		;L ANGLE BRCKT
	BYTE (8) 0,24,24,24,24,24,0,0		;EQ SIGN
	BYTE (8) 0,101,42,24,10,0,0,0		;R ANGLE BRCKT
	BYTE (8) 0,6,1,261,11,6,0,0		;QST MARK
	BYTE (8) 74,102,211,225,235,221,16,0	;AT SIGN
	BYTE (8) 374,22,21,21,21,22,374,0	;A
	BYTE (8) 377,377,211,211,211,211,216,160	;B
	BYTE (8) 74,102,201,201,201,201,201,102	;C
	BYTE (8) 377,377,201,201,201,201,102,74	;D
	BYTE (8) 377,377,211,211,211,211,201,201	;E
	BYTE (8) 377,377,11,11,11,11,1,1	;F
	BYTE (8) 74,102,201,201,241,241,241,142	;G
	BYTE (8) 377,377,10,10,10,10,377,377	;H
	BYTE (8) 201,201,201,377,377,201,201,201	;I
	BYTE (8) 140,201,201,201,377,177,1,1	;J
	BYTE (8) 377,377,20,10,24,42,101,200	;K
	BYTE (8) 377,377,200,200,200,200,200,200	;L
	BYTE (8) 376,1,2,14,14,2,1,376		;M
	BYTE (8) 377,6,14,30,60,140,377,0	;N
	BYTE (8) 74,102,201,201,201,201,102,74	;O
	BYTE (8) 377,377,11,11,11,11,6,0	;P
	BYTE (8) 74,102,201,201,201,241,102,274	;Q
	BYTE (8) 377,377,21,21,21,61,121,216	;R
	BYTE (8) 100,206,211,221,221,221,142,0	;S
	BYTE (8) 1,1,1,377,377,1,1,1		;T
	BYTE (8) 77,100,200,200,200,200,100,77	;U
	BYTE (8) 37,40,100,200,100,40,37,0	;V
	BYTE (8) 177,200,100,40,100,200,177,0	;W
	BYTE (8) 203,104,40,30,40,104,203,0	;X
	BYTE (8) 7,10,10,370,370,10,10,7	;Y
	BYTE (8) 201,321,261,221,231,225,203,0	;Z
	BYTE (8) 377,201,201,201,201,0,0,0	;L BRCKT
	BYTE (8) 7,6,14,30,70,160,340,0		;BCK SLASH
	BYTE (8) 0,0,201,201,201,201,377,0	;R BRACKT
	BYTE (8) 0,10,14,376,14,10,0,0		;UP ARW
	BYTE (8) 10,34,76,10,10,10,10,0		;BK ARW
	BYTE (8) 0,0,0,1,2,4,0,0		;BACKWARD ACCENT

;**********
;**********  END OF SRI-AIC INSERTION.
;**********

CONLIS:	ASCIZ /


*********************************************************************************************************
********************************                                         ********************************
********************************     THIS LISTING IS CONTINUED LATER     ********************************
********************************                                         ********************************
*********************************************************************************************************
/
	END START
