;<SOURCES>SETTIM.MAC;2    10-JUN-75 07:56:39    EDIT BY HEDBERG
;<SOURCES>SETTIM.MAC;1    29-MAY-75 05:53:07    EDIT BY HEDBERG
;<SOURCES>SETTIM.MAC;13     2-MAR-75 15:15:50    EDIT BY GEOFF
;REMOVED ARC FROM HOST LIST SINCE THEY DON'T EXIST ANYMORE.
;<GEOFF>SETTIM.MAC;15    21-FEB-75 15:25:03    EDIT BY GEOFF
;COMBINED SETTIM AND THE TELSER STARTED INTO ONE.
; CHANGED SO TIMES OUT AFTER 90 SECONDS OR SO.

	TITLE	SETTIM
	SUBTTL	SYSTEM JOB TO SET TIME FROM NETWORK
	SEARCH	STENEX


OPDEF	CALL	[PUSHJ P,]
OPDEF	RET	[POPJ P,]

P=17
NPDL==20
MAXDIF==^D15
TIMOUT==^D90000		;GIVE-UP TIME AFTER START

;Main program

START:	RESET
	MOVE	P,[IOWD NPDL,PDL]
	HRROI	1,[ASCIZ \ *** SETTIM HERE ***
\]
	PSOUT
	GJINF
	CAMN	4,[-1]			;IF DETACHED,
	JRST	[MOVEI	1,^D30000	;WAIT FOR THE NET TO GET GOING
		 DISMS
		 JRST	.+1  ]

	TIME			;SAVE FOR TIME-OUT FUNCTION
	MOVE	11,1
	MOVEI	1,400000
	SETOB	2,3
	EPCAP			;LET'S ENABLE OURSELVES

RETRY:	MOVSI	5,-NHOSTS	;5 IS THE HOST NAME POINTER
	SETZ	6,		;6 IS THE FOUND TIMES POINTER
HOSTL:	CALL	GETTIM
	 JRST	GOOB		;THIS HOST DOESN'T HAVE IT, TRY AGAIN
	MOVEM	2,TIMES(6)
	AOJ	6,
	HRROI	1,[ASCIZ \Time from \]
	PSOUT
	HRRO	1,HSTNAM(5)
	PSOUT
	HRROI	1,[ASCIZ \ -> \]
	PSOUT
	CALL	PRNTIM
	MOVEI	1,37
	PBOUT
GOOB:	AOBJN	5,HOSTL		;NEXT
	JUMPE	6,NOTFND	;NOBDY'S GOT THE TIME

	MOVN	5,6
	HRLZ	5,5
	PUSH	P,5		;SAVE AOBJN POINTER

	CALL	AVTIMS		;TAKES AOBJN POINTER IN 5, RETS AVERAGE
				;TIME IN 1
	CAIG	6,2		;ONLY THROW OUT THE WAY OFF ONE IF THERE
	JRST	SETIT		;ARE >2 TIMES TO CHOOSE FROM

	MOVE	5,(P)		;GET OLD AOBJN POINTER
DIFFL:	MOVE	2,TIMES(5)	;AND PUT THE DIFFERENCES IN THE DIFFS ARRAY
	SUB	2,1
	MOVMM	2,DIFFS(5)
	AOBJN	5,DIFFL

	MOVE	5,(P)		;GET OLD AOBJN POINTER
	SETZ	4,
	CAMGE	4,DIFFS(5)	;FIND THE GREATEST GOOF
	JRST	[HRRZ	3,5
		 MOVE	4,DIFFS(5)
		 JRST	.+1   ]
	AOBJN	5,.-2

	HRROI	1,[ASCIZ \Time \]
	PSOUT
	MOVE	2,TIMES(3)
	CALL	PRNTIM
	HRROI	1,[ASCIZ \ thrown out
\]
	PSOUT
	SETZ	2,
	HRRM	2,TIMES(3)	;THROW OUT THE TIME CORRESPONDING TO THE
	SOJ	6,		;GREATEST DIFFERENCE
	POP	P,5
	CALL	AVTIMS

SETIT:	PUSH	P,1		;SAVE AVERAGE
	GTAD			;IS TIME ALREADY SET?
	CAME	1,[-1]
	JRST	GOOD		;YES, DON'T SET

	POP	P,1
	STAD
	 JFCL
	MOVE	2,1
				;PRINT A MESSAGE
	HRROI	1,[ASCIZ \
  Time (\ ]
	PSOUT
	CALL	PRNTIM
	HRROI	1,[ASCIZ \ ) set.

\]
	PSOUT
	
	JRST	DONE

NOTFND:
	TIME			;CHECK TIME-OUT
	SUB	1,11
	CAMLE	1,[TIMOUT]	;TRY FOR A WHILE

	JRST	[GTAD
		 JUMPGE	1,  [HRROI 1,[ASCIZ \
Couldn't find time but someone set it
\]
			     PSOUT
			     JRST  DONE ]	;GIVE UP IF SET NOW
		 JRST	.+1]	;ELSE KEEP TRYING (OPR DIED??) REALLY!

	MOVEI	1,^D3000
	DISMS
	HRROI	1,[ASCIZ \ Trying ...\]
	PSOUT
	JRST	RETRY

GOOD:	HRROI	1,[ASCIZ \Time \]
	PSOUT
	POP	P,2
	CALL	PRNTIM
	HRROI	1,[ASCIZ \ averaged, but already set correctly.
\]
	PSOUT
	
	JRST	DONE

PRNTIM:	MOVEI	1,101		;ACCEPTS TIME IN AC2, PRINTS IT
	MOVE	3,[336021,,0]
	ODTIM
	RET

AVTIMS:	SETZ	1,		;PUTS THE TIMES AVERAGE IN 1
AVT1:	MOVE	2,TIMES(5)
	TLZ	2,-1
	ADD	1,2		;GET AVERAGE OF ALL THE TIMES
	AOBJN	5,AVT1
	IDIV	1,6
	HLL	1,TIMES		;IT IS NOW IN 1
	RET

GETTIM:	HRRO	1,HSTNAM(5)	;NAME IN 1
	MOVEI	2,15		;FOREIGN SOCKET IN 2
	MOVEI	3,0		;LOCAL SOCKET IN 3
	CALL	GETJFN
	 RET			;ERROR
	MOVEM	1,SNDJFN
	MOVE	2,[40B5+1B19]	;READ, 32-BIT BYTES
	OPENF
	 JRST	RELSND
	BIN			;GET THE SOCKET GIVEN BY FS 15
	MOVEM	2,SOCKET
	CLOSF
	 JFCL
	HRRO	1,HSTNAM(5)
	MOVEI	3,2
	CALL	GETJFN		;AND GTJFN THE SEND CONNECTION
	 RET
	MOVEM	1,SNDJFN
	HRRO	1,HSTNAM(5)
	MOVE	2,SOCKET
	MOVEI	3,2
	CALL	GETJFN		;THEN GTJFN THE RECEIVE CONNECTION
	 JRST	RELSND
	MOVEM	1,RECJFN
	MOVE	2,[7B5+6B9+1B19]
	OPENF			;AND OPEN IT 7 BIT BYTES, IMMEDIATE RETURN 
	 JRST	RELREC
	MOVE	1,SNDJFN
	MOVE	2,[7B5+1B20]	;THEN OPEN THE SEND 7 BIT BYTES
	OPENF
	 JRST	[MOVE	1,RECJFN  ;ABORT
		 CLOSF
		 JFCL
		 JRST	RELSND]

	MOVE	1,RECJFN
DONTQ:	SETZ	2,
	IDTIM
	 JRST	[GTSTS
		 TLNE 2,1000
		  JRST CLZBTH
		 JRST DONTQ ]
	MOVE	1,2		;GET THE TIME IN 1
	AOS	0(P)		;GOOD RETURN

CLZBTH:	MOVE	1,RECJFN
	CLOSF
	 JFCL
	MOVE	1,SNDJFN
	CLOSF
	 JFCL
	RET

RELREC:	MOVE	1,RECJFN
	RLJFN
	 JFCL
RELSND:	MOVE	1,SNDJFN
	RLJFN
	 JFCL
	RET

GETJFN:	PUSH	P,2		;SAVE FOREIGN SOCKET
	PUSH	P,1		;NAME POINTER
	PUSH	P,3		;AND LOCAL SOCKET
	MOVE	1,[POINT 7,STRING]
	HRROI	2,[ASCIZ /NET:/]
	SETZ	3,		;CONSTRUCT A NAME OF THE FORM
	SOUT			;" NET:<ls>.<fname>-<fs>;T "
	POP	P,2
	MOVEI	3,10
	NOUT
	 JRST	4,.		;HORRIBLE ERROR
	MOVEI	2,"."
	IDPB	2,1
	POP	P,2
	SETZ	3,
	SOUT
	POP	P,2
	MOVNS	2		;TO GET THE "-" CHEAPLY
	MOVEI	3,10
	NOUT
	 JRST	4,.
	HRROI	2,[ASCIZ /;T/]
	SETZ	3,
	SOUT
	MOVSI	1,(1B17)
	HRROI	2,STRING
	GTJFN
	 RET			;ERR RET
	AOS	0(P)
	RET			;OK

	LIT

HSTNAM:
	[ASCIZ \SRI-AI\]
	[ASCIZ \BBN\]
	[ASCIZ \ISI\]
	[ASCIZ \UTAH-10\]

NHOSTS==.-HSTNAM

STRING:	BLOCK 20
PDL:	BLOCK NPDL
SOCKET:	BLOCK 1
SNDJFN:	BLOCK 1
RECJFN:	BLOCK 1
TIMES:	BLOCK NHOSTS
DIFFS:	BLOCK NHOSTS

DONE:	RESET
	GJINF
	CAME	4,[-1]		;ARE WE DETACHED?
	HALTF			;NO, QUIT

	MOVSI	1,100001	;YES, RUN BATCON
	HRROI	2,[ASCIZ \<SYSTEM>BATCON.SAV\]
	GTJFN
	 JRST	[PUSH	P,1
		 HRROI	1,[ASCIZ \GTJFN ON BATCON FAILED -- \]
		 PSOUT
		 MOVEI	1,100
		 POP	P,2
		 HRLI	2,400000
		 SETZ	3,
		 ERSTR
		  JFCL
		  JFCL
		 MOVEI	1,37
		 PBOUT
		 SETO	1,
		 LGOUT
		 JRST	.-1  ]

	HRLI	2,FROM
	HRRI	2,TO
	BLT	2,16		;move some code to the registers

	HRLI	1,400000
	MOVEM	1,17
	JRST	TO

;	CODE DOWN ON REGS.

FROM:
	PHASE	4
TO:	SETO	1,
	HRLZI	2,400000
	MOVEI	4,5
	PMAP
	AOJ	2,
	SOJGE	4,.-2

	MOVE	1,17
	GET
	JRST	140

	DEPHASE

	END	START

