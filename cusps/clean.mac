	TITLE	CLEAN - CLEAN UP DIRECTORY
	SEARCH	STENEX

;	Michael A. Heathman
;	SUMEX 5/75

P=17
NPDL=20

FCOL=^D15				;FIRST COLUMN OF COLUMNIZED OUTPUT

; CHAR DEFINITIONS

EOL=37
TAB==11
CR==15
LF==12
DEL==177

; FDB BLOCK OFFSET DEFINITIONS

FDBCTL==1				;CTL BITS
FDBBYV==11				;PAGE COUNT
FDBCRV==13				;CREATE DATE/TIME
FDBWRT==14				;WRITE DATE/TIME
FDBREF==15				;REFERENCE DATE/TIME
FDBBCK==17				;BACKUP WORDS

; FDB BITS DEFINITIONS

FDBDEL==040000				;DELETE BIT
FDBARC==200000				;ARCHIVE REQUEST
FDBNAR==100000				;DON'T (EVER) ARCHIVE
AFDBDL==10000				;DON'T DELETE AFTER ARCHIVE
FDBAAR==4000				;FILE IS ALREADY ARCHIVED

	OPDEF CALL [PUSHJ P,]
	OPDEF RET [POPJ P,]

START:	RESET
	MOVE P,[IOWD NPDL,PDL]

	HRROI 1,[ASCIZ /
file/]
	PSOUT
	MOVEI 2,FCOL-5			;PUT REST OF HEADER AT FCOL
	MOVEI 1," "
	PBOUT
	SOJG 2,.-1
	HRROI 1,[ASCIZ /pgs	   create	   write	   read

/]
	PSOUT

	MOVSI 1,100101			;GET FIRST FILE JFN
	HRROI 2,[ASCIZ /*.*;*/]
	GTJFN
	 0
	MOVEM 1,JFN#
	SETZM BACKF#			;INIT BACKUP FLAG
FILE:	MOVEI 1,101			;WRITE FILE NAME
	HRRZ 2,JFN
	MOVE 3,[1B8+1B11+1B14+1B21+1B35]
	JFNS

	HRRZ 1,JFN			;GET FDB
	MOVE 2,[25,,0]
	MOVEI 3,FDB
	GTFDB

	MOVEI 1,101			;GET CURSOR POSITION
	RFPOS
	HRRZS 2				;GET JUST COLUMN NUMBER
	CAIL 2,FCOL			;IF NAME TOO LARGE
	 JRST  [MOVEI 1,EOL		;GO TO NEW LINE
		PBOUT
		SETZ 2,
		JRST .+1]
	SUBI 2,FCOL
	MOVEI 1," "			;ELSE SPACE OUT TO 25TH COLUMN
	PBOUT
	AOJL 2,.-1

	MOVEI 1,101			;WRITE OUT PAGES
	MOVEI 3,^D10			;IN DECIMAL
	HRRZ 2,FDB+FDBBYV
	NOUT
	 0

	MOVEI 1,TAB
	PBOUT

	MOVEI 1,101			;WRITE OUT CREATE DATE
	MOVE 2,FDB+FDBCRV
	MOVSI 3,045240
	SKIPN 2
	 JRST  [HRROI 1,[ASCIZ /  no date    /]
		PSOUT
		JRST .+2]
	ODTIM

	MOVEI 1,TAB
	PBOUT

	MOVEI 1,101			;WRITE OUT LAST WRITE DATE
	MOVE 2,FDB+FDBWRT
	MOVSI 3,045240
	SKIPN 2
	 JRST  [HRROI 1,[ASCIZ /  no date    /]
		PSOUT
		JRST .+2]
	ODTIM

	MOVEI 1,TAB
	PBOUT

	MOVEI 1,101			;WRITE OUT LAST REF DATE
	MOVE 2,FDB+FDBREF
	MOVSI 3,045240
	SKIPN 2
	 JRST  [HRROI 1,[ASCIZ /  not read   /]
		PSOUT
		JRST .+2]
	ODTIM
	
	HRROI 1,[ASCIZ / ?/]
	PSOUT
FILE1:	MOVEI 1,100			;SET TO NO ECHO
	RFMOD
	PUSH P,2
	TRZ 2,3B25
	SFMOD
	PBIN				;GET COMMAND
	ANDI 1,177
	CAIL 1,"a"			;RAISE
	CAILE 1,"z"
	 JRST .+2
	TRZ 1," "
	EXCH 1,(P)			;SAVE CHAR, GET MODE WORD
	MOVEI 1,100			;RESET ECHO
	SFMOD
	POP P,1

	CAIL 1," "			;ECHO PRINTABLE CHARS
	PBOUT

	CAIN 1,"?"
	 JRST QUES
	CAIE 1,LF
	CAIN 1,EOL			;OK?
	 JRST NXTFIL			;YES
	CAIN 1,"^"			;GO BACK ONE?
	 JRST BACKUP			;YES
	CAIE 1,DEL
	CAIN 1,"D"			;DELETE?
	 JRST DELETE
	CAIN 1,"A"			;ARCHIVE
	 JRST ARCHIV
	CAIN 1,"U"
	 JRST UNDELE
	CAIN 1,"B"
	 JRST START
	CAIN 1,"Q"
	 JRST QUIT
	CAIN 1,"R"
	 JRST RESARC			;RESET ARCHIVE
	CAIN 1,"E"
	 JRST EXEC

	HRROI 1,[ASCIZ / ?? /]
	PSOUT
	JRST FILE1
NXTFIL:	MOVEI 1,EOL			;GO ONTO NEXT FILE 
	PBOUT
	
	SKIPLE BACKF			;ARE WE ALREADY BACKED UP?
	 JRST  [MOVE 1,BACKF		;YES
		SETOM BACKF		;RESET FLG
		EXCH 1,JFN		;JUST SWITCH JFNS
		RLJFN			;AND RELEASE OLD ONE
		 0 
		JRST FILE]

	HRROI 1,NAMBUF			;SAVE FILE NAME FOR BACKUP
	HRRZ 2,JFN
	MOVE 3,[1B8+1B11+1B14+1B35]
	JFNS
	SETOM BACKF			;AND SET FLAG

	MOVE 1,JFN		
	GNJFN
	 JRST .+2			;GNJFN EXHAUSTED
	JRST FILE

QUIT:	RESET
	HALTF
	JRST START

UNDELE:	SKIPA 3,[0]			;TURN OFF DELETE BIT
DELETE:	MOVSI 3,FDBDEL			;TURN ON DELETE BIT
	HRLI 1,FDBCTL
	HRR 1,JFN
	MOVSI 2,FDBDEL
	CHFDB
	JRST NXTFIL

BACKUP:	SKIPN BACKF			;ANY FILE TO BACKUP TO?
	 JRST  [HRROI 1,[ASCIZ /
Cannot backup from the beginning.
/]
		JRST ERRMSG]
	SKIPL BACKF			;ALREADY ONE BACKUP?
	 JRST  [HRROI 1,[ASCIZ /
Cannot backup more than one file.
/]
		JRST ERRMSG]
	MOVE 1,JFN
	MOVEM 1,BACKF

	MOVSI 1,101001			;GTJFN FROM SAVED STRING
	HRROI 2,NAMBUF
	GTJFN
	 0
	MOVEM 1,JFN

	MOVEI 1,37
	PBOUT
	JRST FILE

ERRMSG:	PSOUT
	JRST FILE
ARCHIV:	MOVE 2,FDB+FDBBCK		;CAN WE ARCHIVE?
	TLNE 2,FDBAAR			;ALREADY ARCHIVED?
	 JRST  [HRROI 1,[ASCIZ /
File already archived.
/]
		JRST ERRMSG]
	TLNE 2,FDBNAR			;NEVER ARCHIVE?
	 JRST  [HRROI 1,[ASCIZ /
File cannot ever be archived.
/]
		JRST ERRMSG]
	TLNE 2,FDBARC			;ARCHIVE ALREADY PENDING
	 JRST  [HRROI 1,[ASCIZ /
File already archived.
/]
		JRST ERRMSG]

	MOVSI 2,FDBARC			;SET BACK UP BITS 
	MOVE 3,2
ARCH1:	HRLI 1,FDBBCK			
	HRR 1,JFN
	CHFDB

	JRST NXTFIL

RESARC:	MOVSI 2,FDBARC+AFDBDL		;RESET ARCHIVE REQUEST AND DON'T
	SETZ 3,				;DELETE AFTER ARCHIVING
	JRST ARCH1
	
	
QUES:	HRROI 1,QESMES
	PSOUT
	JRST FILE

EXEC:	MOVEI 1,EOL
	PBOUT

	MOVSI 1,100001			;GET LOWER FORK EXEC
	HRROI 2,[ASCIZ /<SYSTEM>EXEC.SAV/]
	GTJFN
	 0
	MOVEM 1,EXECJ#

	MOVSI 1,(1B1)			;CREATE FORK WITH SAME CAPS
	CFORK
	 0
	MOVEM 1,EXECF#
	FFORK

	HRLZS 1
	HRR 1,EXECJ			;GET JFN
	GET
					
	MOVE 1,EXECF			;START FORK
	SETZ 2,
	SFRKV
	RFORK

	WFORK				;WAIT FOR FORK TO FINISH
	KFORK				;AND KILL

	MOVE 1,EXECJ			;RELEASE JFN
	RLJFN
	 0
	JRST FILE

PDL:	BLOCK NPDL
NAMBUF:	BLOCK 30
QESMES:	ASCIZ /
<cr> or <lf> to skip to next file; <del> or "D" to delete file;
"A" to archive file; "^" to backup to last file;
"U" to undelete a deleted file; "R" to reset archive bits;
"E" for lower fork exec; "B" to start over; and "Q" to quit.
/

FDB:	BLOCK 25
	BLOCK 7

	END START

