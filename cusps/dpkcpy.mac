	SUBTTL	DPKCPY
;MODIFIED FOR RP03'S ON A KI10 BY MICHAEL HEATHMAN @ SUMEX 11/11/74
;EDITED BY E.POLLACK 25-JAN-73
	;MODIFIED BY D.SAGALOWICZ
;		LAST CHANGED 30-OCT-73 1200 DS
;		CHANGED 24-JUL-74 1500 DS FOR DISK PARAMETERIZATION
	SUBTTL DISK HARDWARE PARAMETERS
;GENERAL PARAMETERS
;
;ADDRESSES OF BUFFERS, MAX SIZE IS 1 CYLINDER
BUF=40000	;BUFFER ADR FOR READ/WRITE BUFFER
BUF1=BUF+NWRDTK	;BUFFER FOR CHECKING THAT WRITE WAS OK
FMTBUF=BUF1+NWRDTK  ;BUFFER FOR FORMAT ADDRESS
;
;MAIN CONTOLLING WORDS, NOT ASSEMBLY PARAMS TO FACILITATE
;CONTROL OF PROGRAM FROM DDT.
;
;FIRST AND LAST ARE LOGICAL (SECTOR) ADDRESSSES WHICH SPECIFY
;THE RANGE OF ADDRESSES TO BE COPIED.  THEY DO NOT INCLUDE
;THE UNIT IN THE ADDRESS.  THE DEFAULT VALUES ARE 0 TO NSECUN-1
;WHICH MEANS ALL THE SECTORS ON A PACK
;
FIRST:	0	;STARTING LOGICAL DISK ADDRESS
LAST:	NSECUN-1	;LAST LOGICAL ADR TO BE TRANSFERRED
READ:	1	;PHYSICAL UNIT NO FOR INPUT DISK PACK [1,4]
WRITE:	2	;PHYSICAL UNIT NO FOR OUTPUT DISK PACK [1,4]
FORMAT: 0	;1 IF FORMATTI'G REQUESTED,0 IF NOT
COMPAR:	1	;0 IF BUFFER COMPARISON IS NOT WANTED
DSKINC:	NSECTK	;SECTORS/XFER = ONE CYL
NBADCH:	BLOCK 1	;NO OF CHECK ERRORS
CNTCHK:	BLOCK 1	;RETRY COUNT FOR CHECK ERROR RECOVERY
LSTCHK:	BLOCK 1	;LAST DISK ADDR AT WHICH CHECK ERR OCCURRED
FMTADR: BLOCK 1 ;FORMAT LOGICAL DISC ADDRESS
FMTHAD:	BLOCK 1	;FORMAT HARD DISC ADDRESS
NBADRR: BLOCK 1 ;NUMBER OF REREAD ERRORS
RRDFLG: 0       ;REREAD FLAG
NBADRD:	BLOCK 1	;NO OF READ ERRORS
NBADWT:	BLOCK 1 ;NO OF WRITE ERRORS
NBADFM:	BLOCK 1	;NO OF FORMAT ERRORS
	SUBTTL PROGRAM SETUP UNDER TENEX
;REPEAT 0.<
;ASSEMBLY INSTRUCTIONS
;@MACRO
;*DPKCPY_OFFMON,DPKCPY
;*^C
;@LINK10
;*SYS:EDDT
;*DSK:DPKCPY$
;@START
;@SAVE 20 (TO) 777777 (ON) DTA-:DPKCPY.SAV
;@
;THE START INSTRUCTION STARTS THE PROGRAM AT INIT WHICH SETS UP THINGS FOR 
;EDDT. THE PROGRAM WILL START IN DDT. AFTER SETTING READ AND WRITE,START
;PROGRAM AT START:
;>					;END OF REPEAT 0
;
;
AVANTE:	HALTF
	JRST .-1
	SUBTTL DSKDUP PROGRAM  INITIALIZATION PART

;
;START OF PROGRAM
;
BEGIN:	
	SETZM NBADRD
	SETZM NBADWT
	SETZM NBADFM
	SETZM NBADCH
	SETZM NBADRR
	SETZM LSTCHK
        SETZM RRDFLG
        MOVE 1,FORMAT
        CAIN 1,0        ;IS FORMATTING REQUIRED
        JRST STRT2         ;IF NO, END OF CHECK
        MOVE 1,FIRST
        CAIE 1,0           ;CHECK START.ADDRESS
        JRST NOFMT
        MOVE 1,LAST
        CAIE 1,NSECUN-1    ;CHECK FINISH ADDRESS
        JRST NOFMT
        MOVE 1,DSKINC
        CAIE 1,NSECTK      ;CHECK TRANSFER LENGTH
        JRST NOFMT
;
STRT2:  CONO TTY,410
	MOVE T1,[XWD -NUNIT,1]
CKPAK:	HRRZ 1,T1	;FORM UNIT NO. FOR DATAO
	ROT 1,-6
	TLO 1,(DSKNOP)	;DO NO-OP TO SELECT DRIVE
	DATAO DSK,1
	DATAI DSK,T2	;GET STATUS FOR THIS UNIT
	HRRZ 1,T1	;GET UNIT NO.
	CAMN 1,READ	;IS THIS THE INPUT UNIT?
	JRST [	TDNN T2,[ONLINE]	;YES, SKIP IF ON LINE
		JRST [HRRZI 2,[ASCIZ/
INPUT DRIVE MUST BE ON-LINE.
/]
			JRST INITER]
		TDNN T2,[RDONLY]	;SKIP IF READ ONLY
		JRST [HRRZI 2,[ASCIZ/
INPUT DRIVE MUST BE WRITE-PROTECTED IN CONTROLLER
/]
			JRST INITER]
		JRST CKPAK1	]
	CAMN 1,WRITE	;IS THIS THE OUTPUT UNIT?
	JRST [	TDNN T2,[ONLINE]	;YES, ON LINE AND WRITEABLE?
		JRST [HRRZI 2,[ASCIZ/
OUTPUT DRIVE MUST BE ON-LINE.
/]
			JRST INITER]
		TDNE T2,[RDONLY]
		JRST	[HRRZI 2,[ASCIZ/
OUTPUT DRIVE MUST BE WRITEABLE IN CONTROLLER AND ON DRIVE.
/]
			JRST INITER]	;NO, ERROR
		SKIPN FORMAT		;FORMATTING?
		JRST CKPAK1		;NO
		TDNE T2,[WRTLOK]	;YES WRITE HEADER ENABLED?
		JRST	[HRRZI 2,[ASCIZ/
CONTROLLER MUST BE WRITE HEADER ENABLED FOR FORMATTING.
/]
			JRST INITER]
		JRST CKPAK1]
	TDNE T2,[ONLINE]	;ALL OTHERS MUST BE OFF LINE
	JRST [HRRZI 2,[ASCIZ/
ALL DRIVES EXCEPT INPUT AND OUTPUT MUST BE OFF-LINE.
/]
		JRST INITER]
CKPAK1:	AOBJN T1,CKPAK	;CHECK NEXT UNIT
;
	MOVE T1,READ
	IMULI T1,NSECUN
	MOVEM T1,INADR	;LOG ADR 0 ON READ
	MOVE T1,WRITE
	IMULI T1,NSECUN
	MOVEM T1,OUTADR		;LOGICAL ADR 0 ON WRITE
;
	MOVE T1,DSKINC	;NO. OF SECTORS TO XFER
	IMULI T1,NWSEC
	MOVEM T1,WCNT	;NO. OF WORDS TO XFER
;
	MOVEI T1,DSKCM
	MOVEM T1,DSKCP	;SET UP CHANNEL COMMAND POINTER
;
	MOVE T1,FIRST	;STARTING LOGICAL ADR
	MOVEM T1,CURRNT
	ADDM T1,INADR	;STARTING ADR ON READ
	ADDM T1,OUTADR	;STARTING LOGICAL ADR ON WRITE
	PUSHJ P,CVDSA	;CONVERT INADR AND OUTADR
        MOVE 1,INABS#
        PUSHJ P,RECAL
        MOVE 1,OUTABS#
        PUSHJ P,RECAL
	SETOM FIRSFL#		;TO CHECK IF DISK DRIVE IS WRITING 

	MOVE 1,INABS	;DO READ SEEK
	CALL SEEK
	CALL FOUND	;AND WAIT FOR COMPLETION
	MOVE 1,OUTABS	;DO WRITE SEEK
			;AND GO ON TO OVERLAP FIRST READ
	SUBTTL DSKDUP PROGRAM   MAIN LOOP
;MAIN COPY LOOP,  READ N SECTORS FROM LOGICAL ADR, INADR
;WRITE THEM TO LOGICAL ADR, OUTADR, AND THEN READ
;THEM BACK FROM OUTADR INTO A SEPARATE BUFFER TO VERIFY
;THAT IT WAS WRITTEN OK. NO DATA COMPARE IS DONE, THE
;DISK HARDWARE CHECK IS ASSUMED SUFFICIENT
;	THE NUMBER OF SECTORS TRANFERRED IS IN DSKINC
;
;
RDNXT:	MOVE 1,INABS
	LDB T1,[POINT 8,1,13]
	TRNE 1,1B19		;ADD IN CYL 256 BIT (DAMN RP03'S)
	TRO T1,400		
	DATAO PI,T1		;DISPLAY TRACK NO. IN LIGHTS
	MOVE 2,[XWD NBADRD,[ASCIZ / WHILE READING /]]
	MOVEM 2,ERRWRD
	IOR 1,[DSKRD+DSKCP]
	SETZM	ERRFLG#	;RESET ERROR INDICATOR
	MOVEI 2,BUF-1	;BUFFER ADR IN IOWD FORMAT
	PUSHJ P,RWDISK	;READ NEXT GROUP OF SECTORS
	JRST RDERR	;HARD READ ERROR
FMTPAK:	CALL FOUND	;WAIT FOR WRITE SEEK TO COMPLETE
	 SKIPN FORMAT    ;TEST IF FORMATTING IS REQUIRED
        JRST REWRIT
	MOVE 1,[XWD NBADFM,[ASCIZ / WHILE FORMATTING /]]
	MOVEM 1,ERRWRD
        MOVE 1,OUTABS
        MOVEM 1,FMTHAD	;SAVE PRESENT SURFACE FORMATTING
FMTNXT: PUSHJ P,FMTINI	;SET UP FORMAT BUFFER
	MOVE 1,FMTHAD
        IOR 1,[DSKFMT+DSKCP] ;FORMAT COMMAND PREPARED
        MOVEI 2,FMTBUF-1
        PUSHJ P,RWDISK  ;FORMAT NEXT TRACK
        JRST FMTERR     ;IT FAILED
	MOVE T1,[HEDONE]
        ADDB T1,FMTHAD	;ADDS TO TRACK NUMBER
        LDB T1,[POINT 5,T1,18]	;GET HEAD NUMBER
        CAIGE T1,NSURF  ;CHECKS HEAD NUMBER
        JRST FMTNXT     ;FORMATS NEXT TRACK
REWRIT:	MOVE 1,OUTABS
	IOR 1,[DSKWRT+DSKCP]	;OR IN WRITE COMMAND
	MOVE 2,[XWD NBADWT,[ASCIZ / WHILE WRITING /]]
	MOVEM 2,ERRWRD
	MOVEI 2,BUF-1
	PUSHJ P,RWDISK	;WRITE TO WRITE
	JRST WTERR	;CAN'T WRITE IT
	MOVE 1,OUTABS	;SAVED PHYSICAL ADR FROM CVDSK
	MOVEM 1,COMADR#	;SAVE FOR LATER POSSIBLE COMPARE ERROR
	IOR 1,[DSKRD+DSKCP]
	MOVE 2,[XWD NBADCH,[ASCIZ / WHILE CHECKING /]]
	MOVEM 2,ERRWRD
	MOVEI 2,BUF1-1	;ALTERNATE BUFFER
	PUSHJ P,RWDISK	;READ TO SEE IF OK
	JRST CHKERR	;CAN'T READ IT, TRY REWRITING
GTNXT:	MOVE T1,DSKINC	;PREPARE TO COPY NEXT GROUP OF SECTORS
	ADDM T1,INADR
	ADDM T1,OUTADR	;INCREMENT LOGICAL ADDRESSES
	ADDB T1,CURRNT
	PUSHJ P,CVDSA		;GET HARD ADDRESSES

GTNXT0:	CAMLE T1,LAST	;BEYOND LAST CYLINDER?
	JRST GTNXT2	;YES,DON'T DO THIS SEEK
	SKIPN T3	;IS THE ERROR COUNTER ALREADY
	MOVEI T3,NRETRY	;NO: SET IT
	MOVE 1,INABS
	PUSHJ P,SEEK	;DO SEEK ON "READ" DISC

GTNXT2:	SKIPE ERRFLG	;ANY HARD ERROR OCCURED?
	JRST GTNXT1	;YES: SKIP COMPARISON
	SKIPE FIRSFL	;FIRST CYLINDER?
	JRST	[SETZM FIRSFL	;YES:RESET FLAG
		JRST GTNXT3]	;AND COMPARE TO CHECK IF WRITING
	SKIPN COMPAR	;IS BUFFER COMPARISON REQUIRED?
	JRST GTNXT1	;NO

GTNXT3:	PUSH P,1	;YES,SAVE REGISTER 1 ,MAYBE USEFUL IN CASE OF ERRORS
	MOVE 1,DSKINC
	IMULI 1,-NWSEC	;NUMBER IN BUFFERS
	HRLZ 1,1
	ADDI 1,BUF
	MOVEI 2,BUF1
COMP1:	CONSZ APR,1B19	;ANY PARITY ERRORS (KI10 MUST LOOK AT APR)
	PUSHJ P,PARERR	;YES:REPORT IT
	MOVE T2,0(1)	;COMPARISON LOOP
	CAME T2,0(2)
	JRST	[CONSZ APR,1B19	;FAILED!!! SEE IF PARITY ERROR
		PUSHJ P,PARERR	;YES:REPORT IT
		JRST COMERR]	;AND REPORT COMPARISON ERROR TOO
	AOS 2
	AOBJN	1,COMP1	;LOOP BACK
	POP P,1

GTNXT1:	CAMLE T1,LAST	;BEYOND LAST DISC ADDRESS?
	JRST REREAD	;YES, READ SEEK NOT DONE, NO NEED FOR WRITE SEEK

	CALL FOUND	;NO, WAIT FOR END OF SEEK 
	MOVE 1,OUTABS	;DO A SEEK ON "WRITE DRIVE"
	PUSHJ P,SEEK
	JRST RDNXT	;AND GO READ NEXT SECTORS ON "READ DRIVE"
;***REREAD REREADS THE WHOLE DISK ***
REREAD: SKIPN FORMAT     ;ONLY IF FORMATTING REQUIRED
        JRST DONE
        SETOM RRDFLG     ;FLAG TO INDICATE REREAD 
        MOVE T1,WRITE
        IMULI T1,NSECUN
        MOVEM T1,OUTADR
        MOVE T1,FIRST
        MOVEM T1,CURRNT
        ADDM T1,OUTADR
        MOVE 1,OUTADR
	PUSHJ P,CVDSK	;GET HARD ADDRESS
	MOVEM 1,OUTABS
        PUSHJ P,RECAL    ;RECALIBRATE IS PERFORMED
;
RRDNXT: MOVE 1,OUTABS
        LDB T1,[POINT 8,1,13]
	TRNE 1,1B19		;DAMN RP03'S
	TRO T1,400
        DATAO PI,T1
        IOR 1,[DSKRD+DSKCP]
	MOVE 2,[XWD NBADRR,[ASCIZ / WHILE REREADING /]]
	MOVEM 2,ERRWRD
        MOVEI 2,BUF1-1
        PUSHJ P,RWDISK
        JRST RRDERR
;
RRDNX1: MOVE T1,DSKINC
        ADDM T1,OUTADR
        ADDB T1,CURRNT
	PUSHJ P,CVDSA

RDNX2:	
	CAMLE T1,LAST	;FINISHED?
	JRST RRDNX3	;YES
	MOVEM 1,OUTABS	;NO
	CALL SEEK	;GO TO NEXT CYLINDER
	CALL FOUND	;WAIT TO GET THERE
	MOVE 1,OUTABS
	JRST RRDNXT	;AND READ IT

RRDNX3:
        SETZM RRDFLG  ;RESET REREAD FLAG
DONE:	SETZ T1,
	EXCH T1,LPTOUT	;GET MESSAGE TO GO TO TTY NO MATTER WHAT
	HRRZI 2,[ASCIZ /
DONE.
/]
	PUSHJ P,CTYSTR
	MOVE 1,[DSKNOP+<NUNIT-1>B5]
	DATAO DSK,1		;RESET DSK TO DESELECT OUTPUT DISK PACK
	EXCH T1,LPTOUT
	JRST DDT	;RETURN CONTROL TO DDT
	SUBTTL DSKDUP PROGRAM   ERROR IN INITIALIZATION PART
;INITIALIZATION ERROR:FORMATTING NOT POSSIBLE
;
NOFMT: HRRZI 2,[ASCIZ /
FORMATTING NOT POSSIBLE WITH PARTIAL COPY ONLY.
RESET FORMAT AND TRY AGAIN.GOOD LUCK.
/]
        PUSHJ P,CTYSTR
        JRST  DDT
;INITIALIZATION ERROR, PACKS NOT READY, OR INPUT PACK NOT READ ONLY
;
INITER:	MOVE 1,[DSKNOP+<NUNIT-1>B5] ;SELECT HIGHEST DRIVE (SHOULD BE OFFLINE)
	DATAO DSK,1	;TO DESELECT DRIVES SO BUTTONS WORK
	PUSHJ P,CTYSTR	;TYPE MESSAGE OUT
	JRST DDT	;AND GO BACK TO DDT
;
	SUBTTL DSKDUP PROGRAM   HARD ERROR ROUTINES
;HARD READ ERROR.  CONTINUE TO READ NEXT GROUP OF SECTORS
;SHOULD EVENTUALLY TRY TO READ THIS GROUP IN SMALLER UNITS
;
RDERR:	SETOM ERRFLG	;SET ERROR INDICATOR
	HRRZI 2,[ASCIZ /

*****HARD READ ERROR/]
	PUSHJ P,CTYSTR
	MOVE T1,DSKINC
	CAIE T1,1	;IF ^D1 THEN BAD SECTOR
	JRST RDERR1
	HRRZI 2,[ASCIZ / AT SECTOR NUMBER /]
	PUSHJ P,CTYSTR
	MOVE 2,CURRNT
	PUSHJ P,NOUT	;BAD SECTOR NUMBER
	PUSHJ P,CRLF
	SKIPE HLTF	;HALTING ON ERRORS?
	JSR ERRHLT	;YES
	JRST REWRIT	;WRITE BEST EFFORT AND CONTINUE
;MORE THAN 1 SECTOR IN BAD XFER, SHOULD TRY IN SMALLER INCREMENTS
RDERR1:	PUSHJ P,RCOPYM
	PUSHJ P,CRLF
	SKIPE HLTF
	JSR ERRHLT
        SKIPN FORMAT
        JRST GTNXT      ;IF NO FORMATTING,DON'T WRITE BAD BUFFER
        JRST FMTPAK     ;ELSE KEEP ON FORMATTING
;
;TREAT WRITE ERROR LIKE READ ERROR,  DOESN'T REALLY HAPPEN IF
;PACK IS FORMATTED OK.
;
WTERR:	SETOM ERRFLG	;SET ERROR INDICATOR
	HRRZI 2,[ASCIZ /

*****HARD WRITE ERROR/]
	PUSHJ P,CTYSTR
	PUSHJ P,RCOPYM
	HRRZI 2,BADPAK
	PUSHJ P,CTYSTR
        MOVE 1,FORMAT
        CAIE 1,0
        JRST FMTER1
	SKIPE HLTF
	JSR ERRHLT
	JRST GTNXT
;CHECK ERROR MEANS DATA WASN'T CORRECTLY WRITTEN,
;RETRY NRETRY TIMES BEFORE REPORTING HARD ERROR
;
CHKERR:	CAMN 1,LSTCHK	;ERROR ON THIS ADR BEFORE?
	JRST CKERR1	;YES,  SEE IF GOING TO TRY AGAIN
;FIRST CHECK ERROR FOR THIS ADDRESS
	MOVEM 1,LSTCHK
	MOVEI T1,NRETRY
	MOVEM T1,CNTCHK
CKERR1:	SOSGE CNTCHK	;RETRY?
	JRST CKERR2	;NO, HARD ERROR
	HRRZI 2,[ASCIZ /REWRITING/]
	PUSHJ P,CTYSTR	;ON SAME LINE AS LAST REPORT
	MOVE 1,OUTABS
	PUSHJ P,RECAL
	MOVE 1,OUTABS
	CALL SEEK
	JRST FMTPAK
CKERR2:	SETOM ERRFLG
	HRRZI 2,[ASCIZ /

****HARD CHECK ERROR/]
	PUSHJ P,CTYSTR
	PUSHJ P,RCOPYM
	HRRZI 2,BADPAK
	PUSHJ P,CTYSTR
	SKIPE HLTF
	JSR ERRHLT
	JRST GTNXT
;
;FORMATTING ERROR IS TREATED LIKE READ ERROR
;HOWEVER FOR ALL PRACTICAL PURPOSES DUPLICATING FAILED
;CAN AS WELL STOP.
;
FMTERR:	SETOM ERRFLG
	HRRZI 2,[ASCIZ /
*****HARD FORMATTING ERROR
FROM/]
        PUSHJ P,CTYSTR
        PUSHJ P,RCPYM1
FMTER1: HRRZI 2,[ASCIZ /
TRY AGAIN WITH OFF-LINE FORMATTING.GOOD LUCK.
/]
        PUSHJ P,CTYSTR
        SKIPE HLTF
        JSR   ERRHLT
        JRST GTNXT
;
RRDERR: HRRZI 2,[ASCIZ/
*****HARD REREAD ERROR
YOU SHOULD PROBABLY REFORMAT THE WHOLE DISK.GOOD LUCK/]
        PUSHJ P,CTYSTR
        PUSHJ P,RCOPYM
        HRRZI 2,BADPAK
        PUSHJ P,CTYSTR
        SKIPE HLTF
        JSR ERRHLT
        JRST RRDNX1
	SUBTTL	DSKDUP PROGRAM   COMPARE ERROR(SOFT AND HARD)
COMERR:	MOVEM T2,DATA1#	;SAVE ORIGINAL DATA
	MOVE T2,0(2)	;AND WHAT WE GOT
	MOVEM T2,DATA2#
	HRRZI 2,[ASCIZ/
COMPARE ERROR /]
	PUSHJ P,CTYSTR
	POP P,2		;GET IOWD BACK
	MOVE 2,COMADR	;GET LAST WRITE ADDRESS
	PUSH P,T3	;TYPDAD CLOBBERS T3
	PUSHJ P,TYPDAD	;TYPE LAST DISC ADDRESS
	HRRZI 2,[ASCIZ/
DATA SHOULD BE:/]
	PUSHJ P,CTYSTR
	MOVE 2,DATA1
	PUSHJ P,NOUT12
	HRRZI 2,[ASCIZ/ WAS:/]
	PUSHJ P,CTYSTR
	MOVE 2,DATA2
	PUSHJ P,NOUT12
	HRRZI 2,[ASCIZ/ XOR:/]
	PUSHJ P,CTYSTR
	MOVE 2,DATA2
	XOR 2,DATA1
	PUSHJ P,NOUT12
	MOVN T1,DSKINC	;GET BACK TO LAST DISC ADDRESS
	ADDM T1,INADR
	ADDM T1,OUTADR
	ADDB T1,CURRNT
	PUSHJ P,CVDSA	;GET HARD ADDRESSES
	POP P,T3
	AOS ERRCNT	;INCREASE ERROR COUNT
	SETOM ERRFLG	;SKIP COMPARE CYCLE AFTER INCREMENTING ADDRESSES
	SOJLE T3,COMER4	;HARD ERROR?
	CALL FOUND	;DISC FREE?
	JRST GTNXT0	

COMER4:	SKIPE FIRSFL	;IS THIS THE FIRST CYLINDER?
	JRST	[HRRZI 2,[ASCIZ/
CHECK IF OUTPUT DRIVE IS WRITEABLE ON DRIVE ITSELF.
/]
		PUSHJ P,CTYSTR
		JRST .+1]

COMER1:	HRRZI 2,[ASCIZ/
*****HARD ERROR /]
	PUSHJ P,CTYSTR
	MOVE T1,DSKINC	;ONE SECTOR AT A TIME?
	CAIE T1,1
	JRST COMER2	;NO
	HRRZI 2,[ASCIZ / AT SECTOR NUMBER /]
	PUSHJ P,CTYSTR
	MOVE 2,CURRNT
	PUSHJ P,NOUT
COMER3:	PUSHJ P,CRLF
	SKIPE HLTF
	JSR ERRHLT
	SETZ T3,
	CALL FOUND
	JRST GTNXT

COMER2:	PUSHJ P,RCOPYM
	JRST COMER3

;TELL THE USER THE RANGE OF DISK ADDRESSES
;THAT HE SHOULD TRY TO RECOPY.  THESE NUMBERS
;SHOULD GO INTO FIRST AND LAST.  DSKINC SHOULD BE REDUCED
;
RCOPYM:	HRRZI 2,[ASCIZ /, TRY RECOPYING FROM /]
	PUSHJ P,CTYSTR
RCPYM1: MOVE 2,CURRNT
	PUSHJ P,NOUT
	HRRZI 2,[ASCIZ / THROUGH /]
	PUSHJ P,CTYSTR
        MOVE 2,CURRNT
	ADD 2,DSKINC
	SUBI 2,1
	PUSHJ P,NOUT
	POPJ P,
CVDSA:	MOVE 1,INADR	;TRANSFORM SOFT ADDRESSES
	PUSHJ P,CVDSK	;INTO HARD ADDRESSES
	MOVEM 1,INABS
	MOVE 1,OUTADR
	PUSHJ P,CVDSK
	MOVEM 1,OUTABS
	POPJ P,
	SUBTTL	ROUTINE TO GENERATE FORMAT BUFFER FOR PACK FORMATTING

FMTINI:	SETZM FMTBUF			;NULL OUT FORMAT HEADER BUFFER
	MOVE 1,[XWD FMTBUF,FMTBUF+1]
	BLT 1,FMTBUF+NSEC*HWDSEC-1
	MOVE T1,FMTHAD			;SET UP FORMAT HEADER ADDRESSING
	AND T1,[XWD 007777,600000]	;FIELD
	TRZE T1,1B19
	TLO T1,(1B5)
	LSH T1,-<^D30-^D18>
	MOVSI 2,-NSEC
	MOVEI 1,FMTBUF+^D31		;SET UP ADDRESSING FIELD POINTER
FMTIN1:	MOVEM  T1,(1)			;MOVE IN ADDRESSING WORD
	AOS T1				;INCREMENT SECTOR FIELD
	ADDI 1,HWDSEC			;INCREMENT ADDRESSING FIELD PTR
	AOBJN 2,FMTIN1			;ON TO NEXT SECTOR
	POPJ P,
	END INIT

