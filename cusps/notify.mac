;<SOURCES>NOTIFY.MAC;2    EDIT BY COWER     15-JULY-75 08:00:00   
;HOPEFULLY FOR THE LAST TIME - TOOK OUT REQUIREMENT WHICH NEEDED
;PARAMS FOR CALCULATION OF ARPA,TYMNET LINES - NOW MAKES  LINE 
;NUMBER CALCULATION AT RUN TIME
;EDIT BY COWER     1-APR-75 13:00:00 ADDED 1. OUTPUT TO ARPA LINES
;ONLY (A), TYMNET LINES ONLY (T), BOTH (T,A) 2. NO OUTPUT TO NON-
;LOGGED IN JOBS (PSEUDO TTYS) 
   
;** WARNING USES SUMEX JSYS GETBLT TO READ SYSTEM TABLE **
;** WARNING USES TABLES WHICH MAY NOT BE AVAILABLE AT OTHER SITES **

	SEARCH	STENEX

        TITLE NOTIFY


MAXCH==1000		;MAX NUMBER OF CHARS IN MESSAGE
MAXTTY==200		;MAX NUMBER OF TTYS



F=0			;FLAGS AC
MOREF=400000		;MORE TO DO FLAG
p=17
npdl==20
	INTERN BEG

 	MLON

BEG:	
	SETZ F,		;CLEAR ALL FLAGS
	MOVEI 1,101
	RFCOC			;GET CONTROL CHAR BITS
	TLZ 2,(3B3+3B9)		;TURN OFF ^A AND ^D
	TLZ 3,(3B13)		; AND ^X
	SFCOC
	MOVEI 1,400000
	RPCAP
	MOVE 3,2
	EPCAP		       		;ENABLE ALL POSSIBLE CAPS
; ** SUMEX TABLE - ONE WRD LONG - REG 1 CONTAINS LH - HIGHEST TYMNET **
; ** LINE NUMBER,,RH - LOWEST TYMNET LINE NUMBER		     **
	MOVE	1,[SIXBIT /TYMLIN/]	;GET IT            
	SYSGT
	HRRZ	2,1
	MOVEM	2,TYMTTL
	HLRZ	1,1
	MOVEM	1,JUNK
	SUB	1,2
	ADDI	1,1
	MOVEM	1,NTYMTT
	MOVEM	2,JUNK1 
;** SAME AS ABOVE COMMENT - ARPA LINES NUMBERS IN REG 1	**
	MOVE	1,[SIXBIT /NVTLIN/]
	SYSGT
	HRRZ	2,1
	MOVEM	2,NVTLO
	HLRZ	1,1
	MOVEM	1,JUNK2
	SUB	1,2
	ADDI	1,1
	MOVEM	1,NNVTLN
	MOVEM	2,JUNK3
	MOVE 1,[SIXBIT /TTYJOB/]
	SYSGT			;GET NUMBER OF TTYS ON SYSTEM
	HLROM 2,NLNS		;NEGATIVE NUMBER OF LINES
	HRRO	1,2
	HRRZI	2,TTYS
;** SUMEX JSYS TO READ ENTIRE TABLE FOLLOWS BE FORWARNED **  
	GTBLT
	JFCL
	MOVEI 3,1
	MOVM 4,NLNS
	SOS	4
INIT:	SKIPGE	TTYS(4)		;SEE IF A JOB IS THERE
	JRST	 [SETZM	TTYS(4)
		 JRST	INIT1]
	MOVEM 3,TTYS(4)
INIT1:	SOSL 	4 
	JRST	INIT
	MOVEI 1,101
	HRROI 2,[ASCIZ /
TTYS: /]
	SETZ 3,
	SOUT
GMES4:	MOVEI 1,100
	MOVEI 3,^D8
	NIN
	JRST GMES3
	MOVM 3,NLNS		;GET POSITIVE NUMBER OF LINES
	CAIGE	2,0		;NEGATIVE NUMBER SEND TO ALL
	JRST	ALL
	CAMLE	2,3
	JRST	[ HRROI 1,[ASCIZ /SORRY BUB, BUT THAT AIN'T THE WAY IT'S DONE

TTYS:/]
		 ESOUT
		 JRST	GMES4  ]
	SKIPN	TTYS(2)
	JRST	GMES3
	MOVE 3,BUFPTR		;SETUP INITIAL STRING PTR
	MOVEM 3,TTYS(2)
GMES3:	BKJFN
	JFCL
GMES5:	PBIN
	CAIN 1,","
	JRST GMES4
	CAIN	1,124			;TEST FOR TYMNET TTYS
	JRST	TYMNET			;CHANGE THIS
	CAIN	1,164			;MAY BE LOWER CASE
	JRST	TYMNET			;GO DO SOMETHING
	CAIN	1,101			;ARPA TTY'S
	JRST	ARPA			  
	CAIN	1,141			;LOWER CASE?
 	JRST	ARPA			;GO DO SOMETHING
	CAIE 1,37
	JRST	FILLIT

SMESS:	MOVEI 1,101
	HRROI 2,[ASCIZ /
MESSAGE: /]
	SETZ 3,
	SOUT

GMES:	MOVEI 15,MAXCH
	MOVE 16,BUFPTR
GCH:	PBIN
	CAIN 1,"A"-100
	JRST DELCH
	CAIN 1,"X"-100
	JRST DELALL
	CAIN 1,"D"-100
	JRST GMES1
	CAIN 1,37
	JRST GEOL
GMES2:	IDPB 1,16
	SOJG 15,GCH
GMES1:	SETZ 1,
	IDPB 1,16
	MOVEM 16,ENDMES		;SAVE BYTE POINTER OF END OF STRING
	SKIPA 15,[^D600]	;FIRST TIME, WAIT 60 SEC BEFORE 
MESLP3:	MOVEI 15,^D100		;PRINTING UNFINISHED TTYS
MESLP2:	HRLZ 16,NLNS		;SET TO SCAN EXISTING LINES
MESLP:	SKIPN 6,TTYS(16)	;SOMETHING TO DO ON THIS LINE?
	JRST MESLP1		;NO
	CAIN	6,1
	JRST	MESLP1
;CODE TO PREVENT OUTPUT TO BINARY LINES, TO STOP GARBAGING BINARY XFERS
;!!  MAH @ SUMEX 12/74 !!
	HRRZ 1,16		;GET TTY #
	TRO 1,400000		;MAKE INTO TTY DESIGNATOR
	RFMOD			;GET MODES
;!! rrc removed on 8/14/75 the two lines below
;	TRNN 2,3B29		;BINARY?
;	 JRST MESLP1		;YES SKIP
;;;
	TRO F,MOREF		;YES, REMEMBER ANOTHER CHECK NEEDED
     	MOVEI 1,400000(16)	;CONSTRUCT SPECIFIC TTY DESIGNATOR
	SOBE			;OUTPUT BUFFER EMPTY?
	JRST [	JUMPN 15,MESLP1	;NO, TIME TO PRINT WAITING TTYS?
		MOVEI 1,101	;YES
		MOVEI 2,0(16)	;ITS NUMBER
		MOVE 3,[1B2+4B17+10] ;OCTAL IN FOUR BIT FIELD
		NOUT
		JFCL
		JRST MESLP1]
  	MOVE 2,TTYS(16)		;GET CURRENT STRING POINTER
 	MOVEI 3,^D200		;TYPE 200 CHARS
 	SETZ 4,			;OR STOP ON NULL
        SOUT
	MOVEM 2,TTYS(16)	;SAVE UPDATED STRING POINTER
	CAME 2,ENDMES		;REACHED END OF STRING?
	JRST MESLP1		;NO
	SETZM TTYS(16)		;YES, RESET LINE
	CAILE 15,^D100		;FIRST TIME?
	MOVEI 15,^D10		;YES, SET TO LIST WAITS
MESLP1:	AOBJN 16,MESLP
	TRZN F,MOREF		;ANYTHING LEFT TO DO?
	HALTF			;NO
	MOVEI 1,^D100		;YES, WAIT 1/10 SEC.
	DISMS
	SOJG 15,MESLP2		;TRY AGAIN, UNLESS TIME TO LIST WAITS
	JUMPL 15,MESLP3		;RESET WAIT COUNT
	HRROI 1,[ASCIZ /
WAITS: /]
	PSOUT
	JRST MESLP2

BUFPTR:	XWD 440700,MESBUF	;INITIAL BUFFER POINTER

ERR1:	SETZM TTYS(2)		;CLEAR THE ERRONEOUS NUMBER
ERR:	MOVEI 1,101
	HRROI 2,[ASCIZ /?  /]
	SETZ 3,
	SOUT
	JRST GMES4

ALL:	MOVE  3,BUFPTR
	MOVM 4,NLNS
	SOS	4 
ALL1:	SKIPE	TTYS(4)		;SEE IF A JOB IS THERE
	MOVEM 3,TTYS(4)
	SOSL 	4 
	JRST	ALL1
	JRST	GMES3

FILLIT:	CAIE	1,"-"
	JRST	ERR1
	MOVEM	2,SFILL
	MOVEI	1,100
	MOVEI	3,^D8
	NIN
	JRST	ERR1
	MOVM	4,NLNS
	CAMLE	2,4
	MOVE	2,4
	MOVEM	2,EFILL
	MOVE	5,SFILL
	AOJ	5,
	MOVE	3,BUFPTR
FLOOP:	SKIPE	TTYS(5)
	MOVEM	3,TTYS(5)
	AOJ	5,
	CAMG	5,EFILL
	JRST	FLOOP
	JRST	GMES3
GEOL:	CAIGE 15,2
	JRST GMES1
	MOVEI 1,15
	IDPB 1,16
	SUBI 15,1
	MOVEI 1,12
	JRST GMES2

DELCH:	CAIL 15,MAXCH
	JRST DELALL
	MOVEM	1,GARB#
	MOVEI	1,"/"
	PBOUT
	MOVE	1,GARB
	ADDI 15,1
	LDB 1,16
	PBOUT
	SUBI 16,1
	IBP 16
	IBP 16
	IBP 16
	IBP 16
	JRST GCH
TYMNET:	MOVE	3,BUFPTR
	MOVE 	4,NTYMTT  
	SUBI	4,1
	ADD	4,TYMTTL
TYMNT1:	SKIPE	TTYS(4)
	MOVEM	3,TTYS(4)
	SOS 	4 
	CAML	4,TYMTTL
	JRST	TYMNT1
	JRST	GMES5
ARPA:	MOVE	3,BUFPTR
	MOVE 	4,NNVTLN  
	SUBI	4,1
	ADD	4,NVTLO 
ARPA1:	SKIPE	TTYS(4)
	MOVEM	3,TTYS(4)
	SOS 	4 
	CAML	4,NVTLO
	JRST	ARPA1
	JRST	GMES5

DELALL:	MOVEI 1,37
	PBOUT
	JRST GMES

	LIT

NLNS:	0
SFILL:	0
EFILL:	0
TABNO:	BLOCK	1
MESBUF:	BLOCK MAXCH/5+1
ENDMES:	0			;END OF STRING
JUNK:	BLOCK	1		;JUST WHAT THEY ARE (JUNK)
JUNK1:	BLOCK	1
JUNK2:	BLOCK	1
JUNK3:	BLOCK	1
TYMTTL:	BLOCK	1		;LOWEST TYMNET LINE NUMBER
NVTLO:	BLOCK	1		;LOWEST ARPA LINE NUMBER
NTYMTT:	BLOCK	1		;NUMBER OF TYMNET PORTS
NNVTLN:	BLOCK	1		;NUMBER OF NET PORTS
TTYS:	BLOCK MAXTTY

        END    BEG



