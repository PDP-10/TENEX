	SEARCH	STENEX
;
STKSIZ==20
P==17
	TITLE	ACCESS	
;
ACCESS:	MOVE	P,[IOWD STKSIZ,STACK]
	HRROI	1,[ASCIZ \
The following programs are accessable to guests:
\]
	PSOUT
	HRLZI	1,(1B2!1B11!1B17)	;OLD FILE, ACCEPT *'S, SHORT CALL
	HRROI	2,[ASCIZ \<SUBSYS>*.*\]
	GTJFN
	 0
	MOVEM	1,JFN		;SAVE IT WITH THE FLAGS
;
FDBS:	HRRZ	1,JFN
	MOVE	2,[XWD 1,24]	;GET THE USW
	MOVEI	3,3		;PUT IT IN 3
	GTFDB

	TLNE	3,(1B0)		;IS BIT 0 ON
	PUSHJ	P,PRINT		;YES

	MOVE	1,JFN
	GNJFN			;GET THE NEXT FILE IN THE GROUP
	TRNA			;NO MORE FILES
	JRST	FDBS		;LOOP

	RESET
	HALTF
	JRST	.-1

PRINT:	MOVEI	1,101		;ME
	HRRZ	2,JFN
	MOVE	3,[1B8!1B11!1B14!1B35]
	JFNS
	MOVEI	1,37		;MAKE SURE IT'S READABLE
	PBOUT
	POPJ	P,
	
	LIT
	VAR
JFN:	0			;JFN + FLAGS
STACK:	BLOCK	STKSIZ
;
	END	ACCESS
