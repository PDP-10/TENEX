;<SOURCES>ALIAS.MAC;2     9-MAY-75 18:41:33    EDIT BY HEDBERG
;Alias now moves itself around, blt's acs before jumping
;<SOURCES>ALIAS.MAC;1    29-APR-75 13:50:57    EDIT BY HEDBERG
;Sets entry vector before starting
;
	SEARCH STENEX
;
	LOC	140			;LET'S BE CONSISTANT, HUH.
;
P==17
STKSIZ==100
;
DEFINE	TYPE	(TEXT)	<
	HRROI	1,[ASCIZ \TEXT\]
	PSOUT	>
;
ALIAS:	MOVE	P,[IOWD STKSIZ,STACK]
	SETZM	SUBFL		;NO SUBCOMMANDS YET
	SETZM	CHACCT		;DON'T CHANGE ACCOUNT
	SETZM	INDX		;INDEX OF 0 INTO ENTRY VECTOR
;	
	HRLZI	1,(1B0)		;FOR OUTPUT USE
	MOVEM	1,JFNTAB	;SET IT IN THE TABLE
	MOVEI	1,JFNTAB
	SETZ	2,		;LONG CALL JFN
	GTJFN	
	  JRST	GOOF
	MOVEM	1,AJFN		;ALIAS JFN
;
	MOVEI	1,100
	BKJFN			;GET THE TERMINATING CHARACTER
	 JFCL
	PBIN
	MOVE	2,1
	MOVEI	1,40
	CAIE	2,40		;IS IT A SPACE
	PBOUT			;NO, OUTPUT ONE
				;YES, GO ON
;
	TYPE	<(to) >
	HRLZI	1,(1B2)		;OLD FILE
	MOVEM	1,JFNTAB
	MOVEI	1,JFNTAB
	SETZ	2,
	GTJFN	
	  JRST	GOOF
	MOVEM	1,OJFN		;ORIGINAL JFN
;
	MOVEI	1,100
	BKJFN
	 JFCL
ENDLIN:	PBIN
	CAIN	1,37		;IF TERMINATED WITH EOL GO ON
	JRST	SUB
	CAIN	1,","		;IF WITH "," DO SUBCOMMANDS
	SETOM	SUBFL
	JRST	ENDLIN
;
SUB:	SKIPN	SUBFL		;WAS A "," ENTERED?
	JRST	REAL		;NO, GO ON
SUBCMD:	TYPE	<@@>		;OFFICIAL PROMPT
	PBIN
	CAIN	1,"?"		;IS HELP REQUESTED
	JRST	INFO
	TRZ	1,40		;MAKE UPPER CASE
	CAIN	1,"A"		;SET ACCOUT TO RUN ON?
	JRST	.ACCT
	CAIN	1,"E"		;SET ENTRY INDEX
	JRST	.ENTRY
	CAIN	1,37		;DONE?
	JRST	REAL
	PBIN
	CAIE	1,37
	JRST	.-2
SUBMES:	TYPE	< ?
>
	JRST	SUBCMD
;
INFO:	TYPE	<
The available commands are:
account (to change to at run time)
entry (offset)
>
	JRST	SUBCMD
;
.ACCT:	MOVE	2,[POINT 7,[ASCIZ \account \]]
	PUSHJ	P,WFESC		;WAIT FOR ESC OR SPACE
	HRROI	1,ACOUNT
	MOVEI	2,^D20
	SETZ	3,		;READ THE ACCOUNT
	PSTIN
	DPB	3,1
	SETOM	CHACCT		;SET THE FLAG TO CACCT
	JRST	SUBCMD
;	
.ENTRY:	MOVE	2,[POINT 7,[ASCIZ \entry \]]
	PUSHJ	P,WFESC
	MOVEI	1,101
	MOVEI	3,12
	NIN			;INPUT THE INDEX
	 JRST	SUBMES
	MOVEM	2,INDX		;SAVE IT
	JRST	SUBCMD
;
WFESC:	IBP	2,2	
	PBIN			;WAITS FOR EOL OR ESC
	CAIN	1,37
	JRST	SUBMES		;IT'S AN EOL, RETURN
	CAIN	1,40
	POPJ	P,
	CAIE	1,33		;IT'S AN ESC, FILL IN COMMAND
	JRST	WFESC
	MOVE	1,2
	PSOUT
	POPJ	P,
;
REAL:	MOVEI	1,37		;OUT AN EOL
	PBOUT
;
	MOVEI	1,101		;PRIMARY OUTPUT
	HRRZ	2,AJFN		;ALIAS JFN
	MOVE	3,[2B2!1B5!1B8!1B11!1B14!1B35]	;JFNS FLAGS
	JFNS			;DO IT NOW CAUSE SAVE RELEASES THE JFN
;
	HRROI	1,FILNAM	;PUT THE NAME OF OJFN IN FILNAM
	HRRZ	2,OJFN
	MOVE	3,[2B2!1B5!1B8!1B11!1B35]	;<DIR>NAM.EXT;VER
	JFNS			;PUT THE COMPLETE ORIGINAL NAME IN FILNAM
	MOVEI	1,400000
	MOVEI	2,FAKE
	HRLI	2,1
	SEVEC			;SET PROPER ENTRY VECTOR
	MOVE	1,AJFN
	HRLI	1,400000
	MOVE	2,SVTBL
	SAVE			;AND SAVE IT UNDER THE ALIAS' NAME
;
	TYPE	< is now an alias of >
;
	MOVEI	1,101
	HRRZ	2,OJFN		;SAME FOR THE ORIGINAL FILE
	MOVE	3,[2B2!1B5!1B8!1B11!1B35]	;<DIR>NAM.EXT;VER
	JFNS
;
	TYPE	<,
with an entry vector index of >
;
	MOVEI	1,101
	MOVE	2,INDX
	MOVEI	3,10
	NOUT
	 JFCL
	MOVEI	1,37
	PBOUT
;
REALLY:	RESET
	HALTF
	JRST	.-1
;
GOOF:	TYPE	< ?
>
	JRST	REALLY
;
OJFN:	0
AJFN:	0
SVTBL:	SAVLEN,,FAKE		;'TABLE' TO TELL SAVE HOW MUCH TO SAVE
NUMSTR:	BLOCK	2
NAME1:	BLOCK	16
SUBFL:	0
STACK:	BLOCK	STKSIZ
JFNTAB:	0			;SET BY THE CALLER
	100,,101
	0
	0
	0
	[ASCII /SAV/]			    
	0				    
	0				    
	0			      

	LIT			;KEEP LOW-PART LITERALS LOW
;
		;BEGINNING OF CODE SAVED UNDER ALIAS
HFAKE==771

	LOC	<<./1000>+1>*1000
LFAKE==./1000
	PHASE	HFAKE*1000
FAKE:	JFCL
	SKIPN	CHACCT		;CHANGE ACCOUNT?
	JRST	FAKEIT		;NO, GO ON
	HRROI	1,ACOUNT	;GET THE ACCOUNT
	CACCT
	 JFCL
FAKEIT:	HRLZI	1,(1B2!1B17)	;OLD FILE, SHORT CALL
	HRROI	2,FILNAM
	GTJFN			;GET A HANDLE ON THE ORIGINAL FILE
	 HALTF
	HRRM	1,ITSJFN	;SAVE THE JFN
	MOVE	1,INDX		;GET OUR ENTRY INDEX
	HRRM	1,WTPINX	;SAVE IT IN THE INSTRUCTION TOO.
	HRLI	1,FROM
	HRRI	1,17-LREG+1
	BLT	1,17		;move some code to the registers

	HRLI	1,400000
	HRR	1,ITSJFN
	SETZ	0,		;so blt clears
	JRST	17-LREG+1	;go to the regs.

;	RELOC CODE DOWN ON REGS.
;	Do not mess with this code unless you KNOW what you're doing.
;				  AC
FROM:	GET			; 4- get the original in core
	HRRZI	1,400000	; 5- get this fork's
	GEVEC			; 6- entry vector
	TLNE	2,777000	; 7- is it a DEC entry vector?
	HRRZ	2,120		;10- yes, get address from jobsa(120)
WTPINX:	ADDI	2,0		;11- no, 2 has the entry vector, add index
				;    (set up by FAKE)
	HRLI	2,1		;12- entry vector length of one
	SEVEC			;13- set entry vector for start, etc.
	HRR	17,2		;14- save the destination
	HRRZI	15,1		;15- make a blt pointer
	BLT	15,16		;16- zero the regs.
	JRST	0		;17- go to it (set by 14)
LREG==<.-FROM>       
;

FILNAM: BLOCK 8
INDX:	0
ITSJFN: 0
ACOUNT:	BLOCK	5
CHACCT:	0
	LIT
SAVLEN=.-FAKE
;
HMOVER==<./1000>+1
	DEPHASE
LMOVER==<<./1000>+1>
	LOC	LMOVER*1000
;
MOVER:
	PHASE	HMOVER*1000

	HRLI	1,400000
	MOVE	2,1
	HRRI	1,LFAKE
	HRRI	2,HFAKE
	HRLZI	3,(1B2!1B4!1B9)		;READ, EXECUTE, COPY ON WRITE
	PMAP				;FAKE TO 710
	HRRI	1,LMOVER
	HRRI	2,HMOVER
	PMAP				;MOVER TO 711
	MOVES	HMOVER*1000		;MAKE HI MOVER PRIVATE
	MOVES	HFAKE*1000		;MAKE HI FAKE PRIVATE
	JRST	. +1			;GO TO HI MOVER
	SETO	1,
	HRRI	2,LFAKE
	PMAP				;LFAKE GOES BYEBYE
	HRRI	2,LMOVER
	PMAP				;AS DOES LMOVER
	MOVEI	1,400000
	HRLI	2,1
	HRRI	2,ALIAS
	SEVEC
	MOVSI	1,(1B0+1B17)		;SHORT, NEXT VERSION
	HRROI	2,ALINAM
	GTJFN
	HALTF
	HRLI	2,<HMOVER*1000>-ALIAS-1
	HRRI	2,ALIAS			;EVERTHING BUT US CHICKENS
	SAVE
	HRROI	1,ALIMES
	PSOUT
	HALTF
IF2, <
PRINTX	**********************************
PRINTX	RUN ONCE TO MAKE ALIAS SAVE ITSELF
PRINTX	**********************************
>
ALIMES:	ASCIZ \Ok, I'm saved as ALIAS.SAV on this directory.\
ALINAM:	ASCIZ \ALIAS.SAV\
;
	DEPHASE
	END	MOVER		      
