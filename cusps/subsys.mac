	TITLE SUBSYS -- INDIRECT SUBSYS MAKER FOR PROPRIETARY PROGRAMS
	SUBTTL E.A.TAFT/EAT -- 28 JUNE 1974

	SEARCH STENEX

;PARAMETERS
	RUNPAG==10		;PAGE THAT RESULTANT SUBSYSTEM WILL RUN IN


SUBSYS:	RESET
	HRROI 1,[ASCIZ /Indirect subsystem maker.
Subsystem to be installed (including directory):
/]
SUBS1:	PSOUT
	MOVEI 1,[1B2+1B3+1B4	;OLD FILE, CONFIRM
		100,,101
		0
		0
		0
		-1,,[ASCIZ /SAV/]
		0
		0]
	GTJFN			;GET JFN FOR ACTUAL SUBSYSTEM
	  JRST [HRROI 1,[ASCIZ / ? /]
		JRST SUBS1]	;ERROR, RETRY
	MOVEI 2,(1)		;COPY JFN
	HRROI 1,SYSTXT		;WHERE TO PUT REAL NAME
	MOVE 3,[2B2+1B5+1B8+1B11+1B35]
	JFNS			;FULL NAME OF ACTUAL SUBSYSTEM
	HRROI 1,SSNAME		;NOW THE DEFAULT FILENAME FOR <SUBSYS>
	MOVSI 3,(1B8)
	JFNS
	MOVEI 1,(2)		;RELEASE JFN FOR REAL SUBSYSTEM
	RLJFN
	  CAI
	HRROI 1,[ASCIZ /Alias on <SUBSYS>:  /]
SUBS2:	PSOUT
	MOVEI 1,[1B0+1B3+1B4	;FOR OUTPUT USE, CONFIRM
		100,,101
		0
		-1,,[ASCIZ /SUBSYS/]
		-1,,SSNAME
		-1,,[ASCIZ /SAV/]
		0
		0]
	GTJFN			;GET JFN FOR INDIRECT SUBSYSTEM BEING CREATED
	  JRST [HRROI 1,[ASCIZ / ? /]
		JRST SUBS2]	;TRY AGAIN ON ERROR
	MOVE 3,1		;SAVE JFN
	MOVEI 1,400000		;SET ENTRY VECTOR FOR CODE BEING SAVED
	MOVE 2,[1,,RUNSUB]
	SEVEC
	MOVE 1,3		;RESTORE JFN
	HRLI 1,400000		;SAVE THE RUNTIME CODE (JUST 1 PAGE)
	HRROI 2,520000+RUNPAG
	SETZ 3,
	SSAVE
	HALTF			;DONE


	LIT

;THE FOLLOWING CODE CONSTITUTES THE INDIRECT SUBSYSTEM ON <SUBSYS>.

	LOC RUNPAG*1000

RUNSUB:	RESET
	MOVSI 1,(1B2+1B17)	;OLD FILE, SHORT FORM
	HRROI 2,SYSTXT		;REAL NAME OF SUBSYSTEM
	GTJFN			;GET A JFN FOR IT
	  JRST [CAIE 1,GJFX17	;NO SUCH DIR?
		CAIN 1,GJFX24	;OLD FILE REQUIRED?
		HRROI 1,[ASCIZ /Subsystem not available/]
		CAIN 1,GJFX35	;NO ACCESS TO DIR?
		HRROI 1,[ASCIZ /Proprietary subsystem/]
		JUMPL 1,ESOUTX	;PRINT APPROPRIATE MSG IF WE KNOW WHY
		JRST EROUT]	;ELSE NORMAL JSYS ERROR MESSAGE
	MOVEM 1,SUBJFN		;SAVE JFN
	MOVEI 1,400000		;INITIALIZE PSI SYSTEM
	MOVE 2,[LEVRUN,,CHNRUN]
	SIR
	EIR
	MOVSI 2,(1B15)		;ENABLE FOR INSTRUCTION TRAPS
	AIC
	MOVE 1,SUBJFN		;MAKE ARG FOR "GET"
	HRLI 1,400000		;THIS FORK
	MOVE LAST,[PROG,,2]	;COPY REST OF PROG INTO AC'S
	BLT LAST,LAST
	JRST 2			;ACTUALLY DO THE "GET" AND START THE SUBSYSTEM


;PHASED CODE EXECUTED IN AC'S

PROG:	PHASE 2

	GET			;GET THE SUBSYSTEM INTO OUR SPACE
	MOVEI 1,(1)		;TRY TO RELEASE THE JFN
	RLJFN
	  CAI			;CAN FAIL LEGITIMATELY DUE TO SHARING
	MOVEI 1,400000		;CLEAR INTERRUPT SYSTEM
	SETO 2,
	DIC
	SETZ 2,
	SIR
	DIR
	GEVEC			;GET ENTRY VECTOR FOR SUBSYSTEM
	TLNE 2,777000		;10/50 STYLE?
	HRR 2,120		;YES, USE 10/50 START ADDR
LAST:!	JRST (2)		;START THE SUBSYSTEM

	DEPHASE
IFG LAST-17,<PRINTX PROG TO BIG FOR AC'S>

;PSEUDO-INTERRUPT STUFF, FOR HANDLING ITRAP ON "GET" FAILURE

LEVRUN:	TRAPPC			;LEVEL TABLE
	0
	0

CHNRUN:	BLOCK ^D15		;CHANNEL TABLE
	1 ,, ITRAP
	BLOCK ^D20

;HERE ON INSTRUCTION TRAP (PRESUMABLY FROM "GET")
ITRAP:	MOVEI 1,400000		;GET ERROR CODE FOR MOST RECENT ERROR
	GETER
	MOVEI 1,(2)		;COPY IT
	CAIE 1,OPNX3		;READ ACCESS NOT ALLOWED?
	CAIN 1,OPNX5		;EXECUTE ACCESS NOT ALLOWED?
	HRROI 1,[ASCIZ /Proprietary subsystem/]
	JUMPL 1,ESOUTX		;PRINT THIS MESSAGE IF THAT IS WHAT IT WAS

;HERE TO PRINT NORMAL ERROR MESSAGE FOR JSYS ERROR
EROUT:	MOVEI 2,(1)		;COPY THE ERROR CODE
	HRLI 2,400000		;THIS FORK
	HRROI 1,[ASCIZ /JSYS error return:
/]
	ESOUT
	MOVEI 1,101		;PRINT THE STRING
	SETZ 3,
	ERSTR
	  CAI
	  JRST [HRROI 1,[ASCIZ /Error string not found for error /]
		PSOUT
		MOVEI 1,101
		MOVEI 2,(2)
		MOVEI 3,10
		NOUT		;PRINT ERROR CODE IN OCTAL
		  CAI
		JRST .+1]
	JRST ESOUTZ		;GO CLEAN UP

;HERE TO PRINT MESSAGE IN 1 AND BOMB OUT
ESOUTX:	ESOUT
ESOUTZ:	MOVEI 1,37		;EOL
	PBOUT
	MOVE HLAST,[PHALT,,1]	;MOVE CLEANUP CODE INTO AC'S
	BLT HLAST,HLAST
	JRST HALTP		;CLEANUP AND HALT

;PHASED CODE TO CLEAN UP AFTER ERROR

PHALT:	PHASE 1

	-1			;PMAP ARGS TO UNMAP "RUNSUB" CODE
	400000,,RUNPAG
	0
HALTP:!	PMAP			;UNMAP THE CODE
	RESET			;CLEAN UP
HLAST:!	HALTF

	DEPHASE
	LIT

SYSTXT:	BLOCK 30		;FULL NAME OF REAL SUBSYSTEM
SUBJFN:	BLOCK 1			;SAVES JFN FOR REAL SUBSYSTEM
TRAPPC:	BLOCK 1			;RETURN PC FOR INSTRUCTION TRAP

IFG .-RUNSUB-1000,<PRINTX RUNSUB TOO BIG FOR ONE PAGE>

	LOC <.!777>+1		;NEXT PAGE

;STORAGE USED IN SUBSYS MAKER PROGRAM

SSNAME:	BLOCK 10		;NAME PART ONLY OF REAL SUBSYSTEM

	RELOC
	END SUBSYS
