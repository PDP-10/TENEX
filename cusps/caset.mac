	TITLE	CASET
	SEARCH	STENEX

ENTVEC:	JRST LOAD
	JRST DUMP

DUMP:	TLOA 1,-1
LOAD:	TLZ 1,-1
	MOVEM 1,DUMPSW#

START:	RESET
	
	MOVSI 1,3
	MOVE 2,[100,,101]
	GTJFN
	 JRST HUH
	MOVEM 1,JFN#

	MOVE 2,[070000,,100000]
	SKIPGE DUMPSW
	MOVE 2,[070000,,200000]
	OPENF
	 JRST HUH

	MOVEI 1,101
	RFMOD
	MOVEM 2,MODE#
	TRZ 2,300
	SFMOD
	TLO 1,(1B4)
	TLINK
	 JRST HUH

	SKIPGE DUMPSW
	 JRST DSTART

LSTART:	MOVEI 1,101
	MOVEI 2,"Q"-100
	BOUT
	
LLOOP:	MOVEI 1,100
	BIN
	ANDI 2,177
	CAIN 2,"S"-100
	 JRST LSTART
	CAIN 2,"Z"-100
	 JRST LEND
	MOVEI 1,101
	BOUT
	MOVE 1,JFN
	BOUT
	JRST LLOOP

LEND:	MOVEI 1,101
	MOVEI 2,"S"-100
	BOUT
	MOVEI 1,100
	CFIBF
	SIBE
	 JRST .-2
	JRST THEEND

; dump file onto cassette

DSTART:	MOVEI 1,101			;turn on cassette unit
	MOVEI 2,"R"-100
	BOUT

DLOOP:	MOVE 1,JFN			;get char from file
	BIN
	MOVE 3,2
	GTSTS
	TLNE 2,(1B8)			;eof?
	 JRST DEND			;yes
	MOVE 2,3			;no
	JUMPE 2,DLOOP			;ignore nulls
	MOVEI 1,101
	BOUT
	CAIE 2,12			;line feed or
	CAIN 2,37			;eol?
	 JRST .+2			;yes
	JRST DLOOP			;no

	MOVEI 2,"S"-100			;send stop char onto tape
	BOUT
	JRST DLOOP			;and turn tape back on
	
DEND:	MOVEI 1,101
	MOVEI 2,"Z"-100
	BOUT
	BOUT
	MOVEI 2,"S"-100
	MOVSI 3,-^D87
	BOUT
	AOBJN 3,.-1
	MOVEI 2,"T"-100
	BOUT

THEEND:	MOVE 1,JFN
	CLOSF
	 JRST CLEAN

	MOVEI 1,101
	MOVE 2,MODE
	SFMOD
	TLO 1,(1B4+1B5)
	TRNE 2,1B26
	TLINK
	 JFCL

	HALTF
	JRST .-1

HUH:	HRROI 1,[ASCIZ / ??/]
	PSOUT
CLEAN:	RESET
	HALTF
	JRST .-1

	END START
