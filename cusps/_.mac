	TITLE SYSJOB COMMUNICATOR
	SEARCH STENEX

	OPDEF PSTIN [JSYS 611]

START:	RESET
	
	MOVEI 1,400000		;SAVE OLD CAPABILITIES
	RPCAP
	MOVEM 3,CAPABS

	SETZ 2,
	IORI 3,1B18
	EPCAP			;ENABLE WHEEL
	
	HRROI 1,BUF
	MOVEI 2,120		;READ EDITED LINE INTO FILE
	SETZ 3,			;BREAK ON CARRIAGE RETURN
	PSTIN
	SETZ 3,
	IDPB 3,1		;MAKE ASCIZ
	
	MOVSI 1,(1B0+1B17)	;GET JFN
	HRROI 2,[ASCIZ /<SYSTEM>SYSJOB.COMMANDS/]
	GTJFN
	 JRST ERROR		;FAILED, REPORT ERROR
	MOVEM 1,FJFN
	
	MOVE 2,[XWD 070000,100000] ;OPEN FOR ASCIZ WRITE
	OPENF
	 JRST ERROR		;FAILED, REPORT ERROR

	HRROI 2,BUF		;PUT INTO FILE
	SETZ 3,
	SOUT

	MOVE 1,FJFN		;CLOSE FILE
	CLOSF
	 JRST ERROR

RESCAP:	MOVEI 1,400000		;RESET CAPABILITIES
	MOVE 3,CAPABS
	EPCAP

	HALTF
	JRST START

ERROR:	MOVEI 1,101		;REPORT ERROR
	MOVE 2,[XWD 400000,-1]
	SETZ 3,
	ERSTR
	 0
	 0
	HALTF
	JRST START

BUF:	BLOCK 21
FJFN:	0
CAPABS:	0

	END START

