;<SOURCES>FTSMAL.MAC;3     7-MAY-75 16:30:35    EDIT BY CLEMENTS
; ENABLE VERIFY-SKT CHECK.
;ADD TIME-OF-WEEK LOGIC TO LOG FILE
;MOVE MAIL.BLOG TO <MAIL2> IF IT EXISTS
;RE-PAGINATE
;<SOURCES>FTSMAL.MAC;2    12-MAR-75 00:05:19    EDIT BY CLEMENTS
;<SOURCES>FTSMAL.MAC;1     4-MAR-75 18:56:36    EDIT BY CLEMENTS
TITLE FTSMAL - FTP SERVER MAIL ROUTINES
SUBTTL R CLEMENTS, J SUSSMAN (C) BOLT BERANEK & NEWMAN, INC., 1975

SEARCH STENEX
SEARCH FTSPAR

INTERNAL MAIL$,MLFL$
EXTERNAL BNLMAL,CLOSER,CLSLOG,CRLFM,ERRRPL,FRMHST,LINEIN
EXTERNAL OPNLOG,PREDAT,RPCRLP,SDUMP0,SDUMPA,TIMEOK,UCASE

;MAIL COMMAND - APPENDS MAIL TO MESSAGE.TXT;1 FOR LOCAL USER
;AND MLFL COMMAND, SAME BUT DATA ON DATA CONN INSTEAD OF TELNET.

MAIL$:	TLZA F,L.STAT		;FLAG MAIL, NOT MLFL
MLFL$:	TLO F,L.STAT		;FLAG MLFL, NOT MAIL
	TLZ F,L.MLUN!L.MFWD	;ASSUME NOT GENL DELIVERY OR FORWARDING
	SETOM LCLJFN
	SETZM IBITCT		;NO BITS READ YET
	SKIPGE $USER		;LOGGED IN?
	SETZM $USER		;NO. FLAG FOR ACCOUNTING
	SETZB A,MLUSR		;SEE IF ARG WINS. CLEAR DEST NAME
	MOVSI B,(7B5)
	ADD B,BP		;POINT TO USER NAME
	PUSHJ P,UCASE		;ALLOW LOWER CASE
	SETZM MLUNST		;PUT THE NAME IT WAS ADDRESSED TO HERE.
	MOVSI B,(7B5)
	ADD B,BP		;POINTER TO NAME
	MOVEI C,5*17
	MOVEI D,0
	HRROI A,MLUNST
	SOUT
	MOVEI B,0
	IDPB B,A		;TERMINATE STRING
	SKIPN MLUNST		;THERE WAS A NAME, WASNT THERE?
	JRST MAILX4		;NO. A LOSING COMMAND
	HRROI A,GTJSTR		;NOW MAKE THE DESTINATION NAME
	MOVEI B,"<"		;STICK IN USER NAME
	BOUT
	HRROI B,MLUNST		;NAME FROM COMMAND
	MOVEI C,0
	SOUT			; ..
	HRROI B,[ASCIZ />MESSAGE.TXT;1/]
	MOVEI C,0
	SOUT
	HRROI B,[ASCIZ /;P777756/]
	SKIPUNLESS CCA
	SOUT
;FALL THRU

;FALLS THRU
	MOVSI A,501001		;SEE IF MAILBOX EXISTS
	HRROI B,GTJSTR
	GTJFN
	  JRST MLFWQ		;IT DOESN'T. SEE IF FORWARDING EXISTS.
	MOVE B,[XWD 1,1]	;MAKE SURE ALLEGED MAILBOX IS
	MOVEI C,C		; PERMANENT. IF NOT, PRETEND
	GTFDB			; IT DOESN'T EXIST
	TLNN C,(1B1)
	JRST [	RLJFN
		  JFCL
		JRST MLFWQ]
	RLJFN			;RELEASE MAILBOX JFN
	  JFCL
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	MOVEI A,0
	HRROI B,MLUNST		;OK, GET DIRECTORY NUMBER
	STDIR			;SEE IF HE EXISTS
	  JRST MLX10		;NO
	  JRST MLX10		; ..
	HRRZM A,MLUSR		;SAVE THE DIRECTORY NUMBER
	MOVEI A,(A)		;JUST THE NUMBER
	CAMN A,SYSDNM		;SYSTEM DIRECTORY?
	JRST MAILX4		;YES. REFUSE IT.

MAIL0A:	TLNN F,L.STAT		;DATA OVER DATA CONNECTION?
	JRST MAIL11		;NO.
	SKIPG B,$BYTE		;YES. MAKE SURE ASCII, 8 BIT ETC.
	MOVEI B,10		;DEFAULT
	CAIN B,10		;RIGHT SIZE?
	SKIPLE $STRU		;AND STRUCTURE?
	JRST MLFLX1		;NO.
	SKIPG $MODE		;ALSO MODE AND TYPE
	SKIPLE $TYPE		; ..
	JRST MLFLX1		;NO.
MAIL11:	HRROI A,GTJSTR		;BUILD A NAME FOR TEMP FILE FOR MAIL.
	HRROI B,[ASCIZ /<SYSTEM>--MAIL--./]
	SKIPUNLESS CCA
	HRROI B,[ASCIZ /<SYS-LOG+MSG>--MAIL--./]
	MOVEI C,0
	SOUT
	MOVE B,THISPX		;MAKE DEPENDENT ON PROCESS NUMBER
	MOVEI C,10		;TO AVOID CONFLICTS
	NOUT
	  JRST MLX10		;CAN'T FAIL
	HRROI B,[ASCIZ /;T;P770000/]	;AND MAKE JOB DEPENDENT.
	MOVEI C,0
	SOUT
MAIL01:	MOVSI A,411001		;GTJFN SHORT, STRING, OUT, TEMP, IG DEL.
	HRROI B,GTJSTR		; ..
	GTJFN
	  JRST MAILX9		;CAN'T?
	MOVEM A,LCLJFN		;STORE JFN
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	MOVE B,[070000,,100000]	;OPEN TO WRITE.
	OPENF
	  JRST MAILX9		;CAN'T?
	HRROI B,[ASCIZ /Mail from /]
	MOVEI C,0
	SOUT
	HRRZ B,FHSTN		;NOW PUT A TIME-STAMP ON. FIRST, HOST.
	CVHST			;HOST NAME
	  JRST [HRROI B,[ASCIZ /site /]
		SOUT
		HRRZ B,FHSTN	;MAKE A NUMBER
		MOVEI C,10
		NOUT
		  JFCL
		JRST .+1]
;FALL THRU

;FALLS THRU
	TLNN F,L.LOGI		;IN CASE NO PASSWORD, DON'T BELIEVE
	JRST MAIL1B		; POSSIBLE PHONY USER NAME.
	HRROI B,[ASCIZ / as local user /]
	TLNE F,L.ANON		;ANONYMOUS USER?
	HRROI B,[ASCIZ / as Anonymous user /]
	MOVEI C,0
	SOUT
	SKIPGE B,$USER		;USER NAME IF HE BOTHERED TO LOG IN
	JRST MAIL1B		;NO.
	TLNE F,L.ANON		;EITHER REAL OR ANON. WHICH?
	JRST [	HRROI B,USRPSW	;ANON. ALLEGED NAME STORED HERE.
		SOUT
		JRST MAIL1B]
	DIRST			; ..
	  MOVE A,0(P)		;NO.
MAIL1B:	HRROI B,[ASCIZ / rcvd at /]
	MOVEI C,0
	SOUT
	MOVSI C,(1B10+1B12+1B13+1B17)
	SETO B,
	ODTIM
	HRROI B,CRLFM		;AND END LINE
	MOVEI C,0
	SOUT
	TLNN F,L.MLUN		;TO UNKNOWN USER?
	JRST MAIL1C		;NO
	HRROI B,[ASCIZ /This mail mis-addressed to /]
	SOUT
	HRROI B,MLUNST		;MAIL USER NAME STRING
	SOUT
	HRROI B,CRLFM
	SOUT
MAIL1C:
	SKIPUNLESS BINL
	PUSHJ P,BNLMAL		;LOG THE MAIL
	TLNE F,L.STAT		;MAIL FILE?
	JRST MLFL01		;YES. DIFFERENT DATA CAPTURE MECHANISM
	MOVE A,SJFN		;TELNET REPLY
	HRROI B,[ASCIZ /350 Type mail, ended by a line with only a "."
/]
	PUSHJ P,SDUMP0		;SEND MSG AND DUMP BUFFER

MAILL1:	PUSHJ P,LINEIN		;NOW READ TELNET LINES.
	  JRST [TLNE F,L.LTL
		JRST MAILX6
		JRST MAILX8 ]	;EOF ON TELNET. ABORT.
	SKIPN DEBUGS
	JRST MAIL1A		;NOT TYPING OUT STUFF
	HRROI A,CMDIB		;TYPE OUT MAIL LINE
	PSOUT
	HRROI A,CRLFM
	PSOUT
MAIL1A:	MOVE A,CMDIB		;SEE IF LINE WAS JUST A DOT
	CAMN A,[ASCII /./]	; ..
	JRST MAIL02		;YES. DEFINES END.
	MOVE A,LCLJFN
	HRROI B,CMDIB		;PUT THE LINE IN THE TEMP FILE
	MOVEI C,0
	SOUT
	HRROI B,CRLFM		;AND A CR LF WHICH WAS STRIPPED
	SOUT
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	JRST MAILL1		;LOOP TILL DOT OR EOF

MAIL02:	MOVE A,LCLJFN		;NOW MOVE THE LOCAL FILE TO THE
	HRLI A,(1B0)		;MAIL FILE. CLOSE THE WRITE.
	CLOSF			;BUT KEEP THE JFN
	  JFCL
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	HRRZ A,LCLJFN		;RE-OPEN FOR READING
	MOVE B,[070000,,200000]	; ..
	OPENF
	  JRST MAILX9		;CAN'T
	RFPTR			;CHECK SIZE OF THE MAIL
	  JRST MAILX9		;CAN'T FAIL
	ASH B,3			;EIGHT BITS PER
	ADDM B,TRBITS
	ASH B,-3
	CAIL B,10000		;DON'T ALLOW SUPER-HUGE FILES.
	JRST MAILX5		;BAD..
;FALL THRU

;FALLS THRU
	TLNE F,L.MFWD		;FORWARDING?
	JRST [HRROI A,GTJSTR	;YES. COPY FILE NAME TO FWD THRU
		HRROI B,MLFWST
		MOVEI C,0
		SOUT
		JRST MAIL2A]
	TLNE F,L.MLUN		;TO PHONY PERSON?
	JRST MAIL2A		;YES.
	HRROI A,GTJSTR		;NOW MAKE THE DESTINATION NAME
	MOVEI B,"<"		;STICK IN USER NAME
	BOUT
	HRRZ B,MLUSR		;HIS DIRECTORY NUMBER
	DIRST
	  JRST MLX10		;SHOULDNT FAIL
	HRROI B,[ASCIZ />MESSAGE.TXT;1/]
	MOVEI C,0
	SOUT
	HRROI B,[ASCIZ /;P777756/]
	SKIPUNLESS CCA
	SOUT

MAIL2A:	MOVEI X,5		;TIMES TO TRY IF BUSY
MAIL2B:	HRROI B,GTJSTR		;NOW GET A JFN FOR MAILBOX
	MOVSI A,501001
	TLNE F,L.MFWD		;FORWARDING?
	TLZ A,101000		;YES. ALLOW NEW FILE
MAIL2C:	TLNN F,L.MLUN		;TO GENERAL DELIVERY?
	JRST MAIL2D		;NO
	MOVSI C,-NUNMLT		;YES. FIND THE SUBSTITUTE MAILBOX
	HLRZ A,UNMLT(C)		;GET HOST NUMBER FROM TABLE
	CAME A,LHOSTN		;IS THAT WHERE I AM RUNNING?
	AOBJN C,.-2		;NO.
	HRRO B,UNMLT(C)		;GET STRING FOR NAME OF MAILBOX
	MOVSI A,401001		;OUTPUT, IGN DELETED.
MAIL2D:	GTJFN
	  JRST MAILX4		;NO SUCH FILE
	PUSH P,A		;KEEP ON STACK
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	MOVE B,[070000,,020000]	;APPEND TO IT.
	OPENF
	  JRST [MOVEM A,B	;SAVE ERROR CODE
		POP P,A		;CAN'T
		RLJFN
		  JFCL
		SOJLE X,MLX15
		MOVEI A,^D2000
		DISMS
		JRST MAIL2B]
	MOVE A,LCLJFN		;GET # OF CHARS IN TEMP FILE
	SIZEF
	  JRST [POP P,A
		CLOSF
		  JFCL
		JRST MAILX9]
	MOVEM B,T1		;SAVE # CHARS IN T1
	TLNE F,L.MFWD		;FORWARDING?
	JRST MAILL2		;YES. DON'T PUT HEADER ON.
	MOVE A,0(P)		;MESSAGE FILE
	SETO B,0		;PUT STANDARD MSG FILE FORMAT ON.
	MOVSI C,(1B13)		;FIRST, DATE AND TIME WITH TIME ZONE.
	ODTIM
	MOVEI B,","		;THEN COMMA
	BOUT
	MOVE B,T1		;SIZE OF TEXT
	MOVEI C,12		;DECIMAL RADIX
	NOUT
	  MOVE A,0(P)
;FALL THRU

;FALLS THRU
	MOVEI B,";"		;NOW BIT FLAG FIELD
	BOUT
	SETZ B,			;IS NORMALLY 0.
	MOVE C,FORNS		;IF FOREIGN ICP SOCKET
	CAIL C,MLSKT		; IS AUTHENTICATED MAIL SOCKET
	CAILE C,MLSKT+5		; IN THIS GROUP OF 6
	SKIPA			;NO GOOD.
	TLO B,(1B7)		; OK, FLAG VERIFIED IN B7
	MOVE C,[1B2+1B3+^D12B17+^D8] ;12 OCTAL DIGITS, LEADING 0'S.
	NOUT
	  MOVE A,0(P)
	HRROI B,CRLFM
	MOVEI C,0
	SOUT			;AND CR, LF ON END OF LINE
;FALL THRU

MAILL2:	MOVE A,LCLJFN		;NOW PUT POINTER BACK AT START
	MOVEI B,0		; ..
	SFPTR
	  JFCL
	MOVE B,T1		;COUNT 8-BIT CHARS, THOUGH INCLUDES
	IMULI B,10		; SMALL ERROR OF LOCAL HEADER
	ADDM B,IBITCT
	ADDM B,TRBITS
MLLUP1:	PUSHJ P,TIMEOK		;LOOP TO COPY FROM TEMP TO MSG FILE
	SKIPG C,T1		;# OF CHARS LEFT TO COPY
	JRST MAIL03		;NO MORE
	CAILE C,5000		;IF > 1 PAGE, JUST DO PAGE
	MOVEI C,5000
	SUB T1,C		;ADJUST # REMAINING CHARS
	MOVE A,LCLJFN		;SET UP TO READ FROM TEMP FILE
	HRROI B,WINDOW		; INTO WINDOW
	PUSH P,C		;SAVE # CHARS TO READ
	MOVEI D,14		;STOP ON FORMFEED (TO CONVERT IT)
MLLUP2:	SIN			;READ
	LDB T2,B		;IF LAST CHAR FF, REPLACE BY LF
	CAIN T2,14
	MOVEI T2,12
	DPB T2,B
	JUMPN C,MLLUP2		;FINISH SIN IF INCOMPLETE
	POP P,C			;# OF CHARS READ
	MOVNS C			;WRITE THEM TO MSG FILE
	HRROI B,WINDOW
	MOVE A,0(P)
	SOUT
	JRST MLLUP1		;LOOP FOR MORE

MAIL03:	POP P,A
	CLOSF
	  JFCL
	PUSHJ P,MLSTAT		;RECORD MAIL STATISTICS
	HRROI X,MAILM2		;MAIL DONE.
	TLNE F,L.STAT		;OR WAS IT MLFL
	HRROI X,MAILM3		;YES.
	JRST MAIL05
MAILM3:	ASCIZ /252 Mail completed successfully./
MAILM2:	ASCIZ /256 Mail completed successfully./


;RECORD MAIL STATISTICS IF APPROPRIATE

MLSTAT:	SKIPIF MLST		;RETURN IF NOT KEEPING STATISTICS
	  POPJ P,0
	SKIPE DEBUGS		;RETURN IF DEBUGGING
	  POPJ P,0
	MOVEI A,0		;SEE IF MAIL2 DIRECTORY EXISTS
	HRROI B,[ASCIZ /MAIL2/]
	STDIR			; ..
	 JFCL
	 TDZA A,A
	SETO A,0		;IT DOES
	PUSH P,A		;REMEMBER IT
	SETO B,			;CALCULATE VERSION NUMBER FOR
	MOVSI D,(1B0+1B2+0B17)	; STATISTICS FILE BASED ON GMT DATE
	ODCNV
	MOVE A,C		;SAVE DAY OF WEEK IN RH, AND
	HRL A,D			;GMT SECONDS SINCE MIDNIGHT IN LH
	MOVEM A,MLTIMT		; IN A TIME TEMP CELL
	HRRZ A,B		;MONTH
	AOS A
	IMULI A,^D100
	HLRZ B,B		;YEAR
	IDIVI B,^D100
	ADD A,C			;VERSION=MMYY
	TLO A,(1B17)		;SHORT FORM GTJFN
	HRROI B,[ASCIZ /<SYSTEM>MAIL.BLOG/]
	SKIPGE 0(P)		;DOES MAIL2 EXIST?
	HRROI B,[ASCIZ /<MAIL2>MAIL.BLOG/]	;YES. USE IT
	POP P,(P)		;DISCARD THAT FLAG
	GTJFN
	  POPJ P,0		;CAN'T, GIVE UP
	MOVEM A,LOGJFN		;SAVE JFN
	MOVE B,[1B19+1B20+1B25]	;READ,WRITE,THAWED
	OPENF
	  JRST MLSTA2		;CAN'T, GIVE UP
	HRL A,LOGJFN		;MAP PAGE 1 OF STATISTICS FILE
	HRRI A,1
	MOVE B,[400000,,<WINDOW/1000>]
	MOVSI C,140000		;READ/WRITE
	PMAP
	MOVE A,FHSTN		;INCREMENT # OF MSGS RECEIVED
	AOS WINDOW(A)		;FROM THIS HOST
	SETO A,			;UNMAP PAGE
	PMAP
	HRL A,LOGJFN		;MAP PAGE 3 OF FILE
	HRRI A,3
	PMAP
	MOVE A,LCLJFN		;GET # OF CHARS IN MESSAGE
	SIZEF			;=LENGTH OF LOCAL FILE
	  SETZ B,		;SHOULDN'T FAIL
	PUSH P,B		;SAVE THE SIZE
	MOVE A,FHSTN		;ADD TO # OF CHARS RECEIVED
	ADDM B,WINDOW(A)	;FROM THIS HOST
	SETO A,			;UNMAP PAGE
	MOVE B,[400000,,<WINDOW/1000>]
	PMAP
;FALL THRU

;FALLS THRU
	HRL A,LOGJFN		;MAP PAGE 4 OF FILE
	HRRI A,4
	MOVSI C,140000		;READ/WRITE
	PMAP
	MOVE C,MLUSR		;NUMBER OF USER RECEIVING MAIL
	IDIVI C,^D36		;CALCULATE WORD AND BIT
	MOVSI A,400000		; CORRESPONDING TO USER #
	MOVNS D
	ROT A,(D)
	IORM A,WINDOW+200(C)	;TURN ON BIT FOR THAT USER
	SETO A,			;UNMAP PAGE
	PMAP
	MOVS A,LOGJFN		;NOW THE TIME-HISTOGRAM PAGES
	HRRI A,10		;PAGE 10 IS CHARS BY TIME OF DAY
	MOVE B,[400000,,<WINDOW/1000>]
	MOVSI C,140000		;READ AND WRITE ACCESS
	PMAP
	HLRZ A,MLTIMT		;GET THE TIME WITHIN DAY
	IDIVI A,^D<60*30>	;THE HALF-HOUR WITHIN THE DAY
	HRRZ D,MLTIMT		;THE DAY IN THE WEEK (MONDAY = 0)
	IMULI D,^D48		;SKIP N DAYS OF HALF-HOURS
	ADD D,A			;AND ADD IN TODAY'S HALF-HOURS
	POP P,A			;GET BACK LENGTH OF MSG
	ADDM A,WINDOW(D)	;RECORD IT
	MOVS A,LOGJFN		;NOW COUNT MSGS BY TIME OF DAY
	HRRI A,7		;IN THIS PAGE
	MOVE B,[400000,,<WINDOW/1000>]
	MOVSI C,140000		;READ AND WRITE ACCESS
	PMAP
	AOS WINDOW(D)		;COUNT A MSG
	MOVEI A,400000		;NOW GET RUN TIME OF THIS FORK
	RUNTM
	SUB A,IFRKTM		;SINCE STARTED
	SUB A,MALCPU		;LESS ANY POSSIBLE PREVIOUS MSG
	ADDM A,MALCPU		;UPDATE FOR THIS MSG
	ADDM A,WINDOW+777	;COUNT IT IN TOTAL, LAST WD THIS PG
	SETO A,			;UNMAP PAGE
	MOVE B,[400000,,<WINDOW/1000>]
	PMAP
MLSTA2:	CLOSE LOGJFN		;CLOSE STATISTICS FILE
	POPJ P,0

MLFWQ:	MOVSI A,100001		;GET JFN OF FORWARDER
	HRROI B,[ASCIZ /<SUBSYS>MAILBOX.SAV/]
	GTJFN
	  JRST MFWDX1		;NOT THERE.
	PUSH P,A		;SAVE JFN
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	MOVSI A,(1B1)		;CREATE AN INFERIOR FORK
	CFORK
	  JRST MFWDX2		;CAN'T
MLFWQ5:	PUSH P,A		;SAVE FORK HANDLE
	HRL A,0(P)		;GET PROG INTO FORK
	HRR A,-1(P)		;JFN
	GET
	HRLZ A,0(P)		;PAGE 0 OF INFERIOR
	MOVSI B,400000		;MAPPED FROM THIS FORK
	HRRI B,BLTPAG		;TEMP PAGE
	MOVSI C,140000		;RD, WRT ACCESS
	PMAP
	MOVSI T1,-10		;COPY NAME
	MOVE A,MLUNST(T1)	;COMMANDED ADDRESSEE
	MOVEM A,BLTADR+140(T1)	;TO INFERIOR
	AOBJN T1,.-2
	MOVE A,0(P)		;FORK HANDLE AGAIN
	MOVEI B,[1]-1		;SET AC1 TO 1 FOR LOCAL SITE
	SFACS
	MOVEI B,2
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	SFRKV			;START UP INFERIOR
	WFORK
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	RFSTS			;SEE IF IT FINISHED OK
	HLRZ A,A		; ..
	CAIE A,2		;HALTF?
	JRST MFWDX3		;NO
	MOVE A,0(P)		;HANDLE AGAIN
	MOVEI B,ACTACS		;ACCOUNT FORK AC BLK IS FREE HERE
	RFACS			;GET ANSWER
	SKIPG T1,ACTACS+1	;SUCCESS ANSWER?
	JRST MFWDX3		;NO
	MOVE A,[440700,,LHSTNM]	;PREVENT LOOPS.
	MOVE B,[440700,,BLTADR+150]	; BY CHECKING FOR LOCAL HOST
	MOVEI C,50
MLFWQ2:	ILDB T1,A
	ILDB T2,B
	CAME T1,T2
	JRST MLFWQ3
	JUMPE T1,MLFWQ1		;IF MATCHED THRU END, CHECK NAME
	SOJG C,MLFWQ2		;LOOK TIL END OR MISMATCH
	JRST MFWDX3		;WIERD FAILURE.

MLFWQ1:	MOVE A,[440700,,MLUNST]	;SEE IF USER NAME MATCHES TOO
	MOVE B,[440700,,BLTADR+140]
	MOVEI C,50
MLFWQ4:	ILDB T1,A
	ILDB T2,B
	CAME T1,T2
	JRST MLFWQ3
	JUMPE T1,MFWDX3
	SOJG C,MLFWQ4
	JRST MFWDX3
MLFWQ3:	HRROI A,MLFWST		;COPY OVER FOR A NEW FILE
	HRROI B,[ASCIZ /[--UNSENT-MAIL--]./]
	MOVEI C,0		;MAILER STANDARD NAME
	SOUT
	HRROI B,BLTADR+140
	SOUT
	MOVEI B,"V"&37
	BOUT			;QUOTE THE AT-SIGN
	MOVEI B,"@"
	BOUT
	HRROI B,BLTADR+150
	SOUT
	HRROI B,[ASCIZ /;P770000/]
	SOUT
	HRROI B,[ASCIZ /951 MAIL WILL BE FORWARDED TO /]
	PUSHJ P,SDUMPA		;TELL USER
	HRROI B,BLTADR+140
	PUSHJ P,SDUMPA		;GIVE HIM ADDRESSEE
	HRROI B,[ASCIZ / at /]
	PUSHJ P,SDUMPA
	HRROI B,BLTADR+150
	PUSHJ P,SDUMPA
	HRROI B,CRLFM		;END OF LINE
	PUSHJ P,SDUMPA
	TLO F,L.MFWD		;FLAG FOR LATER PROCESSING
	POP P,A			;FORK HANDLE
	KFORK
	MOVE A,0(P)		;AND MAILBOX.SAV JFN
	HRLI A,(1B0)
	CLOSF
	  JFCL
	POP P,A
	RLJFN
	  JFCL
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	JRST MAIL0A		;NOW GET THE MAIL

MFWDX3:	POP P,A			;FORK
	KFORK
	MOVE A,0(P)
	HRLI A,(1B0)
	CLOSF
	  JFCL
	POP P,A			;JFN FOR MAILBOX.SAV
	RLJFN
	  JFCL
MFWDX1:	JRST MAILX1

MFWDX2:	MOVEI A,^D2000		;Failed get fork, wait 2 sec & try again
	DISMS
	MOVSI A,(1B1)
	CFORK
	  SKIPA			;Failed again
	JRST MLFWQ5		;Got fork, go on.
	POP P,A			;No fork. Release MAILBOX.SAV JFN
	RLJFN
	  JFCL
	JRST MAILX3

MAILX1:	SKIPIF MLUN
	  JRST MAILX4
	TLO F,L.MLUN		;YES. SO FLAG GOING TO GENL DELIVERY
	HRROI B,MLUNM1		;BUT WARN THE USER IF HE DOESNT WANT TO
	PUSHJ P,SDUMPA		; TRUST THAT MECHANISM
	JRST MAIL0A		;DIVE BACK IN AND TRY AGAIN

MLBUSY:	SKIPIF MLQB		;MAILBOX BUSY. QUEUE LOCALLY IF OK.
	  JRST MAILX7		;LOCAL QUEUE NOT ALLOWED IF SWITCH OFF.
	TLNE F,L.MFWD!L.MLUN	;ONLY POSSIBLE IF TO REAL MAILBOX
	JRST MAILX7
	MOVE A,FHSTN		;DON'T QUEUE IF FROM SAME HOST
	CAMN A,LHOSTN		; TO AVOID CIRCULAR SENDING
	JRST MAILX7
	HRROI A,GTJSTR		;OK TO QUEUE. MAKE FILE NAME
	HRROI B,[ASCIZ /[--UNSENT-MAIL--]./]
	MOVEI C,0
	SOUT
	HRROI B,MLUNST
	SOUT
	MOVEI B,"V"&37
	BOUT
	HRROI B,[ASCIZ /@;P770000/]
	SOUT
	TLO F,L.MFWD
	JRST MAIL2A

MAIL04:	HRROI B,(X)		;REPLY TO CORRECT AC
	CLOSE LCLJFN
	JRST RPCRLP		;BACK TO TOP LEVEL

MAILX3:	JSP	X,MAIL05
	ASCIZ /453 No forks available; please try later./
MAILX4:	JSP	X,MAIL05
MAILM4:	ASCIZ /450 No such mailbox at this site./
MAILX5:	JSP	X,MAIL05
MAILM5:	ASCIZ /451 Message too long./
MAILX6:	JSP	X,MAIL05
MAILM6:	ASCIZ /451 Line too long./
MAILX7:	JSP	X,MAIL05
MAILM7:	ASCIZ /453 Mailbox busy./
MAILX8:	JSP	X,MAIL05
MAILM8:	ASCIZ /453 Net connection closed./
MAILX9:	JSP	X,MAIL05
MAILM9:	ASCIZ /453 Scratch file failure./
MLX10:	JSP	X,MAIL05
MLM10:	ASCIZ /453 Impossible error./
MLX11:	JSP	X,MAIL05
MLM11:	ASCIZ /453 Disk full./
MLX12:	JSP	X,MAIL05
MLM12:	ASCIZ /453 Mailbox damaged./
MLX13:	JSP	X,MAIL05
MLM13:	ASCIZ /453 Unexpected failure to open mailbox./
MLX14:	JSP	X,MAIL05
MLM14:	ASCIZ /450 Append access to mailbox not allowed./

MLX15:	CAIE	B,OPNX1	;diagnose OPENF failure on mailbox
	CAIN	B,OPNX9
	 JRST	MLBUSY
	CAIN	B,OPNX6
	 JRST	MLX14
	CAIN	B,OPNX10
	 JRST	MLX11
	CAIN	B,OPNX16
	 JRST	MLX12
	JRST	MLX13

MLUNM1:	ASCIZ /950 No such mailbox at this site.
950 An operator will attempt to deliver your mail.
/		;THE 950 IS AN EXPERIMENTAL REPLY. IT WILL CHANGE.

MAIL05:	SKIPGE A,LCLJFN
	JRST MAIL5Z
	MOVE A,LCLJFN		;CLOSE OUT THE TEMP FILE.
	HRLI A,400000
	CLOSF
	  JFCL
	HRRZ A,LCLJFN
	DELF
	  JFCL
MAIL5Z:	PUSH P,X		;SAVE MESSAGE
	PUSHJ P,TIMEOK		;UPDATE KILL TIME
	CAME X,[-1,,MAILM3]	;GOOD REPLIES?
	CAMN X,[-1,,MAILM2]	; ..
	SKIPA B,[-1,,[ASCIZ /MAIL TO /]]
	HRROI B,[ASCIZ /NO MAIL TO /]
	PUSHJ P,OPNLOG		;LOG IT
	  JRST MAIL5A		;CAN'T
	HRRZ B,MLUSR		;DESTINATION
	JUMPE B,[HRROI B,MLUNST
		MOVEI C,0
		SOUT
		JRST .+2]
	DIRST
	  JFCL
	PUSHJ P,FRMHST		;IDENTIFY SENDING HOST
	SKIPIF CCA
	  JRST MAIL5C
	MOVEI A,TXPO
	HRRZ B,MLUSR
	JUMPE B,[HRROI B,MLUNST
		MOVEI C,0
		SOUT
		JRST .+2]
	DIRST
	  JFCL
	PUSHJ P,FRMHST

MAIL5C:	PUSHJ P,CLSLOG
MAIL5A:	POP P,X
	JRST MAIL04

;TABLE OF HOST,,WHERE TO SEND UNKNOWN MAIL
UNMLT:	4,,[ASCIZ /<SECRETARY>MESSAGE.TXT;1/]	;UTAH
	37,,[ASCIZ /<SYS-LOG+MSG>MAIL.GENERAL-DELIVERY;1;P777756/]
	205,,[ASCIZ /<NO.OPERATOR>SO.REJECT/]
NUNMLT==.-UNMLT
	0,,[ASCIZ /<SYSTEM>MAIL.GENERAL-DELIVERY;1;P777700/]	;DEFAULT

MLFL01:	TLZ F,L.SEND		;RECEIVE FLAG FOR DATA CONN
	PUSHJ P,PREDAT		;PREPARE DATA CONNECTION
	  JRST ERRRPL		;NO GOOD.
	MOVE A,SJFN		;OK, SEND THE PROMPT.
	HRROI B,[ASCIZ /250 Begin mail file transfer.
/]
	PUSHJ P,SDUMP0		;SEND MSG AND DUMP BUFFER
MLFLL1:	MOVE A,DATJFN		;GET THE MAIL
	BIN
	JUMPN B,MLFLN		;EOF OR NULL?
	GTSTS			;YES. SEE WHICH.
	TLNE B,1000		; ..
	JRST MLFLEF
	JRST MLFLL1		;NULL. THROW IT AWAY.
MLFLN:	CAILE B,177		;AND THROW AWAY TELNET CONTROLS
	JRST MLFLL1		; ..
	MOVE A,LCLJFN		;OK, A REAL CHAR. PUT IN FILE
	BOUT
	PUSHJ P,TIMEOK		;UPDATE TIMEOUT
	JRST MLFLL1		;ONWARD.
MLFLEF:	CLOSE DATJFN
	JRST MAIL02		;NOW COPY TO REAL MAILBOX.

MLFLX1:	JSP X,MAIL04		;BAD PARAMS.
ASCIZ /505 MAIL file must be STREAM, ASCII, 8-Bit, FILE STRUcture./

	END

