;<COWER>HELP.MAC;7	EDIT BY COWER  9-JULY-75  10:29:00
;NOW TAKES ESCAPE ON SUBJECT - 6 CHAR SUBJECT RESTRICTION
;REMOVED
;<COWER>HELP.MAC;4	EDIT BY COWER  23-JUNE-75 11:35:43
;CHANGED SIZEF TO GTFDB - OUTPUT FILE SIZE IN CHARACTERS
;<COWER>HELP.MAC;4      EDIT BY COWER  22-JUNE-75 13:05:00
;ADDED SIZEF JSYS TO FILE LIST (PAGES ONLY)
;<COWER>HELP.MAC;2	EDIT BY COWER  21-JUNE-75 17:05:43
;PUTS KEYWORDS FOUND IN FILE <SYSSUP>KEYWORDS-FOUND.LST
;<COWER>STATIS.SAV WILL GIVE STATISTICS ON THESE FILES
;<COWER>HELP.MAC;2      EDIT BY COWER  8-JUNE-75 18:05:03
;<COWER>HELP.MAC;2	EDIT BY COWER  6-JUNE-75  10:10:00
;ADDED CODE TO WRITE NON-MATCHES OUT TO FILE <SYSSUP>KEYWORDS-
;NOT-FOUND.LST - CHANGED PMAP TO WORK CORRECTLY
;<COWER>HELP.MAC;2      EDIT BY COWER  31-MAY-75  12:41:43
;ADDED VERBOSE SETTING; CHANGED FORMAT OF USER INTERFACE
;FIXED BUG WITH ONE DOCUMENT ONLY
;<SOURCES>HELP.MAC;2    EDIT BY COWER  25-MAY-75  12:45:45
;FIXED BUG IN GETTING CORRECT DOCUMENT FOR READING
;<SOURCES>HELP.MAC;1	EDIT BY COWER  24-MAY-75  17:30
;NOW REQUIRES <SYSSUP>DOC.INDEX, <SYSSUP>EXCLDE.LST, AND
;<SYSSUP>EQUAL.TXT BEFORE <OPERATOR>BUILD.SAV WILL BUILD
;CORRECT FILE <SYSSUP>DOC.INDEX
;<SOURCES>HELP.MAC;1	EDIT BY COWER   5-MAY-75 15:16
;RESET TERMINAL MODE BACK TO ORIGINAL
;<COWER>HELP.MAC;1   	EDIT BY COWER   4-MAY-75 13:00
;<COWER>HELP.MAC;1   	EDIT BY COWER	3-MAY-75 15:00

		SEARCH STENEX
;	  <DOC>  DIRECTORY  SCANNER 



;AC DEFINITIONS
A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
I=11
;STORAGE		
GARBAG:	BLOCK	1
CHAR:	BLOCK	1
TEMP:	BLOCK	1
TRAP:	BLOCK	1
FOUJFN:	BLOCK	1
ALTFLG:	BLOCK	1
PNTLST:	BLOCK	1000
LSTADR:	BLOCK	1000
IPJFN:	BLOCK	1
VERBOS: BLOCK	1
VERCNT:	BLOCK	1
MOD:	BLOCK	1		;RFMOD
INFIL:	BLOCK	1		;JFN	STORAGE
BUFF:	BLOCK	50		;STRING STORAGE
BUFF1:	BLOCK	30		;STRING STORAGE
TMP:	BLOCK	2		;FOR GTFDB WORDS
TMPJFN:	BLOCK	1		;USED FOR GNJFN
MPJFN:	BLOCK	1 
COUNT:	BLOCK	1		;COUNTERS
COUNT1:	BLOCK	1
COUNT2:	BLOCK	1
COUNT3:	BLOCK	1
COUNT4:	BLOCK	1
SAVEP2:	BLOCK	1		;INIPSI
SUPRET:	BLOCK	1	          
RETPC1:	BLOCK	1	       
COADDR:	BLOCK	1	       
NUMB:	BLOCK	1		;NUMBER ENTERED
P==17				;STACK
STPDLL==20			;STACK
STACK:	BLOCK STPDLL		;STACK

;	DEFINITIONS

	OPDEF	CALL [PUSHJ  P,0]
	OPDEF	RET  [POPJ  P,0]

;		  MACRO DEFINITIONS

;ROUTINE TO OUTPUT STRING
DEFINE	TYPE (STRING) <
	JRST	[
	HRROI	A,[ASCIZ /STRING/]
	PSOUT 	  
	JRST	.+1] >
;ROUTINE TO READ ONE CHARACTER FROM TTY
DEFINE	INPUT	<
	MOVEI	A,100
	BIN>

;OUTPUTS AN o TO FILL OUT No
DEFINE	OUT	<	
	MOVEI	A,101
	MOVEI	B,"o"
	BOUT>




;STARTS HERE
BEGIN:	MOVEI	A,BEGIN1
	MOVEM	A,RETPC1
        RESET
	MOVSI	A,100001
	HRROI	B,[ASCIZ/<SYSSUP>DOC.INDEX/]
	GTJFN
	JRST	ERROR			;CHANGE TO ERROR REPORT
	MOVE	B,[440000,,202000]
	OPENF
	JRST	ERROR			;SAME AS ABOVE
	HRLZ	A,A
	MOVE	B,[400000,,PAGE]
	MOVSI	C,100400
	PMAP
	MOVE	D,PAGES+4
	ADDI	D,777
	LSH	D,-11 
MAPPAG:	SOSG	D
	JRST	BEGIN0
	AOS	A 
	AOS	B
	PMAP
	JRST	MAPPAG
BEGIN0:	HRRI	A,400000	
	HRLI	A,10000
	CLZFF
       	SETZM	COUNT1
	SETZM	VERCNT
	SETZM	COUNT2
	MOVEI	A,101
	RFMOD				;READ MODE WORD
	MOVEM	B,MOD
	TRO	B,20  			;CONVERTING CASE ON INPUT
	STPAR				;TO UPPER CASE
	MOVE	P,[IOWD STPDLL,STACK]   ;INITIALIZE STACK
	PUSHJ	P,INIPSI		;INITIALIZE PSI ROUTINE
	MOVEI	A,100			;INPUT
	BKJFN				;BACK IT UP ONE BYTE
	JFCL				;ERROR RETURN
	PBIN				;READ IN ONE BYTE
	CAIN	A,37			;IS FIRST BYTE AN EOL?
	JRST	[PUSHJ	P,VERBO
		JRST	ASK]		;YES - ASSUME NOTHING
	PBIN				;NO - READ ANOTHER
	CAIN	A,"?"			;IS IT A ?
	JRST	[CALL 	EOL
	     	 PUSHJ	P,VERBO
		JRST	ASK]
   	MOVEI	A,100			;SETUP
	BKJFN				;BACKIT UP
	0				;ERROR
	
BEGIN1:	SETZM	COUNT4			;ZERO IT OUT
	SETZM	ALTFLG
	SETZM	TRAP
	SETZM	COUNT3			;SAME
	SETZM	COUNT			;SAME
	HRROI	A,BUFF           	;PUT STRING IN BUFF  
	MOVEI	B,^D39			;READ 39 CHARACTERS (IF NEEDED)
	MOVEI	C,0		       
	PSTIN				;READ THEM IN
BEGIN2:	MOVE	A,[POINT 7,BUFF]	;POINTER          
SKPIT1:	ILDB	B,A
	CAIG	B,"Z"
	CAIGE	B,"0"
	JRST	SKIPIT
	CAIGE	B,"A"
	CAIG	B,"9"
	JRST	SKPIT1
SKIPIT:	CAIN	B,33
	JRST	[
		PUSH	P,A
		SETOM	ALTFLG
		MOVEI	1,"$"
		PBOUT 
		POP	P,A 
		JRST	.+1]
	SETZ	B,
	DPB	B,A
	IDPB	B,A
	IDPB	B,A
	IDPB	B,A
	IDPB	B,A
	MOVE 	E,PAGES 		;OFFSET TO LOOKUP TABLE
	MOVEI	E,PAGES(5)		;AT BEGIN OF PAGES TABLE
	MOVEI	7,LSTADR
	JRST	CMPAR
CMPAR5:	CAIN	7,LSTADR
	JRST	NONE
	SETZM	0(7)
	MOVEI	4,PNTLST
	MOVEI	7,LSTADR
PNTR0:	SKIPN	5,0(7)
	JRST	SORT
	SOS	5
	HRRZ	5,0(5)
	ADDI	5,PAGES
	ADD 	5,PAGES+1
PNTR1:	SKIPN	3,0(5)
	JRST	PNTR2
	MOVEM	3,0(4)
	AOS	4
	AOS	5
	JRST	PNTR1
PNTR2:	AOS	7
	JRST	PNTR0
SORT:	SETZM	0(4)
	MOVEI	4,PNTLST-1
SORT3:	AOS	4
	SKIPN	3,0(4)
	JRST	DELET 
	MOVEI	2,0(3)
SORT0:	MOVEI	5,0(4)
SORT1:	AOS	5
	SKIPN	1,0(5)
	JRST	SORT3
	CAILE	2,0(1)
	JRST	EXCH1
	CAIE	2,0(1)
	JRST	SORT1
	CAML	3,1
	JRST	SORT1
EXCH1:	EXCH	3,0(5)
	MOVEM	1,0(4)
	MOVEI	2,0(3)
	JRST	SORT1
DELET:	MOVEI	5,PNTLST
	MOVEI	4,1(5)
DELET1:	SKIPN	2,0(4)
	JRST	[SETZM	1(5)
		MOVEI	5,PNTLST
		JRST	GOD]
	CAME	2,0(5)
	JRST	[AOS	5
		MOVEM	2,0(5)
		AOS	4
		JRST	DELET1]
	AOS	4
	JRST	DELET1
CMPAR:	HLRZ	A,0(E)			;GET KEYWORD POINTER
	CAIN	A,-1			;END OF FILE
	JRST	CMPAR5			;SORRY BUB
	HLL  	A,[POINT 7,0]		;BASIC POINTER
	ADD	A,PAGES+2	        ;PUT  6  TO TOP OF KEYWRD TABLE
	ADDI	A,PAGES
	AOS	E			;BUMP TO NXT LOOKUP TAB ENTRY
	MOVE	B,[POINT 7,BUFF]
CMPAR1:	ILDB C,A			;FROM 1 TO 3
	ILDB D,B			;FROM 2 TO 4
	CAME C,D			;ARE THEY EQUAL?
	 JRST CMPAR2   			;NO TRY NEXT KEYWRD
	JUMPN C,CMPAR1			;SO FAR SO GOOD  QUIT ON NULL
	SOS	E			;5 PNTS TO LOOKUP TABLE ENTRY
	HRRZ	E,0(E)			;GET ADDRESS OF 1ST ENTRY
	ADDI	E,PAGES	  		;CORRECT ADDRESS	       
	ADD	E,PAGES+1 		;     
GOD:	MOVE	F,PAGES+3		;GET ADDRESS OF STRING SPACE
	ADDI	F,PAGES
	MOVEI	A,NUMBER		;WHERE TO GO ON CONTROL O
	MOVEM	E,H 			;SAVE START OF FILE POINTERS
	MOVEM	A,RETPC1		;PUT IT AWAY
	SKIPN	VERCNT
	PUSHJ	P,VERBO
	SKIPN	TRAP
 	CALL	TYPEIT			;TYPE OUT HEADER (FROM BUFF)
HOW2:	SKIPE	B,0(E)
	CALL 	EOL
     	SKIPN	B,0(E)
	JRST	[			;COULDN'T
		MOVEI	D,1		;FIRST TIME THRU LOOP?
		CAMN	D,COUNT4	;IF COUNT4=1 YES
		JRST	ONE1		;SPECAIL QUESTION (ONLY ONE)
		SKIPN	TRAP
		JRST	NUMBER		;NOT FIRST TIME THRU
		JRST	NUMBE1]
	AOS	COUNT4			;COUNTER
	CALL	NUMJFN			;TYPE NAME OF JFN
	AOS	E
	JRST	HOW2
CMPAR2:	SKIPN	4
	SKIPN	ALTFLG
	JRST	CMPAR
	MOVEM	5,0(7)
	AOS	7
	JRST	CMPAR



AGAIN:	MOVE	B,NUMB
	ADD	B,H 			;FIND POINTER TO FILE NAME
	MOVE	B,0(B)			;GET STRING POINTER
	ADD	B,F 			;CORRECTING ADDRESS
     	MOVEI   A,TABLE  
	GTJFN
	JRST	NONE			;SHOULDN'T FAIL
	HRRZM	A,INFIL 
	CALL EOL	  		;PRINT TWO EOLS
	JRST	OPEN	   		;GO PRINT IT

ONE1:	CALL	EOL
     	SKIPE	VERBOS
	JRST	[
		HRROI	A,[ASCIZ /That's all the documentation we have on /]
		PSOUT
		CALL PRINT
		CALL EOL  
	     	HRROI A,[ASCIZ /Would you like to read it? /]
       		PSOUT
		JRST .+2]
ONE2:	TYPE	<Like to read it? >
	INPUT				;BIN
	CAIN	B,"Y"			;IS IT A YES
	JRST	[			;YES
		HRROI A,[ASCIZ /es/]	;FILL OUT Y WITH ES
		PSOUT
		SETZ	A,		;SETUP NUMB
		MOVEM	A,NUMB
		JRST	AGAIN]
	CAIN	B,"N"			;IS IT A N
	JRST  [				;YES
		OUT			;FILL OUT N WITH o
		JRST	ASK3]		;GO GET ANOTHER SUBJECT
	CALL EOL			;NEITHER Y OR N
	CALL YORN			;ASK FOR Y OR N
	JRST	ONE2			;TRY AGAIN


NUMBER:	MOVEI	A,NUMBE1		;WHERE TO GO
	MOVEM	A,RETPC1		;PUT IT AWAY
	MOVEI	A,NUMBE1		;WHERE TO GO ONE ^O
	MOVEM	A,RETPC1		;PUT IT AWAY
	SKIPE	VERBOS
	JRST	[
		HRROI	A,[ASCIZ /
Enter the number of the document you wish to read,
carriage return for none. /]
		PSOUT
		JRST	NUMBE1]

	 TYPE	<
Number of document, <cr> for none. >  
NUMBE1:	CALL EOL			;PRINT EOL
	TYPE	Number? 		;ASK FOR NUMBER
NUMBE2:	PBIN			        ;READ IT IN
	CAIN  	A,37			;NO NUMBER
	JRST	ASK3			;GET ANOTHER SUBJECT
	CAIN	A,"?"
	JRST	[
		SETOM 	TRAP
		SETZM	COUNT4
		JRST	BEGIN2]
 	MOVEI	A,100			;SOMETHING OTHER THAN EOL ENTERED
	BKJFN				;BACK IT UP ONE BYTE
	JFCL				;ERROR
	MOVEI	C,^D10			;BASE 10
	NIN				;READ IT IN
	JRST	[		     		;DID NOT ENTER A NUMBER
		HRROI	A,[ASCIZ /	  
Please enter a number/]
		PSOUT
		JRST   	NUMBE1]			;TRY AGAIN
       	CAIN  	B,0
	JRST	TOSMAL
	CAMLE	B,COUNT4
	JRST	TOOBIG
	ADD	B,COUNT1			 
	SUBI	B,1
	MOVEM	B,NUMB
	MOVE	A,INFIL 
	RLJFN			;RELEASE IT SO WE CAN GET IT
	JFCL 		        ;ERROR
	AOS 	COUNT1		   
	JRST	AGAIN  		;GO GET IT


TOSMAL:	SKIPE	VERBOS
	JRST	[
	HRROI A,[ASCIZ /The number must be greater than 0/]
	PSOUT
	JRST	.+2]
	TYPE	<Must be greater than 0>
	JRST 	NUMBE1
TOOBIG:	SKIPE	VERBOS		;PRINT EOL
	JRST	[
	HRROI A,[ASCIZ /The number must be /]
	PSOUT
	JRST	.+2]
	TYPE	<Must be > 
       	MOVEI	A,101		;PRIMARY OUTPUT
	MOVE	B,COUNT4	;GET NUMBER OF TIMES THRU GTJFN+GNJFN
	MOVEI	C,^D10		;BASE 10
	NOUT			;OUT IT
	JRST	ERROR		;ERROR????
	TYPE	< or less.>
	JRST	NUMBE1 		;TRY AGAIN
	
QUEST:	CALL	EOL		;PRINT EOL
	HRROI	B,[ASCIZ /<DOC>A-GENERAL.HELP/]	;FILE NAME
	MOVSI	A,100001	;OLD FILE AND STRING POINTER
	GTJFN			;GET JOB FILE NUMBER
	JRST	ERR1		;ERROR RETURN
	MOVEM	A,INFIL 	;SAVE JFN
OPEN:	MOVE	A,INFIL 
	MOVSI	B,7B23		;READ 7 BIT BYTES
	HRRI	B,201000	;OPEN FOR INPUT AND WAIT FOR FILE
	OPENF
	JRST	ERR1		;ERROR RETURN
	MOVEI	A,DONE
	MOVEM	A,RETPC1
LOOP:	MOVE	A,INFIL		;GET INPUT JFN
	BIN			;INPUT A CHARACTER
	MOVE	C,B		;SAVE CHARACTER
	GTSTS			;GET FILE STATUS
	TLNE	B,1000		;CHECK FOR END OF FILE
	JRST	DONE		;IF END WE ARE FINISHED
	MOVEI	A,101		;SET TO PRIMARY OUTPUT DEVICE
	MOVE	B,C		;PUT CHARACTER BACK INTO TWO
	BOUT			;AND PUT IT OUT
	JRST	LOOP		;GO GET ANOTHER CHARACTER


DONE:	HRLZI	A,400000       
	HRR	A,INFIL
	CLOSF			;CLOSE FILE DO NOT RELEASE JFN
	JRST	ERROR		;FAILED
	SKIPN	COUNT3
	JRST   [MOVEI 	D,1	;TEST
       		CAMN   	D,COUNT4;WAS THIS FIRST TIME THRU GTJFN STUFF
       		JRST   	ASK3	;YES GET ANOTHER SUBJECT
		JRST   	ASK4]	;NO SEE IF WANTS TO READ ANY MORE
       	JRST 	[
        MOVEI	D,0
	CAMN 	D,COUNT3
	JRST 	ASK4
	JRST 	ASK3]

ERR1:	TYPE	<		;COULD NOT OPEN
Unable to read that file >	;TELL PERSON
	JRST	NUMBER		;TRY TO GET ANOTHER ONE

ASK:	SKIPE	VERBOS
	JRST	[
		HRROI	A,[ASCIZ /Would you like to read A-GENERAL.HELP? /]
		PSOUT
		JRST	.+2]
	TYPE	<Would you like general help? >
	INPUT
	CAIN	B,"Y"
	JRST	[
		HRROI A,[ASCIZ /es/]
		PSOUT
		AOS	COUNT3	;USED FOR WHERE TO GO AFTER
		CALL	EOL
		JRST	QUEST]	;GO TYPE IT OUT
	CAIN	B,"N"
	JRST	[
		OUT
		JRST	ASK3]
	CALL	YORN		;PLEASE ENTER YES OT NO
	JRST	ASK		;TRY AGAIN
ASK1:	TYPE	<
Would you like to read it? >
	INPUT			;BIN
	CAIN	B,"Y"
	JRST	[    
		HRROI A,[ASCIZ /es/]
		PSOUT
		CALL	EOL
		MOVE	A,INFIL 
		JRST	OPEN]

	CAIN	B,"N"
	JRST	[
		 OUT
		 CALL	EOL
	         RET]
	CALL	YORN			;PRINT Y OR N PROMPT
	CALL	EOL			;PRINT EOL
	JRST	ASK1
ASK3:		SETZM	COUNT1
		SKIPE	VERBOS
		JRST	[HRROI	A,[ASCIZ /
Enter the subject you would like help on,
carriage return for none /]
			 PSOUT
			 JRST	.+2]
		TYPE	<
Subject, <cr> for none >
     		PBIN
		CAIN	A,37
		JRST THANKS		;GO END IT
		MOVEI	A,100
		BKJFN
		JFCL
		JRST	BEGIN1 
ASK4:		CALL EOL
		SKIPE	VERBOS
		JRST	[
		HRROI 1,[ASCIZ /Would you like to read any more 
on the subject /]
		PSOUT
		CALL	PRINT
		HRROI A,[ASCIZ /? /]
		PSOUT
		JRST	.+2]
		TYPE	<Read any more? > 
		INPUT
		CAIN B,"Y"
		JRST	[HRROI A,[ASCIZ /es/]
			PSOUT
			SETZM	COUNT
			SETZM	COUNT1
			CALL 	EOL
			HRROI A,[ASCIZ /Number (? to retype list) /]
			PSOUT
			JRST   NUMBE2]
		CAIN	B,"N"
		JRST	[
			OUT
			JRST	ASK3] 
		CALL	EOL
       		CALL	YORN
       		JRST	 ASK4


;GTJFN FAILED TELL PERSON SO
NONE:	SKIPN	VERCNT
     	CALL   	VERBO
     	SKIPE	VERBOS
	JRST	[
      	HRROI	A,[ASCIZ/
We do not have any information on /]
	PSOUT
	CALL	PRINT
	JRST	NONE1]
	TYPE	<
No info on >
	CALL PRINT
NONE1: 	MOVSI	A,100001
       	HRROI	B,[ASCIZ /<SYSSUP>KEYWORDS-NOT-FOUND.LST/]
	GTJFN
      	JRST   ERROR
	MOVEM	A,MPJFN
	MOVE	B,[070000,,023000]
	OPENF
	JRST	ERROR
	MOVE	A,MPJFN
	HRROI	B,BUFF
	SETZ	C,
	SOUT
	MOVE 	A,MPJFN
	MOVEI	B,37
	BOUT
	MOVE	A,MPJFN
	CLOSF
	JRST	ERROR
NONE2:	RLJFN
	JRST	ERROR
NONE3:	SETZM	BUFF		  ;CLEAR BUFFER
	MOVE	E,[BUFF,,BUFF+1]  ;POINTER  (MOVE BUFF TO BUFF+1)
	BLT	E,BUFF+8		  ;KEEP UP UNTIL BUFF+8
      	AOS	COUNT2		      	;ONLY PUT PERSON HERE A
	MOVEI	A,2			;COUPLE OF TIMES
	CAML 	A,COUNT2
	JRST	[
		CALL	EOL
       		JRST	ASK]		;ASK ABOUT GENERAL HELP
	JRST	ASK3			;ASK FOR SUBJECT

;ROUTINE TO PRINT HEADER
TYPEIT:	SKIPE	VERBOS
	JRST	[
	SKIPE	ALTFLG
	CALL	EOL
       	HRROI A,[ASCIZ /On /]
	PSOUT
	CALL	PRINT
  	HRROI A,[ASCIZ / the following is available:/]
	PSOUT
	JRST	.+2]
	SKIPE	ALTFLG
	CALL	EOL
	TYPE	<Available Documentation:>
	CALL	GETIT1
	MOVE	A,FOUJFN
	HRROI	B,BUFF
	SETZ	C,
	SOUT     
	MOVE	A,FOUJFN
	MOVEI	B,37
	BOUT
	MOVE	A,FOUJFN
	CLOSF
	JRST	ERROR
	MOVE	A,FOUJFN
	RLJFN
	JRST	ERROR
	RET
GETIT1:	MOVSI	A,100001
       	HRROI	B,[ASCIZ /<SYSSUP>KEYWORDS-FOUND.LST/]
	GTJFN
       	JRST 	ERROR
	MOVEM	A,FOUJFN
	MOVE	B,[070000,,023000]
	OPENF
	JRST	ERROR
	RET

;ROUTINE TO PRINT JFNS WITH NUMBER ASSOCIATION
NUMJFN:	TYPE	<	>
	CALL NUMOUT
	TYPE	<. >
	MOVEI	A,101
	SETZM	C
	MOVE	B,0(E)			;GET BYTE POINTER
	ADD	B,F	      		;CORRECT TABLE OFFSET
	MOVEM	B,TEMP
	SOUT
	MOVE	B,TEMP
	MOVEI	A,TABLE
	GTJFN
	JRST	ERROR
	MOVEM	A,GARBAG		;JFN
	MOVE	2,[XWD 2,11]		;READ 2 WORDS STARTING AT 11
	MOVEI	3,TMP			;PUT THEM HERE
	GTFDB				;GET IT
	MOVE	3,TMP+1			;BYTE COUNT
	MOVEM	3,CHAR			;PUT IT AWAY
	LDB	4,[POINT 3,TMP,11]	;GET BITS 6-11
	CAIE	4,7			;7 BIT BYTES
	JRST	[MOVEI	3,5    		;NO MUST BE 36
		 IMUL 	3,TMP+1 	;36 BIT BYTES*5=CHAR CNT
		MOVEM	3,CHAR 		;PUT IT AWAY
		JRST	.+1]		;GO ON
OVER:	MOVEI	A,11			;TAB
	PBOUT
	SKIPE	VERBOS
	JRST	[
	MOVEI	A,100
	RFPOS
	HRRZ	B,B
	CAIN	B,30
	JRST	OVER
	JRST	.+1]
	MOVEI	A,101
	MOVE 	B,CHAR
	MOVEI	C,^D10
	NOUT
	JRST	ERROR
	MOVEI	1,11
	PBOUT
	SKIPE	VERBOS
	TYPE	Characters
	SKIPN	VERBOS
	TYPE	Chars
	MOVE	A,GARBAG
	RLJFN
	JRST	ERROR
	RET

;ROUTINE TO PRINT SUBJECT ENTERED
PRINT:	MOVEI  	A,101
	HRRO   	B,[POINT 7,BUFF]
	SETZ   	C,
	SOUT
	RET

;ROUTINE TO PRINT NUMBER OF DOCUMENT
NUMOUT:	MOVEI	A,101
	MOVE 	B,COUNT4
	MOVEI	C,^D10
	NOUT
	JRST	ERROR
	RET

;ROUTINE TO PRINT EOL
EOL:	MOVEI	A,37
	PBOUT
	RET


;ROUTINE TO PRINT YES OR NO PROMPT  
YORN:	TYPE	<
Please enter Y or N 
>
	RET

;PRINTS THANKYOU AT END
THANKS:	MOVEI	A,100
	MOVE	B,MOD
	STPAR
	CALL EOL
	TYPE	<Thank you.>
	RESET
	HALTF
ERROR:	CALL EOL
	TYPE	<
ERROR: Please sndmsg to COWER       
with brief explanation of what happened. >    
	CALL 	EOL  
	JRST	THANKS

;PSI STUFF
LEVTAB:	COADDR
	COADDR
	COADDR
CHNTAB:	XWD	1,TIMOUT
	XWD	1,MONERR
	REPEAT	^D33,<0>
TIMOUT:	MOVEI	A,1
	DISMS

MONERR:	JFCL
	TYPE	<
..Aborting typeout..
>
	MOVE	G,RETPC1
	MOVEM	G,COADDR
	DEBRK
INIPSI:	RESET
	MOVEI	A,400000
	MOVE	B,[XWD	LEVTAB,CHNTAB]
	SIR
	EIR
	MOVSI	B,(1B0+1B1+1B2)
	AIC
	MOVE	A,[XWD 17,1]
	ATI 	
	SETZM	SUPRET
	POPJ	P,
SUPOUT:	PUSH 	P,A
	PUSH	P,B
	MOVEI	A,101
	SOBE
	SKIPA
	JRST	SUPDON
	MOVEI	A,101
	CFOBF
	MOVEI	A,17 
	PBOUT
	CALL EOL
VERBO:	AOS	VERCNT
	SKIPE	ALTFLG
	CALL	EOL
	TYPE	<Verbose mode (Y,N, or ?)  >
VERBO1:	INPUT
	CAIN	B,"Y"
	JRST	[HRROI A,[ASCIZ /es/]
		PSOUT
		 SETOM	VERBOS 
		 CALL EOL
		 POPJ	P,]
	CAIN	B,"N"
	JRST	[OUT       
		 SETZM	VERBOS 
		 CALL EOL
		 POPJ	P,]
	CAIN	B,"?"
	JRST	[HRROI	A,[ASCIZ / 	 
Y will put program in VERBOSE MODE (many more noise
words), N eliminates most noise words./]
		PSOUT
		CALL	EOL
		HRROI A,[ASCIZ /Verbose mode? /]
		PSOUT
		JRST	VERBO1]
	CALL EOL
	CALL YORN
	JRST VERBO
SUPDON:	SKIPN SUPRET
	JRST	SUPEND
	MOVE	I,SUPRET
	MOVEM   I,RETPC1
SUPEND:	POP	P,B
	POP	P,A
	DEBRK
;LONG GTJFN TABLE
TABLE: 	1B2     		;OLD FILE ONLY
	XWD 377777,377777
	0			;DEFAULT
	POINT 7,[ASCIZ /DOC/]	;DIRECTORY
	0			;SUBJECT WAS PUT HERE
	0			;EXTENSION
	0			;DEFAULT
	0			;DEFAULT
	0			;DEFAULT


	LIT
PAGE=<<.+141>/1000>+1
	LOC	PAGE*1000
PAGES=.
	BLOCK	6000


	END	BEGIN




