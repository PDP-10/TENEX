;<SOURCES>GLUP.MAC;1    27-JUN-75 08:24:57    EDIT BY HEDBERG
	TITLE	GLUP - GUEST LOG UPDATER
	SEARCH	STENEX

P==17
OPDEF	CALL	[PUSHJ	P,0]
OPDEF	RET	[POPJ	P,0]

START:	MOVE	P,[IOWD 40,STACK]

	SETO	2,
	SETZ	4,
	ODCNV
	TRNE	3,-1		;IS IT MONDAY?
	 JRST	QUIT		;NO QUIT
	MOVSI	1,100001
	HRROI	2,[ASCIZ \<SYSTEM>GUESTS.LOG\]
	GTJFN
	 JRST	MAKONE		;WE DON'T HAVE A LOG, LETS MAKE ONE
	PUSH	P,1
	MOVSI	1,400001
	HRROI	2,[ASCIZ \<SYSTEM>GUESTS.OLDLOG\]
	GTJFN
	 JRST	QUIT
	MOVE	2,1
	POP	P,1
	RNAMF			;WE HAVE A LOG, RENAME IT
	 JRST	QUIT
	HRROI	1,[ASCIZ \
Guest log file renamed
\]
	PSOUT

MAKONE:	MOVSI	1,400001
	HRROI	2,[ASCIZ \<SYSTEM>GUESTS.LOG;P777744\]
	GTJFN
	 JRST	QUIT
	MOVE	2,[7B5!3B20]
	OPENF
	 JRST	QUIT
	MOVEI	2,40
	BOUT
	CLOSF
	 JFCL

QUIT:	RESET
	HALTF
	JRST	.-1

STACK:	BLOCK	40
LITS:	LIT
	END	START
