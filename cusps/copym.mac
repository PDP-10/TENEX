
;2 APR 72, 1508:

	TITLE COPYM

	MLON

	SEARCH	STENEX		;ADDED  06MAR74  SRS

P=17
F==0		;FLAGS
SAVB=1B19	;SAVE FILE BEING XFERRED
OPDEF CALL [PUSHJ P,]
OPDEF RET [POPJ P,]
NPDL==40
DTABKS=177		;NUMBER OF WORDS IN DTA BLOCK
EOL=37
PGBF1==200		;FULL PAGE BUFFER, FOR MAPPING
PGBF1A=<PGBF1>B26

;JSYS ERROR RETURN HANDLING
;FIRST ARG GIVES LOC OF ERROR NUMBER, 0 MEANS DO GETER TO GET IT
;SECOND ARG IS WHERE TO GO AFTER PRINTING MESSAGE

	DEFINE JERR (A,L)
<	UJERR [XWD A,L]>

OPDEF UJERR [1B8]

	LOC 41
	PUSHJ P,UUOH
	RELOC

EVEC:	JRST START
	JRST EOF

START:	MOVE P,[IOWD NPDL,PDL]
	RESET
	SETZ F,
	HRROI 1,[ASCIZ /
FILE NAMES FROM FILE: /]
	PSOUT
	MOVSI 1,(1B2+3B17)
	MOVE 2,[XWD 100,101]
	GTJFN
	JRST BADCJ1
	MOVEM 1,IOJFNS
	MOVEI 1,101
	BKJFN
	JFCL
	PBIN			;GET TERMINATOR
	CAIE 1," "		;SPACE OR ESC DO NOT CONFIRM
	CAIN 1,33
	JRST .-3
	CAIE 1,EOL		;EOL CONFIRMS
	JRST BADCJ1		;ANYTHING ELSE ABORTS
	MOVE 1,IOJFNS
	DVCHR			;FIND OUT ABOUT INPUT FILE
	LDB 2,[POINT 16,1,17]	;DEVICE NUMBER
	CAIE 2,12		;TTY?
	JRST CMFI1		;NO
	MOVEI 1,400000(1)	;YES, CONSTRUCT IN AND OUT DESIGNATORS
	HRLI 1,0(1)
	EXCH 1,IOJFNS
	RLJFN			;AND RELEASE JFN JUST GIVEN
	JFCL
	JRST CMFI2

CMFI1:	MOVE 1,IOJFNS		;NON-TTY FILE, OPEN IT
	MOVE 2,[7B5+1B19]
	OPENF
	JERR 1,START		;WOULDN'T OPEN
	HRLI 1,377777		;THIS FILE FOR INPUT, NOTHING FOR OUTPUT
	MOVSM 1,IOJFNS
CMFI2:	CALL GETDEV		;GET THE OUTPUT DEVICE
	GJINF
	MOVE 1,DIRPTR
	DIRST			;SETUP DEFAULT INPUT DIRECTORY
	JERR [DESX1],START	;IMPOSSIBLE
	JRST INNM1

BADCJ1:	HRROI 1,[ASCIZ / XXX/]
	PSOUT
	JRST START

GETDEV:	HRROI 1,[ASCIZ /
OUTPUT TO /]
	PSOUT
	SETZM OUTDVP
	MOVE 1,[XWD OUTDVP,OUTDVP+1]
	BLT 1,OUTDVP+6		;CLEAR OUTPUT JFN TABLE TO DEFAULT

;THE FOLLOWING WON'T WORK UNTIL OUTPUT STARS WORK IN GTJFN

REPEAT 0,<
	MOVEI 1,[1B0+3B12	;OUTPUT, PERMIT STARS
		XWD 100,101
		0		;DEV
		0		;DIR
		XWD -1,[ASCIZ /*/] ;NAME
		XWD -1,[ASCIZ /*/] ;EXT
		0
		0]
	SETZ 2,
	GTJFN
	JERR 1,GETDEV
	MOVEM 1,IOUJFN
	MOVE 7,1
	MOVEI 2,0(1)
	CALL SETODF		;GET STRINGS FOR EVERYTHING NOT STAR
	MOVEI 1,0(7)
	RLJFN
	JFCL
>
;INSTEAD, THE FOLLOWING WILL ACCEPT A DEVICE NAME ONLY

	MOVEI 1,100
	HRROI 2,OUTDEV
	MOVEM 2,OUTDVP
	MOVEI 3,50
	MOVEI 4,EOL		;INPUT TO EOL
	SIN
	MOVE 6,[POINT 7,OUTDEV,-1]
	ILDB 1,6
	CAIE 1,":"
	CAIN 1,EOL
	SETZ 1,
	JUMPN 1,.-4
	DPB 1,6			;PUT NULL AT END
	MOVSI 7,(7B5)		;ASSUME * FOR NAME AND EXT AND VERS
	MOVEM 7,IOUJFN

	TLNE 7,(1B1)		;* FOR DEVICE?
	JRST GETDEV		;LOSE
	MOVSI 1,(44B5)
	MOVEM 1,BYTSIZ		;DEFAULT BYTE SIZE
	HRROI 1,OUTDEV
	STDEV
	JERR 2,GETDEV		;CAN ONLY HAPPEN WITH FAKE GTJFN
	SETZM DEVDSG
	MOVEI 1,1000		;BUFFER 1000 WDS UNLESS DTA
	MOVEM 1,DBSIZ
	MOVE 1,2
	LDB 2,[POINT 16,1,17]	;DEVICE NUMBER
	MOVEM 2,DEVNUM
	CAIE 2,3		;DECTAPE?
	RET			;NO
	MOVEM 1,DEVDSG
	MOUNT
	JERR 1,.+1
GETDV2:	HRROI 1,[ASCIZ /
CLEAR DIRECTORY? /]
	PSOUT
	SETZ 4,
GETDV3:	PBIN
	CAIE 1,EOL		;CONFIRMED?
	JRST [	MOVE 4,1	;NO, SAVE CHAR
		JRST GETDV3]
	CAIN 4,"N"
	JRST GETDV1
	CAIE 4,"Y"
	JRST GETDV2		;MUST BE Y OR N
	MOVE 1,DEVDSG
	INIDR
	JERR 1,.+1
GETDV1:	MOVEI 1,^D574		;NUMBER OF BLOCKS ON DTA
	MOVEM 1,DTAFRB
	MOVEI 1,^D22		;MAX NUMBER OF FILES IN DTA DIRECT
	MOVEM 1,DTAFRE
	MOVEI 1,DTABKS
	MOVEM 1,DBSIZ		;BUFFER ONE BLOCK FOR DTA
	RET

;STORE STRINGS FOR OUTPUT FROM JFN

SETODF:	HRROI 1,OUTDEV
	MOVSI 3,(1B2)
	TLNE 7,(1B0)
	JRST .+3
	MOVEM 1,OUTDVP
	JFNS
	HRROI 1,OUTDIR
	MOVSI 3,(1B5)
	TLNE 7,(1B2)
	JRST .+3
	MOVEM 1,OUTDRP
	JFNS
	HRROI 1,OUTNAM
	MOVSI 3,(1B8)
	TLNE 7,(1B3)
	JRST .+3
	MOVEM 1,OUTNMP
	JFNS
	HRROI 1,OUTEXT
	MOVSI 3,(1B11)
	TLNE 7,(1B4)
	JRST .+3
	MOVEM 1,OUTEXP
	JFNS
	RET

;MAIN COPY CYCLE

INNM1:	MOVEI 2,"_"
	HLRZ 1,IOJFNS
	TRNE 1,400000		;TTY INPUT?
	BOUT			;YES, TYPE READY CHAR
	HLRZ 1,IOJFNS		;SOURCE OF FILE NUMES
	BIN
	CAIE 2,15		;FLUSH RANDOM STUFF
	CAIN 2,12
	JRST INNM1
	CAIN 2,EOL
	JRST INNM1
	CAIN 2,"<"
	JRST INDIR		;DIRECTORY NAME COMING
	CAIN 2,"Z"-100		;CTRL-Z MEANS EOF FROM TTY
	JRST DONE
	GTSTS
	TLNE 2,(1B8)		;AT EOF?
	JRST DONE		;YES
	BKJFN			;UNREAD THE CHARACTER
	JFCL
	MOVEI 1,GJINTB
	SETZ 2,
	GTJFN			;HAVE GTJFN READ THE NAME
	JERR 1,INNM1
	MOVEM 1,INJFN
	HLRZ 1,IOJFNS
	BKJFN
	JFCL
	BIN			;GET THE TERMINATOR
	CAIE 2," "
	CAIN 2,33
	JRST .-3
	CAIN 2,15
	MOVEI 2,EOL
	CAIE 2,EOL
	JRST [	HRROI 1,[ASCIZ / XXX
/]
		PSOUT
		JRST BADIJ]

GRP1:	HRROI 1,NAME
	HRRZ 2,INJFN
	MOVE 3,[2B2+2B5+1B8+1B11+1B14+1]
	JFNS			;PRINT WHOLE NAME
	HRROI 1,NAME
	PSOUT			;PRINT NAME TO TTY
	SETZM DATABF
	HRROI 1,DATABF
	HRRZ 2,INJFN
	MOVSI 3,(1B11)
	JFNS			;GET EXTENSION STRING
	MOVE 1,DATABF
	CAME 1,[ASCIZ /SAV/]	;SAVE FILE?
	JRST COPS1
	HRRZ 1,INJFN		;YES, DECIDE WHETHER THIS FILE SHOULD
	DVCHR			;BE COPIED NORMALLY OR VIA A GET/SAVE
	TLNE 2,777		;IF SOURCE IS NOT DSK,
	JRST COPS1		;DO NORMAL COPY
	HRRZ 1,INJFN
	MOVE 2,[XWD 2,11]
	MOVEI 3,6		;IF SOURCE IS DSK, SEE IF FILE
	GTFDB			;IS OLD STYLE SSAVE
	TLC 6,(44B11)		;SE IF 36 BIT BYTES
	TLNE 6,(77B11)
	JRST COPS1		;NO (STRANGE SAVE FILE??)
	HRREI 6,-1(6)		;SAV OR NEW STYLE SSAVE HAS AT LEAST
	ASH 6,^D9		;512*(NUMBER PAGES -1) WORDS
	CAIGE 7,0(6)		;OLD STYLE ALWAYS HAS LESS
	JRST COPSAV		;OLD STYLE - GO GET/SAV
COPS1:	HRRZ 1,INJFN
	MOVE 2,BYTSIZ
	TRO 2,1B19		;READ
	OPENF			;OPEN
	JRST [	HRRZ 1,INJFN	;FAILED TO OPEN, TRY THAWED ACCESS
		MOVE 2,BYTSIZ
		TLO 2,1B19+1B25	;THAWED BIT
		OPENF
		JERR 1,BADIJ	;STILL FAILS
		JRST .+1]

FULL1:	SKIPN DEVDSG		;DISK?
	JRST COPY3		;YES, NO TEST FOR SIZE NEEDED
	HRRZ 1,INJFN
	DVCHR
	TLNE 1,777		;DISK INPUT?
	JRST COPY3
	HRRZ 1,INJFN		;YES, CAN FIND OUT SIZE OF FILE
	MOVE 2,[XWD 2,11]
	MOVEI 3,6
	GTFDB
	LDB 1,[POINT 6,6,11]	;BYTE SIZE
	MOVEI 2,^D36
	IDIVI 2,0(1)		;GIVES BYTES/WD
	EXCH 2,7
	ADDI 2,-1(7)		;ROUND UP BYTE COUNT TO NEXT FULL WORD
	IDIVI 2,0(7)		;GIVES WORDS IN FILE
COPY4:	ADDI 2,DTABKS-1		;ROUND UP TO INTEGER NUMBER
	IDIVI 2,DTABKS		;OF DTA BLOCKS
	SOSL DTAFRE		;ROOM IN DIRECTORY?
	CAMLE 2,DTAFRB		;ROOM ON TAPE?
	JRST FULL		;NO
	MOVN 2,2
	ADDM 2,DTAFRB
COPY3:	SETCM 7,IOUJFN		;COMPLEMENT * FLAGS
	HRRZ 2,INJFN
	CALL SETODF		;FILL IN ALL STRINGS HAVING *'S
	HLLZS GJOUTB		;USE 0 FOR DEFAULT VERSION
	HRRZ 1,INJFN
	DVCHR
	TLNN 1,777		;DSK INPUT, AND
	TLNE 7,(1B5)		;* FOR VERSION?
	JRST COPY2		;NO
	HRROI 1,OUTVER		;YES, GET VERSION NUMBER IN STRING
	MOVSI 3,(1B14)
	HRRZ 2,INJFN
	JFNS
	HRROI 1,OUTVER
	MOVEI 3,^D10
	NIN			;CONVERT STRING TO BINARY
	JERR 3,.+1
	HRRM 2,GJOUTB

COPY2:	SKIPN DEVDSG		;DSK?
	JRST .+5		;YES
	MOVSI 1,(177B13)	;NO, TRUNCATE NAME
	ANDCAM 1,OUTNAM+1	;TO SIX CHARS
	MOVEI 1,177B27		;AND EXT TO 3 CHARS
	ANDCAM 1,OUTEXT
	MOVEI 1,GJOUTB
	SETZ 2,
	GTJFN
	JERR 1,EOF2
	MOVEM 1,OUJFN
	HRROI 1,[ASCIZ / (TO) /]
	PSOUT
	MOVEI 1,101
	HRRZ 2,OUJFN
	MOVE 3,[2B2+2B5+1B8+1B11+1B14+1]
	JFNS			;PRINT FINAL OUTPUT NAME
	TRNE F,SAVB		;SAVE FILE?
	JRST COPS3		;YES, SPECIAL COPY ACTION
	MOVE 1,OUJFN
	MOVE 2,BYTSIZ
	TRO 2,1B20		;WRITE
	OPENF
	JERR 1,EOF
	LDB 1,[POINT 6,BYTSIZ,5]
	ROT 1,-^D12
	HRRI 1,DATABF-1		;CONSTRUCT BYTE PTR FOR SIN/SOUT
	MOVEM 1,DATAPT
COPY1:	HRRZ 1,INJFN
	GTSTS
	TLNE 2,(1B8)		;DONE WITH INPUT?
	JRST EOF		;YES
	MOVE 2,DATAPT
	MOVN 3,DBSIZ
	SIN
	ADD 3,DBSIZ		;COMPUTE NUMBER COPIED
	JUMPE 3,COPY1		;COULD BE NONE IF EOF HAPPENED
	MOVN 3,3
	MOVE 2,DATAPT
	MOVE 1,OUJFN
	SOUT
	JRST COPY1

BADIJ:	MOVE 1,INJFN
	RLJFN
	JFCL
	JRST INNM1

;COPY A SAVE FILE BY DOING A GET AND A SAVE
;IF SOURCE IS DISK.
;WILL THUS MAKE SSAVE FILES WHICH CAN'T LIVE ON DTA INTO SAV FILES
;WHICH CAN

COPSAV:	TRO F,SAVB
	MOVSI 1,(1B2+1B17)
	HRROI 2,NAME		;NAME STRING FROM JFNS
	GTJFN			;GET SEPARATE JFN FOR 'GET'
	JERR 1,EOF2
	MOVEM 1,SAVJFN
	SETZ 1,
	CFORK			;CREATE A FORK TO DO GET AND SAVE IN
	JERR 1,EOF2
	MOVEM 1,FORKH
	MOVSI 1,0(1)
	HRR 1,SAVJFN
	GET
	SKIPN DEVDSG		;DTA OUTPUT?
	JRST COPY3		;NO, DON'T NEED TO EST SIZE
FULL2:	MOVS 1,FORKH
	MOVSI 5,-1000		;LOOP OVER ALL PAGES IN FORK
	MOVEI 6,1000		;ASSUME 1 WD/PG OVERHEAD
COPS2:	HRRI 1,0(5)
	RPACS
	TLNN 2,(1B5)		;PAGE EXISTS?
	SOJA 6,COPS4		;NO, REDUCE OVERHEAD WORD
	MOVE 2,[XWD 400000,PGBF1] ;YES, MAP IT
	MOVSI 3,(16B5)
	PMAP
	MOVSI 3,-1000
	SKIPN PGBF1A(3)		;COUNT THE WORD IF IT'S NON-0, OR
	SKIPE PGBF1A+1(3)	;IF THE NEXT ONE IS NON-0
	AOJ 6,
	AOBJN 3,.-3
COPS4:	AOBJN 5,COPS2		;SCAN PAGES
	SETO 1,
	MOVE 2,[XWD 400000,PGBF1]
	PMAP			;RELEASE LAST PAGE
	MOVE 2,6
	JRST COPY4

COPS3:	MOVE 1,OUJFN
	HRL 1,FORKH
	SKIPN DEVDSG		;DSK?
	JRST COPS5		;YES
	MOVE 2,[XWD 777760,20]	;SAV 20 777777 ...
	SAVE
COPS6:	MOVE 1,FORKH
	KFORK
	SETZM FORKH
	TRZ F,SAVB
	JRST EOF2

COPS5:	MOVE 2,[XWD -1000,520000]
	SSAVE			;SSAVE 0 777 ...
	JRST COPS6

EOF:	MOVE 1,OUJFN
	CLOSF
	JFCL
EOF2:	MOVE 1,INJFN
	TLNE 1,(17B5)		;GROUP?
	HRLI 1,(1B0)		;YES, DONOT RELEASE JFN
	PUSH P,1
	CLOSF
	JFCL
	POP P,1
	JUMPL 1,.+3		;RELEASING JFN?
	RLJFN			;YES, BE SURE IT IS
	JFCL
	MOVEI 1,EOL
	PBOUT
	MOVE 1,INJFN
	TLNN 1,(17B5)		;* IN INPUT NAME?
	JRST INNM1		;NO
EOF1:	GNJFN			;INDEX THE JFN
	JRST INNM1		;NO MORE IN GROUP
	JRST GRP1

FULL:	MOVE 1,DEVDSG
	DSMNT
	JFCL
	CALL GETDEV
	TRNE F,SAVB		;SAVE FILE?
	JRST FULL2		;YES
	JRST FULL1

INDIR:	MOVE 7,DIRPTR
INDI1:	BIN
	CAIN 2,">"
	JRST INDI2
	CAIE 2,"Q"-100		;CONTROL-Q OR RUBOUT DELETES ALL
	CAIN 2,177
	JRST [	HRROI 1,[ASCIZ / XXX
/]
		PBOUT
		JRST INNM1]
	IDPB 2,7
	JRST INDI1

INDI2:	SETZ 2,
	IDPB 2,7
	HLRZ 1,IOJFNS
	MOVEI 2,EOL
	TRNE 1,400000		;TTY INPUT?
	BOUT			;YES, DO EOL
	JRST INNM1

DONE:	HLRZ 1,IOJFNS
	CAIGE 1,100
	CLOSF
	JFCL
	SKIPE 1,DEVDSG
	DSMNT
	JFCL
	HALTF

;UUO HANDLER, UUO EXECUTES PUSHJ P,UUOH

UUOH:	MOVEM 1,UUOH1
	LDB 1,[POINT 9,40,8]
	ADDI 1,UUOTAB
	EXCH 1,UUOH1
	JRST @UUOH1

UUOTAB:	HALTF
	JRST .JERR

;JSYS ERROR ROUTINE

.JERR:	PUSH P,@40		;SAVE ARGS
	PUSH P,1
	HRROI 1,[ASCIZ /
JSYS ERROR, /]
	PSOUT
	POP P,1
	HLRZ 0,0(P)		;LOCATION OF ERROR NUMBER
	JUMPE 0,[MOVEI 1,400000
		GETER		;GET IT FROM PSB
		JRST .+2]
	HRRZ 2,@0
	MOVEI 1,101
	HRLI 2,400000
	SETZ 3,
	ERSTR			;PRINT ERROR MESSAGE
	JFCL
	JFCL
	MOVEI 1,37
	PBOUT
	POP P,1			;RESUME LOCATION
	EXCH 1,0(P)
	RET

	LIT

;GTJFN TABLE FOR INPUT FILE

GJINTB:	1B2+1B11		;OLD FILE, ALLOW *'S
IOJFNS:	0
	0
DIRPTR:	POINT 7,DIRNAM,-1
	0
	0
	0
	0
	0

;GTJFN TABLE FOR OUTPUT FILE

GJOUTB:	1B0
	XWD 377777,377777
OUTDVP:	0
OUTDRP:	0
OUTNMP:	0
OUTEXP:	0
	0
	0
	0

DIRNAM:	BLOCK 11

OUTDEV:	BLOCK 11
OUTDIR:	BLOCK 11
OUTNAM:	BLOCK 11
OUTEXT:	BLOCK 11
OUTVER:	BLOCK 2

IOUJFN:	0
SAVJFN:	0

NAME:	BLOCK 40

UUOH1:	0
PDL:	BLOCK NPDL

FORKH:	0
OUJFN:	0
INJFN:	0
DTAFRE:	0
DTAFRB:	0
DEVDSG:	0
BYTSIZ:	0
DEVNUM:	0
DBSIZ:	0
DATAPT:	0
DATABF:	BLOCK 1000

	END START
