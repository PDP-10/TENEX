	TITLE GETDMP
COMMENT !
	PROGRAM TO READ IN AI-LAB DUMP FILES, INTO CORE
	!

EXTERNAL JOBREL

ST_74
PC_5
DEFINE TYPE (X) <
	HRROI 1,[ASCIZ/X/]
	PSOUT
>


	PHASE 720000
START:	RESET
	TYPE <GET AI-LAB DUMP FILE
FILE NAME *>
	MOVSI 1,060002		;CONFIRMATION
	MOVE 2,[XWD 100,101]	
	GTJFN
	JRST [TYPE <
FILE DOES NOT EXIST!>
	      JRST START]
	MOVEM 1,JFN
	MOVE 2,[XWD 440000,200000]	;36-BIT BYTES, READ
	OPENF
	JRST [TYPE <
OPENF ERROR!
>
		HALTF]		
	MOVEI PC,ST
	MOVE 1,JFN	
LOOP:	BIN
	JUMPE 2,[MOVE 1,JFN
		 GTSTS
		 TLNE 2,1000
		   JRST DONE
		 SETZ 2,		
		 JRST .+1]
	MOVEM 2,(PC)
	AOJA PC,LOOP
DONE:	TYPE <
FINISHED, PROGRAM BREAK IS AT >
	MOVEI 1,101
	MOVE 2,PC
	MOVEI 3,10		;OCTAL
	NOUT
	JFCL
	MOVEI 1,400000
	MOVE 2,[XWD 254000,400010]
	SEVEC
	MOVE 1,PC
	ORI 1,777
	MOVEM 1,JOBREL		;SET USER'S JOBREL
	HALTF


JFN:	0


	END START
