00100	BEGIN"TRITAP"
00200	REQUIRE "<><>" DELIMITERS;
00250	DEFINE SITE=<ISSUMEX>;
00300	DEFINE ! = <COMMENT>;
00400	
00500	! 	This program is designed to copy DUMPER format tapes from any one
00600	of three formats (IMSSS, PARC, SUMEX) to any other.  The program is
00700	meant to run on any of those three machines.
00800		Later, a more flexible program will be written to convert any
00900	format of tape between these three, and possibly more, installations.
01000	;
01100	
01200	DEFINE ISSUMEX=<0>,ISIMSSS=<1>,ISPARC=<2>;
01300	IFC NOT DECLARATION(SITE) THENC
01400	DEFINE SITE=<ISIMSSS>;			! CHANGE THIS LINE TO CHANGE THE VERSION;
01500	ENDC
01600	
     

00100	! MACROS;
00200	IFC SITE=ISSUMEX THENC DEFINE SITENAME=<"SUMEX">; ENDC
00205	IFC SITE=ISIMSSS THENC DEFINE SITENAME=<"IMSSS">; ENDC
00210	IFC SITE=ISPARC THENC DEFINE SITENAME=<"PARC">; ENDC
00300	
00400	DEFINE SUMEXBUFSIZE=<648>;
00500	DEFINE IMSSSBUFSIZE=<583>;
00600	DEFINE PARCBUFSIZE=<777>;
00700	DEFINE INBUFSIZE=<777>;			! THE BIGGEST OF THE THREE;
00800	DEFINE OUTBUFSIZE=<'1006>;		! BECAUSE DUMPER WRITES A PAGE + 6 WORDS
00900						  OF BOOK-KEEPING;
01000	
01100	! BECAUSE R. SMITH FORGOT TO DEFINE JSYSES IN THE PARC-SUMEX VERSIONS,
01200		THESE ARE TEMPORARILY INCLUDED;
01300	DEFINE GDSTS=<'104000000145>,SDSTS=<'104000000146>;
01400	DEFINE FORE(X,Y)=<FOR X_1 STEP 1 UNTIL Y DO>;
01500	DEFINE CRLF=<"
01600	">;
     

00100	! PROGRAM VARIABLES;
00200	INTEGER INJFN,OUTJFN,LSTJFN;
00300	
00400	INTEGER LISTING;
00500	
00600	INTEGER EOF;
00700	INTEGER INFORMAT,OUTFORMAT;
00800	INTEGER ARRAY INBUF[1:INBUFSIZE],OUTBUF[1:OUTBUFSIZE];
00900	
01000	LABEL BEYOND;
01100	
     

00100	SIMPLE PROCEDURE LISTNAME;
00200	BEGIN
00300	! PICK UP NAME FROM THE OUTBUF, WHICH IS NOW IN DUMPER FORMAT
00400		WE REFER TO THE DUMPER LISTING, WHICH IS R. TOMLINSON'S
00500		ONLY DOCUMENTATION.
00600			THEREIN IT SEEMS TO BE TRUE THAT BLOCK TYPE -2
00700		IS INDICATES THAT THE RECORD IS A HEADER, AND THAT THE
00800		NAME OF THE FILE BEGINS AT THE FIRST DATA WORD -- I.E,
00900		IN OUTBUF[7] IN THIS PROGRAM.;
01000	IF OUTBUF[5]=-2 THEN
01100	    BEGIN
01200		STRING S;
01300		INTEGER C,BP;
01400		! PICK UP THE STRING FROM THE BUFFER.  THERE IS
01500	AN INTERNAL SAIL FUNCTION THAT DOES THIS, BUT BETTER NOT
01600	USE IT.  SOMETHING LESS IMPLEMENTATION-DEPENDENT.;
01700	
01800		S_NULL; BP_POINT(7,OUTBUF[7],-1);	
01900		WHILE (C_ILDB(BP)) DO S_S & C;	
02000		OUT(LSTJFN,S & CRLF);
02100	
02200	    END;
02300	END;
     

00100	SIMPLE PROCEDURE SUMEXCONVERT;
00200	BEGIN
00300	! SUMEX TAPES ARE WRITTEN IN THE STANDARD DEC FORMAT.  THIS
00400		IS PROBABLY THE MOST SENSIBLE THING THAT THEY COULD
00500		HAVE DONE.;
00600	
00700	INTEGER I; INTEGER IBP,OBP;
00800	
00900	IBP_POINT(8,INBUF[1],-1);
01000	OBP_POINT(8,OUTBUF[1],-1);
01100	
01200	FORE(I,OUTBUFSIZE)
01300	    BEGIN
01400		FORC V_1 STEPC 1 UNTILC 4 DOC <IDPB(ILDB(IBP),OBP);> ENDC
01500		DPB(ILDB(IBP) LAND '17,POINT(4,OUTBUF[I],35));
01600	    END;
01700	END;
01800	
01900	
     

00100	SIMPLE PROCEDURE IMSSSCONVERT;
00200	! IMSSS FORMAT PUTS 36 BITS OF DATA INTO 36 BITS ON THE TAPE!
00300		THE FORMAT PUTS 2 PDP-10 WORDS INTO 9 TAPE FRAMES,
00400		WHERE THE FRAMES ARE THE BYTES OF THE TWO WORDS AS
00500		SHOWN IN THIS DIAGRAM:
00600	
00700		------------------------
00800		| 1  |  2 |  3 | 4  |5A|
00900		------------------------
01000		| 6  |  7 |  8 | 9  |5B|
01100		------------------------
01200	
01300	5A AND 5B TOGETHER FORM THE FIFTH FRAME ON THE TAPE.
01400	;
01500	BEGIN
01600	INTEGER I; INTEGER IBP,OBP;
01700	
01800	IBP_POINT(8,INBUF[1],-1);
01900	OBP_POINT(8,OUTBUF[1],-1);
02000	
02100	FORE(I,OUTBUFSIZE)
02200	    BEGIN
02300		INTEGER FIFTHBYTE;
02400	
02500		FORC V_1 STEPC 1 UNTILC 4 DOC <IDPB(ILDB(IBP),OBP);> ENDC
02600		FIFTHBYTE_ILDB(IBP);
02700		DPB(FIFTHBYTE LSH -4,POINT(4,OUTBUF[I],35));
02800	
02900		I_I+1;				! NOW INTO NEXT WORD;
03000		FORC V_1 STEPC 1 UNTILC 4 DOC <IDPB(ILDB(IBP),OBP);> ENDC
03100		DPB(FIFTHBYTE LAND '17,POINT(4,OUTBUF[I],35));
03200	
03300	    END;	
03400	END;
03500	
     

00100	SIMPLE PROCEDURE PARCCONVERT;
00200	BEGIN
00300	! PARC FORMAT TAPES ARE WRITTEN IN A 36-BITS-IN-48-BIT FORMAT,
00400		OSTENSIBLY TO PLACATE THEIR NOVA.;
00500	INTEGER I; INTEGER IBP,OBP;
00600	
00700	IBP_POINT(8,INBUF[1],-1);
00800	OBP_POINT(8,OUTBUF[1],-1);
00900	
01000	FORE(I,OUTBUFSIZE)
01100	    BEGIN
01200		INTEGER JUNK;
01300		FORC V_1 STEPC 1 UNTILC 4 DOC <IDPB(ILDB(IBP),OBP);> ENDC
01400		JUNK_ILDB(IBP);
01500		DPB(ILDB(IBP) LSH -4,POINT(4,OUTBUF[I],35));
01600	    END;
01700	END;
     

00100	
00200	SIMPLE PROCEDURE CONVERT;
00300	BEGIN
00400	CASE INFORMAT OF
00500	    BEGIN
00600		SUMEXCONVERT;
00700		IMSSSCONVERT;
00800		PARCCONVERT;
00900	    END;
01000	END;
     

00100	SIMPLE PROCEDURE READIN;
00200	BEGIN
00300	ARRYIN(INJFN,INBUF[1],(CASE INFORMAT OF (SUMEXBUFSIZE,IMSSSBUFSIZE,PARCBUFSIZE)));
00400	END;
00500	
     

00100	SIMPLE PROCEDURE READOUT;
00200	BEGIN
00300	ARRYOUT(OUTJFN,OUTBUF[1],'1006);
00400	END;
00500	
     

00100	DEFINE ENDOFTAPE=<(OUTBUF[5]=-4)>;
     

00100	
00200	SIMPLE PROCEDURE BYTE4(INTEGER JFN);
00300	! SET STATUS OF TAPE FOR 4-BYTE MODE AT IMSSS OR SUMEX.  
00400		SOMETHING LIKE THIS WINS AT PARC -- I THINK THIS IS IT,
00500		BUT SOME CHECKING IS NEEDED;
00600	START!CODE
00700	DEFINE P=<'17>;
00800		PUSH	P,JFN;
00900		PUSHJ	P,CVJFN;
01000		GDSTS;
01100		TRO 	2,'1000;	! TURN ON 4 BYTE MODE AT SUMEX OR IMSSS;
01200		SDSTS;	
01300	END;
     

00100	
00200	SIMPLE PROCEDURE INPUTGET;
00300	BEGIN
00400	
00500	OUTSTR(CASE INFORMAT OF ("SUMEX","IMSSS","PARC"));
00600	OUTSTR(" input tape *");
00700	INJFN_OPENFILE(NULL,"RC");
00800	MTAPE(INJFN,"W");
00900	BYTE4(INJFN);
01000	SETINPUT(INJFN,200,0,EOF);
01100	
01200	END;
01300	
01400	SIMPLE PROCEDURE OUTPUTGET;
01500	BEGIN
01600	OUTSTR(SITENAME);
01700	OUTSTR(" output tape  *");
01800	OUTJFN_OPENFILE(NULL,"WC");
01900	MTAPE(OUTJFN,"W");
02000	END;
02100	
     

00100	BEGIN
00200	STRING S;
00300	
00400	DEFINE GETIT(MSG,RESULT)=<
00500	BEGIN
00600	LABEL LAB;
00700	LAB:
00800	OUTSTR(msg & " tape format (S, I, P or ?)  *");
00900	S_INCHWL;
01000	
01100	IF S="?" THEN
01200	    BEGIN
01300		OUTSTR("Type 
01400		S 	for SUMEX
01500		I	for IMSSS
01600		P	for PARC
01700	");
01800		GOTO LAB;
01900	    END ELSE
02000		IF S="S" THEN 
02100		    RESULT_ISSUMEX 
02200		ELSE 
02300		IF S="I" THEN 
02400		    RESULT_ISIMSSS 
02500		ELSE 
02600		IF S="P" THEN
02700		    RESULT_ISPARC 
02800		ELSE
02900		   BEGIN
03000			OUTSTR("Illegal site!
03100	");
03200			GOTO LAB;
03300		   END;
03400	END>;
03500	
03600	GETIT("Input",INFORMAT);
03700	
03800	OUTSTR("Listing file name (CRLF for none)  *");
03900	S_INCHWL;
04000	IF NOT S THEN LISTING_FALSE ELSE
04100	    BEGIN
04200		LISTING_TRUE;
04300		LSTJFN_OPENFILE(S,"W");
04400	    END;
04500	
04600	OUTSTR("Output tape will be in " & SITENAME & " format.
04700	");
04800	
04900	
05000	END;
     

00100	
00200	INPUTGET;
00300	OUTPUTGET;
00400	
00500	WHILE TRUE DO
00600	    BEGIN"FILES"
00700		WHILE TRUE DO
00800		    BEGIN"COPY"
00900			READIN;
01000			IF EOF THEN DONE;
01100			CONVERT;
01200			READOUT;
01300			IF LISTING THEN LISTNAME;		! IF NEEDED;
01400			IF ENDOFTAPE THEN GOTO BEYOND;
01500		    END"COPY";
01600		MTAPE(OUTJFN,"E");
01700	    END"FILES";
     

00100	BEYOND:
00200	CFILE(INJFN); CFILE(OUTJFN); IF LISTING THEN CFILE(LSTJFN);
00300	
     

00100	END"TRITAP";
     

     

00100	
00200	
00300	
00400	
00500	
00600	
