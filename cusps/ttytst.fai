
	TITLE	TTYTST
	SUBTTL	R.S.TOMLINSON

A_1
B_2
C_3
CCNT_4
LCNT_5
PCNT_6
CH_7
X_10
R_16
P_17

; PARAMETERS

NPGS__3
NLNS__=58
NCLS__=72

START:	RESET
	MOVE P,[XWD -4,PDL-1]
	MOVEI A,100
	RFMOD
	TLNN B,(1B3)
	TDZA A,A
	SETO A,
	MOVEM A,LWRFLG
	MOVEI A,400000
	MOVE B,[XWD LEVTAB,CHNTAB]
	SIR
	MOVE A,[XWD =28,1]
	ATI
	MOVSI B,(1B1)
	MOVEI A,400000
	AIC
	JSP	R,CHART
	JSP	R,LINEFT
	JSP	R,CRT
	JSP	R,SPACET
	JSP	R,SPACE2
	JSP R,TEXTO
	JSP R,CRTEST
	JSP R,NORMO
	JSP R,IOTEST
	RESET
	HALTF
	JRST START

LEVTAB:	PC1
	PC1
	PC1

CHNTAB:	0
	XWD 1,RUBOUT
	REPEAT =34,<0>

RUBOUT:	MOVEM R,PC1
	MOVEI A,101
	CFOBF
	CFIBF
	DEBRK

TEXTO:	MOVEI A,400000
	EIR
	HRROI A,[ASCIZ /

   ***** Text Output Test *****

/]
	PSOUT
	MOVEI CH,40
	MOVEI PCNT,NPGS
TXTO1:	MOVEI LCNT,NLNS
TXTO2:	AOS CH
	SKIPN LWRFLG
	CAIGE CH,140
	CAIL CH,177
	 MOVEI CH,40
	MOVE A,CH
	MOVEI CCNT,NCLS
TXTOL:	PBOUT
	AOS A
	SKIPN LWRFLG
	CAIGE A,140
	CAIL A,177
	 MOVEI A,40
	SOJG CCNT,TXTOL
	MOVEI 1,37
	PBOUT
	SOJG LCNT,TXTO2
	MOVEI A,14
	PBOUT
	SOJG PCNT,TXTO1
	MOVEI A,400000
	DIR
	JRST (R)

CRTEST:	MOVEI A,400000
	EIR
	HRROI A,[ASCIZ /

   ***** Carriage Return Test *****

/]
	PSOUT
	MOVSI X,-NKND
	MOVEI PCNT,NPGS
CRTO1:	MOVEI LCNT,NLNS
CRTO2:	MOVE CCNT,CLN(X)
	MOVEI CH,41
	AOBJN X,CRTL
	MOVSI X,-NKND
CRTL:	MOVE A,CH
	PBOUT
	AOS CH
	SOJG CCNT,CRTL
	MOVEI A,37
	PBOUT
	SOJG LCNT,CRTO2
	MOVEI A,14
	PBOUT
	SOJG PCNT,CRTO1
	MOVEI A,400000
	DIR
	JRST (R)

CLN:	DEC 1,72,5,60,10,50,20,40,1,2,1,30,2,20,2,60,2,25
NKND__.-CLN

NORMO:	MOVEI A,400000
	EIR
	HRROI A,[ASCIZ /

   ***** Normal Text Output *****

/]
	PSOUT
	MOVSI X,-NSTR
	MOVEI PCNT,NPGS
NRMO1:	MOVEI LCNT,NLNS
NRMOL:	HRRO A,STR(X)
	PSOUT
	AOBJN X,.+2
	MOVSI X,-NSTR
	MOVEI A,37
	PBOUT
	SOJG LCNT,NRMOL
	MOVEI A,14
	PBOUT
	SOJG PCNT,NRMO1
	MOVEI A,400000
	DIR
	JRST (R)

STR:
[ASCIZ \Now is the time for all good men to come to the aid of their party.\]
[ASCIZ \The quick brown fox jumps over the lazy dog's back.\]
[ASCIZ \\]
[ASCIZ \This little pig went to market; / This little pig stayed home;\]
[ASCIZ \This little pig had roast beef; / This little pig had none;\]
[ASCIZ /This little pig cried, "Wee, wee, wee!" All the way home./]
[ASCIZ \\]
[ASCIZ /There was an old woman who lived in a shoe./]
[ASCIZ \She had so many children she didn't know what to do.\]
[ASCIZ \She gave them some broth, without any bread,\]
[ASCIZ \She whipped them all soundly, and sent them to bed.\]
[ASCIZ \\]
[ASCIZ \Goosey, goosey, gander! /  Where shall I wander?\]
[ASCIZ \Upstairs and downstairs / And in my lady's chamber.\]
[ASCIZ \There I met an old man / That would not say his prayers.\]
[ASCIZ \I took him by the left leg, / And threw him down the stairs.\]
[ASCIZ \\]

NSTR_.-STR

CHART:	MOVEI	A,400000
	EIR
	HRROI	A,[ASCIZ/

     ***** Character test *****

/]
	PSOUT
	HRLZI	12,-=20
LP:	MOVEI	13,=24
	HRROI	1,STUFF1(12)
	PSOUT
	SOJG	13,.-2
	MOVEI	1,37
	PBOUT
	PBOUT
	AOBJN	12,LP
	HRLZI	12,-5
LP2:	HRRO	1,STUFF2(12)
	PSOUT
	MOVEI	1,37
	PBOUT
	PBOUT
	AOBJN	12,LP2
	MOVEI	1,400000
	DIR
	JRST	(R)

LINEFT:	MOVEI	1,400000
	EIR
	HRROI	1,[ASCIZ/

     ***** Linefeed test *****

/]
	PSOUT
	HRLZI	12,-NLNS
LP3:	MOVEI	1,"\"
	PBOUT
	MOVEI	1,12
	PBOUT
	AOBJN	12,LP3
	MOVEI	1,400000
	DIR
	JRST	(R)


CRT:	MOVEI	1,400000
	EIR
	HRROI	1,[ASCIZ/

     ***** Carriage return test *****

/]
	PSOUT
	HRLZI	12,-=20
	MOVE	12,[XWD -=20,1]
	MOVEI	1,101
	MOVEI	4,37
LP4:	HRROI	2,LIN1
	HRRZ	3,12
	MOVN	3,3
	SOUT
	EXCH	1,4
	PBOUT
	EXCH	1,4
	AOBJN	12,LP4
	SOS	12
	MOVN	12,12
LP5:	MOVE	3,12
	HRROI	2,LIN2
	SOUT
	EXCH	1,4
	PBOUT
	EXCH	1,4
	AOJL	12,LP5
	MOVEI	1,400000
	DIR
	JRST	(R)

SPACET:	MOVEI	1,400000
	EIR
	HRROI	1,[ASCIZ\

     ***** Space test *****

\]
	PSOUT
	MOVEI	12,<NCLS/2>
LP6:	MOVEI	1,"\"
	PBOUT
	MOVEI	1,"/"
	PBOUT
	SOJG	12,LP6
	MOVEI	1,400000
	DIR
	JRST	(R)


SPACE2:	MOVEI	1,400000
	EIR
	HRROI	1,[ASCIZ\

     ***** Carriage/space test *****

\]
	PSOUT
	MOVEI	12,<NCLS/2>
	MOVEI	1,15
	PBOUT
LP7:	MOVEI	1,"/"
	PBOUT
	MOVEI	1," "
	PBOUT
	SOJG	12,LP7
	MOVEI	1,15
	PBOUT
	MOVEI	12,1
LP8:	MOVE	3,12
	MOVEI	1," "
	PBOUT
	SOJG	3,.-1
	MOVEI	1,"\"
	PBOUT
	MOVEI	1,15
	PBOUT
	SETZ	1,
	PBOUT
	AOS	12
	AOS	12
	CAIGE	12,NCLS
	JRST	LP8
	MOVEI	1,400000
	DIR
	JRST	(R)


STUFF1:	ASCIZ\ABC\
	ASCIZ\DEF\
	ASCIZ\GHI\
	ASCIZ\JKL\
	ASCIZ\MNO\
	ASCIZ\PQR\
	ASCIZ\STU\
	ASCIZ\VWX\
	ASCIZ\YZ0\
	ASCIZ\123\
	ASCIZ\456\
	ASCIZ\789\
	ASCIZ\<?>\
	ASCIZ\.,/\
	ASCIZ\!"#\
	ASCIZ\$%&\
	ASCIZ\(')\
	ASCIZ\*=@\
	ASCIZ\:-;\
	ASCIZ\^_+\
STUFF2:	[ASCIZ\ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890!"#$%&'()*=:-_@+;^<>?/.,1234567890!"\]
[ASCIZ\ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890!"#$%&'()*=:-_@+;^<>?/.,1234567890!"\]
	[ASCIZ\
\]
	[ASCIZ\NOW IS THE TIME FOR ALL GOOD MEN TO COME TO THE AID OF THEIR COUNTRY.\]
	[ASCIZ\THE QUICK BROWN FOX JUMPED OVER THE LAZY DOG'S BACK.1234567890!"#$%&'():\]
LIN1:	ASCIZ\!"#$%&'()*=:-@_+;?/.\
LIN2:	ASCIZ\1234567890ABCDEFGHIJ\
IOTEST:	MOVEI A,400000
	EIR
	HRROI A,[ASCIZ /

   ***** Input-Output Test *****


Type input text edit with control-Q, control-A, end with control-D.

/]
	PSOUT
	MOVEI A,101
	MOVE B,[BYTE (2)0,0,1,1,0,1,1,2,1,2,2,1,2,2,1,1,1,0]
	MOVE C,[BYTE (2)0,1,1,1,1,1,1,1,1,3,1,1,1,2]
	SFCOC
IOLP:	MOVEM R,SR#
IOI:	MOVE B,[POINT 7,TXTBUF-1,34]
IOIG:	PBIN
	MOVEI R,IOIR
	CAIN A,"A"-100
	 JRST [	CAMN B,[POINT 7,TXTBUF-1,34]
		 JRST DING
		MOVEI A,"\"
		PBOUT
		LDB A,B
		PBOUT
		MOVE A,B
		BKJFN
		 JFCL
		MOVE B,A
		JRST IOIG]
	CAIN A,"Q"-100
	 JRST [	CAMN B,[POINT 7,TXTBUF-1,34]
		 JRST DING
		HRROI A,[ASCIZ /##
/]
		PSOUT
		PUSHJ P,GETBL
		MOVE B,A
		JRST IOIG]
	CAIN A,"R"-100
	 JRST [	MOVEI A,37
		PBOUT
		SETZ A,
		MOVE C,B
		IDPB A,C
		PUSHJ P,GETBL
		PSOUT
		JRST IOIG]
	IDPB A,B
	CAIE A,"D"-100
	 JRST IOIG
	SETZ A,
	DPB A,B
	HRROI A,TXTBUF
	PSOUT
	JRST .-2

GETBL:	MOVE A,B
	CAMN A,[POINT 7,TXTBUF-1,34]
	 POPJ P,
	BKJFN
	 0
	LDB C,A
	CAIE C,37
	 JRST GETBL+1
	POPJ P,

DING:	MOVEI A,7
	PBOUT
	JRST IOI

IOIR:	MOVE R,SR
	HRROI A,[ASCIZ /

Input:
/]
	PSOUT
	JRST IOLP

LIT

SET VARPC,100000
USE VARPC

VAR

LWRFLG:	BLOCK 1
PC1:	BLOCK 1
PDL:	BLOCK 5
TXTBUF:	BLOCK 20000

USE

	END START
