	PRINTX	<FILE PRSV.MAC REQUIRED>
	JUMPL	5,ASKFORK
	PRINT	<MULTI-FORK SUPERVISOR>
	PRINT	<TYPE ? FOR HELP>
	SETZM	FORK
	MOVE	1,[XWD FORK,FORK+1]
	BLT	1,FORK+17
	MOVEI	1,100
	RFMOD
	MOVEM	2,MODWD
	MOVEI	1,100
	RFCOC
	MOVEM	2,CCWD1
	MOVEM	3,CCWD2
	JRST	ASKF2
ASKFORK:
	MOVEI	1,100
	MOVE	2,MODWD
	SFMOD
	MOVEI	1,100
	MOVE	2,CCWD1
	MOVE	3,CCWD2
	SFCOC
ASKF2:
	PRINT	<FORK: >
	MOVEI	1,100
	MOVEI	3,12
	NIN
	JRST	[MOVEI	1,100	;SEE IF IT WAS A "?"
		BKJFN
		QPASA
		PBIN
		CAIN	1,"?"
		JRST	HELP
		JRST	LIST]
	MOVM	AA,2
	CAIL	AA,1
	CAILE	AA,17
	JRST	ASKFORK
	JUMPL	2,KILL
	SKIPE	1,FORK-1(AA)
	JRST	RESUME
	PRINT	<NEW FORK>
	HRLZI	1,100001
	HRROI	2,[ASCIZ/<SYSTEM>EXEC.SAV/]
	GTJFN
	JRST	[QPASA
		JRST	ASKFORK]
	MOVEM	1,EXJFN
	MOVEI	2,0
	HRLZI	1,200000
	CFORK
	JRST	[QPASA
		JRST	ASKFORK]
	MOVEM	1,EXFKH
	MOVEM	1,FORK-1(AA)
	HRLZ	1,1
	HRR	1,EXJFN
	GET
	MOVE	1,EXFKH
	SETZ	2,2
	SFRKV
	WFORK
	CALL	TELLFK
	JRST	ASKFORK

RESUME:
	MOVEI	1,100		;RESET TTY MODES
	MOVE	2,MODWD(AA)
	SFMOD
	MOVEI	1,100
	MOVE	2,CCWD1(AA)
	MOVE	3,CCWD2(AA)
	SFCOC
	MOVE	1,FORK-1(AA)
	RFSTS
	MOVE	1,FORK-1(AA)
	SFORK
	WFORK
	CALL	TELLFK
	JRST	ASKFORK

KILL:
	SKIPE	1,FORK-1(AA)
	KFORK
	SETZM	FORK-1(AA)
	PRINT	<FORK KILLED>
	JRST	ASKFORK

TELLFK:			;TELL HIM THE FORK EXITED
	MOVEI	1,100		;GET TTY PARAMS AND SAVE THEM FOR NEXT TIME
	RFMOD
	MOVEM	2,MODWD(AA)
	MOVEI	1,100
	RFCOC
	MOVEM	2,CCWD1(AA)
	MOVEM	3,CCWD2(AA)
	PRINT	<FROM FORK >
	MOVEI	1,101
	MOVE	2,AA
	MOVEI	3,12
	NOUT
	QPASA
	RETURN

HELP:
	PRINT	<FORK # IS DECIMAL INTEGER, MAGNITUDE FROM 1 TO 15>
	PRINT	<POSITIVE NUMBER n WILL CREATE OR RESUME A FORK WITH INDEX n>
	PRINT	<-n KILLS FORK WITH INDEX n>
LIST:
	PRINT	<EXISTING FORKS ARE:>
	SETZB	2,4
LIST1:
	MOVEI	1,101
	MOVE	3,[XWD 100003,12]
	AOJ	2,2
	SKIPN	FORK-1(2)
	JRST	LIST2
	NOUT
	JFCL	0
	AOJ	4,4
LIST2:
	CAIG	2,^D15
	JRST	LIST1
	JRST	ASKFORK

	LIT

MODWD:	BLOCK 20		;TTY MODE WORDS FOR EACH FORK
CCWD1:	BLOCK 20		;TTY CONTROL WORDS FOR EACH FORK
CCWD2:	BLOCK 20
EXJFN:	0
EXFKH:	0
FORK:	BLOCK	20

	END	START
