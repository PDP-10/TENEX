BEGIN
COMMENT THIS PROGRAM READS THE SYSTEM ASSEMBLY FILE  <SYSTEM>STENEX.FAI
	FINDING THE JSYS DEFINITIONS, AND WRITES THEM. 
	THIS VERSION CREATES A FILE PHASES TO PAGE 777, FOR DDT USE;

DEFINE ! = "COMMENT",
	TAB="""	""",
	CRLF="""
""",
	SPACE=""" """;

DEFINE !!!  = "COMMENT";


COMMENT INCODES THE CHARACTER.  THIS SHOULD PERHAPS BE A TABLE.;
INTEGER SIMPLE PROCEDURE TABLE(INTEGER J);
RETURN(IF J GEQ '60 AND J LEQ '71 THEN J-'57 ELSE
	IF J GEQ '101 AND J LEQ '132 THEN J-('101-'13) ELSE
	IF J="." OR J="" OR J="!" THEN '45 ELSE
	IF J="$" THEN '46 ELSE IF J="%" THEN '47 ELSE 0);


Comment decodes the RADIX50 character into ASCII.  Also should be a table.;

INTEGER SIMPLE PROCEDURE UNCODE(INTEGER J);
RETURN(IF J = 0 THEN SPACE ELSE IF J GEQ '1 AND J LEQ '12 THEN J+'57
	ELSE IF J GEQ '13 AND J LEQ '44 THEN J+('101-'13) ELSE
	IF J='45 THEN "." ELSE IF J='46 THEN "$" ELSE IF J='47 THEN "%" ELSE SPACE);




INTEGER SIMPLE PROCEDURE RADIX50(STRING S);
BEGIN COMMENT GETS THE RADIX 50 REPRESENTATION OF THE
FIRST 6 CHARACTERS OF A STRING.  THESE ARE COMPARED TO 
THE SYMBOLS.  ;
INTEGER ANS,C,K;
ANS_0;
FOR K_1 STEP 1 UNTIL 6 DO
    BEGIN
	C_LOP(S);
	IF C=0 THEN DONE;
	ANS_ANS*'50+TABLE(C);
    END;
!!! OUTSTR(CVOS(ANS) & CRLF);
RETURN(ANS);
END;

STRING SIMPLE PROCEDURE DECODE(INTEGER R50; REFERENCE INTEGER STATUS);
BEGIN
COMMENT Takes the RADIX50 symbol and converts to an ASCII string.
The flags are the incoded into the first four bits.   ;
INTEGER I,J; STRING T;
STATUS_J_0;

COMMENT GET THE FLAGS;

START!CODE
	MOVE	'13,R50;
	MOVE	'14,'13;	! GET ANOTHER COPY;	
	AND	'14,['740000000000];	! LEAVE ONLY STATUS BITS;
	LSH	'14,-'36;		! SHIFT OVER;
	MOVEM	'14,STATUS;	! SAVE IT;
	TLZ	'13,'740000; 	! IGNORE THE FIRST 4 BITS.;
	MOVEM	'13,J;
END;

T_NULL;
WHILE J DO
    BEGIN
	START!CODE
	MOVE	'13,J;
	IDIVI	'13,'50;
	MOVEM	'13,J;
	MOVEM	'14,I;
	END;
	IF I THEN T_UNCODE(I) & T; ! DONT CONCATENATE SPACES FOR NULL CHARS;
    END;
RETURN(T);	
END;



INTEGER INJFN,OUTJFN;

DEFINE ISGLOBAL="'040000000000";
INTEGER COUNT;

PROCEDURE SEND(STRING S; INTEGER INST);
BEGIN
OUT(OUTJFN,TAB & CVOS(ISGLOBAL+RADIX50(S)) & "		;" & S & "
	" & CVOS(INST LSH 27) & CRLF);
COUNT_COUNT+2;
END;

SIMPLE PROCEDURE COLLA(REFERENCE STRING S);
BEGIN
INTEGER C;
WHILE (C_S)=SPACE OR C=TAB DO C_LOP(S);
WHILE (C_S[INF FOR 1])=SPACE OR C=TAB DO S_S[1 TO INF-1];
END;

DEFINE SE(X,Y)="";

STRING DATA,OUTER; INTEGER BR,AEOF;
INTEGER I;
STRING S;

OUTSTR("STENEX file   *");
SETINPUT(INJFN _ OPENFILE(NULL,"ROC"),500,0,AEOF);
OUTSTR("Output file   *");
OUTJFN _ OPENFILE(NULL,"WC");

SETBREAK(1,'12,'14&'15,"IN");
SETBREAK(2,SPACE,NULL,"IN");
SETBREAK(3,",",NULL,"IN");

OUT(OUTJFN,"
COMMENT !  
	THESE ARE THE JSYS'S IN THE TENEX SYSTEM,
PROCESSED FOR DDT10X.MAC.  THIS IS A MACRO SOURCE FILE, TO
BE CONCATENATED TO THE END OF DDT10X.MAC.
	TWO THINGS ARE DEFINED HERE.
	JTBL	:START OF TABLE
	JTBLSZ	:LENGTH OF TABLE

THE TABLE IS CREATED FROM SYS:STENEX.FAI BY DJMAK

	!


JTBL:


");
COUNT_0;
SEND("JSYS",'104);

WHILE NOT AEOF DO
    BEGIN
	STRING S,T;
	STRING J; INTEGER WHICH; INTEGER JUNK;
	LABEL CONT;
	S_INPUT(INJFN,1);
	COLLA(S);
	IF NOT S THEN GOTO CONT;
	IF NOT EQU(S[1 FOR 5],"DEFJS") THEN GOTO CONT;
	T_SCAN(S,2,JUNK);		
	J_SCAN(S,3,JUNK);	
	COLLA(J);
	IF EQU(J,"JSYS") THEN GOTO CONT; COMMENT DO NOT USE THIS ONE;

	OUT(OUTJFN,TAB & CVOS(ISGLOBAL+RADIX50(J)) & "		;" & J & "
	" & CVOS('104000000000 + CVO(S)) & "
");	
	COUNT_COUNT+2;			! SIZE OF TABLE;
      CONT:
    END;
! NOW PUT IN THE DEC UUOS;
SE(CALL,'40);
SE(INIT,'41);
SE(CALLI,'47);
SE(OPEN,'50);
SE(TTCALL,'51);
SE(RENAME,'55);
SE(IN,'56);
SE(OUT,'57);
SE(SETSTS,'60);
SE(STATO,'61);
SE(GETSTS,'62);
SE(STATZ,'63);
SE(INBUF,'64);
SE(OUTBUF,'65);
SE(INPUT,'66);
SE(OUTPUT,'67);
SE(CLOSE,'70);
SE(RELEAS,'71);
SE(MTAPE,'72);
SE(UGETF,'73);
SE(USETI,'74);
SE(USETO,'75);
SE(LOOKUP,'76);
SE(ENTER,'77);
SE(UJEN,'100);


OUT(OUTJFN,'14 &
"COMMENT ! END OF TABLE !

JTBLSZ==" & CVOS(COUNT) & "

DDTEND:	IFN EDDT&1,<

EMDDT==.
	DEPHASE
>;IFN EDDT&1

	END
");

CFILE(INJFN); CFILE(OUTJFN);
END;






