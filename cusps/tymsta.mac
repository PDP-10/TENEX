	TITLE	TYMSTA - TYMNET STATUS
	SEARCH	STENEX

NPDL==20
P=17
QCKCNT==5
ACCCNT==^D20

START:	RESET
	MOVE P,[IOWD NPDL,PDL]

; MEASURE TYMNET LINE RESPONSE (YELLOW BALL TIME)

	MOVEI 1,101
	GTTYM
	JUMPL 2,[HRROI 1,[ASCIZ \For TYMNET lines only\]
		PSOUT
		HALTF
		JRST START]
	ANDI 2,7777		;JUST GET NODE 
	MOVEM 2,NODE#		;SAVE NODE

ST0:	HRROI 1,[ASCIZ /Quick or Accurate test (Q or A)? /]
	PSOUT

	PBIN
	TRZ 1,40		;UPPER CASE
	CAIN 1,"A"
	 JRST  [HRROI 1,[ASCIZ /ccurate/]
		MOVSI 2,-ACCCNT
		JRST ST1]
	CAIN 1,"Q"
	 JRST  [HRROI 1,[ASCIZ /uick/]
		MOVSI 2,-QCKCNT
		JRST ST1]
	HRROI 1,[ASCIZ / ??
/]
	PSOUT
	JRST ST0

ST1:	PSOUT
	MOVEM 2,FREQ#

	MOVEI 1,101
	HRROI 2,[ASCIZ /
Response to node /]
	SETZ 3,
	SOUT

	MOVE 2,NODE		;GET NODE
	MOVEI 3,^D8
	NOUT
	0

	HRROI 2,[ASCIZ / is /]
	SETZ 3,
	SOUT

	DOBE

	SETZ 3,			;SUM TIME IN REG 3

	MOVE 4,FREQ
TYMRCL:	TIME			;RESPONSE TIME IN MILLISECONDS IS
	SUB 3,1			;(YELLOW BALL TIME) - (MON WAKE UP TIME)
				;(YELLOW BALL TIME) IS MEASURED WITH A DOBE
				;(MON WAKE UP TIME) IS TIME IN EXCESS OF A
	MOVEI 1,101		;DISMS
	DOBE

	TIME
	LSH 1,1			;TIMES 2 TO FINISH OUT THE DOBE MEASUREMENT
	ADD 3,1			;AND START THE NEGATIVE EXCESS DISMS MEASURE

	MOVEI 1,^D499		;DISMS LESS THAN ^D500 TO KEEP BAL SET
	DISMS			;RETENTION THE SAME AS FOR THE DOBE

	TIME
	SUBI 1,^D499		;TAKE OUT THE DISMS TIME TO GET THE EXCESS 
	SUB 3,1

	AOBJN 4,TYMRCL

	LSH 3,-1		;HALF OF ROUND TRIP TIME FOR ONE WAY
	FLTR 2,3		;float summed time
	FLTR 4,4		;float divisor
	FDVR 2,4		;GET AVERAGE TIME
	FIX 2,2			;RE FIX IT
	MOVEM 2,RESP#

	MOVEI 1,101		;TO TTY
	MOVEI 3,^D10		;IN DECIMAL INTEGER
	NOUT
	0

	HRROI 2,[ASCIZ \ msec
\]
	SETZ 3,
	SOUT

	MOVSI 1,(1B17)		;YES, LOG RESPONSE
	HRROI 2,[ASCIZ /<SYSTEM>TYMNET-RESPONSE.LOG/]
	GTJFN
	 JRST TYMST4

	MOVE 2,[070000,,020000]	;APPEND FROZEN ASCII
	OPENF
	 JRST TYMST4

	SETO 2,			;LOG CURRENT TIME
	MOVE 3,[1B3+1B6+1B8+1B10]
	ODTIM

	MOVEI 2," "
	BOUT
	BOUT

	MOVE 5,1
	GJINF
	MOVE 2,1
	MOVE 1,5
	DIRST
	0

	MOVEI 2," "
	BOUT
	BOUT

	MOVE 2,NODE
	MOVEI 3,^D8
	NOUT
	 JRST TYMST4

	MOVEI 2," "
	BOUT
	BOUT

	MOVE 2,RESP
	MOVEI 3,^D10
	NOUT
	 JRST TYMST4

	MOVEI 2," "
	BOUT

	MOVEI 2,"a"
	HLRZ 4,FREQ		;WAS AN ACCURATE MEASUREMENT DONE?
	CAIE 4,-ACCCNT
	 MOVEI 2,"q"
	BOUT

	MOVEI 2,37
	BOUT

TYMST4:	RESET
	HALTF
	JRST START

PDL:	BLOCK NPDL

	END START
