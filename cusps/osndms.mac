;<DURHAM>SNDMSG.MAC;8    18-MAR-74 10:15:02	EDIT BY DURHAM
;<DURHAM>SNDMSG.MAC;6     5-MAR-74 11:45:30	EDIT BY DURHAM
;Code to allow ^<file-name> to be included in a CTRL-B file
;CHANGED GRIPE TO USE A SPECIFIC FILE
;I4-TENEX:<SOURCES>SNDMSG.MAC;4 8-JUN-73 14:00:00 EDIT BY PJ
;ADDED CHANGE TO UPDATE <OPERATOR>SNDMSG.LOG;1
;I4-TENEX:<SOURCES>SNDMSG.MAC;3 26-APR-73 11:27:00 EDIT BY PJ
;<SOURCES>SNDMSG.MAC;19    21-MAR-73 10:41:32	EDIT BY JOHNSON
;<JOHNSON>SNDMSG.MAC;4    19-MAR-73 23:09:45	EDIT BY JOHNSON
;<JOHNSON>SNDMSG.MAC;3    19-MAR-73 22:37:49	EDIT BY JOHNSON
;<JOHNSON>SNDMSG.MAC;2    19-MAR-73 22:31:32	EDIT BY JOHNSON
; ADD CODE FOR ADDMSG (TO ADD A MESSAGE TO A GIVEN FILE).
;<SOURCES>SNDMSG.MAC;18    14-MAR-73 17:24:43	EDIT BY TOMLINSON
; FIXED UP LINE BUFFERING FOR USER LIST
;<SOURCES>SNDMSG.MAC;13    14-MAR-73 11:26:37	EDIT BY TOMLINSON
; MOVED HOST RECOGNITION TO INPUT TIME
;<SOURCES>SNDMSG.MAC;12    13-MAR-73 09:03:31	EDIT BY TOMLINSON
; FIXED LEFT OVER QUEUE INTERRUPT PROBLEM AND 0 USERS PROBLEM
;<SOURCES>SNDMSG.MAC;11     7-MAR-73 14:11:41	EDIT BY CLEMENTS
;<CLEMENTS>SNDMSG.MAC;1     7-MAR-73 12:36:29	EDIT BY CLEMENTS
;<SOURCES>SNDMSG.MAC;10     5-MAR-73 10:58:41	EDIT BY TOMLINSON
; MADE MESSAGE.COPY HAVE 770000 PROTECTION
;<SOURCES>SNDMSG.MAC;9    16-FEB-73 13:37:55	EDIT BY JOHNSON
; ADDED ENTRY AND CODE FOR TPSMSG (SNDMSG FOR NON-LOGGED USERS).
; FIXED BUG IN RECOVERY FROM BAD USER NAME.
;<SOURCES>SNDMSG.MAC;8    14-FEB-73 20:11:40	EDIT BY TOMLINSON
; FIXED MESSAGE.COPY FAILURE
;<SOURCES>SNDMSG.MAC;7    13-FEB-73 16:05:15	EDIT BY TOMLINSON
; BACKSPACE OVER LF ALSO DELETES THE CR
;<SOURCES>SNDMSG.MAC;6    13-FEB-73 15:51:43	EDIT BY TOMLINSON
; ADDED ,<CR> FOR LINE CONTINUATION OF USER NAME LIST
;<SOURCES>SNDMSG.MAC;5    14-DEC-72 13:38:31	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;5    14-DEC-72 13:32:11	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;4    14-DEC-72 13:15:17	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;3    14-DEC-72 13:03:17	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;2    14-DEC-72 12:52:46	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;1    14-DEC-72 12:15:12	EDIT BY TOMLINSON
; ADDED TTYTRB ENTRY AND CODE
;<SOURCES>SNDMSG.MAC;4     8-DEC-72 11:51:48	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;2     8-DEC-72 11:37:21	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;1     8-DEC-72 11:09:11	EDIT BY TOMLINSON
; ERRORS DURING FILE INPUT -- FLUSH FILE, RUBOUT NOT CANCEL INPUT
;<SOURCES>SNDMSG.MAC;3     6-DEC-72 15:17:22	EDIT BY CHIPMAN
;<TOMLINSON>SNDMSG.MAC;39     6-DEC-72 15:14:18	EDIT BY CHIPMAN
;<TOMLINSON>SNDMSG.MAC;38    13-NOV-72 22:39:13	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;37    13-NOV-72 22:24:06	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;36    13-NOV-72 21:51:19	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;35    13-NOV-72 21:49:07	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;34    13-NOV-72 20:25:30	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;33    13-NOV-72 20:16:53	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;32    13-NOV-72 20:16:05	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;31    13-NOV-72 19:12:13	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;30    13-NOV-72 18:47:19	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;29    12-NOV-72 16:36:29	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;28    12-NOV-72 16:28:53	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;24    12-NOV-72 12:28:45	EDIT BY TOMLINSON

;<TOMLINSON>SNDMSG.MAC;23	11-OCT-72  8:54:39 EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;22    11-OCT-72  8:47:20	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;21     9-OCT-72 13:01:40	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;20     8-OCT-72 12:25:41	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;19     8-OCT-72 12:13:30	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;18     8-OCT-72 12:04:48	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;17     6-OCT-72 10:16:12	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;16     6-OCT-72  9:38:19	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;15     6-OCT-72  9:33:23	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;14     6-OCT-72  9:30:14	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;13     6-OCT-72  9:21:12	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;12     6-OCT-72  9:18:32	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;11     6-OCT-72  8:57:57	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;10     6-OCT-72  8:42:36	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;9     4-OCT-72 20:03:21	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;8     4-OCT-72 20:00:15	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;7     4-OCT-72 18:14:48	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;6     4-OCT-72 17:18:39	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;5     4-OCT-72 17:12:17	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;4     4-OCT-72 17:05:21	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;3     4-OCT-72 15:52:07	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;2     4-OCT-72 15:22:19	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;1     4-OCT-72 14:59:30	EDIT BY TOMLINSON
;<SOURCES>SNDMSG.MAC;2     4-OCT-72 14:09:03	EDIT BY TOMLINSON
; BUG FIXES TO INSERT FILE LOGIC
;<TOMLINSON>SNDMSG.MAC;1     4-OCT-72 14:03:44	EDIT BY TOMLINSON
;<SOURCES>SNDMSG.MAC;1     4-OCT-72 13:55:29	EDIT BY TOMLINSON
; SEND OVER NETWORK VIA FTP MAIL
;<TOMLINSON>SNDMSG.MAC;23     4-OCT-72 12:17:17	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;22     4-OCT-72 12:00:35	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;21     4-OCT-72 10:54:32	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;19     4-OCT-72 10:35:39	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;18     4-OCT-72 10:27:28	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;17     4-OCT-72 10:23:27	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;16     4-OCT-72 10:01:41	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;15     4-OCT-72  9:59:23	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;14     4-OCT-72  9:40:51	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;13     4-OCT-72  0:11:05	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;12     3-OCT-72 23:56:29	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;11     3-OCT-72 23:55:46	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;10     3-OCT-72 23:29:49	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;9     3-OCT-72 23:11:08	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;7     3-OCT-72 22:51:07	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;5     3-OCT-72 22:32:55	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;4     3-OCT-72 22:29:39	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;3     3-OCT-72 22:26:00	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.MAC;2     3-OCT-72 22:20:42	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.FAI;5     3-OCT-72 22:20:09	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.FAI;4     3-OCT-72 17:43:30	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.FAI;3     3-OCT-72 17:37:45	EDIT BY TOMLINSON
;<SOURCES>SNDMSG.FAI;5    26-SEP-72  9:07:32	EDIT BY TOMLINSON
; SAY "THANK YOU"
;<SOURCES>SNDMSG.FAI;4    25-JUL-72  8:42:42	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.FAI;2    25-JUL-72  8:32:07	EDIT BY TOMLINSON
;<TOMLINSON>SNDMSG.FAI;1    25-JUL-72  8:31:01	EDIT BY TOMLINSON

	TITLE	SNDMSG
	SUBTTL	R.S.Tomlinson
	SEARCH	STENEX

OPDEF	ESOUT[PSOUT]	;;;I4 CHANGE

; Accumulators

F=0
A=1
B=2
C=3
D=4
X=5
PTR=6
EPTR=7
Y=10
BPTR=11
FLG=12
SF=15
R=16
P=17

; Flags in F

QUIETF==400000
QF==200000
BUFFUL==100000
AMBIGF==40		;IN HOST NAME RECOGNIZER
MATCHF==20		; ..
TEMPF==10
FULLF==2
RAISEF==4
TMP2F==1

; FLAGS IN FLG

NTLOGF==400000
RVRSF==200000

; Parameters

NFILS==10		; Number of input files open at once
NUSRS==500		; NUMBER OF USERS
NDIST==^D20		; MAXIMUM NUMBER OF DISTRIBUTION LISTS
NHSTTB==1000		;SPACE FOR HOST NAME TABLES
PDLL==100		;LENGTH OF STACK
FILCHR="^"
MAXMSG==^D250000	; MAXIMUM MESSAGE SIZE


ENTVEC:	JRST SNDMSG
	JRST GRIPE
	JRST TTYTRB
	JRST TPSMSG
	JRST ADDMSG
EVECL==.-ENTVEC

; MAIN ROUTINE FOR SNDMSG

SNDMSG:	MOVE P,PDP
	MOVE PTR,[POINT 7,STRING-1,34]
	SETZ	FLG,		;CLEAR  FLG
	MOVEI	A,"@"
	MOVEM	A,ATCHAR	;SET ATCHAR TO @
	PUSHJ P,INIPSI		; INITIALIZE PSI SYSTEM
	PUSHJ P,INITIM		; INITIALIZE TIMER FORK
	PUSHJ P,INITTY		; INITIALIZE TTY MODES
	PUSHJ P,INILH		; INITIALIZE LOCAL HOST STUFF
	PUSHJ P,GETUSR		; GET LIST OF USERS
	SKIPL NUSERS
	 JRST .-2
	PUSHJ P,GETSBJ		; GET SUBJECT
	PUSHJ P,GETTXT		; GET TEXT OF MESSAGE
	MOVE X,NUSERS

; SEND TO ALL USERS

SNDLUP:	PUSHJ P,MAKHED		; GENERATE HEADING
	SKIPE HOST(X)		; LOCAL?
	 JRST SNDNET		; NO
	PUSHJ P,OPNMSG		; OPEN MESSAGE.TXT
	 JRST CANT
	PUSHJ P,OUTMRK		; OUTPUT DATE AND TIME STAMP
	PUSHJ P,OUTMSG		; PUT OUT MESSAGE
	CLOSF
	 JFCL
	HRROI B,[ASCIZ / -- ok/]
	JRST ENDSND

; Send message via network FTP MAIL facility

SNDNET:	MOVE A,TIMFRK
	FFORK
	MOVEM P,SAVEP
	MOVEI B,TIMER
	SFORK
	RFORK
	PUSHJ P,DOICP
	 JRST CANT
	 JRST QUEUE
	PUSHJ P,WAITOK
	 JRST QUEUE
	MOVE A,SJFN
	HRROI B,[ASCIZ /MAIL /]
	SETZ C,
	SOUT
	MOVE B,USRTAB(X)
	SOUT
	HRROI B,[ASCIZ /
/]
	SOUT
	MOVEI B,21
	MTOPR
WATMR1:	PUSHJ P,WAITOK
	 JRST [	CAIN C,^D451
		 JRST CANT
		JRST QUEUE]
	CAIE C,^D350
	 JRST WATMR1
	MOVE A,TIMFRK
	FFORK
	SETOM MONITV
	MOVEI B,MONITR
	SFORK
	RFORK
	PUSHJ P,OUTMSG
	HRROI B,[ASCIZ /.
/]
	SETZ C,
	SOUT
	MOVEI B,21
	MTOPR

WATMR2:	AOSE MONITV
	 JRST CANT		; MESSAGE REJECTED
	MOVE A,TIMFRK
	WFORK
	FFORK
	SKIPLE MONITV
	 JRST CANT
	MOVE A,SJFN
	CLOSF
	 JFCL
	MOVE A,RJFN
	 CLOSF
	 JFCL
	HRROI B,[ASCIZ / -- ok/]
	JRST ENDSND

; TRY TO QUEUE THE MESSAGE

QUEUE:	MOVE A,TIMFRK
	FFORK
	PUSHJ P,OPNQUE
	 JRST CANT
	PUSHJ P,OUTMSG
	MOVE A,SJFN
	CLOSF
	 JFCL
	HRROI B,[ASCIZ / -- queued/]
	JRST ENDSND

; CAN'T SEND MESSAGE

CANT:	HRROI B,[ASCIZ / -- can't/]
ENDSND:	PUSH P,B
	MOVEI A,101
	MOVEI 2,37
	BOUT
	PUSHJ P,OUTUSR
	MOVE B,(P)
	SETZ C,
	SOUT
	SETO A,
	CLOSF
	 SETO A,
	RLJFN
	 JFCL
	POP P,B		; SEND COPY OF MSG TO LOG
;	PUSHJ P,LOGMSG
	AOBJN X,SNDLUP
	HALTF
	JRST SNDMSG

; MAIN GRIPE CODE

GRIPE:	MOVE P,PDP
	MOVE PTR,[POINT 7,STRING-1,34]
	RESET
	SETZ	FLG,		;CLEAR  FLG
	MOVEI	A,"@"
	MOVEM	A,ATCHAR	;SET ATCHAR TO @
	PUSHJ P,INITTY
;	;;;I4 CHANGE..... SENDS GRIPES TO A SPECIFIC FILE
	HRROI	B,[ASCIZ/<NEWS>SYSTEM.GRIPES;1;A220100/]
	PUSHJ	P,OPNMS1
	 JRST	[HRROI	A,[ASCIZ/Gripe file unavailable...sorry/]
		ESOUT
		HALTF
		JRST GRIPE]

	HRROI	A,[ASCIZ/Griping on subject of: /]
	PSOUT
	PUSHJ	P,GETSB0
	HRROI	A,[ASCIZ/
Complaint: /]
	PSOUT
	PUSHJ	P,GETTX0
	PUSHJ	P,MAKHED
;;; END OF I4 CHANGE
	PUSHJ P,OUTMRK
	PUSHJ P,OUTMSG
	MOVE A,SJFN
	CLOSF
	 JFCL
	HRROI A,[ASCIZ /
Thanks./]
	PSOUT
	HALTF
	JRST GRIPE

; DO TTY TROUBLE REPORT

TTYTRB:	MOVE P,PDP
	MOVE PTR,[POINT 7,STRING-1,34]
	RESET
	SETZ	FLG,		;CLEAR  FLG
	MOVEI	A,"@"
	MOVEM	A,ATCHAR	;SET ATCHAR TO @
	PUSHJ P,INITTY
	MOVEI X,[POINT 7,TTYMAN]-USRTAB
	PUSHJ P,OPNMSG
	 JRST [	HRROI A,[ASCIZ /
TROUBLE FILE NOT AVAILABLE/]
		ESOUT
		HALTF
		JRST TTYTRB]
	HRROI A,[ASCIZ /
LOCATION OF TERMINAL: /]
	PSOUT
	PUSHJ P,GETSB0
	HRROI A,[ASCIZ /
DESCRIBE TROUBLE (INCLUDE TENEX TTY#): /]
	PSOUT
	PUSHJ P,GETTX0
	PUSHJ P,MAKIHD
	SETZ C,
	HRROI B,[ASCIZ /
LOC'N:/]
	SOUT
	SKIPN B,SUBJCT
	HRROI B,[ASCIZ /NOT STATED/]
	SOUT
	PUSHJ P,MAKEHD
	PUSHJ P,OUTMRK
	PUSHJ P,OUTMSG
	MOVE A,SJFN
	CLOSF
	 JFCL
	HRROI A,[ASCIZ /
THANK YOU FOR REPORTING YOUR DIFFICULTY.
IT WILL BE INVESTIGATED SHORTLY.
/]
	PSOUT
	HALTF
	JRST TTYTRB

; NAME OF TTY REPAIR MAN

TTYMAN:	ASCIZ /LARS/


; MAIN CODE FOR SNDMSG FOR NON-LOGGED USERS (IE TIP USERS)
; Site number in AC1
; User name in AC10-AC14 (up to 24 chars)

TPSMSG:	MOVE	P,PDP
	MOVE	PTR,[POINT 7,STRING-1,34]
	MOVEM	A,SITEN		;SITE ADDR OF USER IN AC 1
	MOVEM	PTR,FROMNM		;get user name.
	MOVE	A,PTR
	MOVE	B,[POINT 7,10]
	SETZ	C,
	SOUT
	MOVE	PTR,A			;PUT NEW PTR BACK
	IDPB	C,PTR			;AND PUT IN ZERO BYTE.
	MOVEI	FLG,NTLOGF		;SET TO "NOT-LOGGED"
	MOVEI	A,"%"		;USE % AS ATCHAR (INSTEAD OF @)
	MOVEM	A,ATCHAR
	PUSHJ P,INIPSI		; INITIALIZE PSI SYSTEM
	PUSHJ P,INITIM		; INITIALIZE TIMER FORK
	PUSHJ P,INITTY		; INITIALIZE TTY MODES
	PUSHJ P,INILH		; INITIALIZE LOCAL HOST STUFF
	PUSHJ	P,GETSIT	; GET SITE NAME
	HRROI	A,QMSG5
	PSOUT
	PUSHJ P,GETUSR		; GET LIST OF USERS
	PUSHJ P,GETSBJ		; GET SUBJECT
	PUSHJ P,GETTXT		; GET TEXT OF MESSAGE
	MOVE X,NUSERS
	JRST	SNDLUP		;SEND THE MESSAGE.


;GET SITE NAME

GETSIT:	MOVE	A,PTR
	MOVEM	A,SITE
	MOVE	B,SITEN
	MOVEI	C,10
	CVHST
	 NOUT			;USE NUMBER IF NAME NOT KNOWN
	 JFCL
	MOVEM	A,PTR
	IBP	PTR
	POPJ	P,



; ADD A MESSAGE TO A GIVEN FILE.
; AC1 - OUTPUT JFN (OF MESSAGE FILE).
; AC2 - B(0)=1 => ADD MESSAGE AT FRONT OF FILE.
ADDMSG:	MOVE P,PDP
	MOVE PTR,[POINT 7,STRING-1,34]
	SETZ FLG,
	JUMPGE B,.+2		;SKIP IF NORMAL OUTPUT.
	 TRO FLG,RVRSF		;OUTPUT AT FRONT OF FILE.
	MOVEM A,SJFN		;SAVE OUTPUT JFN.
	MOVEI A,"@"
	MOVEM A,ATCHAR
	PUSHJ P,INITTY		;INITIALIZE TTY PARAMS.
	PUSHJ P,GETSBJ		;GET SUBJECT.
	PUSHJ P,GETTXT		;GET MESSAGE TEXT.
	PUSHJ P,MAKIHD		;INITIAL PART OF HEADING.
	PUSHJ P,MAKHSB		;SUBJECT.
	PUSHJ P,MAKEHD		;END OF HEADING.
	MOVE A,SJFN
	MOVE B,[7B5+1B22]
	TRNE FLG,RVRSF
	 MOVE B,[7B5+1B19+1B20+1B26]	;R W AND WAIT IF BUSY.
	OPENF
	 JRST [	HRROI A,[ASCIZ /
CAN'T OPEN THAT FILE.  MESSAGE SAVED ON MESSAGE.COPY./]
		PSOUT
		HALTF
		JRST .-1 ]
	TRNN FLG,RVRSF
	 JRST ADDMS1		;APPEND MESSAGE.
	MOVEM PTR,ENDPTR	;ADD MESSAGE AT BEGINNING.
	MOVE A,SJFN
	MOVE B,ENDPTR
	MOVNI C,300000
	SIN			;READ WHAT IS THERE ALREADY.
	MOVEM C,OLDCNT		;SAVE COUNT.
	SETZ B,0
	SFPTR			;BACK TO START.
	 JFCL
ADDMS1:	PUSHJ P,OUTMRK		;OUTPUT TIME STAMP.
	PUSHJ P,OUTMSG		;OUTPUT MESSAGE.
	MOVE A,SJFN
	TRNN FLG,RVRSF
	 JRST ADDMS2		;JUST CLOSE IT.
	MOVE B,ENDPTR		;FIRST WRITE OLD CONTENTS.
	MOVNI C,300000
	SUB C,OLDCNT		;GET NEGATIVE BYTE COUNT.
	SOUT
ADDMS2:	CLOSF
	 JFCL
	HRROI A,[ASCIZ /
YOUR MESSAGE HAS BEEN INSERTED SUCCESSFULLY./]
	PSOUT
	HALTF
	JRST	.-1


; USER NAME INSTRUCTIONS

QMSG1:	ASCIZ /
Type user names of the form <user name> or @<host name> or ^file-name.
Separate names with comma, end with carriage return.  Typing just 
@<host name>causes that host to apply to subsequent users.  Typing 
@<null string> refers to the local host.  Each user's message will 
indicate to whom else the message was sent unless a named distribution 
list is stated by using <distribution name>:.  
E.g. TENEX-users:Tomlinson,... .
If one or more distribution list names are specified, they are used
in place of the user names.  Control-B (STX) may be used to substitute
the contents of a file for typed input.
/
; MESSAGE TYPING INSTRUCTIONS

QMSG2:	ASCIZ /
Your message should be typed in and be terminated with control-Z (SUB).
A copy of the text of the message will be saved on the file
MESSAGE.COPY;T if you need to send it again.
/
QMSG3:	ASCIZ /
Use the following control characters to edit:
A (SOH)	character delete
B (STX)	inserts the following file
H (BS)	also character delete
Q (DC1)	line delete
R (DC2)	retype current line or item
S (DC3) retypes entire text or all items
W (ETB) deletes last word
X (CAN)	cancels entire text or all items (start over)
Z (SUB)	terminates input
/

; Subject instructions

QMSG4:	ASCIZ /
The subject should be a one line summary of the message or a null string
ended with carriage return.
/
QMSG5:	ASCIZ	/
In the following use % instead of @ wherever necessary.
^C (Control C) may be used to abort a message.
/


; INTERRUPT STUFF

LEVTAB:	RETPC1
	RETPC1
	RETPC1

CHNTAB:	XWD 1,TIMOUT
	REPEAT ^D35,<0>

TIMOUT:	MOVE P,[XWD 10000,QUEUE]
	MOVEM P,RETPC1
	MOVE P,SAVEP
	DEBRK

TIMER:	MOVEI A,^D15000
	DISMS
	MOVEI A,777777
	MOVSI B,(1B0)
	IIC
	HALTF

MONITR:	MOVEI P,PDL2-1
	PUSHJ P,WAITOK
	 JRST MONITF
	 CAIE C,^D256
	 JRST MONITR
	HALTF

MONITF:	AOSE MONITV
	 HALTF
	PUSHJ P,WAITOK
	JRST .-1
	JRST .-2

; INITIALIZE PSI SYSTEM

INIPSI:	RESET			; RESET THE WORLD
	MOVEI A,400000		; INITIALIZE INTERRUPT SYSTEM
	MOVE B,[XWD LEVTAB,CHNTAB]
	SIR
	EIR
	MOVSI B,(1B0)
	AIC			; TURN ON CHANNEL 0
	POPJ P,

; INITIALIZE TIMER FORK

INITIM:	MOVSI A,(1B0+1B1)
	CFORK			; GET A FORK TO DO TIMING
	 HALTF
	MOVEM A,TIMFRK		; REMEMBER FORK HANDLE
	POPJ P,

; INITIALIZE TTY STUFF

INITTY:	MOVEI A,100		; PRIMARY INPUT
	MOVEM A,INFIL		; IS INITIAL INPUT FILE
	MOVE B,[BYTE (2) 1,0,0,1,1,1,1,2,0,3,3,3,3,3,1,1,1,0]
	MOVE C,[BYTE (2) 0,0,1,1,1,0,0,1,1,0,1,1,1,3]
	SFCOC			; NO ECHO ON CONTROL-A,B,H,Q,R,S,W,X ALT
	TRZ F,QUIETF		; TURN ON RESPONSES
	MOVE SF,[XWD -NFILS,FILSTK-1]	; INIT FILE STACK
	POPJ P,

; INITIALIZE LOCAL HOST THINGS

INILH:	MOVE A,[SIXBIT /LHOSTN/]
	SYSGT			; GET LOCAL HOST NUMBER
	MOVEM A,LHOSTN
	MOVE A,PTR
	MOVEM A,LHOST		; GET STRING FOR LOCAL HOST
	MOVE B,LHOSTN		; NUMBER
	MOVEI C,10
	CVHST			; CONVERT TO STRING
	 NOUT			; IF FAILS, USE NUMERIC HOST
	 JFCL
	MOVEM A,PTR		; REST OF STRINGS GO AFTER THIS
	IBP PTR			; SKIP OVER NULL
	MOVEI A,HSTTAB		;INITIAL FREE SPACE POINTER
	MOVEM A,HSTFRE		; ..
	MOVE A,['HOSTN ']
	MOVEI B,HOSTN
	PUSHJ P,SYSGET		;GET THE NUMBERS OF ALL HOSTS
	MOVE A,['HSTNAM']	;AND THE NAMES IN ASCIZ
	MOVEI B,HSTNAM
	PUSHJ P,SYSGET
	POPJ P,

; GET USER LIST

GETUSR:	HRROI A,[ASCIZ /Type ? for help
Users: /]
	PSOUT			; PROMPT
	MOVSI A,-NDIST
	MOVEM A,XDIST		; POINTER FOR STORING DISTRIBUTION NAMES
	MOVSI X,-NUSRS		; ALLOW ONLY NUSRS USERS
	MOVEI F,FULLF+RAISEF	; BREAK ON PUNCT
	MOVEM PTR,BPTR
RULUP:	MOVEM PTR,USRTAB(X)	; SAVE BEGINNING OF STRING
	PUSHJ P,INSTR		; GET USER/HOST NAME
GETUS0:	 JRST [	MOVE PTR,BPTR
		JRST GETUSR]
RULUP1:	CAIN A,"?"		; TERMINATOR = ??
	 JRST [	HRROI A,QMSG1	; YES, PRINT INSTRUCTIONS
		PSOUT
		HRROI A,QMSG3
		PSOUT
		JRST RULUP]	; AND DO IT AGAIN
	CAIN A,":"		; DISTRIBUTION LIST?
	 JRST [	SKIPL B,XDIST
		 JRST BADUSR	; TOO MANY
		MOVE C,USRTAB(X)
		MOVEM C,DIST(B)
		ADD B,[1,,1]
		MOVEM B,XDIST
		IDPB A,EPTR	; KEEP TERMINATOR
		MOVE PTR,EPTR
		JRST RULUP]
	CAIN A,177		; ABORTED?
	 JRST [	MOVE PTR,USRTAB(X)	; YES, RESTORE PTR
		HRROI A,[ASCIZ /XXX /]	; AND ACKNOWLEDGE ABORTION
		PSOUT
		JRST RULUP]	; AND LOOP
	CAMN EPTR,PTR		; NULL STRING TYPED?
	 JRST [	CAMN A,ATCHAR	; YES, @ TERMINATOR?
		 JRST SETHST	; YES, READ STRING AS PERMANENT HOST
		CAIN A,FILCHR
		JRST GETUFL
		CAIE A,37	; NO, EOL TERMINATOR
		 JRST BADUSR	; NO. CALL IT AN ERROR
		JRST RUL3]	; YES, DONE COLLECTING USER NAMES
	CAIE A,33
	JRST RUL1
	MOVSI A,400000
	MOVE B,USRTAB(X)	; GET BACK TO BEGINNING OF STRING
	STDIR
	 JRST [	SKIPE DEFHST	; IF WE ARE TALKING ABOUT ANOTHER HOST
		 JRST .+1	; JUST CALL IT AMBIGUOUS
		JRST BADUSR]	; IT THIS HOST, CALL IT BAD
USRAMB:	JRST [	MOVEI A,7
		PBOUT
		PUSHJ P,RLUP
		JRST RULUP1]
	MOVE A,EPTR		; POINTER TO TAIL
	MOVEM B,EPTR		; PASS OVER TAIL
	PSOUT			; PRINT TAIL
	PUSHJ P,.BIN		; GET REAL TERMINATOR
	JRST RUL2

RUL1:	CAME A,ATCHAR
	SKIPE DEFHST
	 JRST RUL2		; SKIP CHECK IF OTHER HOST
	MOVE B,USRTAB(X)
	MOVEM A,C		; SAVE CHARACTER.
	SETZ A,
	STDIR
	 JRST BADUSR
	 JRST BADUSR
	MOVE A,C		; RESTORE CHARACTER.
RUL2:	CAMN A,ATCHAR		; USER AT FOREIGN SITE WANTED?
	 JRST RUL6		; YES, READ WHERE
	MOVE B,DEFHST
	MOVEM B,HOST(X)		; REMEMBER DEFAULT
RUL63:	CAIE A,"S"-100
	CAIN A,"R"-100
	 JRST [	MOVE B,EPTR
		SETZ C,
		IDPB C,B
		CAIE A,"R"-100
		SKIPA B,BPTR
		 MOVE B,PTR
		MOVEI A,37
		PBOUT
		MOVE A,B
		PSOUT
		PUSHJ P,.BIN
		JRST RUL63]
RULCON:	IDPB A,EPTR		; STORE THE TERMINATOR FOR RETYPE
	MOVEM EPTR,PTR		; KEEP THE STRING
	CAIE A,","
	CAIN A,37
	SKIPA
	JRST BADUSR
	AOBJP X,RUL3
	CAIN A,","
	 JRST [	PUSHJ P,.BIN	; READ AHEAD
		CAIN A,15	; IF CR
		 PUSHJ P,[IDPB A,PTR
			JRST .BIN]
		MOVE B,A
		MOVE A,INFIL
		CAIE B,37	; IF FOLLOWED BY EOL
		CAIN B,12	; OR LF
		 JRST [	IDPB B,PTR
			JRST RULUP]
		BKJFN		; ELSE BACK UP
		 JFCL
		JRST RULUP]	; AND REPROCESS
RUL3:	HLRE A,X
	ADDI A,NUSRS
	MOVNS A
	HRLZM A,NUSERS
	SETZ C,
	MOVE A,BPTR
DPNULL:	ILDB B,A
	CAIE B,","
	CAMN B,ATCHAR
	 DPB C,A
	CAIE B,12
	CAIN B,15
	 DPB C,A
	CAIE B,":"
	CAIN B,37
	 DPB C,A
	JUMPN B,DPNULL
	TRZ F,RAISEF
	POPJ P,

RUL6:	IDPB A,EPTR		; STORE THE TERMINATOR FOR RETYPE
	MOVEM EPTR,PTR		; KEEP THE STRING
	PUSHJ P,GETHST		; GET HOST NAME
	 JRST GETUS0		; ABORT ALL
	 JRST [	MOVE PTR,USRTAB(X)
		JRST RULUP]	; FLUSH WHOLE USER SPEC
	 JRST BADUS1		; FAILURE
	MOVEM B,HOST(X)		; SAVE HOST
	JRST RUL63

GETHST:	PUSH P,PTR		; SAVE START POINT
GETHS0:	PUSHJ P,INSTR		; GET STRING
	 JRST POP1		; GET OUT
	CAIN A,177
	 JRST [	POP P,PTR
		JRST SKPRET]
	CAMN EPTR,PTR
	 JRST [	SETZM 0(P)	; NULL STRING, USE LOCAL
		JRST GETHS1]
	PUSH P,A		; SAVE TERMINATOR
	MOVE A,-1(P)		; WHERE TO WRITE RECOGNIZED STRING
	MOVE B,A		; IS ALSO SOURCE
	SETZ C,
	PUSHJ P,HSTSOU		; PERFORM RECOGNITION
	TRNE F,AMBIGF		; AMBIGUOUS?
	 JRST [	POP P,A		; GET TERMINATOR
		CAIN A,33	; ALTMODE?
		 JRST [	MOVEI A,7
			PBOUT	; DING
			PUSH P,[GETHS0+1]
			JRST RLUP]
		HRROI A,[ASCIZ / Ambiguous host name /]
		POP P,PTR
		JRST SK2RET]
	TRNN F,MATCHF		; Match found?
	 JRST [	HRROI A,[ASCIZ / No such host /]
		SUB P,[1,,1]
		POP P,PTR
		JRST SK2RET]
	MOVE B,EPTR		; Save where tail starts
	MOVEM A,EPTR		; Update EPTR
	POP P,A			; Get terminator
	CAIE A,33		; ALTMODE?
	 JRST GETHS1
	MOVEI A,101
	SETZ C,
	SOUT
	PBIN			; GET ANOTHER TERMINATOR
GETHS1:	POP P,B
SK3RET:	AOS 0(P)
SK2RET:	AOS 0(P)
SKPRET:	AOS 0(P)
CPOPJ:	POPJ P,

POP1:	SUB P,[1,,1]
	POPJ P,

SETHST:	IDPB A,PTR
	PUSHJ P,GETHST
	 JRST GETUS0
	 JRST RULUP
	 JRST BADUS1
	MOVEM B,DEFHST
	SUB X,[1,,1]		; UNDO AOBJP AT RUL63
	JRST RUL63

BADUSR:	HRROI A,[ASCIZ /? INVALID USER NAME.
TRY AGAIN: /]
BADUS1:	PUSH P,A
	MOVE PTR,USRTAB(X)
	MOVE A,INFIL
	CAIE A,100
	  JRST [	PUSHJ P,ENDFIL
		HRROI A,[ASCIZ / FILE INPUT TERMINATED/]
		JRST .+2]
	POP P,A
	ESOUT
	SETZ C,
	MOVE A,PTR
	IDPB C,A
	MOVEI A,37
	PBOUT
	MOVE A,BPTR
	PSOUT
	JRST RULUP



GETUFL:	MOVS	A,[GTJTAB,,[1B35
		100,,101
		0
		0
		POINT 7,[ASCIZ/MESSAGE/]
		POINT 7,[ASCIZ/TXT/]
		0
		0
		0
		0
		]]
	BLT	A,GTJTAB+17
	MOVE	A,INFIL
	HRLM	A,GTJINF
	MOVEI	A,GTJTAB
	SETZM	B
	GTJFN
	JRST	GETUFE
	HRRZM	A,FILSAV
	MOVEI	A,FILCHR
	IDPB	A,EPTR
	MOVE	A,EPTR
	HRRZ	B,FILSAV
	MOVE	C,[2B5!1B8!1B9!1B12!1B35]
	JFNS
	MOVEM	A,EPTR
	MOVE	A,INFIL
	BKJFN
	JFCL
	PUSHJ	P,.BIN
	CAIN	A,33
	PUSHJ	P,.BIN
	JRST	RULCON
GETUFE:	HRROI	A,[ASCIZ/? INVALID FILE NAME,
REENTER DESTINATION: /]
	JRST	BADUS1





; GET SUBJECT LINE

GETSBJ:	HRROI A,[ASCIZ /
Subject: /]
	PSOUT
GETSB0:	MOVEM PTR,BPTR
	MOVEM PTR,SUBJCT
GETSB1:	TRO F,FULLF
	PUSHJ P,INSTR
GETSB2:	 JRST [	MOVE PTR,BPTR
		JRST GETSBJ]
	CAIN A,"?"
	 JRST [	HRROI A,QMSG4
		PSOUT
		JRST GETSB1]
	CAIN A,177
	 JRST [	HRROI A,[ASCIZ /XXX/]
		PSOUT
		JRST GETSB1]
	CAIE A,37
	 JRST [	IDPB A,EPTR
		PUSH P,[GETSB2]
		JRST INSTRC]	; CONTINUE GATHERING STRING
	CAMN PTR,EPTR
	 SETZM SUBJCT		; MARK NULL SUBJECT THIS WAY
	MOVEM EPTR,PTR
	IBP PTR
	POPJ P,

; GET THE TEXT OF A MESSAGE

GETTXT:	HRROI A,[ASCIZ /
Message (? for help):
/]
	PSOUT
GETTX0:	MOVEM PTR,MSG
	SETZ F,
	PUSHJ P,INSTR0
GETTX:	 JRST GETTXT
	CAIN A,177
	 JRST [	PUSH P,[GETTX]
		JRST INSTRC]
	CAIN A,"?"
	 JRST [	HRROI A,QMSG2
		PSOUT
		HRROI A,QMSG3
		PSOUT
		JRST GETTXT]
	MOVEI A,37
	PBOUT
	LDB A,EPTR		; IS THERE A LF
	CAIE A,12		; AT THE END?
	 JRST [	MOVE A,EPTR	; NO
		HRROI B,[ASCIZ /
/]
		SETZ C,
		SOUT		; PUT ONE THERE
		MOVEM A,EPTR
		JRST .+1]
	MOVEM EPTR,PTR
	IBP PTR
	MOVSI A,(1B0!1B17)
	HRROI B,[ASCIZ /MESSAGE.COPY;P770000;T/]
	GTJFN
	 JRST GTXTE
	PUSH P,A
	MOVE B,[XWD 70000,100000]
	OPENF
	 JRST [	POP P,A
		RLJFN
		 JFCL
		JRST GTXTE]
	MOVE B,MSG
	SETZ C,
	SOUT
	POP P,A
	CLOSF
	 JFCL
GTXTE:	MOVE A,MSG
	PUSHJ P,CNTMSG		; COUNT LENGTH OF MESSAGE
	MOVEM B,MSGLEN		; SAVE
	POPJ P,

; OPEN MESSAGE.TXT FILE

OPNMSG:	MOVE	B,USRTAB(X)
	ILDB	A,B		;GET FIRST CHAR
	CAIN	A,FILCHR	;SKIP UNLESS ITS A FILE-NAME
	JRST	OPNFM		;ELSE OPEN FILE
	MOVE A,[POINT 7,FILNAM]
	MOVEI B,"<"
	IDPB B,A
	MOVE B,USRTAB(X)
	SETZ C,
	SOUT
	HRROI B,[ASCIZ />MESSAGE.TXT;1/]
	SOUT
	HRROI B,FILNAM
OPNMS1:				; CALLED FROM 'GRIPE'
OPNFM:	MOVSI A,(1B0!1B17)
	GTJFN
	 POPJ P,
	MOVEM A,SJFN
	MOVE B,[7B5+1B22+1B26]
	OPENF
	 JRST [	MOVE A,SJFN
		RLJFN
		 JFCL
		POPJ P,]
	AOS 0(P)
	POPJ P,

; OPEN QUEUE FILE

OPNQUE:	MOVE A,PTR
	HRROI B,[ASCIZ /[--UNSENT-MAIL--]./]
	SETZ C,
	SOUT
	MOVE B,USRTAB(X)
	SOUT
	MOVEI B,"V"-100
	IDPB B,A
	MOVEI B,"@"
	IDPB B,A
	SKIPE B,HOST(X)
	PUSHJ P,HSTSOU		;COMPLETE HOST NAME THEN SOUT
	HRROI B,[ASCIZ /;P770000/]
	SOUT
	MOVE B,PTR
	MOVSI A,400001
	GTJFN
	 POPJ P,
	MOVEM A,SJFN
	MOVE B,[70000,,100000]
	OPENF
	 JRST [	MOVE A,SJFN
		RLJFN
		 JFCL
		POPJ P,]
	AOS 0(P)
	POPJ P,

; OUTPUT USER NAME AT SITE STRING

OUTUSR:	SETZ C,
	MOVE B,USRTAB(X)
	SOUT
	SKIPN HOST(X)
	 POPJ P,
	HRROI B,[ASCIZ / at /]
	SOUT
	MOVE B,HOST(X)
	PUSHJ P,HSTSOU		;COMPLETE HOST NAME THEN SOUT
	POPJ P,

; DO ICP TO FTP

DOICP:	MOVE A,PTR
	HRROI B,[ASCIZ /NET:0./]
	SETZ C,
	SOUT
	MOVE B,HOST(X)
	PUSHJ P,HSTSOU		;COMPLETE HOST NAME THEN SOUT
	MOVN B,FTPSKT
	MOVEI C,8
	NOUT
	 HALT
	HRROI B,[ASCIZ /;T/]
	SETZ C,
	SOUT
	MOVE B,PTR
	MOVSI A,1
	GTJFN
	 POPJ P,		; NO SKIP -- NO SUCH HOST
	AOS 0(P)		; AT LEAST ONE SKIP FROM HERE
	MOVEM A,SJFN
	MOVE B,[XWD 400000,200000]
	OPENF
	 POPJ P,
	BIN
	MOVEM B,FSKT
	CLOSF
	 JFCL
	MOVE A,PTR
	HRROI B,[ASCIZ /NET:2./]
	SETZ C,
	SOUT
	MOVE B,HOST(X)
	SOUT
	MOVN B,FSKT
	MOVEI C,10
	NOUT
	 JFCL
	HRROI B,[ASCIZ /;T/]
	SETZ C,
	SOUT
	MOVE B,PTR
	MOVSI A,1
	GTJFN
	 POPJ P,
	MOVEM A,SJFN
	MOVE B,PTR
	MOVSI A,1
	GTJFN
	 POPJ P,
	MOVEM A,RJFN
	MOVE B,[XWD 103400,200000]
	OPENF
	 POPJ P,
	MOVE A,SJFN
	MOVE B,[XWD 103400,100000]
	OPENF
	 POPJ P,
	AOS 0(P)
	POPJ P,

;HSTSOU - HOST SOUT ROUTINE - COMPLETES HOST NAMES AND DOES SOUT

HSTSOU:	SKIPE HOSTN		;DID THE TABLES READ OK?
	SKIPN HSTNAM		; ..
	JRST SOUTPJ		;NO. JUST SOUT AS IT STANDS
	PUSH P,A		;DESTINATION STRING
	PUSH P,B		;SOURCE STRING TO RECOGNIZE
	PUSH P,C		;COUPLE AC'S FOR TEMPS
	PUSH P,D		; ..
	PUSH P,X		;WILL USE AS COUNTER THRU TABLE
	TRZ F,AMBIGF!MATCHF	;INITIALIZE NOT AMBIGUOUS OR MATCHED
	MOVE X,HOSTN		;POINTER TO HOST NUMBERS AND NAME OFFSET
HSOUL2:	MOVE B,0(X)		;GET A HOST NAME
	ADD B,HSTNAM		;POINT TO TEXT STRING
	HRLI B,440700		;BYTE PTR
	MOVE A,-3(P)		;USER'S NAME
HSOUL1:	ILDB C,A		;CHAR FROM USER
	ILDB D,B		;CHAR FROM TABLE
	CAIG C,172		;USER MAY BE LOWER CASE
	CAIGE C,141
	SKIPA			;NOT
	TRZ C,40		;LOWER. MAKE UPPER.
	CAME C,D		;CHARACTERS MATCH?
	JRST HSOUT1		;NO
	JUMPN C,HSOUL1		;YES. LOOP UNLESS TO END OF STRINGS
HSOUTM:	TRZ F,AMBIGF		; INDICATE SUCCESS FOR INTERESTED CALLER
	TRO F,MATCHF
	MOVE B,0(X)		;EXACT MATCH
	ADD B,HSTNAM		;POINT TO STRING IN TABLE
	HRLI B,440700
	MOVEM B,-3(P)		;PUT IT IN AC B ON STACK
HSOUTR:	POP P,X			;RESTORE ACS
	POP P,D
	POP P,C
	POP P,B
	POP P,A
SOUTPJ:	SOUT
	POPJ P,0

HSOUT1:	JUMPN C,HSOUT2		;IF NOT END OF USER STRING, NO MATCH.
	SKIPL 0(X)		;SERVER BIT ON FOR THIS HOST?
	JRST HSOUT2		;NO. DONT RECOGNIZE IT.
	TROE F,MATCHF		;YES. IT'S A MATCH. FIRST?
	TRO F,AMBIGF		;NO. LOSES. AMBIGUOUS.
	MOVEM X,HSOUTX		;STASH THE INDEX OF THE MATCH
HSOUT2:	AOBJN X,HSOUL2		;MOVE ON TO NEXT HOST NAME
	MOVE B,HSOUTX		;TRIED ALL. POINT TO ANY MATCH
	MOVE B,0(B)
	ADD B,HSTNAM
	HRLI B,440700		;STRING POINTER, MAYBE TO A NAME.
	TRNE F,MATCHF		;A MATCH?
	TRNE F,AMBIGF		;AND NOT AMBIGUOUS?
	SKIPA			;NOT A WIN
	MOVEM B,-3(P)		;WINS. PUT IT ON STACK FOR SOUT
	JRST HSOUTR		;AND RESTORE ACS, SOUT, RETURN.

; GET A RESPONSE FROM FTP

WAITOK:	MOVE A,RJFN
	TRZ F,10
	SETZ C,
NINLP:	BIN
	CAIL B,200
	 JRST NINLP
	CAIG B,"9"
	CAIGE B,"0"
	 JRST NINDUN
	TRO F,10
	IMULI C,^D10
	ADDI C,-60(B)
	JRST NINLP
NINDUN:	TRNN F,10
	POPJ P,
	BIN
	JUMPE B,CPOPJ
	CAIE B,12
	 JRST .-3
	CAIL C,^D400
	CAIL C,^D600
	AOS 0(P)
	POPJ P,

; GENERATE FIRST PART OF HEADING

MAKIHD:	MOVE A,PTR
	MOVEM A,HEAD
	HRROI B,[ASCIZ /-------
Date: /]
	SETZ C,
	SOUT
	MOVSI C,(1B10!1B12)
	SETO B,
	ODTIM
	HRROI B,[ASCIZ /
From: /]
	SETZ C,
	SOUT
	TRNE	FLG,NTLOGF	; IS USER LOGGED IN?
	 JRST	MAKIH1		;NO, USE FROM NAME.
	PUSH P,A
	GJINF
	MOVE B,A
	POP P,A
	DIRST
	 JFCL
	POPJ P,
MAKIH1:	MOVE	B,FROMNM	;USE FROM NAME
	SOUT
	POPJ	P,

; PUT SUBJECT LINE INTO HEADING

MAKHSB:	SKIPN SUBJCT
	 POPJ P,
	HRROI B,[ASCIZ /
Re:   /]
	SETZ C,
	SOUT
	MOVE B,SUBJCT
	SOUT
	POPJ P,

; MAKE HEADING FOR SNDMSG

MAKHED:	PUSHJ P,MAKIHD		; GENERATE INITIAL PART OF HEADING
	TRNN	FLG,NTLOGF	; IF THIS IS A NON-LOGGED USER OR
	SKIPE HOST(X)		; IF THIS IS TO A DIFFERENT HOST
	 JRST [	HRROI B,[ASCIZ / at /]; SAY WHAT HOST IT'S FROM
		SETZ C,
		SOUT
		MOVE B,LHOST
		TRNE	FLG,NTLOGF	;IS USER LOGGED IN?
		 MOVE	B,SITE		;NO, USE SITE NAME.
		PUSHJ P,HSTSOU		;COMPLETE HOST NAME THEN SOUT
		JRST .+1]
	PUSHJ P,MAKHSB
	SKIPGE Y,DISTL		; PRINT DISTRIBUTION LIST IF ANY
	 JRST DOCC
	MOVE Y,NUSERS
	CAML Y,[-1,,0]
	JRST ENDHED
DOCC:	HRROI B,[ASCIZ /
cc:   /]
	SETZ C,
	SOUT
	PUSH P,A
	TRZ F,1
CCLUP:	CAMN Y,X
	JRST CCNXT
	PUSH P,A
	HRROI B,[ASCIZ /, /]
	TROE F,1
	SOUT
	SKIPGE DISTL
	 JRST [	MOVE B,DIST(Y)
		SOUT
		JRST CCCHKE]
	EXCH X,Y
	PUSHJ P,OUTUSR
	EXCH X,Y
CCCHKE:	HRRZ B,A
	SUBI B,@-1(P)
	CAILE B,^D12
	 JRST [	POP P,A
		HRROI B,[ASCIZ /,
      /]
		TRZ F,1
		SOUT
		MOVEM A,0(P)
		JRST CCLUP]
	SUB P,[XWD 1,1]
CCNXT:	AOBJN Y,CCLUP
	SUB P,[XWD 1,1]
ENDHED:	PUSHJ P,MAKEHD		; DO THE END OF THE HEADING
	POPJ P,

; FINISH UP HEADING

MAKEHD:	HRROI B,[ASCIZ /
- - - -
/]
	SETZ C,
	SOUT
	MOVEM A,PTR
	IBP PTR
	MOVE A,HEAD
	PUSHJ P,CNTMSG		; GET ITS LENGTH
	MOVEM B,HEDLEN
	POPJ P,

; OUTPUT DATE AND SIZE STAMP

;LOGMRK:	SKIPA A,LJFN
OUTMRK:	MOVE A,SJFN
	SETO B,
	MOVSI C,(1B17)
	ODTIM
	MOVEI B,","
	BOUT
	MOVE B,MSGLEN
	ADD B,HEDLEN
	ADDI B,9
	MOVEI C,12
	NOUT
	 JFCL
	MOVEI B,15
	BOUT
	MOVEI B,12
	BOUT
	POPJ P,

; OUTPUT MESSAGE

OUTMSG:	MOVE A,SJFN
	MOVE B,HEAD
	MOVN C,HEDLEN
	SOUT
	MOVE B,MSG
	MOVN C,MSGLEN
	SOUT
	HRROI B,[ASCIZ /-------
/]
	SETZ C,
	SOUT
	POPJ P,

; COUNT LENGTH OF MESSAGE

CNTMSG:	SETZ B,
	ILDB C,A
	JUMPE C,CPOPJ
	AOJA B,.-2

; LOG THE MESSAGE INFORMATION IN <OPERATOR>SNDMSG.LOG;1

;LOGMSG:	PUSH P,B		; SAVE STRING TO LOG
	MOVSI A,(1B17)
	HRROI B,[ASCIZ /<OPERATOR>SNDMSG.LOG;1/]
	GTJFN
	 POPJ P,
	MOVEM A,LJFN
	MOVE B,[7B5+1B22+1B26]
	OPENF
	 POPJ P,
	GJINF
	MOVE B,A
	MOVE A,LJFN
	DIRST
	 JFCL
	SETZ C,
	HRROI B,[ASCIZ /: /]
	SOUT
;	PUSHJ P,LOGMRK		; TIME STAMP IN LOG.
	HRROI B,[ASCIZ /TO: /]
	SETZ C,
	SOUT
	PUSHJ P,OUTUSR
	POP P,B			; TYPE OUT MSG.
	SOUT
	HRROI B,[ASCIZ /, /]
	SOUT
	MOVE B,MSGLEN		; SAY HOW MANY CHRS
	MOVEI C,12
	NOUT
	 JFCL
	SETZ C,
	HRROI B,[ASCIZ / CHARS.
/]
	SOUT
	CLOSF
	 JFCL
	POPJ P,

; Collect string
; If fullf = 1, then string is terminated by control-Z
; otherwise it is terminated by any punctuation (@ , eol altmode now)
; Initial ? always returns, rubout always returns

INSTR0:	MOVEM PTR,BPTR
INSTR:	TRZ F,BUFFUL
	MOVE EPTR,PTR
INSTRC:
RLUP:	PUSHJ P,.BIN
	TRNE	FLG,NTLOGF		;IS USER LOGGED IN?
	 JRST	[ CAIE	A,"C"-100	;NO CHECK FOR ^C
		   JRST	.+1		;NONE. GO ON.
		  PBOUT
		  HALTF			;FOUND ONE. EXIT.
		  JRST	RLUP		;ON CONT GO ON.
		]
	CAIE A,"H"-100
	CAIN A,"A"-100
	 JRST [	CAMN EPTR,PTR
		 JRST DING
		MOVEI A,"\"
		PBOUT
		LDB A,EPTR
		PBOUT
		PUSH P,A
		MOVE A,EPTR
		BKJFN
		 0
		MOVE EPTR,A
		POP P,A
		CAIN A,12
		 JRST .
		JRST RLUP]
	CAIN A,"Q"-100
	 JRST [	CAMN EPTR,PTR
		JRST [	MOVEI A,7
			PBOUT
			JRST RLUP]
		MOVEI A,"_"
		PBOUT
		MOVEI A,37
		PBOUT
		PUSHJ P,BKLIN
		MOVEM A,EPTR
		JRST RLUP]
	CAIN A,"W"-100
	 JRST [	CAMN PTR,EPTR
		 JRST DING
		PUSHJ P,BKWORD
		MOVEI A,"_"
		PBOUT
		JRST RLUP]
	CAIN A,"R"-100
	 JRST [	MOVEI A,37
		PBOUT
		PUSHJ P,BKLIN
		PSOUT
		JRST RLUP]
	CAIN A,"S"-100
	 JRST [	MOVEI A,37
		PBOUT
		SETZ B,
		PUSH P,EPTR
		IDPB B,EPTR
		POP P,EPTR
		MOVE A,BPTR
		PSOUT
		JRST RLUP]
	CAIN A,"X"-100
	 JRST [	HRROI A,[ASCIZ /___
/]
		PSOUT
		POPJ P,]
	CAMN PTR,EPTR
	CAIE A,"?"
	CAIN A,177
	 JRST ENDIN
	CAIN A,"Z"-100
	JRST ENDIN
	TRNE F,FULLF
	 JRST [	CAIN A,15
		 JRST RLUP	; IGNORE
		CAIN A,12
		 MOVEI A,37	; LF BECOMES EOL
		CAIE A,","
		CAIN A,37
		JRST ENDIN
		CAME A,ATCHAR
		CAIN A,FILCHR
		JRST ENDIN
		CAIN A,33
		JRST ENDIN
		CAIN A,":"
		 JRST ENDIN
		JRST DEPBYT]
DEPBYT:	HRRZ B,EPTR
	CAIL B,ESTRING-25
	 JRST [	TROE F,BUFFUL
		 JRST RLUP
		HRROI A,[ASCIZ /
String buffer full.  Finish input soon or you will lose.
/]
		ESOUT
		JRST RLUP]
	CAIL A,140
	CAILE A,177
	 JRST NOTLWR
	TRNE F,RAISEF
	 TRZ A,40
NOTLWR:	CAIN A,37
	 JRST [	MOVEI A,15
		IDPB A,EPTR
		MOVEI A,12
		JRST .+1]
	IDPB A,EPTR
	PUSH P,EPTR
	SETZ A,
	IDPB A,EPTR
	POP P,EPTR
	JRST RLUP

BKWORD:	MOVE A,EPTR
BKWRL1:	LDB B,A
	PUSHJ P,INVCHK
	 JRST BKWRD1
	CAME A,PTR
	BKJFN
	 JRST BKWRDN
	JRST BKWRL1

BKWRD1:	CAME A,PTR
	BKJFN
	 JRST BKWRDN
	LDB B,A
	PUSHJ P,INVCHK
	 JRST BKWRD1
BKWRDN:	MOVEM A,EPTR
	SETZ B,
	IDPB B,A
	POPJ P,

INVCHK:	CAILE B," "
	CAIL B,177
	AOS 0(P)
	POPJ P,
BKLIN:	MOVE A,EPTR
	SETZ B,
	IDPB B,A
	MOVE A,EPTR
BKLINL:	CAMN A,PTR
	 POPJ P,
	BKJFN
	 0
	LDB B,A
	CAIE B,37
	CAIN B,12
	POPJ P,
	JRST BKLINL

DING:	MOVEI A,7
	PBOUT
	JRST RLUP

ENDIN:	SETZ B,
	MOVE C,EPTR
	IDPB B,C
	AOS 0(P)
	POPJ P,

; BYTE INPUT FROM MULTI-FILES

.BIN:	PUSH P,B
	MOVE A,INFIL
	BIN
	JUMPE B,[GTSTS
		TLNN B,1000
		 JRST .-1
		PUSHJ P,ENDFIL
		POP P,B
		JRST .BIN]
	CAIN B,"B"-100
	 JRST NEWFIL
	MOVE A,B
	POP P,B
	POPJ P,

SYSGET:	PUSH P,A		;PRESERVE AC'S
	PUSH P,B
	SETZM 0(B)		;IN CASE FAILS, CLEAR ANSWER.
	SYSGT
	JUMPE B,BAPOPJ		;RETURN IF NO TABLE
	MOVE A,HSTFRE		;POINT TO FREE SPACE
	HLL A,B			;PUT IN COUNT
	MOVEM A,@0(P)		;THATS THE ANSWER
	PUSH P,B		;NOW GET DATA. SAVE POINTER
	HLLZ X,B		;AOBJN COUNTER
SYSGTL:	MOVSI A,(X)		;ENTRY NUMBER IN TABLE
	HRR A,0(P)		;TABLE NUMBER
	GETAB
	  JRST SYSGTF		;FAILED!!
	AOS C,HSTFRE		;MOVE ON DOWN THE FREE SPACE
	CAILE C,HSTTAB+NHSTTB	;OVERFLOW TABLE?
	JRST SYSGTF		;YES.
	MOVEM A,-1(C)		;NO. STORE DATUM
	AOBJN X,SYSGTL		;COUNT THRU TABLE
SYSGTY:	POP P,(P)		;PEEL STACK
BAPOPJ:	POP P,B
	POP P,A
	POPJ P,0

SYSGTF:	SETZM @-1(P)		;FAILURE. CLOBBER ANSWER.
	JRST SYSGTY		;AND RETURN

; POP FILE STACK

ENDFIL:	CLOSF
	 JFCL
	POP SF,INFIL
	CAMN SF,[XWD -NFILS,FILSTK-1]
	 TRZ F,QUIETF
	HRROI A,[ASCIZ /EOF)/]
	TRNN F,QUIETF
	 PSOUT
	POPJ P,

; SET NEW INPUT FILE, PUSH FORMER

NEWFIL:	PUSH SF,INFIL
	TRNE	FLG,NTLOGF		;IS USER LOGGED IN?
	 JRST	[ HRROI	A,[ASCIZ /(Files not accessible non-logged-in users.)/]
		  PSOUT
		  JRST	NONEWF  ]
	HRROI A,[ASCIZ /(Insert file: /]
	TRNN F,QUIETF
	PSOUT
	MOVSI A,(1B2!3B17)
	MOVEI B,100
	CAME B,INFIL
	 SKIPA B,[377777]
	 MOVEI B,101
	HRL B,INFIL
	GTJFN
	 JRST [	HRROI A,[ASCIZ / ?)/]
		TRNN F,QUIETF
		PSOUT
		JRST NONEWF]
	MOVEM A,INFIL
	MOVE B,[XWD 70000,200000]
	OPENF
	 JRST [	MOVE A,INFIL
		RLJFN
		 JFCL
		HRROI A,[ASCIZ / can't open)/]
		TRNN F,QUIETF
		PSOUT
		JRST NONEWF]
	HRROI A,[ASCIZ /.../]
	TRNN F,QUIETF
	PSOUT
	TRO F,QUIETF
	POP P,B
	JRST .BIN

NONEWF:	POP SF,INFIL
	POP P,B
	JRST .BIN

FTPSKT:	3
PDP:	IOWD PDLL,PDL

LIT
; MASSIVE RE-ORGANIZATION

; VARIABLES

LOC 10000
FILSAV:	BLOCK 1
GTJTAB:	BLOCK 1
GTJINF:	BLOCK	20
MONITV:	BLOCK 1
PDL2:	BLOCK 4
PDL:	BLOCK PDLL
FILSTK:	BLOCK NFILS
INFIL:	BLOCK 1
MSG:	BLOCK 1
MSGLEN:	BLOCK 1
HEAD:	BLOCK 1
HEDLEN:	BLOCK 1
SUBJCT:	BLOCK 1
DEFHST:	BLOCK 1
XDIST:	BLOCK 1
DISTL:	BLOCK 1
DIST:	BLOCK NDIST
HOST:	BLOCK NUSRS
USRTAB:	BLOCK NUSRS
FILNAM:	BLOCK NFILS
NUSERS:	BLOCK 1
TIMFRK:	BLOCK 1
SAVEP:	BLOCK 1
RETPC1:	BLOCK 1
LHOSTN:	BLOCK 1
LHOST:	BLOCK 1
FSKT:	BLOCK 1
LJFN:	BLOCK 1
RJFN:	BLOCK 1
SJFN:	BLOCK 1
ENDPTR:	BLOCK 1
OLDCNT:	BLOCK 1
ATCHAR:	BLOCK 1
SITEN:	BLOCK 1
SITE:	BLOCK 1
FROMNM:	BLOCK 1
HSTFRE:	BLOCK 1		;SPACE COUNTER INTO HSTTAB
HOSTN:	BLOCK 1		;POINTER TO HOST NUMBERS AND BITS
HSTNAM:	BLOCK 1		;POINTER TO HOST ASCII STRINGS
HSOUTX:	BLOCK 1		;TEMP FOR HSTSOU ROUTINE
HSTTAB:	BLOCK NHSTTB	;SPACE FOR HOST NAMES AND NUMBERS
STRING:	BLOCK MAXMSG/5
ESTRING:

RELOC

	END XWD EVECL,ENTVEC
