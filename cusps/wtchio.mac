	TITLE WATCH
	SUBTTL  M HANSON 1/9/71 FROM J BURCHFIEL 7/9/70
	SUBTTL JOHN PREBUS 10-23-72 
	SEARCH STENEX

	ENTRY SOUT,TTCAL,TTSEND,GETVAL,TIM,DSMS,ERROR,TICKS,FILO,GFILO
	ENTRY SIXPT,SETP,JFNTAB,CLOSEO,NAMES,LAND

	;THIS SET OF ROUTINES IS CALLED FROM A FORTRAN PROGRAM
	;CALLED WATCH.F4 IN PREBUS'S DIRECTORY
	;THEY ARE AN UPDATE AND SLIGHT EXTENSION OF BURCHFIELS
	;ORIGINAL WATCH PROGRAM - FROM WHICH ALOT OF THIS
	;MACHINE CODE IS TAKEN

	MLON
	P=17

LAND:	Z
	MOVE	0, @0(16)
	AND	0, @1(16)
	JRA	16, 2(16)

SIXPT:	Z
	MOVE 3,[XWD -6,0]
	MOVE 1,OUTJFN
	HRLZI 4,(POINT 6,)
	HRRI 4,@(16)
LP1:	ILDB 2,4
	ADDI 2,40
	BOUT
	AOBJN 3,LP1
	JRA 16,1(16)

SETP:	Z
	MOVEI 1,400000
	CLZFF
	MOVE P,[XWD -10,STK]
	JRA 16,(16)


JFNTAB:	XWD 460000,0
	XWD 100,101
	[ASCIZ /DSK/]
	0
	[ASCIZ /WATCH/]
	[ASCIZ /DAT/]
	EXP Z,Z,Z

SOUT:	0
	MOVE 1,OUTJFN
	HRRO 2,(16)
	SETZ 3,
	SOUT
	JRA 16,1(16)

TTCAL:	0
	TTCALL @0(16)
	JRA 16,1(16)
TTSEND:	0
	TTCALL 1,@0(16)
	JRA 16,1(16)

GETVAL:	Z
	MOVE 1,@0(16)
	GETAB
	PUSHJ P,ERROR
	MOVE 0,1
	JRA 16,1(16)

TIM:	Z
	TIME
	MOVEM 1,@(16)
	JRA 16,1(16)

DSMS:	Z
	MOVE 1,@(16)
	DISMS
	JRA 16,1(16)

ERROR:	MOVEM 1,SAV1
	MOVEM 2,SAV2
	MOVEM 3,SAV3
	MOVEM 16,SAV16
	HRROI 1,[ASCIZ/
 BUG HALTED - CALL PREBUS
/]
	PSOUT
	MOVEI 1,101
	MOVE 2,[XWD 400000,-1]
	SETZ 3,
	ERSTR
	HALTF
	HALTF
	HALTF
	LIT

TICKS: 	Z
	MOVE 1,[SIXBIT/TICKPS/]
	SYSGT
	MOVEM 1,@(16)
	TIME
	MOVEM 2,@1(16)
	JRA 16,2(16)

GFILO:	Z
	SETZ 2,
	MOVEI 1,JFNTAB
	GTJFN
	PUSHJ P,ERROR
	HRRZM 1,OUTJFN
	DVCHR
	TLNE 2,(1B2)
	JRST DRCT	;DIRECTORY DEVICE
	MOVE 2,[XWD 70000,100000] 	;NON-DIRECTORY DEVICE
	MOVEM 2,OUT	;SET FOR OPEN
	SETOM @(16)
	JRA 16,1(16)
DRCT:	MOVE 2,[XWD 70000,20000]	;SET FOR APPEND
	MOVEM 2,OUT	;SET FOR APPEND
	SETZM @(16)
	JRA 16,1(16)
FILO:	0
	MOVE 1,OUTJFN
	MOVE 2,OUT
	OPENF
	PUSHJ P,ERROR
	JRA 16,(16)

CLOSEO:	Z
	MOVE 1,OUTJFN
	TLO 1,400000
	CLOSF
	PUSHJ P,ERROR
	JRA 16,(16)

NAMES:	Z
	MOVE 1,[SIXBIT/JOBRT/]
	SYSGT
	MOVEM 2,@(16)
	MOVE 1,[SIXBIT/SYSTAT/]
	SYSGT
	MOVEM 2,@1(16)
	MOVE 1,[SIXBIT/SNAMES/]
	SYSGT
	MOVEM 2,@2(16)
	MOVE 1,[SIXBIT/STIMES/]
	SYSGT
	MOVEM 2,@3(16)
	MOVE 1,[SIXBIT/SBLKTM/]
	SYSGT
	MOVEM 2,@4(16)
	MOVE 1,[SIXBIT/SPFLTS/]
	SYSGT
	MOVEM 2,@5(16)
	MOVE 1,[SIXBIT/SWAKES/]
	SYSGT
	MOVEM 2,@6(16)
	MOVE 1,[SIXBIT/JOBNAM/]
	SYSGT
	MOVEM 2,@7(16)
	JRA 16,8(16)
OUTJFN:	BLOCK 1
SAV1:	BLOCK 1
SAV2:	BLOCK 1
SAV3:	BLOCK 1
SAV16:	BLOCK 1
OUT:	BLOCK 1
STK:	BLOCK 10

	END

