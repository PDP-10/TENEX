
;9 FEB 71, 2233:
;COMMANDS FROM FILE PROGRAM - D. MURPHY  7 FEB 71

	TITLE RUNFIL
	MLON

P=17
NPDL==40

OPDEF CALL [PUSHJ P,0]
OPDEF RET [POPJ P,0]

START:	MOVE P,[IOWD NPDL,PDL]
	RESET
	SETZM FORKH
	SETZM IMODE
	MOVEI 1,400000
	MOVE 2,[XWD LEVTAB,CHNTAB]
	SEARCH STENEX
	SIR
	EIR
	MOVE 2,[1B0+1B19]
	AIC
	MOVE 1,[XWD 2,0]
	ATI			;ASSIGN CONTROL-B FOR INTERRUPTING
	MOVEI 1,400000
	GPJFN			;REMEMBER INITIAL PRIMARY JFNS
	MOVEM 2,PJFNS
	HLRZM 2,IPJFN
	SETOM OPJFN
	MOVE 1,IPJFN
	DVCHR			;GET LINE NUMBER OF CTRL TTY
	MOVSM 3,CTTYWD
	MOVE 1,[SIXBIT /TTYJOB/]
	SYSGT
	HRRM 2,CTTYWD		;CONSTRUCT ARG FOR GETAB
	HRROI 1,[ASCIZ /
COMMANDS FROM FILE: /]
	PSOUT
	MOVSI 1,(1B2+3B17)
	MOVE 2,[XWD 100,101]
	GTJFN
	JRST HBAD
	MOVEM 1,IJFN
	MOVEI 1,100
	BKJFN
	JFCL
RUNF1:	PBIN
	CAIE 1," "
	CAIN 1,33
	JRST RUNF1
	CAIE 1,","		;CR OR COMMA CONFIRMS
	CAIN 1,37
	SKIPA 10,1		;SAVE TERMINATOR
	JRST HBAD
	MOVE 1,IJFN
	MOVE 2,[7B5+1B19]
	OPENF
	JRST [	HRROI 1,[ASCIZ /CANNOT OPEN/]
		PSOUT
		JRST HBAD]
	CAIE 10,","
	JRST RUNF2		;TERMINATED WITH CR, ASSUME TTY OUT
RUNF3:	HRROI 1,[ASCIZ /
OUTPUT TO FILE: /]
	PSOUT
	MOVSI 1,(1B0+1B3+3B17)
	MOVE 2,[XWD 100,101]
	GTJFN
	JRST RUNF3
	MOVEM 1,OJFN
	MOVEI 1,100
	BKJFN
	JFCL
RUNF4:	PBIN
	CAIE 1," "
	CAIN 1,33
	JRST RUNF4
	CAIE 1,37
	JRST RUNF3
	MOVE 1,OJFN
	MOVE 2,[7B5+1B20]
	OPENF
	JRST [	RLJFN
		JFCL
		JRST RUNF3]
	HRRM 1,PJFNS
	HRRZM 1,OPJFN
	MOVEI 1,100
	RFMOD
	MOVEM 2,IMODE
	TRZ 2,3B25
	SFMOD
RUNF2:	MOVSI 1,(1B1)
	CFORK			;CREATE FORK AND XMIT CAPABILITIES
	0
	MOVEM 1,FORKH
	MOVSI 1,(1B2+1B17)
	HRROI 2,[ASCIZ /<SYSTEM>EXEC.SAV/]
	GTJFN
	0
	HRL 1,FORKH
	GET			;LOAD EXEC INTO FORK
	MOVE 1,FORKH
	MOVE 2,PJFNS
	SPJFN			;SET FORKS PRIMARY IO
	MOVEI 2,0
	SFRKV			;START FORK AT MAIN STARTUP
STIPRG:	MOVE 1,IJFN
	SKIPG 2,LCH		;GET SAVED CHAR IF ANY
	BIN
	PUSH P,2
	GTSTS
	TLNE 2,(1B8)
	JRST [ POP P,2		;END OF FILE
		JRST STIEND ]
	POP P,2
	SETZM LCH
	JUMPE 2,STIPRG		;IGNORE ZEROES
	CAIN 2,"^"-100		;CONTROL ^ (SHIFT-CTRL-N)
	JRST DOCTRL		;MEANS TAKE NEXT CHAR AS CTRL
	CAIN 2,15
	JRST [	BIN		;MAKE EOL'S BE CR ONLY
		CAIE 2,12
		MOVEM 2,LCH	;NOT FOLLOW BY LINE FEED, DON'T FLUSH
		MOVEI 2,15
		JRST STIDO]
STIDO:	HRRZ 1,IPJFN
	DIBE
	STI
	PUSH P,1
	MOVEI  1,^D100
	DISMS
	POP P,1
	SKIPGE 1,OPJFN
	JRST STIPRG
	CAIN 2,15
	MOVEI 2,37
	BOUT
	JRST STIPRG

DOCTRL:	BIN
	CAIG 2,"9"
	CAIGE 2,"0"
	JRST DOCT1
	BKJFN
	0
	MOVEI 3,12
	NIN
	0
	MOVE 1,2
	DISMS
	JRST STIPRG

DOCT1:	SUBI 2,100
	CAIN 2,"C"-100
	JRST DOCC
	CAIN 2,"B"-100
	JRST DOCCW
	JRST STIDO

DOCC:	HRRZ 1,IPJFN
	CFIBF
DOCC1:	STI
	CALL WAITC
	JRST STIPRG

DOCCW:	CALL WAITC
	HRRZ 1,IPJFN
	MOVEI 2,"C"-100
	JRST DOCC1

WAITC:	MOVEI 1,^D1000
	DISMS
	MOVE 1,FORKH
	RFSTS
	HLRZ 1,1
	CAIE 1,2		;FORK HALTED OR CRASHED?
	CAIN 1,3
	JRST [	POP P,1		;YES, QUIT
		JRST STIE1]
	HRRZ 1,IPJFN
	DIBE
	MOVE 1,CTTYWD
	GETAB
	JFCL
	MOVEI 1,0(1)
	CAIE 1,-1		;INPUT AWAITED ON LINE?
	RET			;YES
	MOVEI 1,^D1000		;NO, WAIT AWHILE
	DISMS
	JRST WAITC

HBAD:	HRROI 1,[ASCIZ /  ? /]
	PSOUT
	JRST START

STIEND:	CALL WAITC
STIE1:	HRROI 1,[ASCIZ /
FINISHED.

/]
	PSOUT
STIE2:	SKIPE 1,FORKH
	KFORK
	RESET
	MOVEI 1,100
	SKIPE 2,IMODE
	SFMOD
	HALTF

ABORTQ:	HRROI 1,[ASCIZ /
ABORTED.
/]
	PSOUT
	MOVEI 1,STIE2
INTR1:	MOVEM 1,IPC1
	DEBRK

FKTRM:	MOVEI 1,STIE1
	JRST INTR1

LEVTAB:	XWD 0,IPC1
	XWD 0,IPC2
	XWD 0,IPC3

CHNTAB:	XWD 1,ABORTQ
	REPEAT ^D19-^D1,<EXP 0>
	XWD 1,FKTRM
	REPEAT ^D35-^D20,<EXP 0>

IPC1:	BLOCK 1
IPC2:	BLOCK 1
IPC3:	BLOCK 1

PJFNS:	BLOCK 1
IPJFN:	BLOCK 1
OPJFN:	BLOCK 1
LCH:	BLOCK 1
CTTYWD:	BLOCK 1
IJFN:	BLOCK 1
OJFN:	BLOCK 1
FORKH:	BLOCK 1
IMODE:	BLOCK 1
PDL:	BLOCK NPDL

	END START



