;<SOURCES>DELVER.MAC;2    12-SEP-74 13:59:14	EDIT BY MANN
;<KI-MON>DELVER.MAC;2    10-OCT-73 14:18:12	EDIT BY DURHAM
;HACK TO AVOID PROBLEMS WITH CODE AT LP0
;<TOMLINSON>DELVER.FAI;1    14-FEB-73 19:39:14	EDIT BY TOMLINSON
; ALLOW LIST OF FILE
;<SOURCES>DELVER.FAI;2    14-FEB-73 19:08:37	EDIT BY TOMLINSON
;<SOURCES>DELVER.FAI;5    24-JAN-73 23:39:04	EDIT BY TOMLINSON
;<SOURCES>DELVER.FAI;2    24-JAN-73 23:16:02	EDIT BY TOMLINSON
; SHORTENED DIALOG, IMPROVED ERROR RECOVERY
	TITLE	DELVER
	SUBTTL	R.S.TOMLINSON  5 APRIL 71
	SEARCH	STENEX

MJFN==20

OPDEF	ESOUT[PSOUT]	;;;I4 CHANGE...
	OPDEF SIBE [SKIPA]


A=1
B=2
C=3
D=4
X=5
P=17

START:	RESET
	MOVE P,[IOWD 20,PDL]
	MOVEI A,100
	SIBE
	 JRST SS1
	HRROI A,[ASCIZ /
DELETE OLDEST? /]
	PSOUT
SS1:	PUSHJ P,YNO
	JRST START
	MOVEM A,DELOLD
STZ:	MOVEI A,37
	PBOUT
	MOVEI A,100
	SIBE
	 JRST SS2
	HRROI A,[ASCIZ /DELETE 2ND NEWEST? /]
	PSOUT
SS2:	PUSHJ P,YNO
	JRST STZ
	MOVEM A,DEL2ND
	MOVEI A,37
	PBOUT
	MOVEI A,400000
	SETZM WHEEL
	RPCAP
	TRNE C,600000
	SETOM WHEEL
STW:	MOVEI A,100
	SIBE
	 JRST SS3
	HRROI A,[ASCIZ /FILE GROUP = /]
	PSOUT
SS3:	MOVSI X,-MJFN
SS31:	MOVEI A,GTJFB
	SETZ B,
	GTJFN
	 JRST [	MOVEI A,100
		BKJFN
		 JFCL
		PBIN
		CAIN 1,177
		 JRST [	HRROI A,[ASCIZ /XXX  /]
			PSOUT
			JRST SS31]
		HRROI A,[ASCIZ /
/]
		ESOUT
		JRST SS31]
	MOVEM A,JFN(X)
	ADD X,[1,,1]
	MOVEI A,100
	BKJFN
	 JFCL
	PBIN
	CAIN A,33
SS32:	PBIN
	CAIE A,37
	 JRST [	JUMPL X,SS31
		HRROI A,[ASCIZ / (FULL) /]
		ESOUT
		JRST SS32]
	MOVNI A,0(X)
	HRLZ X,A
LP0: ;	SKIPN WHEEL	;;;I4 CHANGE...
	 JRST LP1
	HRRZ B,JFN(X)
	MOVSI C,(<BYTE (3)0,1>)
	HRROI A,STRING
	JFNS
	HRROI B,STRING
	SETZ A,
	STDIR
	 0
	 0
	MOVE A,B
	MOVEI B,DIRBLK
	SETZ C,
	GTDIR
	LDB A,[POINT 2,DIRBLK+11,35]	; GET DEFAULT DIRECTORY BACKUP
	CAIE A,2
	 JRST NXTDIR
LP1:	HRRZ B,JFN(X)
	MOVE A,[POINT 7,STRING]
	MOVE C,[BYTE (3)0,1,1,1,0,0,0(1)0,0,0,0,0(5)0,1]
	JFNS
	MOVEI B,";"
	IDPB B,A
	MOVEM A,SAVP
	MOVEI B,"0"
	IDPB B,A
	SETZ B,
	IDPB B,A
	HRROI B,STRING
	MOVSI A,100001
	GTJFN
	 JRST [	SETOM LSTVER
		JRST XX1]
	MOVE B,[XWD 1,7]
	MOVEI C,C
	GTFDB
	HLRZM C,LSTVER
	RLJFN
	 0
XX1:	MOVE A,SAVP
	HRROI B,[ASCIZ /-2/]
	SETZ C,
	SOUT
	HRROI B,STRING
	MOVSI A,100001
	GTJFN
	 JRST [	SETOM FSTVER
		JRST XX2]
	MOVE B,[XWD 1,7]
	MOVEI C,C
	GTFDB
	HLRZM C,FSTVER
	RLJFN
	 0
XX2:	MOVE A,SAVP
	MOVE B,LSTVER
	SOJLE B,[SETOM SECVER
		JRST LP2]
	MOVEI C,^D10
	NOUT
	 0
	HRROI B,STRING
	MOVSI A,100001
	GTJFN
	 JRST [	SETOM SECVER
		JRST LP2]
	MOVE B,[XWD 1,7]
	MOVEI C,C
	GTFDB
	HLRZM C,SECVER
	RLJFN
	 0
LP2:	HRRZ A,JFN(X)
	MOVE B,[XWD 1,11]
	MOVEI C,C
	GTFDB
	TLNE C,770000
;	 JRST LP3     ;;sumex change we use these bits for VERSIONS to RETAIN
	HRRZ A,JFN(X)
	MOVE B,[XWD 1,7]
	MOVEI C,C
	GTFDB
	HLRZS C
	SKIPN DELOLD
	SKIPGE FSTVER
	JRST YY1
	CAMN C,FSTVER
	JRST LP3
YY1:	SKIPN DEL2ND
	SKIPGE SECVER
	JRST YY2
	CAMN C,SECVER
	JRST LP3
YY2:	CAMN C,LSTVER
	JRST LP3
	MOVE B,A
	MOVEI A,101
	SETZ C,
	JFNS
	MOVE A,JFN(X)
	DELF
	 JRST [	HRROI A,[ASCIZ /	Not deleted./]
		PSOUT
		JRST .+1]
	MOVEI A,37
	PBOUT
LP3:	MOVE A,JFN(X)
LP3A:	GNJFN
	 JRST DONE
	TLNN A,17
	JRST LP2
	JRST LP1

NXTDIR:	HRROI A,[ASCIZ /
SKIPPING /]
	PSOUT
	HRROI A,STRING
	PSOUT
	MOVEI A,37
	PBOUT
	MOVE A,JFN(X)
	TLZ A,70000
	JRST LP3A

DONE:	AOBJN X,LP0
	HALTF
	JRST START

YNO:	PBIN
	CAIL A,140
	SUBI A,40
	CAIE A,"Y"
	CAIN A,"N"
	JRST YNO1
	HRROI A,[ASCIZ /  TYPE Y OR N ONLY
/]
	ESOUT
	POPJ P,

YNO1:	CAIE A,"Y"
	TDZA A,A
	SETO A,
	AOS (P)
	POPJ P,

GTJFB:	XWD 100103,-3
	XWD 100,101
	0
	0
	REPEAT 2,<POINT 7,[ASCIZ /*/]>
	REPEAT 6,<0>

PDL:	BLOCK 20
JFN:	BLOCK MJFN
LSTVER:	BLOCK 1
FSTVER:	BLOCK 1
SECVER:	BLOCK 1
DIRBLK:	BLOCK 20
WHEEL:	BLOCK 1
DELOLD:	BLOCK 1
DEL2ND:	BLOCK 1
SAVP:	BLOCK 1
STRING:	BLOCK 100

	END START
