;<SOURCES>FTSTOP.MAC;9    12-MAR-75 00:04:26    EDIT BY CLEMENTS
;<SOURCES>FTSTOP.MAC;8     4-MAR-75 18:55:56    EDIT BY CLEMENTS
; SET UP TENEX VERSION NUMBER ON STARTUP
;<SOURCES>FTSTOP.MAC;7    23-DEC-74 16:49:19    EDIT BY SUSSMAN
;<SOURCES>FTSTOP.MAC;6     8-DEC-74 13:08:08    EDIT BY SUSSMAN
;<SOURCES>FTSTOP.MAC;5    12-NOV-74 22:06:52    EDIT BY SUSSMAN
;<SOURCES>FTSTOP.MAC;4    14-AUG-74 10:30:53    EDIT BY SUSSMAN
;<SOURCES>FTSTOP.MAC;3    11-JUL-74 23:46:56    EDIT BY CLEMENTS
TITLE FTSTOP - FILE TRANSFER SERVER TOP LEVEL (ICP, CFORK)
SUBTTL R CLEMENTS / (C) BOLT BERANEK & NEWMAN, INC., 1973

SEARCH STENEX
SEARCH FTSPAR

EXTERNAL APOPJ,BADINT,BAPOPJ,BPOPJ,CHSSKT,FNDSKX,FTPSA,MAKFRK
EXTERNAL PCRLF,RELSKT,RLDINT,SCRREP,SNDREP,TIMESA,TIMINT

INTERNAL GO,CPOPJ,CPOPJ1,KILL,OPNLOG,CLSLOG
INTERNAL BOMB,BNLAPP,BNLBPW,BNLDEL,BNLRNM,BNLMAL,BNLRET,BNLLGI
INTERNAL CLOSER,CLOSEK,PDP,TIMCHN,TIMCHB,CRLFM
INTERNAL BITTAB,BNLSTO,JFNTXT,LPTPFX,LV2PDP

;START OF PROGRAM. INITIALIZE STUFF.

GO:	RESET			;CLEAR STUFF IN SYSTEM
	MOVEI F,0		;ALL FLAGS OFF
	MOVE P,GPDP		;TOP FORK USES GLOBAL STACK POINTER
	MOVEI A,400000		;THIS PROCESS NEEDS ABSOLUTE SOCKETS
	RPCAP			; AND FILE ACCESS CAPABILITIES
	SETO C,			;SO TURN ON ALL CAPABILITIES 
	EPCAP			; ..
	GTAD			;CAN'T TYPE TIME AND DATE IF SYS HAS NONE
	JUMPLE A,GOWAIT		;JUMP IF ANSWER WAS -1
	SETZM DEBUGS		;ASSUME NOT DEBUGGING
	MOVE A,[400000,,770]	;DOES THIS FORK HAVE A PAGE 770?
	RPACS
	TLNE B,(1B5)		; ..
	SETOM DEBUGS		;YES. ENABLE DEBUG CODE
	MOVE A,[SIXBIT /LHOSTN/];GET LOCAL HOST NUMBER FROM SYSTEM
	SYSGT			;GET TABLE NUMBER AND ITEM 0
	JUMPE B,[MSG <FTPSRV: CANNOT GET LOCAL HOST NUMBER FROM SYSTEM
>
		JRST HALTGO]
	MOVEM A,LHOSTN		;SAVE MY LOCATION
	MOVEI B,(A)		;AND MY NAME IN ASCIZ
	HRROI A,LHSTNM		;..
	CVHST
	  JRST [MSG <FTPSRV: CANNOT GET LOCAL HOST NAME FROM SYSTEM
>
		JRST HALTGO]
	SETZM SVERNM		;IN CASE GETTAB NOT DEFINED
	MOVE A,['VERNUM']	;GET SYSTEM VERSION NUMBER FROM TENEX
	SYSGT			; ..
	SKIPE B			;IS IT THERE?
	MOVEM A,SVERNM		;YES, SET IT.
	MOVSI B,-NSWV		;SEARCH TABLE FOR THIS SITE'S SWITCHES
	HRRZ A,SWVECT(B)	;GET A SITE NUMBER FROM TABLE
	CAME A,LHOSTN		;THIS SITE?
	AOBJN B,.-2		;NO. TRY ANOTHER.
	MOVE A,SWVEC1(B)	;GET SITCHES FOR THIS SITE, OR
				; DEFAULT IF COUNTED THRU TABLE
	MOVEM A,SWTCHS		;AND STORE AWAY FOR RUNTIME TESTING
	MOVE A,['NETRDY']	;SEE IF THE IMP IS UP.
	SYSGT			;ASK MONITOR
	JUMPL A,IMPUP		;NEGATIVE IMPRDY MEANS OK.
GOWAIT:	MOVEI A,^D30000		;WAIT THIRTY SECONDS AND SEE IF IMP UP
	DISMS			; ..
	JRST GO			;TAKE IT FROM THE TOP

IMPUP:	MOVE A,['NETLSK']	;NEED SOME TABLE INDEXES FROM SYSGT
	SYSGT
	HRRZM B,LSKN
	MOVE A,['NETFSK']
	SYSGT
	HRRZM B,FSKN
	MOVE A,['NETAWD']
	SYSGT
	HRRZM B,AWDN
	MOVE A,['NETSTS']
	SYSGT
	HRRZM B,STSN
	HLLZM B,NSKTS		;AND AOBJN COUNTER WORD
	HRROI A,VERSTR		;NUMBERS TO STRING STORAGE
	MOVEI C,12		;IN DECIMAL
	HRRZ B,VERSIO		;MAJOR VERSION
	NOUT
	  JFCL
	MOVEI B,"."
	BOUT
	HLRZ B,VERSIO		;MINOR VERSION (EDIT)
	NOUT
	  JFCL
	MOVEI B,"."
	BOUT
	HLRZ B,PATCHX	;LOCAL SITE VERSION
	NOUT
	  JFCL
	MOVEI B,"."
	BOUT
	HRRZ B,PATCHX		;PATCH NUMBER IF BINARY PATCHED
	NOUT
	  JFCL
	SKIPN DEBUGS		;DEBUGGING?
	JRST IMPUP0		;NO. DON'T BOTHER HERALDING
	SKIPUNLESS CCA
	  JRST IMPUP2
	MSG <
FTP Server >
	HRROI A,VERSTR		;NOW TYPE VERSION
	PSOUT
	MSG < in operation >
	MOVEI A,101		;ANNOUNCE WITH DATE AND TIME
	SETO B,0		;CURRENT TIME
	MOVSI C,200201		;REASONABLE FORMAT
	ODTIM			;TYPE IT OUT
	PUSHJ P,PCRLF		;AND A CRLF
IMPUP2:
IMPUP0:	HRROI B,[ASCIZ /** RESTARTED FTPSRV VERSION /]
	PUSHJ P,OPNLOG		;LOG THE RESTART
	  JRST IMPUP1		; CAN'T?
	HRROI B,VERSTR		; ..
	MOVEI C,0
	SOUT
	SKIPIF CCA
	  JRST IMPUP3
	MOVEI A,TXPO
	HRROI B,VERSTR
	MOVEI C,0
	SOUT
IMPUP3:
	PUSHJ P,CLSLOG		;CLOSE LOG FILE
IMPUP1:	SKIPUNLESS BINL
	PUSHJ P,BNLRES		;LOG THE RESTART


;INITIALIZATION OF STORAGE

	MOVSI A,-NFORKS		;CLEAR TABLES ASSOCIATED WITH FTP FORKS
INIL1:	SETOM FORKXT(A)		;FORK INDEX SEEN FROM TOP LEVEL
	SETZM KTIMET(A)		;TIME TO KILL FORK IF HANGS
	AOBJN A,INIL1		;FOR ALL FORKS
	TIME			;GET SYSTEM UPTIME CLOCK
	MOVEM A,TODCLK		;SAVE TIME OF DAY
	MOVEM B,TICKPS		;MORE IMPORTANT, SECONDS CONVERSION

	MOVEI A,0		;FIND SYSTEM DIRECTORY NUMBER
	HRROI B,[ASCIZ /SYSTEM/]
	STDIR			;FOR USE BY MAIL OPERATION.
	  JFCL
	  MOVEI A,1		;GOTTA BE THERE. ASSUME 1.
	HRRZM A,SYSDNM		;SAVE IT.
	SETOM LPTDNM		;NO SPOOLED LPT IF FLAG -1
	MOVEI A,0		;SEE IF SPOOLER DIRECTORY EXISTS
	HRROI B,LPTNAM		;NAME OF SPOOLER DIRECTORY
	STDIR
	  JFCL
	  SKIPA			;NO SUCH DIRECTORY
	HRRZM A,LPTDNM		;IT EXISTS. STORE NUMBER AS FLAG .GT.0
;START-UP CODE

	PUSHJ P,MAKFRK		;CREATE THE TIMING FORK
	  JRST [MSG <FTPSRV:CAN'T CREATE TIMING FORK
>
		JRST HALTGO]
	MOVEM A,TIMFRK		;SAVE HANDLE AS SEEN FROM TOP FORK
	MOVEI B,TIMESA		;START ADDRES OF TIMER
	SFORK			;MAKE IT GO.
	MOVEI A,400000		;SET UP PI SYSTEM
	MOVE B,[LEVTAB,,CHNTAB]	; ..
	SIR
	EIR
	MOVE B,CHNMSK
	AIC

TOPLP:	SETO PX,		;FLAG AM TOP-LEVEL PROCESS.
	PUSHJ P,LISTEN		;CREATE THE LISTENER FOR REQUESTS
	  JRST NOLSN
TOPLP1:	MOVEI A,^D30000		;LONG SLEEP
SLEEP:	DISMS
	SKIPE LSNCHK		;LISTEN BEEN POKED BY A USER?
	JRST LSNICP		;YES. GO INVESTIGATE
	MOVE A,['NETRDY']	;SEE IF IMP STILL HAPPY
	SYSGT
	JUMPGE A,IMPDED		;IF DEAD, JUMP.
	MOVE A,LSNJFN		;SEE IF SOCKET STILL HAPPY
	GDSTS			;NET-DEPENDENT VALUES
	HLRZ A,B		;MAY HAVE JUST MISSED PSI FOR RFCR
	ANDI A,740000
	CAIE A,(<FSLSNG>B3)	;STILL LISTENING?
	JRST LSNICP		;NO. LOOK AT IT.
	ANDI C,777		;YES. SEE IF FOREIGN SOCKET STILL WILD
	CAIN C,NOHOST		; ..
	JRST TOPLP1		;YES. WAIT PATIENTLY SOME MORE
	MOVE A,LSNJFN		;NO. NET GLITCHED. CLEAR SOCKET.
	TLO A,200000
	CLOSF
	  JFCL
	JRST TOPLP		;AND REOPEN IT.

NOLSN:	MOVE P1,A		;SAVE THE ERROR NUMBER
	MOVE A,['NETRDY']	;LISTEN ATTEMPT FAILED. IS IMP SICK?
	SYSGT
	JUMPGE A,IMPDED		;JUMP IF IMP IS DOWN.
	MSG <FTPSRV: CAN'T CREATE FTP LISTENER SOCKET >
	HRROI A,(X)		;GET THE MESSAGE
	PSOUT			;TYPE IT
	MOVEI A,101		;AND TYPE THE ERROR NUMBER
	MOVE B,P1
	MOVEI C,10
	NOUT
	  JFCL
	MOVEI B,40		;SPACE
	BOUT
	SETO B,			;TIME STAMP
	MOVSI C,240
	ODTIM
	PUSHJ P,PCRLF
	MOVEI A,^D60000		;WAIT A MINUTE
	DISMS			;AND CALL ME IN THE MORNING
	JRST TOPLP		;TRY AGAIN.

IMPDED:	MOVEI A,^D60000		;WAIT A MINUTE FOR FORKS TO
	DISMS			;CLEAN UP IF THEY CAN
	JRST GO			;AND RESTART PROGRAM COMPLETELY.

;CREATE LISTENER SOCKET FOR REMOTE REQUESTS

LISTEN:	SETOM LSNJFN		;THESE JFNS ARE NOT IN USE
	SETOM RJFN		; ..
	SETOM SJFN		; ..
	SETZM LSNCHK		;NO USERS OF THE SOCKET YET.
	SETZM SKTBIT		;NO BIT ASSIGNED FOR SOCKET GROUP
	HRROI A,GTJSTR		;STRING ADDRESS FOR JFN NAME
	HRROI B,[ASCIZ /NET:/]	;DEVICE FIELD
	MOVEI C,0		; TO STRING
	SOUT
	MOVE B,FTPSKT		;AND SOCKET NUMBER SPECIFIED BY FTP
	SKIPE DEBUGS		;DEBUGGING?
	ADDI B,1000		;DIFFERENT SOCKET
	MOVEI C,10		;COULD BE PART OF SOUT BUT FOR DEBUG
	NOUT
	  PUSHJ P,BOMB
	HRROI B,[ASCIZ /#./]	;ABSOLUTE SYSTEM SOCKET NUMBER
	MOVEI C,0
	SOUT			;AND NO EXT (REMOTE) I.E., LISTEN
	MOVSI A,400001		;SHORT STRING OUTPUT JFN
	HRROI B,GTJSTR		;ADDRESS OF NAME
	HRROI X,[ASCIZ /GTJFN /]
	GTJFN
	  POPJ P,0		;CAN'T GET IT
	MOVEM A,LSNJFN		;STORE THE JFN
	MOVE B,[400000,,100000]	;OPEN SEND, 32BIT SIZE
	HRROI X,[ASCIZ /OPENF /]
	OPENF
	  JRST [PUSH P,A
		MOVE A,LSNJFN
		RLJFN
		  JFCL
		JRST APOPJ]
	MOVSI C,777700+LSNCHN	;REQUEST TO BE INTERRUPTED WHEN RFC
	MOVEI B,24		;COMES IN.
	MTOPR			; ..
CPOPJ1:	AOS 0(P)		;SKIP RETURN FROM LISTEN
CPOPJ:	POPJ P,0		;RETURN

;HERE ON INTERRUPT DUE TO STATE CHANGE OF LISTEN SOCKET

LSNINT:	SETOM LSNCHK		;FLAG TOP LEVEL TO LOOK AT FTP SOCKET
	MOVEM A,NTIIA		;STASH AC
	HRRZ A,RETPC2		;SEE IF BROKE OUT OF SLEEP
	CAIE A,SLEEP
	CAIN A,SLEEP+1
	SKIPA A,[10000,,SLEEP+1];YES. SET TO USER MODE AFTER SLEEP
	SKIPA			;NO.
	MOVEM A,RETPC2		;YES. UPDATE PC
	MOVE A,NTIIA		;RESTORE AC
	DEBRK

;HERE WHEN LISTENING SOCKET HAS BEEN REFERENCED

LSNICP:	SETZB X,ICPERM		;NO ERROR MSG YET
	SETZM RJFN		;NO TELNET SOCKETS
	SETZM SJFN		; ..
	MOVE A,LSNJFN		;GET JFN FOR LISTENING SOCKET
	CVSKT			;GET ABSOLUTE LISTEN SOCKET NUMBER
	  JRST REJECT		;CAN'T HAPPEN
	TRO B,1			;IT'S A SEND SOCKET
	MOVEM B,ABSLSS		;SAVE FOR TABLE SEARCHING
	MOVE A,LSNJFN		;SET UP JFN OF LISTENER AGAIN
	GDSTS			;SEE WHAT ITS STATE IS AND WHO CALLED
	MOVEM C,FHSTN		;SAVE HOST AND SOCKET (ASSUMING VALID)
	MOVEM D,FORNS		; ..
	HLRZ A,B		;CHECK CONNECTION SIZE AND STATE
	ANDI A,77
	CAIE A,40		;REQUIRED 32 BITS?
	JRST REJECT		;NO.
	HLRZ A,B		;NOW STATE
	ANDI A,740000		; ..
	CAIE A,(<FSRFCR>B3)	;SHOULD BE RFCR
	JRST REJECT
	PUSHJ P,FNDSKX		;FIND SOCKET INDEX IN SYSTEM TABLES
	  JRST REJECT		;NOT THERE.
;ADD HERE ANY HOST NUMBER FILTERING DESIRED
ACCEPT:	PUSHJ P,CHSSKT		;OK TO ACCEPT. MAKE SURE HAVE SOCKET
	MOVEM A,SKTS		;CHOOSE SOCKET "S", SAVE IT.
	HRROI A,GTJSTR		;NOW GET A COUPLE TELNET JFNS
	HRROI B,[ASCIZ /NET:/]	;BUILD THE JFN STRING FOR THEM
	MOVEI C,0
	SOUT
	MOVE B,SKTS		;SOCKET NUMBER INTO NAME FIELD
	MOVEI C,10		;OCTAL
	NOUT
	  PUSHJ P,BOMB
	MOVEI B,"."
	IDPB B,A		;SEPARATOR FOR FOREIGN SOCKET FIELD
	HRRZ B,FHSTN		;FOREIGN HOST, AS OCTAL NUMBER
	NOUT
	  PUSHJ P,BOMB
	MOVEI B,"-"		;DASH BETWEEN HOST AND SOCKET
	IDPB B,A
	MOVE B,FORNS		;SOCKET NUMBER, OCTAL
	ADDI B,2		;SOCKETS U+2 AND U+3
	NOUT
	  PUSHJ P,BOMB
	HRROI B,[ASCIZ /;T/]	;MAKE SOCKET NUMBER JOB-RELATIVE
	MOVEI C,0
	SOUT

ACPT1:	MOVSI A,1		;SHORT STRING JFN CALL
	HRROI B,GTJSTR
	GTJFN
	  JRST REJECT		;CAN'T GET JFN, DON'T ACCEPT CONNECT
	MOVEM A,SJFN		;SAVE TELNET SEND JFN
	MOVSI A,1		;ANOTHER FOR RECEIVE SIDE
	HRROI B,GTJSTR
	GTJFN
	  JRST REJECT
	MOVEM A,RJFN		;JFN FOR TELNET RECEIVE SIDE
	CVSKT			;GET REAL NUMBER TO TELL USER ICP
	  JRST REJECT		;OOPS - CANT HAPPEN
	MOVEM B,ABSTNS		;SAVE ABSOLUTE TELNET SOCKET
	MOVE A,LSNJFN		;NOW LOOKS LIKE CAN ACCEPT ICP.
	MOVEI B,20		;ACCEPT CALL NUMBER
	MTOPR
;NOW WANT TO WAIT FOR ALLOCATIONS SO THIS JOB DOESN'T HANG IN BOUT
	MOVE A,['NETBAL']
	SYSGT			;HAVE TO POKE AROUND IN TABLES FOR IT
	HRRZM B,BALN
	MOVE A,['NETSTS']
	SYSGT
	MOVEM B,STSN
	MOVEI X,[ASCIZ /BIT ALLOC NOT RECEIVED/]
	MOVNI A,WATNSC		;NUMBER OF SECONDS TO WAIT
	MOVEM A,WATCNT		;SAVE.
WATBAL:	MOVE A,BALN		;TABLE FOR BIT ALLOCATION
	HRL A,LSNSKX		;INDEX FOR LISTEN SOCKET
	GETAB
	  JRST REJECT
	CAIGE 1,^D32		;ROOM FOR 32 BIT MSG?
	  JRST [MOVEI A,^D1000
		DISMS
		AOSG WATCNT
		JRST WATBAL
		JRST REJECT]
	MOVEI X,[ASCIZ /MESSAGE ALLOC NOT RECEIVED/]
WATMAL:	MOVE A,STSN		;TABLE FOR MESSAGE ALLOCATION
	HRL A,LSNSKX		;LISTEN SOCKET INDEX
	GETAB
	  JRST REJECT
	ANDI A,177777
;;	CAIGE A,1
	SKIPA			;;PROBLEM WITH 1.30.00
	  JRST [MOVEI A,^D1000
		DISMS
		AOSG WATCNT
		JRST WATMAL
		JRST REJECT]

;NOW HAVE DONE ALL POSSIBLE CHECKING TO AVOID HANGING IN BOUT
ACPSND:	MOVE A,LSNJFN		;JFN TO SEND THE SOCKET NUMBER "S" ON
	MOVE B,ABSTNS		;ABSOLUTE TELNET SOCKET NUMBER
	BOUT			;SEND THE NUMBER AS A 32 BIT BYTE
	TLO A,200000
	CLOSF			;LET GO OF MASTER FTP SOCKET
	  JFCL			;IF THAT LOSES, PUNT.
;NOW THE OTHER GUY KNOWS ABOUT OUR TELNET SOCKETS. TRY TO OPEN THEM.
	SETZM LSNJFN		;FLAG THAT THE LISTENER IS GONE.
	MOVE A,SJFN		;OPEN TELNET SEND SIDE WITHOUT WAITING
	MOVE B,[103400,,100000]	;WRITE 8-BIT BYTES, DON'T WAIT, BFRD SEND.
	MOVEI X,[ASCIZ /CAN'T OPEN TELNET SEND/]
	OPENF
	  JRST REJEC1		;CAN'T?
	MOVE A,RJFN		;AND THE RECEIVE SIDE
	MOVE B,[100000,,200000]	;READ 8-BIT BYTES.
	MOVEI X,[ASCIZ /CAN'T OPEN TELNET RECEIVE/]
	OPENF			;OPEN RECEIVE SIDE
	  JRST REJEC1
	JRST MKFTPP		;READY TO GO. ICP COMPLETED.

REJECT:	SKIPGE A,LSNJFN		;IF ANY LISTEN JFN,
	JRST REJEC1		; NO.
	HRLI A,(1B1)		;YES. CLOSE IT, WAIT FOR NCP.
	CLOSF
	  JFCL
	SETOM LSNJFN
REJEC1:	CLOSE SJFN		;CLOSE THE TWO TELNET JFN'S
	CLOSE RJFN		; ..
	MOVEM X,ICPERM		;SAVE ERROR MESSAGE
	PUSHJ P,RELSKT		;RELEASE SOCKET BIT IF ANY
	SKIPN DEBUGS
	JRST REJEC2
	HRROI A,[ASCIZ /
FTPSRV: ICP FAILED - /]
	PSOUT
	HRRO A,ICPERM
	TRNN A,-1
	HRRI A,[ASCIZ /UNSPECIFIED/]
	PSOUT
	PUSHJ P,PCRLF		;PRIMARY CRLF
REJEC2:	JRST TOPLP

;CREATE AN INFERIOR FOR FTP, IF ANY LEFT.
;START OF FTP SERVER PER SE. SEND SIGNON MESSAGE

MKFTPP:	SKIPN DEBUGS		;DEBUGGING?
	JRST MKFTP0		;NO
	MSG <
CONNECTION TO FTP FROM >
	MOVEI A,101
	HRRZ B,FHSTN
	MOVEI C,10		;IDENTIFY THE FOREIGN HOST
	CVHST
	  NOUT
	    JFCL
	PUSHJ P,PCRLF		;PRIMARY CRLF

MKFTP0:	HRROI A,REPLYM		;STRING BUFFER FOR REPLY
	HRROI B,[ASCIZ /300 /]	;NUMERIC TYPE FOR HELLO
	MOVEI C,0
	SOUT
	HRRZ B,LHOSTN		;IDENTIFY THE SITE
	CVHST
	  JFCL
	HRROI B,[ASCIZ / FTP Server /]
	SOUT
	HRROI B,VERSTR		;VERSION AS TEXT
	MOVEI C,0
	SOUT

MKFTP2:	HRROI B,[ASCIZ / - at /]
	MOVEI C,0
	SOUT
	SETO B,			;CURRENT DATE AND TIME
	MOVSI C,200221		;FORMAT FLAGS FOR DATE AND TIME AND ZONE
	ODTIM
	MOVEM A,REPLYP		;STORE REPLY CURRENT STRING POINTER
	PUSHJ P,SCRREP		;SEND CRLF AND REPLY OUT.
	MOVE A,['ENTFLG']	;SEE IF THE SYSTEM SHOULD ACCEPT USERS
	SYSGT
	JUMPE B,ENTOK1		;NO SUCH GETAB
	JUMPN A,ENTOK1		;JUMP IF SWITCH IS ON
	HRROI A,REPLYM		;NOT ALLOWED ON. BUILD ERROR MSG
	HRROI B,[ASCIZ /401 System is not accepting users now. Goodbye./]
	MOVEI C,0
	SOUT
	MOVEM A,REPLYP
	PUSHJ P,SCRREP		;SEND THIS REPLY
	JRST REJEC1		;AND CHASE HIM OFF

ENTOK1:	PUSHJ P,MAKFRK		;TRY TO MAKE A FORK FOR FTP
	  JRST NOFORK		;CAN'T. SYSTEM OR PROCESS TOO FULL

;BEGINNING OF PROCESS FOR FTP ACTUAL TRANSFER. SET DEFAULTS
;AND GET READY TO GO.

DOFTP:	PUSH P,A		;SAVE HANDLE
	MOVEI A,400000		;THIS FORK
	DIR			;STOP TIMING INTERRUPTS A MOMENT
	POP P,A			;GET BACK INFERIOR'S HANDLE
	MOVEM A,FORKXT(PX)	;STORE PROCESS HANDLE.
	TIME			;GET CURRENT TIME
	IMULI B,WAT1ST		;FIRST COMMAND WAIT IN SECONDS
	ADD B,A			;PLUS CURRENT TIME
	MOVEM B,KTIMET(PX)	;IS TIME TO KILL THIS FORK
	MOVE A,FORKXT(PX)	;GET BACK FORK HANDLE
	MOVEI B,FTPSA		;CODE STARTING ADDRESS FOR FTP PROCESS
	SFORK			;AND AWAY IT GOES.
	SETO PX,		;NOW RESET PX TO TOP-LEVEL.
	MOVEI A,400000		;TURN INT'S BACK ON
	EIR
	JRST TOPLP		;AND GO MAKE ANOTHER LISTENER.

;HERE IF CAN'T MAKE THE FORK FOR SOME REASON. SEND LOSE MESSAGE.

NOFORK:	HRROI A,REPLYM
	HRROI B,[ASCIZ /401 FTP Service full. Please try again later.
/]
	MOVEI C,0
	SOUT
	PUSHJ P,SNDREP
KILL:	SKIPGE $USER		;LOGIN OR MAIL?
	JRST KILL1		;NO. DON'T LOG.
	SKIPUNLESS BINL
	PUSHJ P,BNLDIS		;LOG THE DISCONNECT
	HRROI B,[ASCIZ /OFF  USED /]
	PUSHJ P,OPNLOG		;LOG HIM OFF.
	  JRST KILL1		;CAN'T
	PUSH P,A		;SAVE JFN
	MOVEI A,400000		;FORK TIME
	RUNTM			; ..
	MOVE B,A
	POP P,A
	MOVEI C,12		;DECIMAL MILLISECONDS
	NOUT
	  MOVE A,LOGJFN
	SKIPIF CCA
	  JRST KILL04
	PUSH P,A
	MOVEI A,TXPO
	NOUT
	  JFCL
	POP P,A
KILL04:	HRROI B,[ASCIZ / MSEC/]
	MOVEI C,0
	SOUT
	SKIPIF CCA
	  JRST KILL4A
	PUSH P,A
	MOVEI A,TXPO
	HRROI B,[ASCIZ / MSEC/]
	MOVEI C,0
	SOUT
	POP P,A
KILL4A:	MOVE B,$ACCT
	CAMN B,[-1]		;WAS AN ACCOUNT EVER SET?
	JRST KILLG2		;NO
	JUMPGE B,KILLG3		;YES, JUMP IF NUMERIC
	HRROI B,[ASCIZ /, ACCT /]
	MOVEI C,0
	SOUT
	HRRO B,$ACCT
	SOUT			;LOG THE ACCOUNT
	JRST KILLG2		;DONE STRING ACCOUNT
KILLG3:	HRROI B,[ASCIZ /, ACCT# /]
	MOVEI C,0
	SOUT
	MOVE B,$ACCT
	MOVEI C,12
	NOUT
	  MOVE A,LOGJFN
KILLG2:	PUSHJ P,CLSLOG		;CLOSE THE LOG FILE
KILL1:	CLOSE DATJFN		;MAKE SURE ALL SOCKETS AND FILES CLOSED
	CLOSE RJFN
	CLOSE SJFN
	CLOSE LCLJFN
	CLOSE LOGJFN
	PUSHJ P,RELSKT		;RELEASE THE BIT FOR SOCKET S, IF ANY.
	JUMPL PX,TOPLP		;IF AT TOP LEVEL, GO LISTEN.
	SKIPN DEBUGS		;DEBUGGING?
	JRST KILL01		;NO
	MSG <KILLED PROCESS >
	MOVEI A,101
	MOVEI B,(PX)		;PROCESS NUMBER
	MOVEI C,10		;OCTAL
	NOUT
	  JFCL
	PUSHJ P,PCRLF
KILL01:	TRNN PX,400000		;IN CASE NEGATIVE SOMEHOW
	SETOM KTIMET(PX)	;SAY TIME TO KILL.
	MOVEI A,100000		;LONG DISMIS
	DISMS			;WAIT FOR TOP FORK TO KILL.

	JRST .-2

;LOGGING ROUTINES

OPNLOG:
	SKIPIF TXTL
	  POPJ P,0		;NON-SKIP DISABLES ALL CALLS HERE.
	SKIPE DEBUGS		;DON'T LOG DURING DEBUGGING
	POPJ P,0		;GIVE FAIL RETURN
	MOVNI A,^D10		;TRY FIVE TIMES
	MOVEM A,LOGCNT
	PUSH P,B		;MESSAGE TO START LOG LINE WITH
	MOVSI A,401001		;IGNORE DELETED, OUTPUT, SHORT.
	HRROI B,[ASCIZ /<SYSTEM>FTPSRV.LOG;1/]
	SKIPUNLESS CCA
	HRROI B,[ASCIZ /<SYS-LOG+MSG>FTPSRV.LOG;1/]
	GTJFN
	  JRST BPOPJ		;CAN'T
	HRRZM A,LOGJFN
OPNLG2:	MOVE A,LOGJFN
	MOVE B,[070000,,020000]	;APPEND, ASCII
	OPENF			;OPEN FOR APPENDING MSG
	  JRST OPNLG1		;CAN'T
	SKIPUNLESS CCA
	  JRST OPLCC1		;DO IT CCA STYLE
	HRRZ B,THISPX		;PROCESS NUMBER
	MOVEI C,12		;DECIMAL?
	HRLI C,120003		;COLUMNATED
	NOUT
	  JFCL
	MOVEI B,40		;SPACE
	BOUT
	SETO B,			;CURRENT DATE AND TIME
	MOVSI C,000240		;FORMAT
	ODTIM
	MOVEI B,40		;A SPACE
	BOUT
	HRRO B,0(P)		;STRING TO OUTPUT
	MOVEI C,0		;ASCIZ ENDS ON NULL
	SOUT
	JRST OPLCC2

OPLCC1:	SETO B,			;CURRENT DATE AND TIME
	MOVSI C,(1B1+0B9)	;CCA STD FORMAT
	ODTIM
	HRRZ B,THISPX
	MOVEI C,12
	HRLI C,120003		;PROCESS NUMBER
	NOUT
	  JFCL
	MOVEI B," "
	BOUT
	HRRO B,0(P)
	MOVEI C,0		;STRING FROM CALLER
	SOUT

OPNCCA:	MOVEI A,TXPO		;AND PUT IT ON PRIMARY TTY TOO
	DOBE
	MOVEI A,^D2000		;WAIT FOR TTY
	DISMS
	MOVEI A,TXPO
	SOBE			;STILL EMPTY?
	  JRST OPNCCA		;NO, WAIT AGAIN
	SETO B,			;YES. OUTPUT CURRENT DATE AND TIME
	MOVSI C,(1B1+0B9)
	ODTIM
	HRRZ B,THISPX
	MOVEI C,12
	HRLI C,120003
	NOUT
	  JFCL
	MOVEI B," "
	BOUT
	HRRO B,0(P)
	MOVEI C,0
	SOUT

	MOVE A,LOGJFN		;SET UP FOR CALLER TO USE THIS.
OPLCC2:	POP P,B			;RESTORE AC
	JRST CPOPJ1		;GOOD RETURN.

OPNLG1:	AOSLE LOGCNT		;TRY AGAIN?
	JRST [MOVE A,LOGJFN	;NO. QUIT.
		CLOSF
		  JFCL
		SETOM LOGJFN
		JRST BPOPJ]	; ..
	MOVEI A,^D5000		;WAIT A MOMENT
	SKIPUNLESS CCA
	MOVEI A,^D10000		;WAIT LONGER FOR TTY OUTPUT
	DISMS
	JRST OPNLG2		;TRY AGAIN

CLSLOG:
	SKIPUNLESS CCA
	PUSHJ P,PCRLF		;EOL TO TTY TOO
	MOVE A,LOGJFN		;ADD A CRLF
	HRROI B,CRLFM		; ..
	MOVEI C,0
	SOUT
	CLOSF
	  JFCL
	SETOM LOGJFN		;NOW NOT OPEN
	POPJ P,0

;BINARY LOG FILE CODE

BNLLGI:	MOVEI X,LC.LGI		;CODE FOR LOGIN
	PUSHJ P,BNLSET		;DO COMMON STUFF
	;FALL INTO CLOSE. NO MORE DATA ON LOGIN
BNLCLS:	TRNE X,777700		;DID DATA OVERFLOW?
	MOVEI X,77		;YES. ONLY 63 WORDS
	SKIPIF FCTL
	JRST FCTLG1
	PUSH P,A		;FACT FILE VIA EFACT
	MOVNI A,(X)		;NEGATIVE COUNT
	MOVSI A,(A)		;IN LH
	HRRI A,BNLDAT		;DATA ADDR IN RH
	EFACT			;PUT IT IN FACT FILE
	  JFCL			;CANT
	JRST APOPJ
FCTLG1:				;ELSE, PUT IN FTP'S OWN LOG
	PUSH P,A		;DONT CLOBBER ANY AC'S EXCEPT X
	PUSH P,B
	PUSH P,C
	DPB X,[POINT 6,BNLDAT,35]
	SETOM BNLJFN		;NO FILE OPEN YET
	MOVEI C,10		;COUNT OF TRIES TO OPEN FILE
	HRROI B,BNLFFN		;BINARY LOG FACT FILE NAME
	SKIPUNLESS CCA
	HRROI B,CCAFFN		;DIFFERENT DIRECTORY AT CCA
	MOVSI A,1001		;IGNORE DELETED
	GTJFN
	  JRST BNLCL1		;CAN'T GET IT?
	MOVEM A,BNLJFN		;STORE JFN
BNLCL2:	MOVE A,BNLJFN		;TRY TO OPEN THIS JFN
	MOVE B,[440000,,020000]	;FOR APPENDING
	OPENF
	  JRST [MOVEI A,^D5000	;TRY AGAIN IN FIVE SECONDS
		DISMS
		SOJG C,BNLCL2
		JRST BNLCL1]	;NO GO
	MOVNI C,(X)		;COUNT FOR SOUT
	MOVE B,[444400,,BNLDAT]	;DATA
	SOUT
BNLCL1:	CLOSE BNLJFN		;LET GO JFN
	JRST CBAPJ		;RETURN
BNLFFN:	ASCIZ /<SYSTEM>FTPSRV.BLOG;1/
CCAFFN:	ASCIZ /<SYS-LOG+MSG>FTPSRV.BLOG;1/

BNLSET:	PUSH P,A
	PUSH P,B
	PUSH P,C
	SETZM BNLDAT		;CLEAR DATA AREA
	MOVE A,[BNLDAT,,BNLDAT+1]
	BLT A,BNLDAE-1
	MOVSI A,(<FTPLGC>B8)
	MOVEM A,BNLDAT		;OVERALL CODE
	DPB X,[POINT 12,BNLDAT,29]	;SUB CODE
	MOVE A,THISPX		;PROCESS NUMBER
	DPB A,[POINT 9,BNLDAT,17]
	MOVEI X,BNLVDO		;VARIABLE DATA ORIGIN
	SKIPGE A,$HOST		;USE DIRECTED HOST, IF ANY, ELSE TELNET.
	MOVE A,FHSTN		;FOREIGN HOST
	MOVEM A,BNLDAT+4
	GTAD			;TIME
	MOVEM A,BNLDAT+2
	SKIPGE A,$ACCT		;ACCOUNT SET YET?
	JRST BNLST1		;MAY BE STRING
	MOVEM A,BNLDAT+5	;NUMERIC OR 0, STORE.
	JRST BNLST2
BNLST1:	AOJE A,BNLST2		;JUMP IF -1 (DEFAULT)
	HRRO A,X		;WHERE TEXT GOES
	MOVEM A,BNLDAT+5	;STRING POINTER
	MOVSI C,ACCTST-ACCTSE	;- NUMBER OF WORDS IN ACCT STORAGE
BNLST3:	SKIPN A,ACCTST(C)	;DATA?
	JRST BNLST2		;NO. DONE.
	MOVEM A,BNLDAT(X)	;YES.  COPY IT.
	ADDI X,1		;NEXT OUTPUT SLOT
	AOBJN C,BNLST3		;NEXT INPUT SLOT
BNLST2:	MOVEI A,400000		;THIS FORK
	RUNTM			;RUN TIME OF THE FORK
	SUB A,IFRKTM		;MINUS THAT AT START
	MOVEM A,BNLDAT+3
	SKIPG A,$USER		;USER DECLARED?
	JRST BNLST4		;NO, MAY BE ANON
	MOVEM A,BNLDAT+1	;YES. STORE IT
	JRST BNLST5
BNLST4:	TLNN F,L.ANON		;ANONYMOUS?
	JRST BNLST5		;NO. UNKNOWN
	HRRO A,X		;YES. COPY THE NAME.
	MOVEM A,BNLDAT+1	;POINTER TO IT
	MOVSI C,USRPSW-USRBKE	;COUNT OF WORDS (STORED IN PSW SPACE)
BNLST6:	SKIPN A,USRPSW(C)
	JRST BNLST5		;DONE.
	MOVEM A,BNLDAT(X)	;STORE WORD OF ASCII
	ADDI X,1
	AOBJN C,BNLST6
BNLST5:	JRST CBAPJ

BNLRET:	MOVEI X,LC.RET		;RETRIEVE CODE
	JRST BNLFIL
BNLSTO:	MOVEI X,LC.STO
	JRST BNLFIL
BNLAPP:	MOVEI X,LC.APP		;APPEND
	JRST BNLFIL
BNLRNM:	MOVEI X,LC.RNM		;RENAME CODE
	JRST BNLFIL
BNLDEL:	MOVEI X,LC.DEL		;DELETE FILE CODE
BNLFIL:	PUSHJ P,BNLSET		;DO COMMON PART
	PUSH P,A
	PUSH P,B
	HRRO A,X
	MOVEM A,BNLDAT+7	;STORAGE FOR FILE NAME
	SKIPG A,$BYTE		;CURRENT BYTE SIZE
	MOVEI A,10
	MOVEM A,BNLDAT+6
	MOVSI B,JFNTXS-JFNTXE
BNLFL1:	SKIPN A,JFNTXS(B)
	JRST BNLFL2
	MOVEM A,BNLDAT(X)
	ADDI X,1
	AOBJN B,BNLFL1
BNLFL2:	POP P,B
	POP P,A
	JRST BNLCLS

BNLDIS:	MOVEI X,LC.DIS		;DISCONNECT ENTRY
	PUSHJ P,BNLSET
	MOVE A,TSBITS		;TOTAL SENT BITS
	MOVEM A,BNLDAT+6
	MOVE A,TRBITS		;TOTAL RECEIVED BITS
	MOVEM A,BNLDAT+7
	JRST BNLCLS

BNLBPW:	MOVEI X,LC.BPW		;BAD PASSWORD ATTEMPT
	PUSHJ P,BNLSET
	JRST BNLCLS

BNLRES:	MOVEI X,LC.RES		;RESTART CODE
	PUSHJ P,BNLSET
	JRST BNLCLS		;NO OTHER DATA

BNLMAL:	MOVEI X,LC.MAL		;MAIL CODE
	PUSHJ P,BNLSET		;COMMON STUFF
	PUSH P,A
	TLNE F,L.MLUN		;GENL DELIVERY?
	JRST BNLML1		;YES.
	HRRZ A,MLUSR
	MOVEM A,BNLDAT+7	;REAL USER DIRECTORY
	POP P,A
	JRST BNLCLS

BNLML1:	PUSH P,B
	MOVSI B,-20
	HRRO A,X		;STRING POINTER
	MOVEM A,BNLDAT+7
BINML2:	SKIPN A,MLUNST(B)
	JRST BINML3
	MOVEM A,BNLDAT(X)
	ADDI X,1
	AOBJN B,BINML2
BINML3:	POP P,B
	POP P,A
	JRST BNLCLS

;ODDS AND ENDS

BOMB:	HRROI A,[ASCIZ /
? "IMPOSSIBLE ERROR" IN FTP SERVER PROCESS AT LOCATION /]
	PSOUT
	HRRZ B,0(P)
	SUBI B,1
	MOVEI A,101
	MOVEI C,10
	NOUT			;TYPE ERROR LOC IN OCTAL
	  PUSHJ P,BOMB
HALTGO:	HALTF			;HALT AND RESTART
	JRST GO

CLOSER:	SKIPGE 0(A)		;IS THERE A JFN OPEN?
	POPJ P,0		;NO.
	PUSH P,A		;MAYBE.
	PUSH P,B
	HRRZ A,0(A)		;GET THE JFN
	GTSTS			;ITS STATUS
	JUMPGE B,[	RLJFN
			  JFCL
			JRST CLOSR1]
	CLOSF
	  JFCL
CLOSR1:	POP P,B
	POP P,A
	SETOM 0(A)
	POPJ P,0

CLOSEK:	SKIPGE 0(A)		;JFN THERE?
	POPJ P,0		;NO
	PUSH P,A
	MOVE A,(A)
	HRLI A,400000		;KEEP JFN
	CLOSF
	  JFCL
	POP P,A
	POPJ P,0

JFNTXT:	PUSH P,A
	PUSH P,B
	PUSH P,C
	SETZM JFNTXS
	MOVE A,[JFNTXS,,JFNTXS+1]
	BLT A,JFNTXE-1		;CLEAR FILE NAME STORAGE
	HRRZ B,-2(P)		;JFN IN A ON CALL
	HRROI A,JFNTXS		;DESTINATION
	MOVE C,[211110,,1]	;FORMAT
	JFNS			;OUTPUT NAME
CBAPJ:	POP P,C
	JRST BAPOPJ

CRLFM:	BYTE (7)15,12,0

VERSIO:	XWD NREVIS,NVERS	;FOR HEADINGS
PATCHX:	XWD NLOCAL,NPATCH	;PATCH THIS NUMBER WHEN PATCHING BINARY
GPDP:	IOWD PDLL,GPDL		;GLOBAL (TOP-FORK) STACK POINTER
PDP:	IOWD PDLL,PDL		;PROCESS STACK POINTER
LV2PDP:	IOWD LV2PLL,LV2PDL	;STACK ON CHANNEL 2 PSI

;HERE ARE THE SWITCH SETTINGS FOR THE VARIOUS INSTALLATIONS.
;THIS TABLE IS SEARCHED AT RUNTIME INITIALIZATION, AND BITS
;ARE COPIED INTO SWTCHS IN GLOBAL STORAGE.
;PLEASE FEED BACK CHANGES TO BBN TO BE INCORPORATED IN THIS TABLE.

DEFINE SW <
SW1(4,<S.BINL!S.MLUN!S.ANON!S.ALGI!S.AFIL>) ;;UTAH
SW1(37,<S.TXTL!S.CCA>)	;;CCA
SW1(40,<S.TXTL!S.AFIL!S.ANON>)	;;PARC-MAXC
SW1(61,<S.ALGI!S.AFIL!S.BINL!S.FCTL!S.ANON!S.BBN!S.MLST!S.MLQB>);;BBN SYSTEM B
SW1(102,<S.TXTL!S.AFIL!S.ANON>)	;;SRI-AI
SW1(70,<S.TXTL!S.AFIL!S.ANON>)   ;;SUMEX-AIM
SW1(126,<S.TXTL!S.AFIL!S.MLST!S.MLQB>)	;;ISI
SW1(162,<S.ALGI!S.AFIL!S.BINL!S.FCTL!S.ANON!S.BBN!S.MLST!S.MLQB>);;BBN SYSTEM D
SW1(305,<S.ALGI!S.AFIL!S.BINL!S.FCTL!S.ANON!S.BBN!S.MLST!S.MLQB>);;BBN SYSTEM A
SW1(361,<S.ALGI!S.AFIL!S.BINL!S.FCTL!S.ANON!S.BBN!S.MLST!S.MLQB>);;BBN SYSTEM C
>

DEFINE SW1(AA,BB)<	EXP AA>
SWVECT:	SW
NSWV==.-SWVECT
DEFINE SW1(AA,BB)<	EXP BB>
SWVEC1:	SW
	EXP DEFSWV			;THIS IS THE DEFAULT, IF HOST
					; NUMBER NOT IN SWVECT TABLE.

BITTAB:	REPEAT ^D36,<EXP 1B<.-BITTAB>>

FTPSKT:	FTPICS			;PROTOCOL ICP SOCKET #

LEVTAB:	RETPC1
	RETPC2
	RETPC3
CHNTAB:	XWD 2,RLDINT		;CHAN 0, RELOAD SELF.
RLDCHN==.-1-CHNTAB
	REPEAT 8,<0>	;1-8
	XWD 2,BADINT	;9 - PDL OV
	0
	XWD 2,BADINT	;11 - FILE DATA ERROR
	REPEAT 3,<0>
	REPEAT 4,<XWD 2,BADINT>	;15-18 ILL INST AND MEM VIOL

	REPEAT 42+CHNTAB-.,<0>
	XWD 2,LSNINT
LSNCHN==.-1-CHNTAB
	XWD 2,TIMINT
TIMCHN==.-1-CHNTAB

TIMCHB:	EXP 1B<TIMCHN>
CHNMSK:	EXP <1B<RLDCHN>+1B<LSNCHN>+1B<TIMCHN>!1B9!1B11!17B18>
LPTNAM:	ASCIZ /PRINTER/
LPTPFX:	ASCIZ /<PRINTER>FTP-/	;PREFIX FOR RECEIVING SPOOLED LPT

CODTOP==.	;TOP OF CODE.

	END GO

