BEGIN"PNTMAK"
REQUIRE "<sail>SAILOR" SOURCE!FILE;
STRING DATA,OUTER;
INTEGER ABRCHAR,AEOF;
INTEGER DEBT;
INTEGER REMOVE!CTRLS,TV!UNDER,LPT!UNDER;
INTEGER BOLD!UNDER;
INTEGER RE!PUB;

DEFINE CRTBL="1",LFTBL="2",COMMATBL="3";
DEFINE CTRL(X)="(""X""-'100)";

STRING SIMPLE PROCEDURE REMOVE!BARS(STRING S);
BEGIN
! REPLACES ASCII '137 AND '30 BY "-";

STRING T;
T_NULL;
WHILE S DO 
   BEGIN INTEGER CHAR; CHAR_LOP(S); 
	IF CHAR="_" OR CHAR="" THEN T_T & "-" ELSE T_T & CHAR; 
   END;
RETURN(T);
END;

STRING SIMPLE PROCEDURE NOCTRL(STRING S);
BEGIN
! REMOVES CTRL-R AND CTRL-S FROM S;
STRING T;
T_NULL;
WHILE S DO 
     BEGIN 
	INTEGER CHAR; 
	CHAR_LOP(S); 
	IF NOT (CHAR=CTRL(R) OR CHAR=CTRL(S)) THEN T_T & CHAR;
     END;
RETURN(T);
END;

SIMPLE STRING PROCEDURE MAKE!TV(STRING S,T);
! MAKES S,T HAVE TVEDIT-UNDERLINES;
BEGIN
INTEGER CS,CT;
STRING U;
U_NULL;
WHILE TRUE DO
    BEGIN
	IF NOT S AND NOT T THEN DONE;
	CS_LOP(S);
	CT_LOP(T);
	IF CT="_" OR CT="" THEN U_U & CTRL(F);
	IF CS=0 THEN U_U & SPACE ELSE U_U & CS;
    END;
RETURN(U);
END;

SIMPLE STRING PROCEDURE NOTABS(STRING S);
BEGIN
STRING T; INTEGER CNT;

T_NULL;
CNT_0;

WHILE LENGTH(S) DO
    BEGIN
	INTEGER C;
	C_LOP(S);
	IF C=TAB THEN
	    BEGIN
		INTEGER I,LIMIT;
		LIMIT _ (8 - (CNT MOD 8));
		FORE(I,LIMIT) T _ T & SPACE;
		CNT _ CNT + LIMIT;
	    END ELSE 
	    BEGIN
		T _ T & C;
		INC(CNT);
	    END;
    END;
RETURN(T);
END;

SIMPLE STRING PROCEDURE REMOVE!TV(STRING S; REFERENCE STRING U);
BEGIN
STRING T; INTEGER C;

S _ NOTABS(S);
T_U_NULL;
WHILE S DO
    BEGIN
	C_LOP(S);
	IF C=CTRL(F) THEN
	    BEGIN
		WHILE LENGTH(U) < LENGTH(T) DO U_U & SPACE;
		U_U & "_";
		T_T & LOP(S);
	    END ELSE T_T & C;
    END;
RETURN(T);
END;
SIMPLE STRING PROCEDURE MAKE!PUB(STRING S);
BEGIN
STRING NEW!S,UNDER;
! STRING IS IN TVEDIT FORMAT.  CONVERT TO PUB, RETURN
THE RESULT, READY TO BE PRINTED OUT;
NEW!S_UNDER_NULL;
WHILE S DO
    BEGIN
	INTEGER C;
	C_LOP(S);
	IF C="F"-'100 THEN
	    BEGIN
		INTEGER L!NEW!S,L!UNDER,I;
		L!NEW!S_LENGTH(NEW!S);
		FOR I_LENGTH(UNDER)+1 STEP 1 UNTIL L!NEW!S DO UNDER_UNDER & SPACE;
		UNDER_UNDER & "_";
		NEW!S_NEW!S & LOP(S);
	    END ELSE NEW!S_NEW!S & C;
    END;
IF UNDER THEN RETURN(NEW!S & '15 & UNDER & CRLF) ELSE RETURN(NEW!S & CRLF);

END;

SIMPLE PROCEDURE MODEGET;
BEGIN
STRING S,T; LABEL AGAIN;
AGAIN:
LPT!UNDER_RE!PUB_BOLD!UNDER_REMOVE!CTRLS_TV!UNDER_0;

OUTSTR("MODES (? FOR HELP)  *");
S_UPPER(INCHWL);
IF EQU(S,"?") THEN
    BEGIN
	OUTSTR("
TYPE A LIST OF MODES, SEPARATED BY COMMAS.
UNDER-LINE DISPOSITION:
	B	EMBOLDEN FOR LINE-PRINTER
		(DESTROYS INFORMATION)
	L	LINE-PRINTER FORMAT	(DEFAULT)
		(FOR A JUNK LISTING -- DESTROYS
		INFORMATION)
CONTROL-CHARACTERS
	RS	REMOVE CTRL-R AND CTRL-S 
		FROM FILE
");
	GOTO AGAIN;
    END;
WHILE S DO
    BEGIN
	DEFINE E(X)="IF EQU(T,""X"") THEN ";

	T_SCAN(S,COMMATBL,ABRCHAR);
	E(B) BEGIN
		RE!PUB_TV!UNDER_LPT!UNDER_FALSE;
		BOLD!UNDER_TRUE;
	     END ELSE
	E(L) BEGIN 
		RE!PUB_TV!UNDER_BOLD!UNDER_FALSE;
		LPT!UNDER_TRUE;
	     END ELSE
	E(T) BEGIN
		TV!UNDER_TRUE;
		RE!PUB_LPT!UNDER_BOLD!UNDER_FALSE;
   	     END ELSE
	E(P)
	    BEGIN
		RE!PUB_TRUE;
		LPT!UNDER_TV!UNDER_BOLD!UNDER_FALSE;
	    END ELSE
	E(RS) BEGIN
		REMOVE!CTRLS_TRUE;
	     END ELSE
	     BEGIN
		OUTSTR("ILLEGAL MODE " & T & CRLF);
		GOTO AGAIN;
	     END;
    END;
IF NOT (TV!UNDER  OR LPT!UNDER OR RE!PUB OR BOLD!UNDER) THEN
    BEGIN
	OUTSTR("LPT CHOSEN BY DEFAULT
");
	LPT!UNDER_TRUE;
    END;
END;

OUTSTR("PNTMAK  April 14, 1974.
");
SETBREAK(COMMATBL,",",NULL,"IN");
MODEGET;

INGET("DATA",DATA,1,0,2,ABRCHAR,AEOF);
OUTGET("OUTER",OUTER,2,0,2);
SETBREAK(CRTBL,'15,NULL,"IN");
SETBREAK(LFTBL,'12,NULL,"IN");


DEBT_0;
DO
   BEGIN"MAIN"
	STRING S,T;
	S_INPUT(1,CRTBL);		! GET UPTO THE CR;
	IF REMOVE!CTRLS THEN S_NOCTRL(S);
	T_INPUT(1,LFTBL);	! GET UPTO THE LF;

	IF BOLD!UNDER THEN
	BEGIN"BOLD"
	IF NOT T THEN S_REMOVE!TV(S,T);
	IF T THEN
	    BEGIN
		STRING U,S1;
		INTEGER C1,C2;
		INTEGER I;

		U_NULL;			! BUILD UP OVERPRINT HERE;
		S1_S;			! COPY S OVER;		
		WHILE T DO
		    BEGIN
			C1_LOP(T);	! GET A CHAR FROM UNDER;			
			C2_LOP(S1);	! AND FROM THE LINE;
			U_U & (IF C1="_" THEN C2 ELSE SPACE);	! CHOOSE TO OVERPRINT OR
									  SPACE;
		    END;			    
		OUT(2,S);		
		FOR I_1 STEP 1 UNTIL 3 DO		
		    BEGIN
			OUT(2,'15);
			OUT(2,U);
		    END;
		OUT(2,CRLF);
	    END ELSE BEGIN OUT(2,S); OUT(2,CRLF); END;
	END"BOLD" ELSE
	IF LPT!UNDER THEN
	BEGIN"LPT"
	IF NOT T THEN S_REMOVE!TV(S,T);
	IF T THEN	
	    BEGIN	    
		OUT(2,S & CRLF);
		OUT(2,REMOVE!BARS(T) & CRLF);	
		INC(DEBT);
	    END
 	ELSE 
	    BEGIN
		IF NOT S THEN
		    BEGIN
			IF DEBT THEN DEBT_DEBT-1 ELSE OUT(2,CRLF);
		    END
		ELSE
		    OUT(2,S & CRLF);
	    END;
    	END"LPT" ELSE
	IF TV!UNDER THEN
	BEGIN"TV"
	IF T THEN OUT(2,MAKE!TV(S,T)) ELSE OUT(2,S);
	OUT(2,CRLF);
	END"TV" ELSE
	IF RE!PUB THEN
	BEGIN"REPUB"
	OUT(2,MAKE!PUB(S));
	END"REPUB";
   END"MAIN" UNTIL AEOF;

RELEASE(1); RELEASE(2);
END"PNTMAK";








