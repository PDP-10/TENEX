;<COWER>BUILD.MAC;2	EDIT BY COWER     9-JULY-75  10:21;00
;RE-FIXED BUG AT CMPR2 - REMOVED 6 CHAR RESTRICTION ON KEYWORDS
;PUT RESET AT NORMAL END TO PREVENT FILE HANGING UP
;<COWER>BUILD.MAC;1	 EDIT BY <COWER>   5-JUNE-75 13:00:05
;FIXED HORRIBLE BUG AT CMPR2, FIXED  PMAP TO WORK CORRECTLY
;<COWER>BUILD.MAC;1        EDIT BY <COWER>  26-MAY-75 15:00:01
;NOWS BUILD EQUIVALENCE LIST FROM <SYSSUP>EQUAL.TXT
;<COWER>BUILD.MAC;1       EDIT BY <COWER>  24-MAY-75 15:00:01 
;NOW BUILDS EXCLUSION LIST FROM FILE <SYSSUP>EXCLDE.LST
;FILE MUST EXIST
 	TITLE	BUILD
	SEARCH	STENEX
;
NAME:	BLOCK	8
COUNT:	BLOCK	1
SJFN:	BLOCK	1
JFN:	BLOCK	1
EXT:	BLOCK	8
BUFF:	BLOCK	8
EQUAL:	BLOCK	8
TO:	BLOCK	8
FILJFN:	BLOCK	1
P==17
STPDLL==200
STACK:	BLOCK	STPDLL

;DEFINITIONS

	OPDEF	CALL	[ PUSHJ   P,0]
	OPDEF	RET 	[ POPJ	P,0]
      

START:	RESET
	MOVE	P,[IOWD STPDLL,STACK]
	MOVE 	16,[POINT 7,STRNGS]
	SETZB	14,15
	SETOM	LOOKTB
	CALL    EXCL			;GO BUILD EXCLUSION LIST
	MOVSI	1,100100
	HRROI	2,[ASCIZ/<DOC>*.*/]
	GTJFN
	JRST	ERROR
	MOVEM	1,FILJFN		;STORAGE FOR GNJFN
	JRST	START2     		;CONVERT IT TO STRING

START1:	MOVE	1,FILJFN
	GNJFN
	JRST	DONE			;TABLES BUILT COMPACT THEM

START2:	CALL    JFNSTR
	JRST	START1

JFNSTR:	SETZM	NAME
	MOVE	2,[NAME,,NAME+1]
	BLT	2,EXT+7
	HRRZ	2,FILJFN		;SETUP FOR JFNS
       	HRROI	1,NAME
	MOVE	3,[1B8+1B11+1B35]	;NAME AND EXT                
	JFNS				;CONVERT IT
	MOVE	1,16
	MOVE	3,[1B8+1B11+1B14+1B35]
	JFNS
	SETZ	3,
       	IDPB	3,1
	EXCH	1,16
	SUBI	1,STRNGS
	MOVEI	2,NAME
	PUSHJ	P,COMPAR
	PUSHJ	P,INSERT
	PUSHJ	P,MATCH
	SKIPN	10
	RET
JFNST1:	PUSHJ	P,CMPR0
	PUSHJ	P,INSERT
	PUSHJ	P,MATCH
	JUMPN	10,JFNST1
	RET
COMPAR:	MOVEI	6,-1(2)         
	HRLI	6,10700			;BYTE POINTER
CMPR0:	MOVE	2,[POINT 7,BUFF]
	SETZM	BUFF
	MOVE 	3,[BUFF,,BUFF+1]
	BLT	3,BUFF+7
	SETZB	3,10
CMPR1:	ILDB	10,6
	CAIG	10,"Z"
	CAIGE	10,"0"
	JRST	CMPR2
	CAIGE	10,"A"
	CAIG	10,"9"
	SKIPA
	JRST	CMPR2
	IDPB	10,2
	AOS	3
	JRST	CMPR1

CMPR2:	JUMPE	10,[CAILE  3,1
		   JRST   CMPR3
		   RET]
    	CAIG	3,1
	JRST	CMPR0

CMPR3:	MOVE	2,[POINT 7,BUFF]
	MOVEI 	3,INVLID
CMPR4:	AOS 	3
	MOVE	4,0(3)
	JUMPE	4,CMPR5
	PUSHJ	P,CHECK
	JRST	CMPR4   
	JUMPN	10,CMPR0
	RET
CMPR5:	MOVEI	13,LOOKTB
COMPA1:	SKIPGE	0(13)
 	POPJ	P,     
	HLRZ	3,0(13)
	ADDI	3,KEYTAB
	PUSHJ	P,CHECK
	SKIPA
	JRST	[AOS  0(P) 
		POPJ	P,]
	AOS	13
	JRST	COMPA1

INSERT:	AOS	0(P)
	HRLZ	3,15	;GET KEYWORD ADDRESS
	HRR	3,14		;GET ENTRY ADDRESS
	MOVEM	3,0(13)	     	;FIND FIRST FREE LOC IN ENTRIES
	SETOM	1(13)		;SET END LOC TO -1
	SETZM	ENTRYS(14)	;ZERO OUT LINK ADDRESS
	MOVEM	1,ENTRYS+1(14)	;PUT IN POINTER TO FILE NAME
	ADDI	14,2	     	;BUMP NEXT ENTRY ADDRESS
INSER1:	MOVE	4,0(2)
	MOVEM	4,KEYTAB(15)
	AOS	15
	SETZ	4,
	LDB	4,[POINT 7,0(2),34]
	JUMPN	4,INSER2
	RET
INSER2:	AOS	2
	JRST	INSER1



MATCH:	HRRZ 	13,0(13)
MATCH1:	HRRZ	2,ENTRYS(13)
     	JUMPE	2,EMATCH
	MOVE	13,2
 	JRST	MATCH1

EMATCH:	MOVEM	14,ENTRYS(13)
	SETZM	ENTRYS(14)
	MOVEM	1,ENTRYS+1(14)
	ADDI	14,2
	RET



CHECK:	MOVE	5,2
CHECK1:	MOVE	4,0(3)
	CAME	4,0(5)
	RET
	SETZ	4,
	LDB	4,[POINT 7,0(5),34]
	AOS	5
	AOS	3
	JUMPN	4,CHECK1
	AOS	0(P)
	RET

DONE:	MOVSI	1,100001
	HRROI	2,[ASCIZ/<SYSSUP>EQUAL.TXT/]
	GTJFN
	JRST ERROR
	MOVEM	1,SJFN
	MOVE	2,[070000,,200000]
	OPENF
	JRST ERROR
BIP:	MOVE	1,SJFN
	HRROI	2,EQUAL
	MOVEI	3,^D40
	MOVEI	4,"_"
	SIN
	SETZ	3,
	DPB	3,2
	MOVE	3,[ASCIZ/0/]
	CAMN	3,EQUAL      
	JRST	FINI 
DIP1:	MOVE	1,SJFN
	HRROI	2,TO
	MOVEI	3,^D40
	MOVEI	4,12
	SIN
	SETZ	3,
	DPB	3,2
	MOVEI	2,TO
	CALL 	COMPAR
	JRST	DIP2
	MOVE	7,13
	MOVEI	2,EQUAL
	CALL	COMPAR
	JRST 	ADDIT
    	HRRZ	13,0(13)
DMATCH:	HRRZ	2,ENTRYS(13)
	JUMPE	2,DMATC1
	MOVE	13,2
	JRST	DMATCH
DMATC1:	HRRZ	7,0(7)
	SKIPA
DMATC2:	HRRZ   	7,ENTRYS(7)	;GET ADDRESS OF 1ST ENTRY
       	MOVEM   14,ENTRYS(13)
     	SETZM	ENTRYS(14)
	MOVE	2,ENTRYS+1(7)	 
	MOVEM	2,ENTRYS+1(14)
	MOVE	13,14
	ADDI	14,2
	SKIPE	ENTRYS(7)
	JRST	DMATC2
DIP2:	SETZM	EQUAL
	MOVE	2,[EQUAL,,EQUAL+1]
	BLT	2,TO+7   
	JRST	BIP



ADDIT:	HRRZ	3,14  
	HRL	3,15
	MOVEM	3,0(13)
	SETOM	1(13)
	CALL 	INSER1
	JRST	DMATC1
  




FINI:	MOVE	1,SJFN
	CLOSF
	JFCL
	RLJFN	
	JFCL

COMP: 	MOVEM	15,OFFSET+2
	MOVEM	16,OFFSET+3
	MOVEI	13,LOOKTB
	SETZ	14,
COMPAC:	HRRZ	2,0(13)
	CAIN	2,-1			;END OF TABLE
	JRST	DONE1
	HRRM	14,0(13)
COMPC1:	MOVE	1,ENTRYS+1(2)
	MOVEM	1,NENTRY(14)
	AOS	14 
	SKIPE	2,ENTRYS(2)
	JRST	COMPC1
       	SETZM	NENTRY(14)
	AOS	14
	AOS	13
	JRST	COMPAC
EXCL:	MOVSI	1,100001
	HRROI	2,[ASCIZ/<SYSSUP>EXCLDE.LST/]
	GTJFN
	JRST 	ERROR
	MOVEM	1,JFN
	MOVE	2,[XWD 070000,200000]
	OPENF
	JRST	ERROR
EXCL1:	MOVE	5,COUNT
	MOVE	1,JFN
	HRROI	2,INVLID+1(5)
	MOVEI	3,^D50
	MOVEI	4,12
	SIN
	AOS	COUNT
	MOVE	1,[POINT 7,INVLID+1(5)]
BUTI:	ILDB	2,1
	CAIG	2,"Z"
	CAIGE	2,"0"
	JRST	BUTI1
	CAIGE	2,"A"
	CAIG	2,"9"
	JRST	BUTI
BUTI1:	SETZ	2,
	DPB	2,1
	IDPB	2,1
	IDPB	2,1
	IDPB	2,1
	IDPB	2,1
	HRROI	1,INVLID+1(5)
	BIN
	CAIE	2,"0"
	JRST	EXCL1
 	MOVE	1,JFN
 	CLOSF
 	JRST	ERROR
 	RLJFN
 	JRST	ERROR
	RET

ZERO:	SETZ	1,
	DPB	1,2
	IDPB	1,2
	IDPB	1,2
	IDPB	1,2
	IDPB	1,2
	RET

DONE1:	SUBI	13,OFFSET-1   		;CALCULATE NEW BEGINNING OF 2ND TAB
	MOVEM	13,OFFSET+1		;PUT IT AWAY
	MOVEI	2,5			;BEGINNING OF 1ST TABLE
	MOVEM	2,OFFSET		;PUT IT AWAY TEMP
	HRRI	2,OFFSET(13)		;INDEXED BY NUMBER OF ELEMENTS IN 1ST
	HRLI	2,NENTRY
	ADD	13,14			;TOTAL ELEMENTS IN BOTH TABLES
	MOVE 	14,OFFSET+2
	MOVEM	13,OFFSET+2		;BEGINNING OF THIRD TABLE
	BLT	2,OFFSET-1(13)
	HRRI	2,OFFSET(13)
	HRLI	2,KEYTAB
	ADD	13,14
	HRRZ	14,OFFSET+3		;BEGINNING OF 4TH TABLE
	MOVEM	13,OFFSET+3
	BLT	2,OFFSET-1(13)
	HRRI	2,OFFSET(13)
	HRLI	2,STRNGS
	SUBI	14,STRNGS-1
	ADD	13,14
	MOVEM	13,OFFSET+4
	BLT	2,OFFSET(13)
	MOVSI	1,100001			;SHORT CALL OLD FILE
	HRROI	2,[ASCIZ /<SYSSUP>DOC.INDEX/]	;NAME
	GTJFN					;GET IT
	JRST	ERROR				;FAILED
	MOVE	2,[440000,,301000]		;36 BIT WAIT
	OPENF
	JRST	ERROR				;FAILED
	HRLZ	2,1
	MOVE	1,[400000,,PAGE]		;CURRENT FORK
	HRLZI	3,160000			;FULL ACCESS
	MOVE	5,OFFSET+4			;ENDING PAGE NUMBER
	ADDI	5,777 				;END ON PAGE BOUNDARY
	LSH	5,-11				;SHIFT IT OFF END
MAPPAG:	PMAP
	AOS	1 
	AOS	2
	SOSLE	5 
	JRST	MAPPAG
	MOVEI	1,400000
	HRLI	1,10000
	CLZFF

EXIT:	HRROI	1,[ASCIZ /DONE
/]
	PSOUT
EXIT1:	RESET
	HALTF
ERROR:	HRROI	1,[ASCIZ/ERROR
/]
	PSOUT
	JRST	EXIT1





INVLID:	BLOCK 50 

	LIT 
	
PAGE=<<.+141>/1000>+1
	LOC	PAGE*1000


OFFSET:	BLOCK	5				;START OF TABLES
LOOKTB:	BLOCK	4000
NENTRY:	BLOCK	10000
KEYTAB:	BLOCK	20000
STRNGS:	BLOCK	20000 
ENTRYS:	BLOCK	10000

	END	START

