	TITLE MTACPY
	SUBTTL J. BURCHFIEL MAR. 17,1971
;	MODIFIED---SRS---22JUL74

	;READS OR WRITES A 10/50 FORMAT TAPE, FILE BY FILE.

	MLON

	SEARCH STENEX

IFNDEF STC,<STC==0>	;SET TO 1 FOR STC DRIVES

	A=1
	B=2
	C=3
	D=4
	P=17
	MAXERR=100	;RETRY 100 TIMES ON ERROR


	;STORAGE ALLOCATOR FOR TMP STORAGE

	DEFINE ALC(NAM,SIZ)
	<	NAM=LC
	IFNB <SIZ>, <  LC=LC+SIZ >>

	;TEMP STORAGE

	LC=140

	ALC JFNCNT,	 1	;NUMBER OF FILES SPECIFIED
	ALC NJFN,	 1	;COUNT THROUGH FILES
	ALC RECSIZ,	 1
	ALC STAT,	 1
	ALC MTANAM,	 2
	ALC BYTCNT,	 1
	ALC EOFFLG,	 1
	ALC MTAJFN,	 1
	ALC OUTJFN,	 1
	ALC INJFN,	 ^D20
	ALC ERRCNT,	 1
	ALC NCOL,	 1
	ALC BYTES,	 1
	ALC RECJFN,	 1
	ALC RECFLG,	 1
	ALC FILNAM,	 5
	ALC JBLOCK,	 11
	ALC STK,	 20
	ALC PATCH,	 20
	ALC BUFF,	 2001


	HISEG

BEG:	RESET
	MOVE P,[IOWD 20,STK]

Q1:	MOVE A,[ASCII/MTAX:/]
	MOVEM A,MTANAM
	SETZM MTANAM+1
	HRROI A,[ASCIZ/
MAGTAPE UNIT NO.=/]
	PSOUT
	PBIN
	CAIG A,"7"	;ALLOW FOR 0-7 FOR FUTURE EXPANSION
	CAIGE A,"0"
	JRST Q1
	DPB A,[POINT 7,MTANAM,27]
	MOVSI A,1
	HRROI B,MTANAM
	GTJFN
	PUSHJ P, ERR
	MOVEM A,MTAJFN
	MOVE B,[XWD 447400,300000]
	OPENF
	PUSHJ P, ERR
	MOVEI B,1	;REWIND
	MTOPR
IFG STC,<
Q4D:	HRROI	A,[ASCIZ /
  7- OR 9-TRACK?(7 OR 9) /]
	PSOUT
	PBIN
	SETZ	D,		;ASSUME 7-TRACK
	CAIN	A,"7"
	 JRST	Q6
	AOJ	D,		;SET 9-TRACK
	CAIE	A,"9"
	 JRST	Q4D		;TRY AGAIN
>

IFE STC,<
Q6:	HRROI A,[ASCIZ/
USE 556 BPI?(Y OR N) /]
	PSOUT
	PBIN
	MOVEI B,1B21	;SUPRESS AUTO ERROR CORRECTION
	CAIN A,"Y"
	JRST Q6A
	CAIE A,"N"
	JRST Q6
Q6D:	HRROI A,[ASCIZ /
DESIRED DENSITY(200 OR 800):/]
	PSOUT
	PBIN
	CAIN A,"8"
	JRST Q6C
	CAIE A,"2"
	JRST Q6D
	IORI B,1B28		;200 BPI

Q6C:	PBIN
	PBIN
	JRST Q6B


Q6A:	IORI B,2B28	;556 BPI

>
IFG STC,<
Q6:	HRROI	A,[ASCIZ/
 USE 556, 800, OR 1600 BPI?(5, 8, OR 1)/]
	PSOUT
	PBIN
	MOVEI	B,1B21		;SUPPRESS AUTO ERROR CORRECTION
	CAIN	A,"5"
	 JRST	Q65
	CAIN	A,"8"
	 JRST	Q68
	CAIE	A,"1"
	 JRST	Q6
	JUMPE	D,[HRROI	A,[ASCIZ/
 1600 BPI ONLY WITH 9-TRACK/]
		PSOUT
		JRST	Q6]
	JRST	Q6B

Q65:	JUMPN	D,[HRROI	A,[ASCIZ/
 556 BPI ONLY WITH 7-TRACK/]
		PSOUT
		JRST	Q6]
	IORI	B,1B28		;556 BPI
	JRST	Q6B
Q68:	JUMPE	D,Q6B
	IORI	B,1B28		;800 BPI
>
Q6B:	SETZM EOFFLG

Q7:	HRROI A,[ASCIZ /
NORMAL ODD PARITY?(Y OR N):/]
	PSOUT
	PBIN
	CAIN A,"Y"
	JRST Q2
	CAIE A,"N"
	JRST Q7
	IORI B,1B26	;EVEN PARITY


Q2:	HRROI A,[ASCIZ/
TO OR FROM MAGTAPE? (T OR F)?/]
	PSOUT
	PBIN
	CAIN A,"T"
	JRST TOTAPE
	CAIE A,"F"
	JRST Q2
	SETOM RECFLG

Q8A:	MOVE A,MTAJFN
	SDSTS
Q3:	HRROI A,[ASCIZ /
TARGET FILE:/]
	PSOUT
	PBIN
	CAIN A,37
	JRST SKPFIL
	MOVEI A,100
	BKJFN
	PUSHJ P, ERR
	MOVSI A,460003
	MOVE B,[XWD 100,101]
	GTJFN
	JRST Q3
	MOVEM A,OUTJFN
	MOVE B,[XWD 060000,100000]	;SIXBIT CHARS FROM TAPE
	OPENF
	PUSHJ P, ERR



Q9:	SKIPL RECFLG
	JRST GO
	MOVSI A,400000
	MOVEM A,JBLOCK
	MOVE A,[XWD 377777,377777]
	MOVEM A,JBLOCK+1
	SETZM JBLOCK+2
	MOVE A,[XWD JBLOCK+2,JBLOCK+3]
	BLT A,JBLOCK+10
	HRROI A,[ASCIZ /RECSIZ/]
	MOVEM A,JBLOCK+5
	HRROI A,FILNAM
	MOVEM A,JBLOCK+4
	MOVSI C,(1B8)
	MOVE B,OUTJFN
	JFNS
	MOVEI A,JBLOCK
	SETZ B,
	GTJFN
	PUSHJ P,ERR
	MOVEM A,RECJFN
	MOVE B,[XWD 070000,100000]
	OPENF
	PUSHJ P,ERR
	HRROI B,[ASCIZ /
DECIMAL LENGTH OF EACH RECORD IN SIX-BIT BYTES

/]
	SETZ C,
	SOUT

GO:	SETZM BYTCNT
	SETZM ERRCNT
	SETZM NCOL

INLP:	MOVE A,MTAJFN
	MOVEI B,ILIST
	DUMPI
	JRST EOFCHK
	JRST RECCHK

SKPFIL:	MOVE A,MTAJFN
	MOVEI B,6
	MTOPR

SKP1:	GDSTS		;FORWARD ONE RECORD
	TRNN B,1B22	;END OF FILE?
	JRST SKP2	;NO- SOME DATA THERE
	SKIPE EOFFLG	;YES- TWO EOF'S IN A ROW?
	JRST FIN2	;YES- END OF TAPE
	SETOM EOFFLG
	SETZ B,
	MTOPR		;CLEAR EOF FLAG
	JRST Q3

SKP2:	MOVEI B,16
	MTOPR			;SKIP OVER THIS FILE
	SETOM EOFFLG
	JRST Q3

EOFCHK:	CAIE A,IOX4
	JRST RECCHK
	SKIPE EOFFLG
	JRST Q10
	SETOM EOFFLG
	MOVE A,OUTJFN
	CLOSF
	PUSHJ P, ERR
	MOVE A,MTAJFN
	SETZ B,			;CLR EOF OF MAGTAPE
	MTOPR
	PUSHJ P,TYPCNT
	SKIPL RECFLG
	JRST Q3
	MOVE A,RECJFN
	MOVEI B,"L"-100
	BOUT
	CLOSF
	PUSHJ P,ERR
	JRST Q3



Q10:	HRROI A,[ASCIZ/
LOGICAL END OF TAPE. KEEP READING?(Y OR N):/]
	PSOUT
	PBIN
	CAIN A,"N"
	JRST FIN2
	CAIE A,"Y"
	JRST Q10
	MOVEI A,37
	PBOUT
	SETZM EOFFLG
	JRST AGAIN


RECCHK:	MOVE A,MTAJFN
	GDSTS
	TRNE B,1B18+1B19+1B20+1B22+1B25		;ERROR?
	JRST TAPERR		;YES
	SKIPGE C		;WAS DATA XFERRED?
	JRST TAPERR		;NO
	HLRZ C,C		;YES, POS WORDCOUNT
	CAIG C,1
	JRST AGAIN		;IGNORE 0 OR 1 WORD RECORDS
	LDB B,[POINT 3,B,31]
	SKIPE B
	SUBI C,1
	IMULI C,6
	ADDI C,(B)
	ADDM C,BYTCNT	;NUMBER OF SIX-BIT BYTES
	MOVEM C,BYTES
	SKIPGE RECFLG
	PUSHJ P,RECORD

OUT1:	MOVN C,BYTES
	MOVE A,OUTJFN
	MOVE B,[POINT 6,BUFF]
	SOUT
	SETZM EOFFLG
AGAIN:	MOVE A,MTAJFN
	SETZ B,
	MTOPR		;CLR STATUS BITS
	SETZM ERRCNT
	JRST INLP


RECORD:	MOVE A,RECJFN
	MOVE B,BYTES
	MOVE C,[XWD 7,12]
	NOUT
	PUSHJ P,ERR
	AOS B,NCOL
	CAIGE B,^D10
	POPJ P,
	HRROI B,[ASCIZ /
/]
	SETZ C,
	SOUT
	SETZM NCOL
	POPJ P,

TOTAPE:	TRZ B,1B21	;ALLOW AUTO ERROR CORRECTION
	MOVE A,MTAJFN
	SDSTS

TOTAP2:	HRROI A,[ASCIZ/
SOURCE FILE(S):/]
	PSOUT
	PBIN
	CAIN A,37
	JRST Q4			;NO NAME- PROBABLY DONE.
	MOVEI A,100
	BKJFN
	PUSHJ P, ERR
	SETZB C,JFNCNT		;COUNT FILES SPECIFIED BY COMMAS

TOTAP5:	MOVSI A,(1B2+1B11+3B17)	;OLD, PERMIT *'S
	MOVE B,[XWD 100,101]
	GTJFN
	JRST TOTAP9
	MOVEM A,INJFN(C)
	MOVEI A,100
	BKJFN
	PUSHJ P,ERR
	PBIN
	CAIN A,33	;ALTMODE?
	PBIN		;YES, READ NEXT CHAR
	CAIE A,40	;SPACE
	CAIN A,","	;OR COMMA
	SKIPA
	JRST TOTAP1	;NO, NO MORE FILE NAMES
	AOS C,JFNCNT	;YES- GET MORE FILE SPECIFIERS
	JRST TOTAP5

TOTAP9:	CAIE A,GJFX3
	CAIN A,GJFX22	;OUT OF JFN'S?
	JRST TOTAP1		;YES- DO THE FILES ALREADY SPECIFIED
	MOVEI A,100
	DOBE
	CFIBF
	MOVEI B,"?"
	BOUT
	JRST TOTAP5

TOTAP1:	SETZM NJFN

TOTAP4:	MOVE C,NJFN	;SCAN THROUGH FILES SPECIFIED
	HRRZ A,INJFN(C)
	SETZM BYTCNT	;NOW DO EACH FILE
	MOVE B,[XWD 440000,200000]
	OPENF
	JRST TOTAP3	;INFORM USER THAT FILE NOT COPIED

OUTLP:	MOVE A,NJFN
	HRRZ A,INJFN(A)
	MOVE B,[POINT 36,BUFF]
	MOVNI C,200
	SIN
	GTSTS
	TLNE B,(1B8)		;EOF?
	JRST ENDUP		;YES
	MOVE A,MTAJFN
	MOVEI B,OLIST
	DUMPO
	PUSHJ P, ERR
	MOVEI A,^D128*6
	ADDM A,BYTCNT
	JRST OUTLP

ENDUP:	HRLI A,400000	;SAVE JFN
	CLOSF
	PUSHJ P, ERR
	ADDI C,200
	JUMPE C,EOFF		;ANY WORDS TRANSFERRED BEFORE EOF?
	MOVEI C,BUFF+1(C)	;YES, CLEAR REMAINDER OF BUFFER
	HRLI C,-1(C)
	SETZM -1(C)
	BLT C,BUFF+177
	MOVE A,MTAJFN
	MOVEI B,OLIST
	DUMPO			;OUTPUT LAST BUFFER
	PUSHJ P, ERR
	MOVEI A,^D128*6
	ADDM A,BYTCNT

EOFF:	MOVE A,MTAJFN
	MOVEI B,3		;WRITE EOF
	MTOPR
	PUSHJ P,TYPCNT

TRYAGN:	SKIPE JFNCNT	;IF THERE IS MORE THAN ONE FILE
	JRST TOTAP7
	MOVE A,INJFN
	TLNN A,770000	;OR A * SPECIFIED IN FILE NAME
	JRST TOTAP8
	JRST TOTAP7

TOTAP3:	HRROI A,[ASCIZ/NOT COPIED:				/]
	PSOUT

TOTAP7:	MOVEI A,100	;THEN TYPE EACH FILE COMPLETED
	MOVE C,NJFN
	HRRZ B,INJFN(C)	;FILE JUST DONE
	SETZ C,
	JFNS
	MOVEI B,37
	BOUT

TOTAP8:	MOVE C,NJFN
	MOVE A,INJFN(C)
	GNJFN		;YES- INDEX TO NEXT FILE
	JRST TOTAP6	;NO MORE
	JRST TOTAP4

TOTAP6:	AOS C,NJFN	;SCAN TO NEXT FILE
	CAMG C,JFNCNT	;ALL DONE?
	JRST TOTAP4	;NO
	JRST TOTAP2

Q4:	PUSHJ P,DUNTST		;DONE?
	JRST FIN		;YES
	JRST TOTAP2		;NO,COPY ANOTHER FILE

DUNTST:	HRROI A,[ASCIZ/
DONE?(Y OR N)/]
	PSOUT
	PBIN
	CAIN A,"Y"
	POPJ P,
	CAIE A,"N"
	JRST DUNTST
	AOS 0(P)
	POPJ P,


FIN:	MOVE A,MTAJFN
	MOVEI B,3		;WRITE SECOND EOF
	MOVEI C,^D10

FINLP:	MTOPR
	SOJG C,FINLP

FIN3:	MOVE A,MTAJFN
	MOVEI B,1		;REWIND
	MTOPR
	RESET
	HRROI A,[ASCIZ/
EXIT.
^C
/]
	PSOUT
	HALTF

FIN2:	HRROI A,[ASCIZ/
LOGICAL END OF TAPE./]
	PSOUT
	JRST FIN3


TAPERR:	AOS A,ERRCNT
	CAIGE A,MAXERR		;TOO MANY ERRORS?
	JRST RETRY		;NO, TRY AGAIN
	HRROI A,[ASCIZ/
TAPE ERROR. FILE STATUS = /]
	PSOUT
	MOVE A,MTAJFN
	GTSTS
	PUSHJ P,TYPHLF
	MOVE A,MTAJFN
	GDSTS
	MOVEM B,STAT
	TRNE B,1B18+1B19+1B22+1B25	;FATAL ERROR?
	SETZ C,		;YES- NO WORDS TRANSFERRED
	HLRZ C,C
	LDB B,[POINT 3,B,31]
	SKIPE B
	SUBI C,1
	IMULI C,6
	ADDI B,(C)
	MOVEM B,RECSIZ
	MOVEM B,BYTES
	MOVEI A,101
	MOVEI C,12
	NOUT
	PUSHJ P, ERR
	HRROI A,[ASCIZ/ (DEC) SIX-BIT BYTES IN RECORD.
DEVICE STATUS = /]
	PSOUT
	MOVE B,STAT
	PUSHJ P,TYPHLF

	SETZM EOFFLG
	MOVE A,OUTJFN
	SKIPG RECSIZ
	JRST TAPER2
	MOVE B,[POINT 6,BUFF]
	MOVN C,RECSIZ
	SOUT
	PUSHJ P,RECORD

TAPER2:	MOVE A,RECSIZ
	ADDM A,BYTCNT
	JRST AGAIN

RETRY:	MOVE A,MTAJFN
	MOVEI B,7
	MTOPR		;BACKSPACE ONE RECORD
	GDSTS
	TRNE B,4000	;BEGINNING OF TAPE?
	JRST INLP	;YES
	MOVEI B,7	;BACK UP ANOTHER RECORD
	MTOPR
	MOVEI B,6
	MTOPR		;AND FORWARD ONE
	JRST INLP	;NOW TRY AGAIN

ERR:	MOVEI A,101
	MOVE B,[XWD 400000,-1]
	SETZ C,
	ERSTR
	JFCL
	JFCL
	HRROI B,[ASCIZ /
LOCATION = /]
	SETZ C,
	SOUT
	HRRZ B,(P)
	MOVEI C,10
	NOUT
	HALTF
	MOVEI B,37
	BOUT
	HALTF

TYPHLF:	HRRZM B,STAT
	HLRZ B,B
	MOVEI A,101
	MOVEI C,10
	NOUT
	PUSHJ P, ERR
	MOVEI B,","
	BOUT
	BOUT
	MOVE B,STAT
	NOUT
	PUSHJ P, ERR
	MOVEI B,37
	BOUT
	POPJ P,


TYPCNT:	MOVEI A,101
	MOVE B,BYTCNT
	MOVEI C,12
	NOUT
	PUSHJ P, ERR
	HRROI A,[ASCIZ / (DECIMAL) SIX-BIT BYTES.	/]
	PSOUT
	POPJ P,

OLIST:	IOWD 200,BUFF
	0

ILIST:	IOWD 2000,BUFF
	0


	LIT

LAST:
	END BEG
