
;9 APR 71, 1302:

;SET MINIMUM CPU PERCENTAGE - D. MURPHY

	TITLE SETMRP

	MLON

	SEARCH	STENEX

P=17
OPDEF CALL [PUSHJ P,0]
OPDEF RET [POPJ P,0]

BGN:	MOVE P,[IOWD 40,PDL]
	MOVEI 1,400000
	RPCAP
	TRNN 3,600000		;WHEEL OR OPER?
	JRST [	HRROI 1,[ASCIZ /
MUST HAVE WHEEL OR OPERATOR ENABLED.
/]
		PSOUT
		HALTF]
J1:	HRROI 1,[ASCIZ /
JOB NUMBER: /]
	PSOUT
	CALL RNUM		;READ NUMBER
	MOVEM 2,JOBNO
J2:	HRROI 1,[ASCIZ /
DESIRED CPU PERCENTAGE: /]
	PSOUT
	CALL RNUM
	CAIGE 2,^D100
	CAIGE 2,0
	JRST [	HRROI 1,[ASCIZ /MUST BE 0-99.
/]
		PSOUT
		HALTF]
	MOVEM 2,PERCNT
	MOVSI 2,0(2)
	MOVE 1,JOBNO
	SJPRI
	JRST [	HRROI 1,[ASCIZ /SJPRI FAILED.
/]
		PSOUT
		HALTF]
	MOVEI 1,400000		;TTY 0 DESIGNATOR
	HRROI 2,[ASCIZ /SETMRP: JOB /]
	SETZ 3,
	SOUT
	MOVE 2,JOBNO
	CALL PNUM
	HRROI 2,[ASCIZ /, /]
	SETZ 3,
	SOUT
	MOVE 2,PERCNT
	CALL PNUM
	MOVEI 2,37
	BOUT
	MOVEI 1,37
	PBOUT
	HALTF

PNUM:	MOVEI 3,^D10
	NOUT
	JFCL
	RET

RNUM:	MOVEI 1,100
	MOVEI 3,^D10
	NIN
	JRST RNBAD
	MOVE 3,2
	BKJFN
	SKIPA 2,[37]		;ASSUME OK IF CAN'T BKJFN
	BIN
	EXCH 2,3
	CAIE 3,37		;CONFIRMED WITH EOL OR CR
	CAIN 3,15
	RET			;OK
RNBAD:	HRROI 1,[ASCIZ / ?  /]
	PSOUT
	JRST RNUM

JOBNO:	0
PERCNT:	0
PDL:	BLOCK 40

	LIT

FFF:
	END BGN
