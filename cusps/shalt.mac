;<SOURCES>SHALT.MAC;3	5-AUG-75 10:11:01 	EDIT BY COWER
;CHANGED START: TO STAR: TO COMPLY WITH JRST STAR IN MHALT
;TO LOAD TOGETHER ORDER IS NOT IMPORTANT - TO LOAD AND SAVE ONLY      
;SHALT (FOR SYSJOB) SET ENTRY VECTOR TO 140
;<SOURCES>SHALT.MAC;2    29-JUN-75 04:39:10    EDIT BY HEDBERG
; Fixed Month wraparound problem in MAKTAD
;<SOURCES>SHALT.MAC;1    25-JUN-75 03:50:11    EDIT BY HEDBERG

	TITLE	SHALT
	SUBTTL	READS THE HALT FILE
	SEARCH	STENEX
INTERN	STAR

P=17
$DOWN==0
$UP==1
$FLAGS==2

OPDEF	CALL	[PUSHJ	P,0]
OPDEF	RET	[POPJ	P,0]


STAR:	MOVE	P,[IOWD 40,STACK]

	SETO	1,
	MOVE	2,[400000,,FILE/1000]
	PMAP
	RESET

	MOVEI	1,400000
	SETOB	2,3
	EPCAP

	MOVSI	1,1
	HRROI	2,[ASCIZ \<SYSTEM>DOWNTIME.\]
	GTJFN			;** GET THE JFN OF THE DOWNTIME FILE
	 0
	MOVE	2,[7B5!1B19]	;READ
	OPENF			;** AND OPEN IT
	 0
	HRLZ	1,1
	HRLI	2,400000
	HRRI	2,FILE/1000
	MOVSI	3,(1B2)		;READ
	PMAP
	MOVE	1,[ FILE,,PAGE ]
	BLT	1,PAGE+777

	GTAD
	MOVEM	1,NOW

	MOVEI	14,0
CLOOP:	MOVE	3,FLAGS(14)	;GET THE FLAGS FOR THIS HALT
	JUMPE	3,CLOOPE	;ZERO MEANS THE END
	SKIPGE	3
	CALL	CYCLI		;CONVERT ALL THE HALTS TO TAD
	ADDI	14,3
	JRST	CLOOP
	
CLOOPE:	SETZ	10,
	MOVEI	11,0
	MOVEI	12,3
SLOOP:	MOVE	2,DOWN(12)	;BUBBLE SORT THEM INTO ORDER
	JUMPE	2,SLOOP1
	CAMGE	2,DOWN(11)
	CALL	SWITCH
	ADDI	11,3
	ADDI	12,3
	JRST	SLOOP
SLOOP1:	JUMPN	10,CLOOPE

	MOVSI	1,377777
	MOVEM	1,BBH		;SET THE BEGIN TO FRI SEPT 26,2217
	SETZM	EBH		;SET THE END TO TUE NOV 16,1858

	MOVEI	14,0
LOOP:	MOVE	3,FLAGS(14)	;GET THE FLAGS FOR THIS HALT
	JUMPE	3,SRCEND	;ZERO MEANS THE END
	MOVE	1,DOWN(14)
	MOVE	2,UP(14)
CMPAR:	
	ANDI	3,17		;ISOLATE REASON CODE
	CAMG	1,NOW		;THE HALT MUST START AFTER NOW
	 JRST	LOOPE
	CAMG	2,BBH		;IF ITS END IS BEFORE THE BEST BEGIN
	 JRST	[MOVEM	1,BBH	;THEN IT IN ITSELF IS THE BEST
		 MOVEM	2,EBH
		 MOVEM	3,CODE
		 JRST	LOOPE]

	CAMLE	1,EBH		;ITS BEGIN MUST BE BEFORE OR EQUAL BESTS END
	 JRST	LOOPE
	CAMG	1,BBH		;IF THE BEGIN IS EARLIER THAN THE BEST
	MOVEM	1,BBH		;THEN IT IS THE BEST
	CAML	2,EBH		;IF ITS END IS LATERER THE BESTS END
	MOVEM	2,EBH		;THEN IT IS THE BEST
LOOPE:	
	ADDI	14,3
	JRST	LOOP

SRCEND:
	HLRZ	1,BBH
	CAIN	1,377777
	JRST	[HRROI	1,[ASCIZ \
*** No useable halt on the file\]
		 PSOUT
		 JRST	DONE	]

	MOVE	1,BBH
	MOVE	2,EBH
	MOVE	3,CODE
	HSYS
	 0

DONE:	SETO	1,
	MOVE	2,[400000,,FILE/1000]
	PMAP
	RESET
	HALTF
	JRST	.-1
	
SWITCH:	SETO	10,		;SAY WE SWITCHED
	PUSH	P,DOWN(11)	;SAVE THE LATER
	PUSH	P,UP(11)
	PUSH	P,FLAGS(11)
	HRLI	1,DOWN(12)	;MOVE THE NEWER TO THE LATER
	HRRI	1,DOWN(11)
	BLT	1,FLAGS(11)
	POP	P,FLAGS(12)	;RESTORE THE LATER
	POP	P,UP(12)
	POP	P,DOWN(12)
	RET

CYCLI:	MOVE	10,DOWN(14)
	CALL	MAKTAD		;CONVERT DOWNTIME TO TAD AND SAVE
	PUSH	P,2
	MOVE	10,UP(14)
	CALL	MAKTAD		;CONVERT UPTIME
	POP	P,1		;RESTORE DOWNTIME
	CAML	1,2
	ADD	2,[7,,0]	;MAKE SURE DOWN IS BEFORE UP
	MOVEM	1,DOWN(14)
	MOVEM	2,UP(14)
	RET

MAKTAD:
	SETO	2,		;GET TODAY'S DAY NUMBER
	SETZ	4,
	ODCNV
	HLRZ	6,10		;GET THE DOWN DAY NUMBER FROM THE FILE
	HRRZ	5,3		;AND TODAYS NUMBER
	SUB	6,5		;GET THE DIFFERENCE
	SKIPGE	6		;WRAP AROUND THE WEEK TO GET THE NUMBER
	ADDI	6,7		;OF DAYS UNTIL THE HALT
	HLRZ	5,NOW		;GET DAYS SINCE NOV 18, 1858
	ADDI	5,0(6)		;ADD DIFFERENCE BETWEEN NOW & HALT DAY
	HRL	2,5
	HRR	2,NOW		;GET SPECIFIC BREAKDOWN OF THAT DAY & TIME
	ODCNV
	HRRZ	4,10		;AND INSERT THE PROPER HALT TIME
	IDCNV			;GET A TAD
	 JRST	[HRROI	1,[ASCIZ \
Impossible cyclical date in file
\]
		PSOUT
		HALTF	]
	RET

CODE:	0
BBH:	0
EBH:	0
NOW:	0

STACK:	BLOCK	40
	LIT
	LOC	10000		

FILE=.	
	LOC	.+1000
PAGE=.

DOWN==PAGE+$DOWN
UP==PAGE+$UP
FLAGS==PAGE+$FLAGS

	END

