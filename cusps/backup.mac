	TITLE	BACKUP
	SEARCH	STENEX

P=17
NPDL=20
PBUF=600000			;PAGE BUFFER FOR PMAPS

START:	RESET
	MOVE P,[IOWD NPDL,PDL]

	MOVSI 1,(1B2+1B16+1B17)		;GET FILE NAME
	MOVE 2,[XWD 100,101]
	GTJFN
	 PUSHJ P,ERROR
	MOVEM 1,FILJFN#
	
	MOVE 2,[XWD 25,0]		;GET FDB FOR FILE
	MOVEI 3,FILFDB
	GTFDB

	SKIPN FILFDB+3			;FILE MUST HAVE PAGES
	 JRST	[HRROI 1,[ASCIZ /NO PAGES IN FILE/]
		JRST PERROR]
	HRRZ 2,FILFDB+11		;(PAGE COUNT)
	JUMPE 2,[HRROI 1,[ASCIZ /NO PAGES IN FILE/]
		JRST PERROR]

	MOVE 2,[XWD 440000,200000]
	OPENF
	 PUSHJ P,ERROR

	MOVE 2,1			;GET FILE NAME TO MAKE BACKUP NAME 
	MOVE 1,FILDEV			
	MOVE 3,[XWD 101100,1]
	JFNS
	SETZ 3,
	DPB 3,2				;MAKE ASCIZ 

	MOVE 3,@FILDEV			;CHECK DEVICE, MUST BE DISK
	TRZ 3,377
	CAME 3,[ASCIZ /DSK:/]
	 JRST	[HRROI 1,[ASCIZ /MUST BE DSK: FILE/]
		JRST PERROR]

	GJINF				;GET CONNECTED DIR #
	HRRM 2,BAKVER			;TO MAKE BACKUP VERSION #

	MOVEI 1,BAKPAR			;GET BACKUP FILE  HANDLE
	MOVE 2,FILNAM			;NAME SAME AS FIRST FILE NAME
	GTJFN
	 PUSHJ P,ERROR
	MOVEM 1,BAKJFN#

	MOVSI 1,11			;COPY FDB BYTE SIZE
	HRR 1,BAKJFN	
	MOVSI 2,7700	
	MOVE 3,FILFDB+11
	CHFDB
	
	HRLI 1,12			;COPY FDB BYTE COUNT
	SETO 2,
	MOVE 3,FILFDB+12
	CHFDB

	MOVE 1,BAKJFN			;OPEN OUTPUT FILE
	MOVE 2,[XWD 440000,100000]
	OPENF
	 PUSHJ P,ERROR

	MOVEI 5,^D511			;COPY PAGES - AC5 IS PAGE #

XPGS:	HRL 1,FILJFN			;SEE IF PAGE EXISTS IN FIL
	HRR 1,5
	RPACS
	TLNN 2,(1B5)			;PAGE EXISTS?
	 JRST NOPAGE
	PUSH P,2			;SAVE PAGE ACCESS

	MOVE 2,[XWD 400000,<PBUF/1000>]	;YES, MAP IT INTO BUFFER
	MOVSI 3,(1B2+1B9)		;COPY-ON-WRITE
	PMAP

	MOVES PBUF			;MAKE PRIVATE COPY

	MOVE 1,2			;NOW MAP IT OUT TO BACKUP FILE 
	HRL 2,BAKJFN			
	HRR 2,5
	PMAP

	MOVE 1,2			;SET ACCESS TO ORIGINAL FILE ACCESS
	POP P,2				
	AND 2,[1B2+1B3+1B4+1B8+1B9]
	SPACS

NOPAGE:	SOJGE 5,XPGS


	SETO 1,				;REMOVE PBUF SO CAN CLOSE FILE
	MOVE 2,[XWD 400000,<PBUF/1000>]
	SETZ 3,
	PMAP

	MOVE 1,FILJFN			;CLOSE BOTH FILES
	CLOSF
	 PUSHJ P,ERROR

	MOVE 1,BAKJFN
	CLOSF
	 PUSHJ P,ERROR

HALT:	RESET				;DONE
	HALTF
	JRST .-1

ERROR:	MOVEM 1,ERRNUM#		;SAVE ACTUAL ERROR CODE
	HRROI 1,[ASCIZ /ERROR AT: /]
	ESOUT

	POP P,2			;GET ERROR ADDRESS
	HRRZI 2,-2(2)		;TURN INTO ACTUAL ERROR ADR
	MOVEI 1,101		;OUTPUT TO TTY
	MOVEI 3,10		;IN OCTAL
	NOUT
	 0
	
	HRROI 1,[ASCIZ /, /]
	PSOUT

	MOVEI 1,101		;OUTPUT ACTUAL ERROR
	HRLOI 2,400000
	SETZ 3,
	ERSTR
	0
	0

	JRST HALT

PERROR:	ESOUT
	JRST HALT
	SUBTTL	WORKING DATA

PDL:	BLOCK NPDL
FILDEV:	POINT 7,BUF		;LOC IN BUFFER  OF FIL DEV NAME
FILNAM:	POINT 7,BUF,27		;LOC IN BUFFER OF FILE NAME
FILFDB:	BLOCK 25
BUF:	BLOCK 40
BAKVER:				;VERSION NUMBER OF BACKUP FILE GOES HERE
BAKPAR:	0
	XWD 377777,377777	;NO JFN I/O
	0			;DSK
	POINT 7,BAKDIR		;POINTS TO BACKUP DIR
	0			;NAME SPECIFIED IN STRING
	0			;EXT SPECIFIED IN STRING
	XWD 500000,777720	;WRITE ONLY PROTECTION FOR PUBLIC
	0			;SYS ACCOUNT
	0			;NO SPECIFIC JFN
BAKDIR:	ASCIZ /BACKUP/		;NAME OF BACKUP DIRECTORY

	END START
