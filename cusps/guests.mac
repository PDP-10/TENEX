;<SOURCES>GUESTS.MAC;1    10-JUN-75 08:13:14    EDIT BY HEDBERG

	SEARCH	STENEX

	TITLE	GUESTS -- TO KEEP TRACK OF OUR VISITORS

STKSIZ==20
PAGE==3
P==17
CC==0
XC==1

DEFINE	ASSERT	(EXP,VAL,MESS)
<
IF2, <IFN <<EXP>-<VAL>>,<
PRINTX
PRINTX MESS>>
>

	
START:	MOVE P,[IOWD STKSIZ,STACK]	;SET UP STACK

	MOVEI 1,400000		;CURRENT FORK
	RPCAP
	SETO 3,
	EPCAP			;ENABLE EVERYTHING

	MOVE 2,[LEVTAB,,CHNTAB]	;PSI'S WILL BE GOING
	SIR
	MOVE 1,[3,,CC]		;ASSIGN ^C TO CHANNEL 0
	ATI
	MOVE 1,[^D24,,XC]	;ASSIGN ^X TO CHANNEL 1
	ATI
	MOVEI 1,400000
	HRLZI 2,(1B<CC>!1B<XC>!1B10)	;ACTIVATE CHANNELS 0,1,10
	AIC
	EIR

	SETZM OURNAM
	HRLI 1,OURNAM
	HRRI 1,OURNAM+1		;ZERO OUT OUR NAME
	BLT 1,OURNAM+7
	SETZM FOUNDN		;WE HAVEN'T FOUND HIS NAME YET

	MOVEI 1,WMESS1		;SET THE POINTER TO THE FIRST
	MOVEM 1,WMESS		;WAITING MESSAGE
	MOVEI 1,CTRLC		;SET THE MAIN ^C HANDLER
	HRRM 1,CHNTAB+CC	;POINTER

	HRLZI 1,(1B2!1B17)	;OLD FILE, SHORT CALL
	HRROI 2,[ASCIZ \<SYSTEM>GUESTS..;\]
	GTJFN			;GET THE ACTIVE GUESTS FILE
	 CALL JERR		;SHOULD NOT OCCUR
	
	MOVE 2,[7B5!1B19!1B25] ;READ, THAWED, 7-BIT BYTES
	OPENF
	 CALL JERR
	MOVEM 1,ACTJFN		;SAVE IT 
	
	HRLZ 1,ACTJFN		;SOURCE FILE HANDLE, PAGE 0
	MOVE 2,[400000,,PAGE]	;DESTINATION FORK & PAGE NUMBER
	HRLZI 3,(1B2)		;READ ACCESS
	PMAP

	GJINF			;WHAT JOB NUMBER ARE WE?
	MOVEM 4,TTYNUM		;SAVE THAT FOR LATER
	IMULI 3,^D8		;GET OUR REAL NAME STARTING
	ADDI 3,PAGE*1000
	HLL 3,[POINT 7,0]	;AT WORD # JOBNUMBER*8 OF
	MOVE 2,[POINT 7,OURNAM]	;<SYSTEM>GUESTS.^C.;;7
	ILDB 1,3
	IDPB 1,2		;TIGHT LOOP TO READ IN NAME
	JUMPN 1,.-2

	SETO 1,
	MOVE 2,[400000,,PAGE]	;MAP IT OUT OF EXISTANCE
	PMAP

	MOVE 1,ACTJFN
	CLOSF			;NO MORE NEED FOR GUESTS.^C.;
	 JFCL
	MOVE 1,ACTJFN
	RLJFN
	 JFCL

	HRLZI 1,(1B17)		;SHORT CALL
	HRROI 2,[ASCIZ \<SYSTEM>GUESTS.WHO\]
	GTJFN			;GET THE GUEST REGISTER
	 CALL JERR		;SHOULD NOT OCCUR
	
	MOVEM 1,WHOJFN		;SAVE IT 
	MOVE 2,[7B5!1B19!1B25]	;READ, 7-BIT BYTES, THAWED
	OPENF
	 CALL OPNERR		;IS IT BUSY?
	
FIND:	MOVE 1,WHOJFN		;RESTORE JFN
	MOVEI 2,21		;^Q
	CALL GOBBLE		;GOBBLE LINE TO ^Q

		;CHECK NAMES
CHKNAM:	MOVE 4,[POINT 7,OURNAM]	;POINTER TO NAME ENTERED
	BIN			;EAT THE SPACE AFTER THE "N"
CHKNA1:	BIN			;CHAR FROM FILE
	ILDB 3,4		;CHAR FROM STRING
	JUMPE 3,CHKNA2
	CAME 2,3		;ARE THEY THE SAME?
	JRST FIND		;NO, TRY AGAIN
	JRST CHKNA1		;YES, COMPARE MORE

CHKNA2:	CAIE 2,15
	JRST FIND
	SETOM FOUNDN		;FOUND HIS NAME
	HRROI 1,[ASCIZ \
 Welcome to SUMEX, \]
	PSOUT
	MOVEI 2,31		;^Y MARKS THE BEGINNING OF THE FULL NAME
	PUSHJ P,GOBBLE		;EAT TILL THERE
	HRROI 2,FILBUF
	MOVEI 3,^D100		;READ IN THE FULL NAME
	MOVEI 4,15		;STOP ON CR
	SIN
	SETZ 4,
	DPB 4,2			;PUT ON NULL TERMINATOR
	HRROI 1,FILBUF
	PSOUT			;PRINT IT
	MOVEI 1,37		;WITH AN EOL
	PBOUT
	JRST ENDIT

	  ;ADD A NEW GUEST TO THE .WHO FILE
;
NEWPER: MOVE 1,WHOJFN
	TLO 1,400000		;CLOSE, BUT DON'T RELEASE
	CLOSF			;CLOSE IT READ ONLY
	 CALL JERR		;HOPE HOPE

	HRROI 1,[ASCIZ /
We would like to get some general information from you as our guest.
(CTRL-X to redo the current prompt.)
/]
	SKIPE	FOUNDN		;DID WE FIND HIS NAME?
				;YES, GIVE HIM A DIFFERENT MESSAGE
	HRROI	1,[ASCIZ \Stranger
You have been here before, but you didn't complete the questions.
(CTRL-X to redo the current prompt.)
\]
	PSOUT

	MOVE 10,[POINT 7,FILBUF] ;POINTER TO FINAL BIG JUICY STRING

	PUSHJ P,COMCDE		;WHERE IS HE COMING FROM

	MOVE 7,XINT		;INTERNAL INFO LOOP
ILOOP:	MOVE 2,IKEYCH(7)
	IDPB 2,10		;PUT OUT KEY CHAR. FOLLOWED BY A SPACE
	MOVEI 2," "
	IDPB 2,10
	MOVE 3,[POINT 7,0]	;GENERATE STRING POINTER WITH E GIVEN
	HRR 3,ISTR(7)		;BY ISTR INDEXED BY AC7
	ILDB 2,3
	JUMPE 2,ILOOP1
	IDPB 2,10		;TIGHT STRING TRANSFER LOOP
	JRST .-3

ILOOP1: MOVEI 2,15
	IDPB 2,10
	MOVEI 2,12		;PUT IN CRLF
	IDPB 2,10
	AOBJN 7,ILOOP

	MOVE 7,XUSER		;LOOP COUNTER / TABLE INDEX
ULOOP:	MOVEM 10,PNTSAV		;SAVE THAT POINTER
	MOVE 2,UKEYCH(7)
	IDPB 2,10		;PUT OUT KEY CHAR. FOLLOWED BY A SPACE
	MOVEI 2," "
	IDPB 2,10
	HRRO 1,USTR(7)		;PRINT PROMPTING MESSAGE
	PSOUT
	MOVE 1,TCHRS(7)		;GET TERMINATION CHARACTER
	MOVEM 1,TRMCHR		;MAKE IT THE CURRENT ONE
	CALL SINPUT
	AOBJN 7,ULOOP

	HRROI 1,[ASCIZ/
Thank you.  If you come in as a guest again, and use the
name "/]
	PSOUT
	HRROI 1,OURNAM
	PSOUT
	HRROI 1,[ASCIZ /", you will skip these questions.
Type "ACCESS" to see what programs are currently available to guests.
/]
	PSOUT

	MOVEI 1,EATCC		;EAT THE ^C'S FROM NOW ON
	HRRM 1,CHNTAB+CC

	MOVE 1,WHOJFN
	MOVE 2,[7B5!1B22]	;APPEND, 7-BIT BYTES, FROZEN
	OPENF
	 CALL OPNERR		;WAIT FOR IT IF BUSY

	SETZ 2,
	IDPB 2,10		;ZAP THE INFO OUT
	MOVE 3,[POINT 7,FILBUF]
	CALL STOF


ENDIT:	HRRZ 1,WHOJFN
	CLOSF
	JFCL
	HRRZ 1,WHOJFN		;MAKE SURE THAT FILE'S DEAD
	RLJFN
	JFCL
	HALTF

	;INTERNAL KEY CHARS.

IKEYCH: 21		;(DC1) NAME GIVEN TO THE "GUEST" COMMAND
	22		;(DC2) METHOD OF COMMUNICATION WITH SUMEX
NINT=.-IKEYCH		;NUMBER OF INTERNALLY GENERATED INPUTS
XINT:	XWD	-NINT,0

	;INTERNAL STRING TABLE

ISTR:	OURNAM
	COMSTR
;
	ASSERT	(.-ISTR,NINT,<
THERE IS AN INTERNAL STRING MISMATCH>)
;
		;USER KEY CHARACTERS
UKEYCH: 31		;(EM) FULL NAME
	32		;(SUB) AFFILIATED ORGANIZATION
	33		;(ESC) ADDRESS (PHONE)
	34		;(FS) NAME OF PERSON WHO GAVE THE PASSWORD
NUSER=.-UKEYCH		;NUMBER OF USER INPUTS
XUSER:	XWD	-NUSER,0
;
	  ;TERMINATING CHARS.
TCHRS:	15		;ONE LINE FOR FULL NAME
	15		;ONE LINE FOR AFF.ORGANIZATION
	32		;^Z TO END ADDRESS
	15		;ONE LINE CONTACT NAME

	ASSERT	(.-TCHRS,NUSER,<
THERE IS A TERMINATING CHARACTER MISMATCH>)

	;USER STRING TABLE

USTR:	[ASCIZ/
Full name? (end with CR)
/]
	[ASCIZ /
Affiliation? (end with CR)
/]
	[ASCIZ /
Mailing address, phone number? (end with ^Z)
/]
	[ASCIZ /
From whom did you get the password? (end with CR)
/]
;
	ASSERT	(.-USTR,NUSER,<
THERE IS A USER STRING MISMATCH>)

	SUBTTL VARIOUS ROUTINES

EATCC:	DEBRK

CTRLC:	PUSH P,1		;LEVEL 3
	HRROI 1,[ASCIZ/

Do you really mean that?  If you do, confirm with CR
and you will be logged out. [Confirm with CR] /]
	PSOUT
	PBIN
	CAIN 1,37		;CR IS THE ONLY CONFIRMATION ALLOWED
	JRST DIE
	HRROI 1,[ASCIZ/
No confirmation ... No LOGOUT
/]
	PSOUT
	JRST CTRLX

DIE:	SETO 1,			;ME
	LGOUT			;GONE

CTRLX:	MOVEI 1,ULOOP		;LEVEL 3
	MOVEM 1,PCLEV3		;RETURN TO REPROMPT
	MOVE 10,PNTSAV		;RESTORE POINTER TO ORIGINAL SHAPE
	POP P,1
	DEBRK

EOFINT: MOVEI 1,NEWPER
	MOVEM 1,PCLEV2		;SET UP A WAY TO GET OUT
	DEBRK
;
COMCDE:	MOVE 3,[POINT 7,[ASCIZ \TTY\]]	;FOR NOW, COMDECODE IS THE TTYN
	MOVE 1,[POINT 7,COMSTR]
	ILDB 2,3
	JUMPE 2,COMC1
	IDPB 2,1		;TIGHT STRING TRANSFER LOOP
	JRST .-3
COMC1:	MOVE 2,TTYNUM
	MOVEI 3,10		;OUTPUT THE NUMBER
	NOUT
	 JFCL
	SETZ 3,			;NULL TO TERMINATE
	IDPB 3,1
	POPJ P,			;RETURN

SINPUT:	MOVE 1,10
	MOVEI 2,^D80		;GET AN EDITED LINE, MAX. 80 CHARS.
	SETZ 3,
	PSTIN
	MOVEM 1,10		;SAVE THE ALL-IMPORTANT POINTER
	LDB 2,1			;CHECK THE TERMINATING CHARACTER
	CAMN 2,TRMCHR		;IS IT THE TERMINATING CHARACTER
	JRST INDONE		;YES, WE'RE DONE
	CAIE 2,15		;IS IT CR
	JRST SINPUT		;NO, GO ON
	MOVEI 2,12		;YES, PUT IN A LF (PSTIN RETURNS A CR
	IDPB 2,10		;WHEN THE RETURN KEY IS HIT)
	MOVEI 2," "
	IDPB 2,10		;FOLLOW IT WITH 2 SPACES
	IDPB 2,10
	JRST SINPUT
INDONE:	BKJFN			;BACK TO WHAT MIGHT BE A SPACE
	 CALL JERR
	BKJFN			;BACK TO WHAT MIGHT BE ANOTHER SPACE
	 CALL JERR
	BKJFN			;BACK TO WHAT MIGHT BE A LF
	 CALL JERR
	BKJFN			;BACK OVER WHAT COULD BE A CR
	 CALL JERR
	LDB 2,1			;READ IT
	CAIE 2,15		;IS IT A CR?
	JRST INPUCR		;NO, PUT CRLF PAIR ON THE END
	MOVE 1,10		;YES, THE CRLF SP SP SITUATION MUST HAVE
	BKJFN			;EXISTED.  AC10 POINTS TO THE ^Z,
	 CALL JERR		;BACK IT UP TO MURDER THE SPACES.
	BKJFN
	 CALL JERR		;2ND SPACE
	BKJFN
	 CALL JERR		;FIRST SPACE
	MOVEM 1,10		;SAVE IT
	RET
INPUCR:	MOVEI 2,15
	DPB 2,10
	MOVEI 2,12		;ADD ON A CRLF
	IDPB 2,10
	RET
TRMCHR:	0			;TERMINATING CHAR.

STOF:	MOVE 1,WHOJFN		;PUT AN ASCIZ STRING ON A FILE
STOF1:	ILDB 2,3		;GET NAME
	BOUT			;AND PUT IT IN THE FILE
	JUMPN 2,STOF1
	MOVEI 2,15		;CR
	BOUT
	MOVEI 2,12		;LF
	BOUT
	RET

OPNERR: PUSH P,2		;SAVE OPEN PARAMS
	CAIE 1,600130		;FILE BUSY?
	 CALL JERR		;NO, MUST BE MESSED UP
	HRRO 1,WMESS		;WAITING MESSAGE
	PSOUT
	MOVEI 1,WMESS2		;SET POINTER TO SECOND WAITING MESSAGE
	MOVEM 1,WMESS
	MOVEI 1,^D15000
	DISMS			;WAIT FOR 15 SECS.

	POP P,2			;RESTORE OPEN PARAMS
	MOVE 1,WHOJFN		;AND THE JFN
	OPENF
	 JRST OPNERR		;TRY AGAIN
	HRROI 1,[ASCIZ / ok, got it./]
	PSOUT
	RET			;SUCCESS

JERR:	HRROI 1,[ASCIZ /
JSYS error at PC /]
	PSOUT
	MOVEI 1,101
	POP P,2
	TLZ 2,777777
	MOVEI 3,10
	NOUT
	 JFCL
	HRROI 1,[ASCIZ /, /]
	PSOUT
	MOVEI 1,101
	HRLOI 2,400000
	SETZ 3,
	ERSTR
	 JFCL
	 JFCL
	HALTF

GOBBLE: MOVE 1,WHOJFN
	MOVE 4,2		;CHAR TO STOP ON IS IN AC2
	HRROI 2,FILBUF		;SOMEPLACE TO PUT IT
	MOVEI 3,^D9000		;BIGGER THAN 80 CHARS/LINE!
	SIN
	RET			;HORRAY, WE GOBBLED IT TO THE CR-LF!

CHNTAB: 3,,CTRLC		;CHANNELS 0
	3,,CTRLX		;1
	0			;2
	0			;3
	0			;4
	0			;5
	0			;6
	0			;7
	0			;8
	0			;9
	2,,EOFINT		;10 (EOF)
	REPEAT ^D35-<.-CHNTAB-1>,
<
	0
>

LEVTAB:	PCLEV1
	PCLEV2
	PCLEV3

PCLEV1:	0			;LEVEL 1 PC
PCLEV2:	0			;  "   2
PCLEV3:	0			;  "   3

ACTJFN:	0			;ACTIVE GUESTS FILE
WHOJFN:	0			;GUESTS.WHO JFN
PNTSAV:	0			;PLACE TO SAVE THE POINTER FOR REPROMPT
TTYNUM:	0			;TTY NUMBER HE/SHE CAME IN ON
FOUNDN:	0			;DID WE FIND HIS NAME?
WMESS:	0			;POINTER TO DIFFERENT WAITING MESSAGES
WMESS1:	ASCIZ /Waiting for file .../  ;FIRST MESSAGE
WMESS2:	ASCIZ /
Still waiting .../		;SECOND MESSAGE
OURNAM:	BLOCK	8		;OUR LOGIN NAME
COMSTR:	BLOCK	^D20		;METHOD OF COMMUNICATING WITH SUMEX
STRBUF:	BLOCK	^D50		;STRING BUFFER OF TEXT ENTERED
FILBUF:	BLOCK	^D300		;STRING BUFFER OF TEXT TO PUT ON FILE
STACK:	BLOCK	STKSIZ

	END	START
