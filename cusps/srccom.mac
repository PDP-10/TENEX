;<SOURCES>SRCCOM.MAC;1    12-APR-74 16:14:29	EDIT BY CLEMENTS
; CONVERTED TO MACRO SOURCE
; PUT CR-LF'S IN PLACE OF EOL'S
;<SOURCES>SRCCOM.FAI;12    26-DEC-73 22:29:38	EDIT BY PLUMMER

	TITLE	SRCCOM
	SUBTTL	R.S.TOMLINSON 28 JUNE 70
	SEARCH STENEX

ENTVEC:	JRST START

NRECF==170
PAGBUF=10000

UUON==1		; OPCODE OF FIRST USER UUO
DEFINE	UUO(NAME)<
EXP Q'NAME
OPDEF	NAME [<UUON>B8]
UUON==UUON+1
>


RMARGN==^D70

A=1
B=2
CH=3
PT1=4
PT2=5
X=6
D=7
HGH1=10
HGH2=11
SCN1=12
SCN2=13
C=14
CNT=15

I1==1
I2==2
O1==3
O2==4

F=0
EOF1==400000
EOF2==200000
OPN1==100000
OPN2==040000
MRK==020000
MSG==010000
LSTF==004000
APPEND==002000
DFONLY==001000
P=17

; UUO'S

UUO40:	PUSH P,CH
	PUSH P,1
	PUSH P,2
	PUSH P,3
	LDB 1,[POINT 9,40,8]
	SKIPN 1,UUOTAB(1)
	HALT
	MOVEM 1,UUODSP#
	MOVE 1,-2(P)
	PUSHJ P,@UUODSP
	POP P,3
	POP P,2
	POP P,1
	POP P,CH
	POPJ P,

UUOTAB:	0
UUO (TMSG)
UUO (CHOUT)
UUO (OUTC2)
UUO (OUTC1)
UUO (OUTM1)
UUO (OUTM2)
REPEAT 40-<.-UUOTAB>,<0>

QTMSG:	HRRZ 1,PRIMRY
OUTMSG:	HRRO 2,40
	MOVEI 3,0
	SOUT
	POPJ P,

QCHOUT:	HRRZ 2,40
	HRRZ 1,PRIMRY
	BOUT
	POPJ P,

QOUTC2:	HRRZ CH,40
OUTC2X:	TRNN F,OPN2
	POPJ P,
	CAIE CH,37
	CAIN CH,12
	JRST [	AOS B,DFLINE
		CAIL B,^D55
		PUSHJ P,DFHDNG
		JRST .+1]
	MOVE B,CH
	MOVE A,DIFJFN
	BOUT
	POPJ P,

QOUTC1:	TRNN F,OPN1
	POPJ P,
	HRRZ CH,40
	CAIE CH,37
	CAIN CH,15
	JRST [	SETZM CH2POS
		JRST OUTC1Z]
	CAIE CH,12
	CAIN CH,14
	JRST OUTC1Z
	CAIN CH,11
	JRST [	MOVEI B,10
		ADD B,CH2POS
		ANDCMI B,7
		MOVEM B,CH2POS
		JRST OUTC1Z]
	CAIGE CH,40
	JRST [	PUSH P,CH
		OUTC1 "^"
		POP P,CH
		TRO CH,100
		JRST .+1]
OUTC1B:	AOS CH2POS
OUTC1Z:	MOVE B,CH
	MOVE A,LSTJFN
	TLNE A,777777
	JRST [	IDPB B,LSTJFN
		POPJ P,]
	BOUT
	POPJ P,

QOUTM1:	HRRZ A,40
	HRLI A,(<POINT 7,0>)
	ILDB CH,A
	JUMPE CH,CPOPJ
	OUTC1 (CH)
	JRST QOUTM1+2

QOUTM2:	HRRZ A,40
	HRLI A,(<POINT 7,0>)
	ILDB CH,A
	JUMPE CH,CPOPJ
	OUTC2 (CH)
	JRST QOUTM2+2

ERR:	HRRZ 1,PRIMRY
	DOBE
	TMSG [ASCIZ /  ? /]
	HLRZ 1,PRIMRY
	CFIBF
START:	RESET
	SETZM BZERO
	MOVSI A,BZERO
	HRRI A,BZERO+1
	BLT A,EZERO-1		; CLEAR TEMPORARY STORAGE AREAS
	MOVE A,[PUSHJ P,UUO40]
	MOVEM A,41
	MOVE A,[XWD 100,101]
	MOVEM A,PRIMRY#
	MOVNI 1,1
	CLOSF
	JFCL
	RLJFN
	JFCL
	MOVE P,[XWD -60,PDL-1]
	MOVEI F,0
	MOVSI A,100001
	HRROI B,[ASCIZ /RECORDOFLISTINGS;1/]
	GTJFN
	JRST NOREC
	MOVEM 1,RECJFN#
	TMSG [ASCIZ /
SPECIAL? /]
	PBIN
	CAIN A,"Y"
	JRST [	MOVE 1,RECJFN
		RLJFN
		JFCL
		JRST NOREC]
	CAIE A,"N"
	JRST START
XL0:	TMSG [ASCIZ /
HOW MANY COPIES DO YOU WANT? /]
	HLRZ 1,PRIMRY
	MOVEI 3,12
	NIN
	JRST XL0
	MOVEM 2,NCOPY#
	BKJFN
	JRST XL1
	BIN
	CAIE 2,37
	JRST XL0
XL1:	MOVE 1,RECJFN
	MOVE 2,[XWD 70000,200000]
	OPENF
	JRST [	MOVE 1,RECJFN
		RLJFN
		JFCL
		JRST NOREC]
	MOVSI X,-NRECF
	MOVE C,[POINT 7,RECFST]
INIRCL:INIRC3:	MOVEM C,RECFPT(X)
	BIN
	CAIE 2,40
	CAIN 2,12
	JRST INIRC3
	JUMPE 2,INIRCD
	CAIE 2,15
	CAIN 2,37
	JRST INIRCD
	SKIPA
INIRC1:	BIN
	CAIN 2,";"
	JRST INIRC2
	IDPB 2,C
	JRST INIRC1

INIRC2:	MOVEI 3,12
	NIN
	JRST INIRCL
	HRLI 2,100001
	MOVEM 2,RECFVR(X)
	MOVEI B,0
	IDPB B,C
	AOBJN X,INIRC3
INIRCD:	MOVN C,X
	HRLZM C,RECFCT
	TRO F,DFONLY!OPN1!OPN2
	SKIPL X,RECFCT
	JRST LSTNXT

LSTLUP:	MOVE 1,RECFVR(X)
	MOVE 2,RECFPT(X)
	PUSH P,2
	PUSH P,1
	GTJFN
	JRST LSTNXT
	MOVEM 1,IN2JFN
	POP P,1
	POP P,2
	HRRI 1,0
	GTJFN
	JRST LSTNXT
	MOVEM 1,IN1JFN
	MOVE 2,[XWD 1,7]
	MOVEI 3,3
	GTFDB
	HLRZ 3,3
	HLL 3,RECFVR(X)
	EXCH 3,RECFVR(X)
	CAMN 3,RECFVR(X)
	JRST LSTNXT
	HRROI B,[ASCIZ /LISTING;T/]
	MOVSI 1,1
	GTJFN
	HALTF
	MOVEM 1,XLSJFN
	MOVE A,[POINT 7,PAGBUF]
	MOVEM A,LSTJFN
	HRROI B,[ASCIZ /DIFFERENCES;T/]
	MOVSI A,1
	GTJFN
	HALTF
	MOVEM 1,DIFJFN
	ANDI F,DFONLY!OPN1!APPEND!LSTF!OPN2
	SETZM CH2POS
	SETZM CHANGE
	TMSG [ASCIZ /
LISTING: /]
	MOVEI 1,101
	MOVE 2,IN1JFN
	MOVEI 3,0
	JFNS
	TMSG [ASCIZ / & /]
	MOVE 2,IN2JFN
	JFNS
	PUSH P,X
	PUSHJ P,STRT2
	TRO F,LSTF!APPEND
	POP P,X

LSTNXT:	MOVNI 1,1
	RLJFN
	JFCL
	AOBJN X,LSTLUP
	MOVE 1,RECJFN
	HRLI 1,400000
	CLOSF
	0
	MOVE 1,RECJFN
	MOVE 2,[XWD 70000,100000]
	OPENF
	0
	SKIPL X,RECFCT
	JRST OURECE
OURECL:	MOVE 2,RECFPT(X)
	MOVEI 3,0
	SOUT
	MOVEI 2,";"
	BOUT
	HRRZ 2,RECFVR(X)
	MOVEI 3,12
	NOUT
	0
	MOVEI 2,15
	BOUT
	MOVEI 2,12
	BOUT
	AOBJN X,OURECL
OURECE:	MOVEI 2,15
	BOUT
	MOVEI 2,12
	BOUT
	CLOSF
	JFCL
	TRNN F,LSTF
	JRST NLIST
	SKIPN C,NCOPY
	HALTF
	HRROI B,[ASCIZ /LPT:/]
	MOVSI 1,1
	GTJFN
	HALTF
	MOVEM 1,LSTJFN
	MOVE 2,[XWD 70000,100000]
	OPENF
	HALTF
	HRROI B,[ASCIZ /LISTING;T/]
	MOVSI 1,1
	GTJFN
	HALTF
	MOVEM 1,IN2JFN
	MOVE 2,[XWD 70000,200000]
	OPENF
	HALTF
	HRROI B,[ASCIZ /DIFFERENCES;T/]
	MOVSI 1,1
	GTJFN
	HALTF
	MOVEM 1,IN1JFN
	MOVE 2,[XWD 70000,200000]
	OPENF
	HALTF
	TMSG [ASCIZ /
GENERATING COPIES.../]
CPYLP:	MOVE 1,IN1JFN
	MOVEI 2,0
	SFPTR
	JFCL
	MOVE 1,IN2JFN
	SFPTR
	JFCL
	MOVEI 2,14
CPY1LP:	MOVE 1,LSTJFN
	BOUT
CPY2LP:	MOVE 1,IN1JFN
	BIN
	JUMPN 2,CPY1LP
	GTSTS
	TLNN 2,1000
	JRST CPY2LP
	MOVEI 2,14
CPY3LP:	MOVE 1,LSTJFN
	BOUT
CPY4LP:	MOVE 1,IN2JFN
	BIN
	JUMPN 2,CPY3LP
	GTSTS
	TLNN 2,1000
	JRST CPY4LP
	MOVEI 2,14
	MOVE 1,LSTJFN
	BOUT
	BOUT
	SOJG C,CPYLP
	MOVNI 1,1
	CLOSF
	JFCL
	HALTF

NLIST:	TMSG [ASCIZ /
NO FILES NEEDED LISTING/]
	HALTF

NOREC:	TMSG [ASCIZ /
COMPARE FILE: /]
	PUSHJ P,GETNAM
	JRST ERR
	MOVEM A,IN1JFN#
	TMSG [ASCIZ / WITH FILE: /]
	MOVE A,[POINT 7,PAGBUF]
	MOVE B,IN1JFN
	SETZB C,DEFDEV
	SETZM DEFDIR
	IDPB C,A
	MOVEM A,DEFNAM
	MOVSI 3,1000
	JFNS
	IDPB C,A
	MOVEM A,DEFEXT
	MOVSI 3,100
	JFNS
	IDPB C,A
	MOVE A,[XWD 100000,-2]
	MOVEM A,DEFVER
	MOVE A,PRIMRY
	MOVEM A,DEFFIL
	MOVEI A,DEFVER
	MOVEI B,0
	GTJFN
	JRST ERR
	MOVEM A,IN2JFN#

	TMSG [ASCIZ /
MARKED LISTING TO FILE: /]
	PUSHJ P,GETONM
	JRST [	TRZ F,OPN1
		JRST STRT1]
	MOVEM A,LSTJFN#
	TRO F,OPN1
	SETZM CH2POS#
STRT0:	TMSG [ASCIZ /
CHANGED PAGES ONLY? /]
	HLRZ 1,PRIMRY
	BIN
	MOVE A,B
	CAIN A,"Y"
	JRST [	TRO F,DFONLY
		MOVE A,[POINT 7,PAGBUF]
		EXCH A,LSTJFN
		MOVEM A,XLSJFN#
		SETZM CHANGE
		JRST STRT1]
	CAIE A,"N"
	JRST STRT0
	TRZ F,DFONLY
STRT1:	TMSG [ASCIZ /
DIFFERENCES TO FILE: /]
	PUSHJ P,GETONM
	JRST [	TRZ F,OPN2
		JRST STRT11]
	MOVEM A,DIFJFN#
	TRO F,OPN2
STRT11:	PUSHJ P,STRT2
	HALTF

STRT2:	MOVE 1,IN1JFN
	MOVE 2,[XWD 070000,200000]
	OPENF
	JRST ERR
	MOVE 1,IN2JFN
	OPENF
	JRST ERR
	MOVE 2,[XWD 070000,100000]
	TRNE F,APPEND
	HRRI 2,020000
	TRNN F,OPN1
	JRST STRT3
	TRNE F,DFONLY
	SKIPA 1,XLSJFN
	MOVE 1,LSTJFN
	OPENF
	JRST ERR
STRT3:	TRNN F,OPN2
	JRST STRT31
	MOVE 1,DIFJFN
	OPENF
	JRST ERR

STRT31:	MOVEI A,20000
	MOVEM A,BUF1
	MOVEI A,400000
	MOVEM A,EBUF1#
	MOVEM A,BUF2
	MOVEI A,777777
	MOVEM A,EBUF2#
	MOVEI A,1
	MOVEM A,LINE1
	MOVEM A,LINE2
	MOVEM A,PAGE1
	MOVEM A,PAGE2
	TRZ F,EOF1!EOF2!MRK!MSG
	SETZM LSTPGE#
	SETZM IN1
	SETZM IN2
	MOVE A,[POINT 7,HDNG]
	HRROI B,[ASCIZ /

; /]
	MOVEI 3,0
	SOUT
	MOVE B,IN1JFN
	JFNS
	HRROI B,[ASCIZ / & /]
	SOUT
	MOVE B,IN2JFN
	JFNS
	MOVEI B,40
	IDPB 2,1
	MOVNI 2,1
	MOVSI 3,(1B10!1B12)
	ODTIM
	HRROI B,[ASCIZ /	PAGE /]
	SETZ 3,
	SOUT
	SETZB B,PAGEY#
	IDPB B,A
	PUSHJ P,PRHDNG
	SETZM PAGED#
	SETZM DFLINE#
	PUSHJ P,DFHDNG
LUP0:	SKIPN IN1
	SKIPE IN2
	JRST LUP01
	TRNE F,EOF1
	TRNN F,EOF2
	JRST LUP01
LUP00:	OUTC2 14
	SKIPE A,IN1JFN
	CLOSF
	JFCL
	SKIPE A,IN2JFN
	CLOSF
	JFCL
	SKIPE A,LSTJFN
	CLOSF
	JFCL
	SKIPE A,XLSJFN
	CLOSF
	JFCL
	SKIPE A,DIFJFN
	CLOSF
	JFCL
	POPJ P,

LUP01:	MOVE PT1,BUF1
	MOVE PT2,BUF2
	HRLI PT1,(<POINT 7,0>)
	HRLI PT2,(<POINT 7,0>)
	PUSHJ P,RDL1		; READ A LINE FROM FILE 1
	PUSHJ P,RDL2		; AND FILE 2
	MOVE A,BUF1
	MOVE B,BUF2
	PUSHJ P,COMP1		; COMPARE ONE LINE
	JRST NEQ
	MOVE A,BUF1
	TRNE F,OPN1
	PUSHJ P,OUT1		; COPY LINE TO LISTING
	AOS LINE1
	AOS LINE2
	SKIPN A,NPAG
	JRST LUP0		; GO BACK FOR NEXT LINE
	ADDM A,PAGE1
	ADDM A,PAGE2
	MOVEI A,1
	MOVEM A,LINE1
	MOVEM A,LINE2
	JRST LUP0

NEQ:	MOVE A,DFLINE
	CAIL A,^D45
	PUSHJ P,DFHDNG
	TRO F,MRK
	PUSHJ P,RD4L1		; READ 4 MORE LINES
	PUSHJ P,RD4L2
	SKIPE IN1
	JRST [	PUSHJ P,RDL1
		JRST .-1]
	SKIPE IN2
	JRST [	PUSHJ P,RDL2
		JRST .-1]
	MOVE HGH1,BUF1
	MOVE HGH2,BUF2
	MOVSI CNT,-^D100
NEQ0:	PUSHJ P,RDL1
	PUSHJ P,RDL2
	MOVE A,HGH1
	PUSHJ P,SKP1L
	MOVEM A,HGH1
	MOVE A,HGH2
	PUSHJ P,SKP1L
	MOVEM A,HGH2
	MOVE SCN1,BUF1
	MOVE SCN2,BUF2
NEQ1:	MOVE A,SCN1
	MOVE B,HGH2
	PUSHJ P,C4L
	JRST NEQ2
	MOVE A,SCN1
	MOVE B,HGH2
	JRST OUTDIF

NEQ2:	MOVE A,HGH1
	MOVE B,SCN2
	PUSHJ P,C4L
	JRST NEQ3
	MOVE A,HGH1
	MOVE B,SCN2
OUTDIF:	MOVEM A,ENDL1
	MOVEM B,ENDL2
	TRO F,MRK
	TRNE F,OPN1
	PUSHJ P,OUTB1
	TRZ F,MRK
	OUTM2 [ASCIZ /

/]
	MOVEI X,0
	PUSHJ P,OUTBLK
	MOVEI X,1
	PUSHJ P,OUTBLK
	MOVE A,ENDL1
	HRLI A,(<POINT 7,0>)
	MOVEM A,IN1
	MOVE A,ENDL2
	HRLI A,(<POINT 7,0>)
	MOVEM A,IN2
	MOVEM PT1,INE1
	MOVEM PT2,INE2
	JRST LUP0

NEQ3:	MOVE A,SCN1
	CAMN A,HGH1
	JRST NEQ4
	PUSHJ P,SKP1L
	MOVEM A,SCN1
	MOVE A,SCN2
	PUSHJ P,SKP1L
	MOVEM A,SCN2
	JRST NEQ1

NEQ4:	AOBJN CNT,NEQ0
	TMSG [ASCIZ /
I SEEM TO HAVE A VERY LONG CHANGE OF AT LEAST /]
	HRRZ A,CNT
	PUSHJ P,TYPNUM
	TMSG [ASCIZ / LINES./]
NEQ41:	TMSG [ASCIZ /
TYPE "C" TO CONTINUE, "M" TO FORCE A MATCH, AND "N" TO ABORT. /]
	HLRZ 1,PRIMRY
	BIN
	MOVE CH,2
	HRLI CNT,-^D100
	CAIN CH,"C"
	JRST NEQ0
	CAIN CH,"N"
	JRST NEQQQQ
	CAIE CH,"M"
	JRST NEQ41
	MOVE A,HGH1
	MOVE B,HGH2
	JRST OUTDIF

NEQQQQ:	OUTM2 [ASCIZ /
...
PLUS MANY MORE DIFFERENCES./]
	JRST LUP00

OUTN2:	TRNN F,OPN2
	POPJ P,
	PUSH P,1
	PUSH P,2
	PUSH P,3
	MOVE 2,A
	MOVE 1,DIFJFN
	MOVEI 3,^D10
	NOUT
	JFCL
	POP P,3
	POP P,2
	POP P,1
	POPJ P,

TYPNUM:	MOVE B,A
	HRRZ 1,PRIMRY
	MOVEI 3,12
	NOUT
	POPJ P,
	POPJ P,

OUTBLK:	MOVE A,ENDL1(X)
	PUSHJ P,SKP1L
	MOVEM A,ENDL1(X)
	OUTM2 [ASCIZ /LINE /]
	MOVE A,LINE1(X)
	PUSHJ P,OUTN2
	OUTM2 [ASCIZ /, PAGE /]
	MOVE A,PAGE1(X)
	PUSHJ P,OUTN2
	OUTM2 [ASCIZ /
/]
	MOVE B,@BUFP(X)
OUTBL1:	HRLI B,(<POINT 7,0>)
	OUTM2 HDM1(X)
OUTBL2:	ILDB CH,B
	CAIN CH,14
	JRST [	OUTC2 "^"
		OUTC2 "L"
		AOS PAGE1(X)
		MOVEI A,1
		MOVEM A,LINE1(X)
		JRST OUTBL2]
	OUTC2 (CH)
	CAIE CH,12
	CAIN CH,37
	AOSA LINE1(X)
	CAIN CH,32
	JRST .+2
	JRST OUTBL2
	MOVEI B,1(B)
	CAME B,ENDL1(X)
	JRST OUTBL1
	POPJ P,



OUT1:	MOVE A,BUF1
	HRLI A,(<POINT 7,0>)
OUT1A:	PUSH P,A
	MOVE A,PAGE1
	CAMN A,PAGE2
	TRNE F,MRK
	CAMG A,LSTPGE
	JRST QA
	AOS CHANGE
	PUSH P,A
	TRNN F,MSG
	TMSG [ASCIZ /
PAGES WITH CHANGES ARE: /]
	TROE F,MSG
	CHOUT ","
	POP P,A
	MOVEM A,LSTPGE
	PUSHJ P,TYPNUM
QA:	POP P,A
OUT1B:	ILDB CH,A
	CAIN CH,14
	JRST QAFF
	CAIN CH,15
	TRNN F,MRK
	JRST QA2
	PUSH P,A
QA3:	MOVE CH,CH2POS
	CAIG CH,RMARGN
	JRST QA4
	OUTM1 [ASCIZ /
/]
	JRST QA3

QA4:	CAIL CH,RMARGN
	JRST QA5
	OUTC1 40
	JRST QA3

QAFF:	TRNN F,MRK
	JRST QAFF1
	PUSH P,A
	CHOUT ","
	AOS A,LSTPGE
	PUSHJ P,TYPNUM
	POP P,A
QAFF1:	OUTC1 "^"
	OUTC1 "L"
	OUTC1 14
	TRNE DFONLY
	JRST [	PUSH P,B
		PUSH P,A
		PUSH P,3
		MOVEI B,0
		IDPB B,LSTJFN
		MOVE A,XLSJFN
		HRROI B,PAGBUF
		MOVEI 3,0
		SKIPL CHANGE
		SOUT
		TRNN F,MRK
		SETOM CHANGE
		MOVE A,[POINT 7,PAGBUF]
		MOVEM A,LSTJFN
		POP P,3
		POP P,A
		POP P,B
		JRST .+1]
	PUSHJ P,PRHDNG
	JRST OUT1B

QA5:	OUTM1 [ASCIZ /**/]
	MOVEI CH,15
	POP P,A
QA2:	CAIE CH,32
	OUTC1 (CH)
	CAIE CH,32
	CAIN CH,12
CPOPJ:	POPJ P,
	JRST OUT1B

OUTB1:	PUSH P,A
	MOVE A,BUF1
	CAME A,(P)
	JRST OUTB1A
	MOVE A,[XWD 440700,[ASCIZ /************* DELETION *************
/]]
	PUSHJ P,OUT1A
	JRST OUTB1B

OUTN1:	TRNN F,OPN1
	POPJ P,
	PUSH P,3
	PUSH P,A
	MOVE A,LSTJFN
	MOVEI 3,12
	NOUT
	JFCL
	MOVEM A,LSTJFN
	POP P,A
	POP P,3
	POPJ P,

DFHDNG:	OUTC2 14
	SETZM DFLINE
	OUTM2 HDNG
	AOS A,PAGED
	PUSHJ P,OUTN2
	OUTM2 [ASCIZ /

/]
	POPJ P,

PRHDNG:	OUTM1 HDNG
	AOS B,PAGEY
	PUSHJ P,OUTN1
	OUTM1 [ASCIZ /

/]
	POPJ P,

OUTB1A:	HRLI A,(<POINT 7,0>)
	PUSHJ P,OUT1A
	MOVEI A,1(A)
	CAME A,(P)
	JRST OUTB1A
OUTB1B:	POP P,A
	HRLI A,(<POINT 7,0>)
	TRZ F,MRK
	JRST OUT1A

DEFINE	RDL(N)<
RD4L'N:	PUSHJ P,RDL'N
	PUSHJ P,RDL'N
RDL'N:	SETZM NPAG#
	MOVE A,EBUF'N
	SUBI A,(PT'N)
	CAIG A,200
	PUSHJ P,XPND'N

RDL'N'X:
	SKIPN A,IN'N
	JRST RDL'N'A
RDL'N'P:
	ILDB CH,A
	CAIE CH,12
	CAIN CH,32
	JRST .+2
	JRST RDL'N'Z
	TLNE A,760000
	JRST RDL'N'P
	CAMN A,INE'N
	MOVEI A,0
RDL'N'Z:
	MOVEM A,IN'N
RDL'N'B:
	JUMPE CH,RDL'N'X
	IDPB CH,PT'N
	CAIN CH,14
	AOS NPAG
	CAIE CH,12
	CAIN CH,32
	JRST .+2
	JRST RDL'N'X
	TLNE PT'N,760000
	JRST RDL'N'B
	POPJ P,

RDL'N'A:
	MOVE 1,IN'N'JFN
	BIN
	SKIPE CH,B
	JRST RDL'N'B
	GTSTS
	TLNN B,1000
	JRST RDL'N'B
	MOVEI CH,32
	TRO F,EOF'N
	JRST RDL'N'B
>

RDL(1)
RDL(2)

XPND1:XPND2:TMSG [ASCIZ /
256K IS NOT ENOUGH!!!/]
	HALT
C4L:	MOVEI X,4
	JRST COMPC

COMP1:	MOVEI X,1
COMPC:	MOVE D,(A)
	XOR D,(B)
	ANDCMI D,1
	JUMPN D,CPOPJ
	MOVE D,(A)
	AOS A
	AOS B
	ANDI D,376
	CAIE D,2*32
	CAIN D,2*12
	SKIPA
	JRST COMPC
	SOJG X,COMPC
	AOS (P)
	POPJ P,

SKP1L:	MOVE B,(A)
	ANDI B,376
	CAIE B,2*32
	CAIN B,2*12
	AOSA A
	AOJA A,SKP1L
	POPJ P,

GETNAM:	SKIPA 1,[XWD 100002,0]
GETONM:	MOVSI 1,460002
	MOVE 2,PRIMRY
	GTJFN
	POPJ P,
	AOS (P)
	POPJ P,


PATCH:PAT:BLOCK 30
HDM1:	ASCIZ /1)	/
HDM2:	ASCIZ /2)	/
BUFP:	BUF1
	BUF2

LIT

BZERO:!	; START OF AREA TO ZERO ON STARTUP
LINE1:	BLOCK 1
LINE2:	BLOCK 1
PAGE1:	BLOCK 1
PAGE2:	BLOCK 1
ENDL1:	BLOCK 1
ENDL2:	BLOCK 1
IN1:	BLOCK 1
IN2:	BLOCK 1
INE1:	BLOCK 1
INE2:	BLOCK 1
HDNG:	BLOCK 20
DEFVER:	BLOCK 1
DEFFIL:	BLOCK 1
DEFDEV:	BLOCK 1
DEFDIR:	BLOCK 1
DEFNAM:	BLOCK 1
DEFEXT:	BLOCK 1
	BLOCK 4
CHANGE:	BLOCK 1
RECFCT:	BLOCK 1		; AOBJN WORD TO INDEX RECORD TABLES
RECFPT:	BLOCK NRECF		; STRING POINTERS TO FILE NAMES
RECFVR:	BLOCK NRECF		; VERSION NUMBERS
RECFST:	BLOCK 4*NRECF		; STRING STORAGE FOR NAMES
BUF1:	BLOCK 1
BUF2:	BLOCK 1
PDL:	BLOCK 60
VAR
EZERO:!	; END OF AREA TO ZERO ON STARTUP

	END START
