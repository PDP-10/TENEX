;<SOURCES>LOGGER.MAC;1    10-JUN-75 08:13:26    EDIT BY HEDBERG
;
;		DATE	TIME	NAME	ACTION	PATH
;
;	CODE -- ACTION
;	----	------
;	  0	LOGIN
;	  1	LOGOUT
;	  2	EXEC
;	  3	SUBSYS
;
	SEARCH	STENEX
STKSIZ==20
P==17
	TITLE	LOGGER 
;
LOGGER:	MOVE	P,[IOWD STKSIZ,STACK]
	MOVE	10,[POINT 7,STRING]	;SET UP OUTPUT STRING POINTER
	MOVEM	1,ACTION	;PARAMS PASSED FROM THE EXEC
	MOVEM	2,SUBSYS
;
	HRLZI	1,(1B2!1B17)	;OLD FILE, SHORT CALL
	HRROI	2,[ASCIZ \<SYSTEM>GUESTS..;\]
	GTJFN
	 0
;
	MOVE	2,[1B19!1B25]	;READ, THAWED
	OPENF
	 0
	PUSH	P,1		;SAVE JFN
;
	HRLZ	1,1		;JFN IN LH, PAGE 0 IN RH
	MOVE	2,[400000,,<PAGE/1000>]	;DESTINATION
	HRLZI	3,(1B2)		;READ ACCESS
	PMAP			;LET'S LOOK FOR OUR NAME
;
	POP	P,1		;GET JFN AGAIN
	CLOSF			;GET RID OF IT
	 JFCL
;
	MOVE	1,10		;GET POINTER
	SETO	2,		;CURRENT TIME
	MOVE	3,[1B3!1B6!1B8!1B10]	;NICE OUTPUT
	ODTIM			;PUT OUT TIME
;
	MOVE	10,1		;SAVE THE CRITICAL BYTE POINTER
	MOVEI	1,40
	IDPB	1,10		;FOLLOW WITH 1 SPACE
;
	GJINF
	MOVEM	4,TTYNUM	;SAVE THE TERMINAL NUMBER
	ROT	3,3		;STARTING WORD OF NAME
	ADDI	3,PAGE		;IN THE RIGHT PAGE
	MOVE	2,[POINT 7,0]	;GET BASIC POINTER
	HRR	2,3		;POINT IT TO THE RIGHT NAME
	MOVEI	3,^D10		;TRANSFER 10 CHARS. (AT LEAST)
	PUSHJ	P,STROUT
;
	MOVEI	1,40		;FOLLOWED BY AT LEAST
	IDPB	1,10		;ONE SPACE. (FILL TO 10 CHARS)
	SOJGE	3,.-1
;
	MOVE	3,ACTION	;GET ACTION
	JRST	@ACTADR(3)
ACTADR:	JRST	ALOGIN		;LOGIN
	JRST	ALOGO		;LOGOUT
	JRST	AEXEC		;EXEC
	JRST	ASUBSY		;SUBSYS
;
ALOGIN:	MOVE	2,[POINT 7,[ASCIZ \LOGIN  TTY\]]
	PUSHJ	P,STROUT
	MOVE	1,10		;GET SACRED POINTER
	MOVE	2,TTYNUM	;AND THE TTY NUMBER
	MOVE	3,[4,,10]	;4 DIGITS, OCTAL
	NOUT
	 JFCL
	MOVE	10,1
;
	MOVE	1,['DIALIN']	;DIAL UPS
	SYSGT
	HLRZ	6,1		;HIGH IN 6
	HRRZ	5,1		;LOW IN 5
	MOVE	1,['TYMLIN']	;TYMNET
	SYSGT
	HLRZ	4,1		;HIGH IN 4
	HRRZ	3,1		;LOW IN 3
	MOVE	1,['NVTLIN']	;THE NET
	SYSGT
	HLRZ	2,1		;HIGH IN 2
	HRRZ	1,1		;LOW IN 1
;
	CAML	6,TTYNUM
	CAMLE	5,TTYNUM
	JRST	.+2
	JRST	DIAL		;IT'S A DIALUP LINE
;
	CAML	4,TTYNUM
	CAMLE	3,TTYNUM
	JRST	.+2
	JRST	TYMNET		;IT'S A TYMNET LINE
;
	CAML	2,TTYNUM
	CAMLE	1,TTYNUM
	JRST	.+2
	JRST	THENET		;IT'S AN ARPANET LINE
;
	MOVE	2,[POINT 7,[ASCIZ \ Hardwired line\]]
	PUSHJ	P,STROUT	;IF ALL ELSE FAILS, IT'S A HARDWIRED LINE
	JRST	SENDIT
;
DIAL:	MOVE	2,[POINT 7,[ASCIZ \ Dial-up line\]]
	PUSHJ	P,STROUT
	JRST	SENDIT
;
TYMNET:	MOVE	2,[POINT 7,[ASCIZ \ Tymnet line\]]
	PUSHJ	P,STROUT
	JRST	SENDIT
;
THENET:	MOVEI	1," "
	IDPB	1,10
	MOVEI	1,"("
	IDPB	1,10
;
	MOVE	1,['NETAWD']
	SYSGT
	HRRO	1,2		;-1,,TABLENUMBER
	HLRE	2,2
	MOVMM	2,NETBLN#	;SAVE THE LENGTH OF THE TABLES
	MOVEI	2,NETAWD	;WHERE TO PUT
	GTBLT			;THE WHOLE TABLE
	 JFCL
;
	MOVE	1,['NETSTS']
	SYSGT
	HRRO	1,2		;-1,,TABLENUMBER
	MOVEI	2,NETSTS	;WHERE TO PUT
	GTBLT			;THE WHOLE TABLE
	 JFCL
;
	MOVE	1,['NETBUF']
	SYSGT
	HRRO	1,2		;-1,,TABLENUMBER
	MOVEI	2,NETBUF	;WHERE TO PUT
	GTBLT			;THE WHOLE TABLE
	 JFCL
;
	MOVE	1,TTYNUM
	SETZ	2,
ICNLOP:	CAMN	1,NETBUF(2)
	JRST	GOTICN
	CAMLE	2,NETBLN
	JRST	SENDIT		;TERRIBLE ERROR, QUIT
ICN1:	AOJA	2,ICNLOP
;
GOTICN:	MOVE	3,NETSTS(2)
	TLC	3,340000	; IN OPND STATE?
	TLNE	3,740000
	 JRST	ICN1
;
	MOVE	3,NETAWD(2)
	LDB	2,[POINT 9,3,17]	;GET THE FOREIGN HOST
	MOVE	1,10
	MOVEI	3,10
	CVHST			;IN STRING FORM
	 NOUT			;OR A NUMBER
	  JFCL
	MOVE	10,1
	MOVEI	1,")"
	IDPB	1,10
	JRST	SENDIT
;
ALOGO:	MOVE	2,[POINT 7,[ASCIZ \Logout  used \]]
	PUSHJ	P,STROUT
	HRROI	1,-5
	RUNTM
	PUSH	P,3		;SAVE CONSOLE TIME
	MOVE	2,1
	PUSHJ	P,TOUT		;OUTPUT THE RUN TIME
	MOVE	2,[POINT 7,[ASCIZ \ IN \]]
	PUSHJ	P,STROUT
	POP	P,2		;GET CONSOLE TIME
	PUSHJ	P,TOUT
	JRST	SENDIT
;
AEXEC:	MOVE	2,[POINT 7,[ASCIZ \Exec\]]
	PUSHJ	P,STROUT
	JRST	SENDIT
;
ASUBSY:	MOVE	2,[POINT 6,SUBSYS]
	MOVEI	3,6
ASUBS1:	ILDB	1,2		;GET SIXBIT CHAR
	ADDI	1,40		;MAKE IT ASCII
	IDPB	1,10		;INTO STRING
	SOJG	3,ASUBS1	;LOOP
	JRST	SENDIT
;
SENDIT:	MOVEI	1,15		;CR
	IDPB	1,10		;STORE IT
	MOVEI	1,12		;LF
	IDPB	1,10		;STORE IT
	SETZ	1,
	IDPB	1,10		;STORE IT
	
;
	HRLZI	1,(1B17)	;SHORT CALL
	HRROI	2,[ASCIZ \<SYSTEM>GUESTS.LOG\]
	GTJFN
	 0
	MOVEM	1,OUTJFN
;
OPENL:	MOVE	2,[7B5!1B22]	;7 BIT BYTES, APPEND
	OPENF
	 JRST	OPNERR
	MOVE	1,OUTJFN
	HRROI	2,STRING	;SHOOT THE STRING OUT
	SETZ	3,
	SOUT
	MOVE	1,OUTJFN	;MAKE SURE THE JFN IS
	CLOSF			;AS DEAD AS POSSIBLE
	 JFCL
	MOVE	1,OUTJFN
	RLJFN
	 JFCL
	HALTF			;BYE
	JRST	.-1
;
OPNERR:	CAIE	1,600130	;IS THE FILE BUSY
	 0			;NO, CRASH
	MOVEI	1,^D20000	;WAIT 20 SECS
	DISMS
	MOVE	1,OUTJFN
	JRST	OPENL		;TRY AGAIN
;
	 ;APPENDS STRING POINTED TO BY 2 TO STRING POINTED TO BY 10
	 ;SUBTRACTS NUMBER OF CHARS TRANSFERRED FROM AC3
STROUT:	PUSH	P,1		;SAVE IT
STROU1:	ILDB	1,2		;GET FIRST CHAR
	JUMPE	1,STROU3	;NULL TERMINATES
	IDPB	1,10		;PUT CHAR
	SOJA	3,STROU1
;
STROU3:	POP	P,1		;RESTORE
	POPJ	P,		;RETURN
;
		;OUTPUT TIME ACCEPTED IN 2 IN THE FORM H:MM:SS:THT
;
TOUT:	IDIVI	2,^D1000
	PUSH	P,3		;PUT REMAINDER OF SECS (IN MS) ON STACK
	IDIVI	2,^D60
	PUSH	P,3		;PUT SECS ON STACK
	IDIVI	2,^D60
	PUSH	P,3		;PUT MINS ON STACK
;
	MOVE	1,10		;GOT HOURS IN 2
	MOVEI	3,^D10
	NOUT			;OUTPUT HOURS
	 MOVE	1,10		;HOPE THE NEXT GOES BETTER
	MOVEI	2,":"
	IDPB	2,1
;
	POP	P,2		;GET MINS. BACK
	MOVE	3,[1B2!1B3!2B17!^D10]	;LEADING 0'S, 2 COLS. BASE 10
	NOUT			;PRINT THEM
	 MOVE	1,10
	MOVEI	2,":"
	IDPB	2,1
;
	POP	P,2		;GET SECS.
	NOUT			;PRINT THEM
	 MOVE	1,10
	MOVEI	2,"."
	IDPB	2,1
;	 
	POP	P,2		;GET MS
	TLO	3,3		;CHANGE TO OUTPUT 3 COLS.
	NOUT
	 MOVE	1,10
	MOVE	10,1		;RETURN POINTER TO PROPER PLACE
;
	POPJ	P,
;
	LIT
	VAR
ACTION:	0			;ACTION CODE
SUBSYS:	0			;SIXBIT NAME OF SUBSYS ENTERED
TTYNUM:	0			;TERMINAL NUMBER (FOR PATH)
OUTJFN:	0			;OUTPUT JFN
;
STACK:	BLOCK	STKSIZ
STRING:	BLOCK	20
;
	LOC	<<./1000>+1>*1000	;NEXT PAGE BOUNDARY
PAGE:	BLOCK	1000
;
NETLEN==^D500
NETAWD:	BLOCK	NETLEN
NETSTS:	BLOCK	NETLEN
NETBUF:	BLOCK	NETLEN
	END	LOGGER 

