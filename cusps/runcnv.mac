;<SOURCES>RUNCNV.MAC;4    12-DEC-72 15:52:41	EDIT BY PLUMMER

;31 OCT 70, 1540:

;CONVERSION PROGRAM FOR ASCII TO FLEXO OR DURA

	TITLE RUNCNV
	SEARCH STENEX

	INTERN CNVINI,FFOUT1,DFOUT1
	EXTERN OUJFN,CNVCAS,CRFLG

FOUT=OUJFN
CAS=CNVCAS

NCP==100
CP=17
EOL=37

	MLON

	OPDEF CALL [PUSHJ CP,0]
	OPDEF RET [POPJ CP,0]

	DEFINE TYPE (A)
<	MOVEI 1,[SIXBIT @A/@]
	CALL TMSG>

; SETUP

CNVINI:	PUSH CP,1		;SAVE REQUEST LETTER
	MOVSI 1,(1B0+1B17)	;FOR OUTPUT, SHORT
	HRROI 2,[ASCIZ /PTP:/]
	GTJFN			;GET JFN FOR PUNCH
	JRST NOPUN		;??
	MOVE 2,[10B5+1B20]	;8-BIT OUTPUT
	OPENF
	JRST NOPUN1
	MOVEM 1,FOUT
	MOVEI 1,100
	CALL TFEED
	SETZM CAS
	SETZM CRFLG
	POP CP,1
	CAIE 1,"F"
	JRST [	MOVEI 1,40	;DURA OUTPUT
		CALL DFEED
		MOVEI 1,20
		CALL DCOQ
		RET]
	MOVEI 1,72
	CALL PCOUT
	RET

NOPUN1:	RLJFN
	JFCL
NOPUN:	MOVEI 1,NOPM
	CALL TMSG
	HALTF

NOPM:	SIXBIT '$CANNOT INIT PUNCH.$/'

;CHARACTER TRANSFER TO/FROM AC1

CIO:	EXCH 1,2		;PUT IT IN 2 FOR BIN/BOUT
	BOUT			;YES
	EXCH 1,2
	RET

;CONVERT ASCII TO FIO-DEC

FFOUT1:	PUSH CP,7		;SAVE AC'S 0-7
	MOVEI 7,1(CP)
	ADD CP,[XWD 7,7]
	BLT 7,0(CP)
	JUMPE 1,ENDFIL
	HRRZ 3,FIOTAB(1)
	CAIL 3,400
	JRST 0(3)
	MOVE 1,3
CNV2:	CALL COUT
CNV0:	MOVSI 7,-6(CP)		;RESTORE AC'S
	BLT 7,6
	SUB CP,[XWD 7,7]
	POP CP,7
	RET

COUT:	MOVE 3,1
	TRNE 1,200
	JRST CNV1	;CASE DOESNT MATTER
	ANDI 1,100
	CAMN 1,CAS
	JRST CNV1	;SAME CASE
	MOVEM 1,CAS
	LSH 1,-5
	ADDI 1,72	;COMPUTE CASE SHIFT CHARACTER
	CALL PCOUT
CNV1:	MOVE 1,3

PCOUT:	ANDI 1,77
	PUSH CP,2
	MOVE 2,1
	IMULI 2,200401
	AND 2,ONES8
	IMUL 2,ONES8
	TLNN 2,10
	TRO 1,200
	MOVE 2,FOUT
	CALL CIO
	POP CP,2
	RET

ONES8:	EXP 11111111

ENDFIL:	MOVEI 1,200
	CALL TFEED
	JRST CNV0

; SPECIAL CHARACTER ROUTINES

SC:	CALL TCO	;TYPE ILLEGAL CHARACTER
	MOVEI 1,BADM1
	CALL TMSG
	MOVEI 1,213	;STOP CODE
	CALL COUT
	MOVEI 1,200	;USE SPACE
	JRST CNV2

BADM1:	SIXBIT ' IS UNTRANSLATABLE.$/'

EXPT:	MOVEI 1,102	;MAKE ! FROM APOSTROPHE AND PERIOD
	CALL COUT
	MOVEI 1,75	;BACKSPACE
EXC1:	CALL COUT
	MOVEI 1,73
	JRST CNV2

COLON:	MOVEI 1,40	;MAKE : FROM CENTERDOT AND PERIOD
	JRST EXC1

SEM:	MOVEI 1,40	;MAKE ; FROM CENTERDOT AND COMMA
	CALL COUT
	MOVEI 1,33
	JRST CNV2

DOLARS:	MOVEI 1,156	;MAKE $ FROM VERT BAR AND CAP. S
	CALL COUT
	MOVEI 1,122
	JRST CNV2

CHZRO:	MOVEI 1,20	;FIO-DEC ZERO
	CALL COUT
	MOVEI 1,75	;BACKSPACE AND SLASH THE ZERO
	CALL COUT
	MOVEI 1,21
	JRST CNV2

XSTAR:	MOVEI 1,173	;MAKE * FROM + AND TIMES SIGN
	CALL COUT
	MOVEI 1,275
	CALL COUT
	MOVEI 1,154
	JRST CNV2

STOPC:	MOVEI 1,5	;SUROUND STOP CODE WITH TAPE FEED
	CALL TFEED
	MOVEI 1,13
	CALL COUT
	MOVEI 1,20
	CALL TFEED
	JRST CNV0

;CONVERSION ROUTINE FOR DURA SELECTRIC TYPEWRITER

DFOUT1:	PUSH CP,7		;SAVE AC'S 0-7
	MOVEI 7,1(CP)
	ADD CP,[XWD 7,7]
	BLT 7,0(CP)
	SKIPE CRFLG		;CR LAST TIME?
	JRST [	SETZM CRFLG	;YES
		CAIN 1,12	;LF THIS TIME?
		JRST DCNV0	;YES, IGNORE IT
		JRST .+1]	;NO, PROCESS WHATEVER IT IS
	JUMPE 1,DEOF
DCNV3:	HRRZ 3,DURATB(1)
	CAIL 3,1000
	JRST 0(3)
	MOVE 1,3
DCNV2:	CALL DCOUT
DCNV0:	JRST CNV0		;RESTORE AC'S AND RETURN

DCOUT:	MOVE 3,1
	TRNE 1,200
	JRST DCNV1
	ANDI 1,400
	CAMN 1,CAS
	JRST DCNV1
	MOVEM 1,CAS
	LSH 1,-^D8
	MOVE 1,DCASC(1)
	CALL DCOQ
DCNV1:	MOVE 1,3
	ANDI 1,177
DCOQ:	PUSH CP,2
	MOVE 2,FOUT
	CALL CIO
	POP CP,2
	RET

DEOF:	JRST ENDFIL

DCASC:	EXP 20,10

DSTOP:	MOVEI 1,5
	CALL DFEED
	MOVEI 1,0		;DURA STOP CODE
	CALL DCOQ
	MOVEI 1,40
	CALL DFEED
	JRST DCNV0

DULINE:	MOVEI 1,500
	CALL DCOUT
	MOVEI 1,40
	CALL DCOQ
	JRST DCNV0

DEXPT:	MOVEI 1,125		;USE ' AND . FOR !
	CALL DCOUT
	MOVEI 1,40
	CALL DCOQ
	MOVEI 1,126
	JRST DCNV2

DURACR:	SETOM CRFLG		;REMEMBER CR THIS TIME
	MOVEI 1,EOL		;YES, SUBSTITUTE EOL
	JRST DCNV3		;OTHERWISE PROCESS THE STRANGE CHAR

NE1:	PUSH CP,1
	TYPE <CAN'T CONVERT >
	MOVE 1,0(CP)
	CALL TCO
	TYPE < (>
	POP CP,2
	MOVEI 1,101
	MOVEI 3,10
	NOUT
	JFCL
	TYPE <)$>
	SETZB 1,CAS
	MOVE 1,DCASC(1)		;SET CASE TO LOWER
	CALL DCOQ
	MOVEI 1,0
	CALL DCOQ
	MOVE 1,CAS
	LSH 1,-^D8
	MOVE 1,DCASC(1)
	CALL DCOQ
	JRST DCNV0

; UTILITY STUFF

TCO:	PBOUT
	RET

DFEED:	PUSH CP,2
	PUSH CP,1
	MOVEI 1,200
	JRST FEED1

TFEED:	PUSH CP,2
	PUSH CP,1
	MOVEI 1,0
FEED1:	MOVE 2,FOUT
	CALL CIO
	SOSLE 0(CP)
	JRST .-2
	POP CP,1
	POP CP,2
	RET

TMSG:	HRLI 1,440600
	PUSH CP,1
TMSG1:	ILDB 1,0(CP)
	CAIN 1,04	;DOLLARS BECOMES EOL
	JRST TMSG3
	CAIN 1,17	;SLASH TERMINATES
	JRST TMSG2
	ADDI 1,40	;CONVERT TO ASCII
TMSG4:	PBOUT
	JRST TMSG1

TMSG3:	MOVEI 1,EOL
	JRST TMSG4

TMSG2:	POP CP,1
	RET

;ASCII TO DURA CONVERSION TABLE
;200 BIT => CASE DOESN'T MATTER
;400 BIT => LOWER CASE IF 0, UPPER CASE IF 1

	DEFINE NE
<	XWD 0,NE1>

DURATB:	NE		;CONTROL CHARS - @
	NE		;A
	NE		;B
	NE		;C
	NE		;D
	NE		;E
	NE		;F
	NE		;G

	NE		;H
	NE		;I
	XWD 0,214	;J - LINE FEED
	NE		;K
	XWD 0,DSTOP	;L - FORM FEED
	XWD 0,DURACR	;M - CR
	NE		;N
	NE		;O

	NE		;P
	NE		;Q
	NE		;R
	NE		;S
	NE		;T
	NE		;U
	NE		;V
	NE		;W

	NE		;X
	NE		;Y
	NE		;Z
	XWD 0,DULINE	;UNDERSCORE
	NE		;\
	NE		;]
	NE		;^
	XWD 0,202	;EOL

	XWD 0,204	;SPACE (40)
	XWD 0,DEXPT	;!
	XWD 0,525	;"
	XWD 0,576	;#
	XWD 0,571	;$
	XWD 0,565	;%
	XWD 0,575	;&
	XWD 0,125	;'

	XWD 0,560	;(
	XWD 0,561	;)
	XWD 0,574	;*
	XWD 0,506	;+
	XWD 0,114	;,
	XWD 0,100	;-
	XWD 0,126	;.
	XWD 0,111	;/

	XWD 0,161	;0
	XWD 0,177	;1
	XWD 0,166	;2
	XWD 0,176	;3
	XWD 0,171	;4
	XWD 0,165	;5
	XWD 0,164	;6
	XWD 0,175	;7

	XWD 0,174	;8
	XWD 0,160	;9
	XWD 0,515	;:
	XWD 0,115	;;
	NE		;<
	XWD 0,106	;=
	NE		;>
	XWD 0,511	;?

	XWD 0,566	;UPPER CASE CHARS - @
	XWD 0,534	;A
	XWD 0,540	;B
	XWD 0,554	;C
	XWD 0,555	;D
	XWD 0,545	;E
	XWD 0,516	;F
	XWD 0,517	;G

	XWD 0,541	;H
	XWD 0,524	;I
	XWD 0,507	;J
	XWD 0,544	;K
	XWD 0,551	;L
	XWD 0,537	;M
	XWD 0,546	;N
	XWD 0,531	;O

	XWD 0,505	;P
	XWD 0,504	;Q
	XWD 0,535	;R
	XWD 0,521	;S
	XWD 0,547	;T
	XWD 0,556	;U
	XWD 0,536	;V
	XWD 0,520	;W

	XWD 0,557	;X
	XWD 0,501	;Y
	XWD 0,567	;Z
	NE		;[
	NE		;\
	NE		;]
	NE		;^
	NE		;_

	NE		;LOWER CASE CHARS - @
	XWD 0,134	;A
	XWD 0,140	;B
	XWD 0,154	;C
	XWD 0,155	;D
	XWD 0,145	;E
	XWD 0,116	;E
	XWD 0,117	;G

	XWD 0,141	;H
	XWD 0,124	;I
	XWD 0,107	;J
	XWD 0,144	;K
	XWD 0,151	;L
	XWD 0,137	;M
	XWD 0,146	;N
	XWD 0,131	;O

	XWD 0,105	;P
	XWD 0,104	;Q
	XWD 0,135	;R
	XWD 0,121	;S
	XWD 0,147	;T
	XWD 0,156	;U
	XWD 0,136	;V
	XWD 0,120	;W

	XWD 0,157	;X
	XWD 0,101	;Y
	XWD 0,167	;Z
	NE		;[
	NE		;\
	NE		;]
	NE		;^
	NE		;_


; ASCII TO FIO-DEC CONVERSION TABLE
; 200 BIT => CASE DOESNT MATTER
; 100 BIT => LOWER CASE IF 0, UPPER CASE IF 1

	DEFINE NG
<	XWD 0,SC>

FIOTAB:	NG	;CONTROL CHARS - 0
	NG	;A
	NG	;B
	NG	;C
	NG	;D
	NG	;E
	NG	;F
	NG	;G

	NG	;H - 10
	NG	;I
	XWD 0,277	;J - LINE FEED
	NG	;K
	XWD 0,STOPC	;L - FORM FEED
	XWD 0,CNV0	;M - CR, ASSUME LF FOLLOWS
	NG	;N
	NG	;O

	NG	;P
	NG	;Q
	NG	;R
	NG	;S
	NG	;T
	NG	;U
	NG	;V
	NG	;W

	NG	;X - 30
	NG	;Y
	NG	;Z
	XWD 0,140	;33 (173 ON 940) USED AS UNDERSCORE
	NG	;\
	NG	;]
	NG	;^
	XWD 0,277	;EOL

	XWD 0,200	;SPACE - 40
	XWD 0,EXPT	;!
	XWD 0,101	;"
	NG	;#
	XWD 0,DOLARS	;$
	NG	;%
	NG	;&
	XWD 0,102	;'

	XWD 0,57	;(
	XWD 0,55	;)
	XWD 0,XSTAR	;*
	XWD 0,154	;+
	XWD 0,33	;,
	XWD 0,54	;-
	XWD 0,73	;.
	XWD 0,21	;/

	XWD 0,CHZRO	;0
	XWD 0,1	;1
	XWD 0,2	;2
	XWD 0,3	;3
	XWD 0,4	;4
	XWD 0,5	;5
	XWD 0,6	;6
	XWD 0,7	;7

	XWD 0,10	;8
	XWD 0,11	;9
	XWD 0,COLON	;:
	XWD 0,SEM	;;
	XWD 0,107	;<
	XWD 0,133	;=
	XWD 0,110	;>
	XWD 0,121	;?

	NG	;@ - 100
	XWD 0,161	;A
	XWD 0,162	;B
	XWD 0,163	;C
	XWD 0,164	;D
	XWD 0,165	;E
	XWD 0,166	;F
	XWD 0,167	;G

	XWD 0,170	;H
	XWD 0,171	;I
	XWD 0,141	;J
	XWD 0,142	;K
	XWD 0,143	;L
	XWD 0,144	;M
	XWD 0,145	;N
	XWD 0,146	;O

	XWD 0,147	;P
	XWD 0,150	;Q
	XWD 0,151	;R
	XWD 0,122	;S
	XWD 0,123	;T
	XWD 0,124	;U
	XWD 0,125	;V
	XWD 0,126	;W

	XWD 0,127	;X
	XWD 0,130	;Y
	XWD 0,131	;Z
	XWD 0,157	;[
	NG	;\
	XWD 0,155	;]
	XWD 0,111	;^
	XWD 0,120	;_

	NG	;LOWER CASE - 140
	XWD 0,61	;A
	XWD 0,62	;B
	XWD 0,63	;C
	XWD 0,64	;D
	XWD 0,65	;E
	XWD 0,66	;F
	XWD 0,67	;G

	XWD 0,70	;H
	XWD 0,71	;I
	XWD 0,41	;J
	XWD 0,42	;K
	XWD 0,43	;L
	XWD 0,44	;M
	XWD 0,45	;N
	XWD 0,46	;O

	XWD 0,47	;P
	XWD 0,50	;Q
	XWD 0,51	;R
	XWD 0,22	;S
	XWD 0,23	;T
	XWD 0,24	;U
	XWD 0,25	;V
	XWD 0,26	;W

	XWD 0,27	;X
	XWD 0,30	;Y
	XWD 0,31	;Z
	NG	;[
	NG	;\
	NG	;]
	NG	;^
	NG	;_

	END
