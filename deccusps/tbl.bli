00100	MODULE TBL=
00200	BEGIN
00300	
00400	%(
00500		THE CHANNEL TABLE
00600	)%
00700		MACRO TABMAX=16$, TABSIZ=15$; % MAX CHANNELS AND SIZE %
00800		STRUCTURE CHTB[I,J]=[(I+1)*(J+1)]((.CHTB+.I*(J+1)+.J)<0,36>);
00900		EXTERNAL CHTB CHNLTAB[TABMAX,TABSIZ];
01000	%(
01100		C H A N N E L   T A B L E   M A C R O S
01200	)%
01300	MACRO	FMODE=CHAN[0]$,
01400		FDEV=CHAN[1]$,
01500		FBUF=CHAN[2]$,
01600		FBUFO=CHAN[2]<18,18>$,
01700		FBUFI=CHAN[2]<0,18>$,
01800		FOBUFH=CHAN[3]$,
01900		FOPTR=CHAN[4]$,
02000		FOCNT=CHAN[5]$,
02100		FIBUFH=CHAN[6]$,
02200		FIPTR=CHAN[7]$,
02300		FICNT=CHAN[8]$,
02400		FFILE=CHAN[9]$,
02500		FEXT=CHAN[10]$,
02600		FERR=CHAN[10]<0,12>$,
02700		FBLK=CHAN[10]<0,18>$,
02800		FPROT=CHAN[11]<27,9>$,
02900		FDMODE=CHAN[11]<23,4>$,
03000		FTIME=CHAN[11]<13,10>$,
03100		FDATE=CHAN[11]<0,12>$,
03200		FPPN=CHAN[12]$,
03300		FPRJ=CHAN[12]<18,18>$,
03400		FPRG=CHAN[12]<0,18>$,
03500		FSIZ=CHAN[12]<18,18>$,
03600		FSTATUS=CHAN[13]$,
03700		FSWITCH=CHAN[14]$,
03800		FNEWPR=CHAN[15]<0,10>$,
03900	%(
04000		STATUS WORD BITS
04100	)%
04200		FUSER=0,18$,	% ENTIRE USER FIELD %
04300		FYEND=0,1$,	% ENDFILE ALLOWED %
04400		FYREW=1,1$,	% REWIND ALLOWED  %
04500		FYREAD=2,1$,	% READING ALLOWED %
04600		FYWRITE=3,1$,	% WRITING ALLOWED %
04700		FYUPDAT=4,1$,	% UPDATING ALLOWED %
04800		FYDUMMY=5,1$,	% DUMMY CHANNEL   %
04900		FYERR=6,1$,	% WRITE ERRORS NOT TRAPPED %
05000		FPRIMARY=7,1$,	% PRIMARY DEFAULT VALUE %
05100	
05200	% SYSTEM BITS OF STATUS WORD %
05300		FSYS=18,18$,	% ENTIRE SYSTEM FIELD %
05400		FROPEN=18,1$,	% FILE OPEN FOR INPUT %
05500		FWOPEN=19,1$,	% FILE OPEN FOR OUTPUT %
05600		FEOF=20,1$,	% ENDFILE ENCOUNTERED %
05700		FIS35=21,1$,	% BIT 35 IS SET %
05800		FSET35=22,1$,	% SET BIT 35 ON NEXT WRITE %
05900		FSEQ=24,1$,	% PROCESSING SEQUENCE NO. %
06000		FPGMK=25,1$,	% PROCESSING PAGE MARK %
06100		FFISTAR=26,1$,	% FILE NAME IS "*" %
06200		FXSTAR=27,1$,	% EXTENSION NAME IS "*" %
06300		FUOPEN=28,1$,	% FILE OPEN FOR UPDATE %
06350		FDEVAS=29,1$,	% DEVICE ASSIGNED %
06375		FIOERR=30,1$,	% I/O ERROR DETECTED %
06400		FNSET=35,1$;	% CHANNEL NOT SET %
06500	MACRO
06600		XFYEND=1^0$,	% ENDFILE ALLOWED %
06700		XFYREW=1^1$,	% REWIND ALLOWED  %
06800		XFYREAD=1^2$,	% READING ALLOWED %
06900		XFYWRITE=1^3$,	% WRITING ALLOWED %
07000		XFYUPDAT=1^4$,	% UPDATE ALLOWED %
07100		XFYDUMMY=1^5$,	% DUMMY CHANNEL %
07200		XFYERR=1^6$,	% ALLOW WRITE ERROR TRAP %
07300		XFPRIMARY=1^7$;	% PRIMARY DEFAULT VALUE %
07400	MACRO	OTHER=-1$;
07500	%(
07600		MACROS WHICH DEFINE THE CALLS
07700	)%
07800	MACRO	UMAX=0$,	% MAXIMUM CHANNEL %
07900		UMODE=1$,	% DATA MODE %
08000		UDEV=2$,	% DEVICE IN SIXBIT %
08100		UOBUFF=3$,	% OUTPUT BUFFER HEADER POINTER %
08200		UIBUFF=4$,	% INPUT BUFFER HEADER POINTER %
08300		UOBUFFR=5$,	% ADDR OF OUTPUT BUFFER %
08400		UOPTR=6$,	% OUTPUT BUFFER BYTE POINTER %
08500		UOCNT=7$,	% OUTPUT BUFFER BYTE COUNT %
08600		UIBUFFR=8$,	% ADDR OF INPUT BUFFER %
08700		UIPTR=9$,	% INPUT BUFFER BYTE POINTER %
08800		UICNT=10$,	% INPUT BUFFER BYTE COUNT %
08900		UFILE=11$,	% FILE NAME IN SIXBIT %
09000		UEXT=12$,	% EXTENSION NAME IN SIXBIT %
09100		UBLK=13$,	% %
09200		UERR=14$,	% ERROR NUMBER %
09300		UPROT=15$,	% PROTECTION KEY %
09400		UDMODE=16$,	% CREATION DATA MODE %
09500		UTIME=17$,	% CREATION TIME %
09600		UDATE=18$,	% CREATION DATE %
09700		UPPN=19$,	% PPN %
09800		USTAT=20$,	% USER BITS OF STATUS WORD %
09900		USSTAT=21$,	% SYSTEM BITS OF STATUS WORD %
10000		USWITCH=22$,	% SWITCH WORD %
10100		UREPROT=23$;	% REPROTECTION WORD %
10200	MACRO	USIZ=24$;	% FILE SIZE %
10300	MACRO	MAX=24$;	% MAXIMUM CODE ALLOWED %
10500	EXTERNAL IOZERROR,IOERR;
10600	EXTERNAL CHNLMAX,INVALID;
     

00100	GLOBAL ROUTINE GETTBL(CHNL,CODE)=
00200	    BEGIN
00300		BIND VECTOR CHAN=CHNLTAB[.CHNL,0]<0,36>;
00400		IF .CODE EQL UMAX THEN RETURN .CHNLMAX;
00500		IF INVALID(.CHNL,14) THEN RETURN 0;
00600		IF .CODE GTR MAX OR .CODE LSS 0 THEN (IOZERROR_.CODE;IOERR(40); RETURN 0);
00700		CASE .CODE OF
00800			SET
00900		%UMAX:%		.CHNLMAX;
01000		%UMODE:%	.FMODE;
01100		%UDEV:%		.FDEV;
01200		%UOBUFF:%	.FBUFO;
01300		%UIBUFF:%	.FBUFI;
01400		%UOBUFFR:%	.FOBUFH;
01500		%UOPTR:%	.FOPTR;
01600		%UOCNT:%	.FOCNT;
01700		%UIBUFFR:%	.FIBUFH;
01800		%UIPTR:%	.FIPTR;
01900		%UICNT:%	.FICNT;
02000		%UFILE:%	.FFILE;
02100		%UEXT:%		.FEXT<18,18>^18;
02200		%UBLK:%		.FBLK;
02300		%UERR:%		.FERR;
02400		%UPROT:%	.FPROT;
02500		%UDMODE:%	.FDMODE;
02600		%UTIME:%	.FTIME;
02700		%UDATE:%	.FDATE;
02800		%UPPN:%		.FPPN;
02900		%USTAT:%	.FSTATUS<FUSER>;
03000		%USSTAT:%	.FSTATUS<FSYS>^18;
03100		%USWITCH:%	.FSWITCH;
03200		%UREPROT:%	.FNEWPR;
03300		%USIZ:%		.FSIZ;
03400		TES
03500	    END;
     

00100	GLOBAL ROUTINE SETTBL(CHNL,CODE,DATA)=
00200	BEGIN
00300		BIND VECTOR CHAN=CHNLTAB[.CHNL,0]<0,36>;
00400		IF .CODE EQL UMAX THEN (IF .DATA GTR TABMAX OR .DATA LSS 0
00500			THEN (IOZERROR_.DATA; IOERR(43); RETURN 0) ELSE
00600				(CHNLMAX_.DATA; RETURN 0));
00700		IF INVALID(.CHNL,20) THEN RETURN 0;
00800		IF .CODE GTR MAX OR .CODE LSS 0 THEN (IOZERROR_.CODE; IOERR(41); RETURN 0);
00850		IF .CODE EQL USSTAT THEN DATA_.DATA^(-18);
00900		(CASE .CODE OF
01000			SET
01100		%UMAX:%		CHNLMAX;
01200		%UMODE:%	FMODE;
01300		%UDEV:%		FDEV;
01400		%UOBUFF:%	FBUFO;
01500		%UIBUFF:%	FBUFI;
01600		%UOBUFFR:%	FOBUFH;
01700		%UOPTR:%	FOPTR;
01800		%UOCNT:%	FOCNT;
01900		%UIBUFFR:%	FIBUFH;
02000		%UIPTR:%	FIPTR;
02100		%UICNT:%	FICNT;
02200		%UFILE:%	FFILE;
02300		%UEXT:%		FEXT<18,18>;
02400		%UBLK:%		FBLK;
02500		%UERR:%		FERR;
02600		%UPROT:%	FPROT;
02700		%UDMODE:%	FDMODE;
02800		%UTIME:%	FTIME;
02900		%UDATE:%	FDATE;
03000		%UPPN:%		FPPN;
03100		%USTAT:%	FSTATUS<FUSER>;
03200		%USSTAT:%	FSTATUS<FSYS>;
03300		%USWITCH:%	FSWITCH;
03400		%UREPROT:%	FNEWPR;
03500		%USIZ:%		FSIZ;
03600		TES)
03700		_ .DATA;
03800	END;
     

00100	END ELUDOM;
