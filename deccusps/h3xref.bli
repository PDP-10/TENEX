!COPYRIGHT 1972,1973,1974 DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS. 01754
!FILENAE:	H3XREF.BLI
!DATE:		10 JULY 73	MGM/FLD

%3.2%	GLOBAL BIND H3XRV=2;	!MODULE VERSION NUMBER




FORWARD XINS,XPRINLEV;
ROUTINE XSORT =
	BEGIN
	LOCAL T1,T2,T3;
	INCR I FROM 0 TO HTSIZE -1 DO 
		IF (T1_.XHT[.I]) NEQ 0 THEN
		BEGIN
		XHT[.I]_0;
	DO (
		XINS(.T1);
		T1_.XT[.T1,2]<0,18>;
	   ) UNTIL .T1 EQL 0;
		END;
	END;

ROUTINE XFIX(PTR)=
	BEGIN
	LOCAL T1;
	T1_XT[.PTR,-1]<1,7>;
	INCR I FROM 1 TO 10 DO IF SCANI(T1) EQL #177 THEN
		REPLACEN(T1,0);
	END;

ROUTINE XOUTSTN(I)=
	IF NOT .LSTFLG THEN
	BEGIN EXTERNAL OUTPUT; LOCAL P,N,C; MACHOP ILDB=#134;
	P_ST[.I,-1]<1,7>; N_0;
	WHILE (C_ILDB(3,P)) NEQ 0 AND .N LSS 10 DO (OUTPUT(2,.C);N_.N+1);
	.N
	END;
ROUTINE XINS(PTR)=
	BEGIN
	LOCAL T1,T2,T3;
	XFIX(.PTR);	! CLEAN UP RUBOUTS
	IF .XHED EQL 0 THEN (XHED_.PTR; XT[.PTR,3]<18,18>_0;RETURN);
	IF .XT[.PTR,0] LSS .XT[.XHED,0] THEN
		(XT[.PTR,3]<18,18>_.XHED;XHED_.PTR;RETURN);
	IF .XT[.PTR,0] EQL .XT[.XHED,0] THEN
		IF .XT[.PTR,1] LSS .XT[.XHED,1] THEN
		(XT[.PTR,3]<18,18>_.XHED; XHED_.PTR; RETURN);
	T1_.XHED;
	DO (T2_.T1; T1_.XT[.T1,3]<18,18>) UNTIL
	   (IF .T1 EQL 0 THEN 1 ELSE IF .XT[.PTR,0] EQL .XT[.T1,0] THEN
		.XT[.PTR,1] LSS .XT[.T1,1] ELSE
		.XT[.PTR,0] LSS .XT[.T1,0]);
	XT[.PTR,3]<18,18>_.T1;
	XT[.T2,3]<18,18>_.PTR;
	END;

ROUTINE XNEWTIT=
	BEGIN
	OUTSTR("Ident",5);
	OUTSTR("ifier",5);
	OUTSTR(" Bl  ",5);
	OUTSTR("Decl ",5);
	OUTSTR("   Us",5);
	OUTSTR("ed",2);
	NEWLINE();
	INCR I FROM 1 TO 6 DO OUTSTR("-----",5);
	END;

ROUTINE XNEWLINE=
	BEGIN
	NEWLINE();
	IF .NLINES EQL 0 THEN (XNEWTIT();NEWLINE());
	END;
ROUTINE XPRINT(X,Y) =
	BEGIN
	LOCAL T1,T2,T3;
	XNEWLINE();
	OUTBLANK(11-XOUTSTN(.X));
%3.20%	OUTNUM(.XT[.Y,2]<18,18>-1,10,2,0);	! BLOCKLEVEL
	OUTBLANK(1);
	IF (T1_.XT[.Y,1]<18,18>) NEQ 0 THEN 
%3.20%		OUTNUM(.T1/16,10,5,0)
	ELSE
		OUTSTR("*****",5);
	OUTBLANK(3);
	T1_.XT[.Y,0]<18,18>;
	T3_0;
	WHILE .T1 GTR 4 DO
		BEGIN
		IF .T3  EQL 6 THEN (XNEWLINE(); OUTBLANK(22);T3_0);
%3.20%		OUTNUM(.XT[.T1,0]<0,18>/16,10,5,0); OUTBLANK(1);
		IF (T2_.XT[.T1,1]<18,18>) NEQ 0 THEN
%3.20%			(OUTNUM(.T2/16,10,5,0); OUTBLANK(1));
		IF (T2_.XT[.T1,1]<0,18>) NEQ 0 THEN
%3.20%			(OUTNUM(.T2/16,10,5,0); OUTBLANK(1));
		T1_.XT[.T1,0]<18,18>;
		T3_.T3+1;
		END
	END;
ROUTINE XSELECT=
	BEGIN
	LOCAL T1,T2,T3;
	T1_.XHED;
	WHILE .T1 NEQ 0 DO (XPRINLEV(.T1,.XT[.T1,3]<0,18>);
		T1_.XT[.T1,3]<18,18>);
	END;

ROUTINE XPRINLEV(X,Y) =
	BEGIN
	LOCAL T1,T2,T3;
	WHILE .XT[.Y,2]<0,18> NEQ 0 DO Y_.XT[.Y,2]<0,18>;
	WHILE .Y NEQ 0 DO
		BEGIN
		XPRINT(.X,.Y);
		IF .XT[.Y,3]<0,18> NEQ 0 THEN XPRINLEV(.X,.XT[.Y,3]<0,18>);
		Y_.XT[.Y,1]<0,18>;
		END;
	END;


%3.1%	GLOBAL ROUTINE XREFWRITE =
	BEGIN
	PAGE(); NLINES_0;
	XNEWTIT();
	XHED_0;
	XSORT();
	XSELECT();
	END;



!END OF H3XREF.BLI
