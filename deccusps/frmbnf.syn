 
							100-310-000-00






	THE SYNTAX OF PDP-10 FORTRAN 10 FORMAT STATEMENT

			NOVEMBER 17,1971


			AUTHOR: T.E. OSTEN, D. B. TOLMAN
			VERSION 2
			EDIT 1


















		THE  INFORMATION IN  THIS MEMORANDUM IS
		SUBJECT TO CHANGE  WITHOUT  NOTICE  AND
		SHOULD NOT BE CONSTRUED AS A COMMITMENT
		BY   DIGITAL  EQUIPEMENT  CORPORATION .
		COPYRIGHT 1974.
;							100-310-000-00
							PAGE 02
.
;THE MODIFIED BNF SYNTAX DESCRIBED IN TERMS OF ITSELF .

BNFDEFINITION      = METASYMBOL "="  BNFCOMPONENT  [ COMMENT ] "." .

BNFCOMPONENT       = ( TERMINAL l
                     METASYMBOL l
                     OR l
                     AND l
                     OPTION l
                     LIST l
                     REPEAT l
                     ACTION ) .

TERMINAL           = """ *ASCIICHARACTER """ ;TERMINAL CHARACTER STRING .

METASYMBOL         = [ *LETTER  "-" ] *LETTER  ;AN ELEMENT
                     OF THE SYNTAX DEFINED BY THE SYNTAX OR BY A LEXICAL
                     PROCESS .

OR                 = "(" BNFCOMPONENT *[ "l" BNFCOMPONENT ] ")" 
                     ;ONLY ONE OF THE BNFCOMPONENTS IS REQUIRED .

AND                = BNFCOMPONENT *[ BNFCOMPONENT ] ;ALL OF THE
                     BNFCOMPONENTS ARE REQUIRED .

OPTION             = "[" BNFCOMPONENT "]" ;USE OF THE BNFCOMPONENT IS
                     OPTIONAL .

LIST               = "+" BNFCOMPONENT ;BNFCOMPONENTS SEPERATED BY
                     COMMAS .

REPEAT             = "*" BNFCOMPONENT ;BNFCOMPONENTS NOT SEPERATED BY
                     COMMAS .

ACTION             = "%" ACTIONNAME "%" ;ACTION TO BE PERFORMED WHEN ENCOUNTERED .

COMMENT            = ";" *ASCIICHARACTER .

;*****************************************************************************************************************
;							100-310-000-00
							PAGE 03
.
;CHECKS THAT THE FORMAT STATEMENT CONFORMS TO THE SYNTAX WHICH FOLLOWS.

FORMATSPEC        = LPAREN  [ FDGROUP ] RPAREN LINEND .

FDGROUP           = +( FIELDDESCRIPTOR [ *SLASH ] ) .

FIELDDESCRIPTOR   = ( ( PLUS l MINUS ) CONST PCHAR [ CONST ] TWODESCRIPTOR l
                    CONST ( PCHAR [ CONST ] TWODESCRIPTOR l
                    TWODESCRIPTOR l ONEDESCRIPTOR l XCHAR l LPAREN  FDGROUP RPAREN )  l
                    TWODESCRIPTOR l ONEDESCRIPTOR l TCHAR CONST l DOLLAR l SLASH l
                    LITSTRING l XCHAR l LPAREN  FDGROUP RPAREN ) .

TWODESCRIPTOR     = ( FCHAR l ECHAR l DCHAR l GCHAR ) [ CONST [ PERIOD CONST ] ] . 

ONEDESCRIPTOR     = ( ICHAR l LCHAR l OCHAR l ACHAR l RCHAR ) [ CONST ] .

