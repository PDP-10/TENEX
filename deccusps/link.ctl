;JOB%2A(244) TO MAKE LINK-10 2A
;SUBMIT WITH COMMAND  .QUEUE I:=LINK/RESTART:1/TIME:1::/HEADER:0
;
;THIS VERSION OF THE CONTROL FILE EXPECTS:
;ALL LINK-10 SOURCES AND CCL FILES ON SELF:
;THE FOLLOWING MODULES ON SELF:
;	C.MAC		7(163)
;	SCNMAC.MAC	6(100)
;	SCAN.REL	6(370)
;	HELPER.REL	5(41)
;ALL OTHER FILES ON [10,7]
;IT FIRST USES VERSION 2 (ON [10,7]) TO CREATE VERSION 2A
;NEXT IT COMPARES VERSION 2A TO VERSION 2 TO VERIFY ONLY EXPECTED CHANGES
;THEN THAT VERSION TO CREATE ITSELF AGAIN
;BOTH VERSIONS ARE THEN COMPARED.
;IT MAKES EITHER THE OVERLAY VERSION OR THE BUNDLED VERSION
;DEPENDING UPON WHETHER THE FILE LNKOV1.MAC.MAC IS PRESENT OR NOT
;
;REQUIRED FILES:  (LATEST RELEASED VERSIONS)
;[10,7]	PIP.SHR
;	DIRECT.SHR
;	MACRO.SHR
;	CREF.SHR
;	COMPIL.SHR
;	FILCOM.SHR
;	LINK.SHR
;	LNK???.SHR
;	JOBDAT.REL
;[SELF]	C.MAC		7(163)
;	SCNMAC.MAC	6(100)
;	SCAN.REL	6(370)
;	HELPER.REL	5(41)
;	LINK-10 SOURCES
;	LINK-10 CMD AND CCL FILES
;	PLOTTER SOURCE FILES PLT???	(FOR OVERLAY VERSION)
;	LINK.HLP
;
;OUTPUT FILE:
;	LINK.SHR
;	LNKSCN.SHR
;	LNKLOD.SHR
;	LNKMAP.SHR
;	LNKXIT.SHR
;	LNKERR.SHR
;	LNK999.SHR
;	LNKOV1.SHR
;	LNKOV2.SHR
;OUTPUT LISTINGS:
;	LINK. MAP
;	LNK???.MAP
;	LINK-10  CREF LISTING
;	LINK.LOG
;
;
;COPY FILES FROM [10,7] AND USE PRIVATE "SYS:"
.RUN DSK:PIP[10,7]
*/X_DSK:[10,7]PIP.SHR,DIRECT.SHR,MACRO.SHR,CREF.SHR,COMPIL.SHR
*/X_DSK:[10,7]JOBDAT.REL
*/X_DSK:[10,7]LINK.SHR,LNK???.SHR
;
SETUP::
.CHKPNT SETUP
;MAKE A RECORD OF WHAT IS BEING USED
.SET WATCH VERSION
.IF (ERROR)	;TOO BAD
.ASSIGN DSK: SYS:
;
;JUST INCASE WE HAVE RESTARTED , DELETE LINK-10 REL FILES
.DELETE LNK???.REL
;
.RUN DSK:DIRECT
*TTY:/CHECKSUM=LINK.*+LNK???.*+*.SHR+*.REL+C.MAC+SCNMAC.MAC+OVRLAY.*+PLT???.*
ASSEM::
.CHKPNT ASSEM
;MAKE THE PARAMETER FILE U.MAC
;THIS ASSEMBLES UNIVERSAL VERSIONS OF C AND SCNMAC
;
.R PIP
*U.MAC=TTY:
*%.C==-3
*^Z
;
;MAKE TEST FOR BUNDLED OR UNBUNDLED VERSION
.RUN TECO[10,7]
*ERLNKOV1.MAC
.IF (NOERROR) .GOTO ASSMU
.COPY LNKCMD.CMD=LNKBUN.CMD
.GOTO ASSM2
ASSMU::
.COPY LNKCMD.CMD=LNKUNB.CMD
;COMPIL OVRLAY.MAC
.COMPILE /CREF /COMPIL OVRLAY.MAC
;COMPIL PLOTTER STUFF
.TYPE LNKPLT.CMD
;
.COMPIL /CREF /COMPIL @LNKPLT.CMD
;
ASSM2::
.CHKPNT ASSM2
;COMPILE ALL LINK-10 SOURCE FILES PRODUCING BINARY AND CREF FILES
.TYPE LNKCMD.CMD
;
.COMPIL /COMPIL /CREF @LNKCMD.CMD
.IF (NOERROR) .GOTO LINK
REASS::
.CHKPNT REASS
;HERE IF MACRO FAILS WITH
;?ILL MEM REF AT USER PC 413533
.DELETE C.REL,SCNMAC.REL,LNKPAR.REL,LNKLOW.REL
.COMPIL /CREF @LNKCMD
.IF (NOERROR) .GOTO LINK
REAS1::
.CHKPNT REAS1
;HERE IF MACRO STILL FAILS
;LAST TRY WITHOUT CREF FILES
.DELETE C.REL,SCNMAC.REL,LNKPAR.REL,LNKLOW.REL
.COMPIL @LNKCMD
;
LINK::
.CHKPNT LINK
.TYPE LNKINI.CCL
;
.R LINK
*@LNKINI.CCL
;
.TYPE LNKSCN.CCL
;
.R LINK
*@LNKSCN.CCL
;
.TYPE LNKLOD.CCL
;
.R LINK
*@LNKLOD.CCL
;
.TYPE LNKMAP.CCL
;
.R LINK
*@LNKMAP.CCL
;
.TYPE LNKXIT.CCL
;
.R LINK
*@LNKXIT.CCL
;
.TYPE LNKERR.CCL
;
.R LINK
*@LNKERR.CCL
.TYPE LNK999.CCL
;
.R LINK
*@LNK999.CCL
;
;TEST FOR UNBUNDLED VERSION AGAIN
.RUN TECO[10,7]
*ERLNKOV1.MAC
.IF (ERROR) .GOTO CHK2A
;
.TYPE LNKOV1.CCL
;
.R LINK
*@LNKOV1.CCL
;
.TYPE LNKOV2.CCL
;
.R LINK
*@LNKOV2.CCL
;
CHK2A::
.CHKPNT CHK2A
;
;BEFORE GOING ON, CHECK THAT THE DIFFERENCES BETWEEN VERSION 2
;AND VERSION 2A ARE JUST THE ONES EXPECTED.
;
.RUN DSK:DIRECT
*TTY:/CHECKSUM=XXX???.SHR
;
;WE EXPECT THAT LOCATIONS 400002, 400004 AND 400007 MAY NOT
;COMPARE BECAUSE THE RIGHT HALF OF .JBHCR MAY BE DIFFERENT,
;THE VERSION IS DIFFERENT, AND THE BYTE CONTAINING THE HIGH SEGMENT
;ORIGIN (PAGE NUMBER) IN 400007 MAY BE DIFFERENT.
;ALSO, LNKXIT AND LNKOV2 SHOULD BE DIFFERENT.  THE DIFFERENCE SHOULD
;ONLY BE IN THE ADDRESS AND ALWAYS EQUAL 1 PRIOR TO THE PATCH,
;AND SHOULD BE SIMILAR BUT OFFSET BY ONE WORD AFTER THE PATCH.
;
.RUN DSK:FILCOM[10,7]
*TTY:=LINK.SHR,XXXX.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
.RUN DSK:FILCOM[10,7]
*TTY:=LNKSCN.SHR,XXXSCN.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
.RUN DSK:FILCOM[10,7]
*TTY:=LNKLOD.SHR,XXXLOD.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
.RUN DSK:FILCOM[10,7]
*TTY:=LNKMAP.SHR,XXXMAP.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
;.RUN DSK:FILCOM[10,7]
;*TTY:=LNKXIT.SHR,XXXXIT.SHR
;.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
.RUN DSK:FILCOM[10,7]
*TTY:=LNKERR.SHR,XXXERR.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
.RUN DSK:FILCOM[10,7]
*TTY:=LNK999.SHR,XXX999.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
;
;TEST FOR UNBUNDLED VERSION AGAIN
.RUN TECO[10,7]
*ERLNKOV1.MAC
.IF (ERROR) .GOTO RENM0
;
.RUN DSK:FILCOM[10,7]
*TTY:=LNKOV1.SHR,XXXOV1.SHR
.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
;.RUN DSK:FILCOM[10,7]
;*TTY:=LNKOV2.SHR,XXXOV2.SHR
;.IF (ERROR)	;DIFFERENCES ARE EXPECTED
;
RENM0::
.CHKPNT RENM0
;NOW DELETE OLD LINK-10 AND RENAME NEW
.DELETE LINK.SHR,LNK???.SHR
;
RENM1::
.CHKPNT RENM1
.RENAME LINK.SHR=XXXX.SHR
.RENAME LNK???.SHR=XXX???.SHR
;
TEST::
.CHKPNT TEST
;TRY IT JUST TO MAKE SURE IT WORKS
.RUN DSK:LINK
*/HELP
;
.RUN DSK:DIRECT
*TTY:/CHECKSUM=LINK.SHR+LNK???.SHR
;
AGAIN::
.CHKPNT AGAIN
.RUN DSK:LINK
*@LNKINI.CCL
;
.RUN DSK:LINK
*@LNKSCN.CCL
;
.RUN DSK:LINK
*@LNKLOD.CCL
;
.RUN DSK:LINK
*@LNKMAP.CCL
;
.RUN DSK:LINK
*@LNKXIT.CCL
;
.RUN DSK:LINK
*@LNKERR.CCL
;
.RUN DSK:LINK
*@LNK999.CCL
;
;
;TEST FOR UNBUNDLED VERSION AGAIN
.RUN TECO[10,7]
*ERLNKOV1.MAC
.IF (ERROR) .GOTO COMP
;
.RUN DSK:LINK
*@LNKOV1.CCL
;
.RUN DSK:LINK
*@LNKOV2.CCL
COMP::
.CHKPNT COMP
.RUN DSK:DIRECT
*TTY:/CHECKSUM=XXX???.SHR
;
;WE EXPECT THAT LOCATION 400007 WILL NOT COMPARE BECAUSE OF
;BYTE CONTAINING HIGH SEGMENT ORIGIN (PAGE NUMBER)
;WE ACCOUNT FOR THIS BY FILCOMING EVERY WORD EXCEPT 4000007
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LINK.SHR,XXXX.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LINK.SHR,XXXX.SHR
.IF (NOERROR) .DELETE XXXX.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKSCN.SHR,XXXSCN.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKSCN.SHR,XXXSCN.SHR
.IF (NOERROR) .DELETE XXXSCN.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKLOD.SHR,XXXLOD.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKLOD.SHR,XXXLOD.SHR
.IF (NOERROR) .DELETE XXXLOD.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKMAP.SHR,XXXMAP.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKMAP.SHR,XXXMAP.SHR
.IF (NOERROR) .DELETE XXXMAP.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKXIT.SHR,XXXXIT.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKXIT.SHR,XXXXIT.SHR
.IF (NOERROR) .DELETE XXXXIT.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKERR.SHR,XXXERR.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKERR.SHR,XXXERR.SHR
.IF (NOERROR) .DELETE XXXERR.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNK999.SHR,XXX999.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNK999.SHR,XXX999.SHR
.IF (NOERROR) .DELETE XXX999.SHR
;
;
;TEST FOR UNBUNDLED VERSION AGAIN
.RUN TECO[10,7]
*ERLNKOV1.MAC
.IF (ERROR) .GOTO CREF
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKOV1.SHR,XXXOV1.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKOV1.SHR,XXXOV1.SHR
.IF (NOERROR) .DELETE XXXOV1.SHR
;
.RUN DSK:FILCOM[10,7]
*TTY:/400000L/400006U=LNKOV2.SHR,XXXOV2.SHR
.IF (ERROR)	;TOO BAD
*TTY:/Q/400010L=LNKOV2.SHR,XXXOV2.SHR
.IF (NOERROR) .DELETE XXXOV2.SHR
;
CREF::
.CHKPNT CREF
;PRODUCE SOURCE LISTING AND TELL OPERATOR
.CREF
;
.ERROR %
.DIRECT *.CRF
.IF (ERROR) .GOTO DONE
.NOERROR	;JUST CREF WHAT WE CAN
.RUN CREF
*LPT:=C.CRF
*LPT:=SCNMAC.CRF
*LPT:=LNKPAR.MAC
*LPT:=LNKLOW.CRF
*LPT:=LNKINI.CRF
*LPT:=LNKSCN.CRF
*LPT:=LNKWLD.CRF
*LPT:=LNKLOD.CRF
*LPT:=LNKHSH.CRF
*LPT:=LNKOLD.CRF
*LPT:=LNKNEW.CRF
*LPT:=LNKF40.CRF
*LPT:=LNKCST.CRF
*LPT:=LNKCOR.CRF
*LPT:=LNKFIO.CRF
*LPT:=LNKLOG.CRF
*LPT:=LNKERR.CRF
*LPT:=LNKMAP.CRF
*LPT:=LNKXIT.CRF
*LPT:=LNKSUB.CRF
*LPT:=LNKTMP.CRF
*LPT:=LNK999.CRF
*LPT:=LNKOV1.CRF
*LPT:=LNKOV2.CRF
*LPT:=LNKOVS.CRF
*LPT:=OVRLAY.CRF
*LPT:=PLTDCL.CRF
*LPT:=PLTPRM.CRF
*LPT:=PLTIO.CRF
*LPT:=PLTMTH.CRF
*LPT:=PLTGLB.CRF
*LPT:=PLTUTL.CRF

DONE::	.ERROR ?
;
FINIS::
.CHKPNT FINIS
.PLEASE LINK-10 SUCCESSFUL
;REMOVE ALL TEMPORARY FILES
.DELETE LNK???.REL,PLT???.REL,MACRO.SHR,CREF.SHR,DIRECT.SHR,*.TMP
.IF (ERROR) ;DON'T CARE IF FAILED
.DELETE JOBDAT.REL,*.UNV
.IF (ERROR) ;
.DELETE U.MAC,LNKCMD.CMD,COMPIL.SHR,PIP.SHR
.IF (ERROR) ;
.GOTO FIN
%CERR::	.GOTO ERR
%ERR::
ERR:: ;LEAVE FILES SO WE CAN RESTART JOB
.PLEASE LINK-10 UNSUCCESSFUL
FIN::
%FIN:: .DEASSIGN SYS
.QUEUE LINK.LOG
.K/F/Z
