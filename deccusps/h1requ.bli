!COPYRIGHT 1972,1973,1974 DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS. 01754
!FILENAME:	H1REQU.BLI
!DATE:		2 JANUARY 1974
!AUTHOR:	CMU/FLD/KR

GLOBAL BIND H1REV=5;		!MODULE VERSION NUMBER

!THIS MODULE HANDLES THE "REQUIRE" DECLARATION


EXTERNAL ?.JBFF,BXA;

STRUCTURE STVEC1[I]=(TABLE+TIMXMF*(@.STVEC1 AND #77777)+.I)<0,36>;

MAP STVEC1 SYM:FUTSYM;

MACHOP CALLI=#47,XCT=#256;

GLOBAL REQDATA[#20],REQCHN;

MACRO
   ABORT(NUM)=RETURN (REQRELEASE();  RECOVER(.NDEL,NUM))$,
   SKIP(OP)=(VREG_1; (OP); VREG_0; .VREG)$,
   EXECUTE(OP,AC,Y)=(N_(OP)^27 OR (AC)^23 OR (Y); XCT(0,N))$,
   CMUDEC=-2$,
   FILE=LOOKUPBLOCK[0]$,
   EXT=LOOKUPBLOCK[1]$,
   PPN=LOOKUPBLOCK[3]$,
   JUNK=LOOKUPBLOCK[2]$,
   DEVCHR=4$,
   OUTPUTF=18,1$,
   ASCIIMF=0,1$,
   STATUS=OPENBLOCK[0]$,
   ODEV=OPENBLOCK[1]$,
   BUFW=OPENBLOCK[2]$;

BIND
   OPEN=#50,
   INBUF=#64,
   LOOKUP=#76,
   RELEASE=#71,
   BUFFSIZE=#203,
   HDRSIZE=2,
   VREG=3<0,36>;
GLOBAL ROUTINE REQRELEASE=
  (RELEASESPACE(.REQDATA[.REQCHN+1]<RIGHTHALF>,BUFFSIZE+HDRSIZE));




ROUTINE REQINIT(DEVICE)=

BEGIN

  LOCAL OPENBLOCK[3],SAVFF;
  REGISTER N;


  ODEV_N_.DEVICE;
  CALLI(N,DEVCHR);
  IF .N EQL 0 THEN RETURN 0;
  IF NOT .N<OUTPUTF> THEN RETURN 0;
  IF NOT .N<ASCIIMF> THEN RETURN 0;

  IF (.REQCHN+1) GTR #17 THEN (RECOVER(.NDEL,#505); RETURN 0);

  REQDATA[.REQCHN+1]_GETSPACE(BUFFSIZE+HDRSIZE);
  REQDATA[.REQCHN+1]<LEFTHALF>_BUFW_ST[.REQDATA[.REQCHN+1]<RIGHTHALF>,0]<0,0>;
  STATUS_0;

  IF NOT SKIP(EXECUTE(OPEN,.REQCHN+1,OPENBLOCK<0,0>))
         THEN ( REQRELEASE();
                RETURN 0  );

!GET BUFFERS

  SAVFF_.?.JBFF;
  ?.JBFF_.REQDATA[.REQCHN+1]<LEFTHALF>+3;
  EXECUTE(INBUF,.REQCHN+1,2);
  ?.JBFF_.SAVFF;

  (@(BXA+#16))<0,36>_'     ';  !BLANKS TO SEQNUM
  1
END;    !OF REQINIT
ROUTINE CVSIX(POINTR)=
BEGIN LOCAL SYMPTR,SIXPTR,SIXSYM; REGISTER R; MACHOP ILDB=#134,IDPB=#136;

SIXSYM_0;
SYMPTR_(@POINTR)<36,7>;
SIXPTR_SIXSYM<36,6>;
DECR I FROM 5 TO 0 DO
  (ILDB(R,SYMPTR);
   IF .R EQL #177 THEN EXITLOOP;
   IF .R LEQ #132 THEN R_.R-#40;
   IDPB(R,SIXPTR)     );
.SIXSYM
END;
GLOBAL ROUTINE SREQUIRE=
BEGIN
LOCAL DEVICE,LOOKUPBLOCK[4];
REGISTER N;


FILE_EXT_JUNK_PPN_0;

DEVICE_IF .FUTDEL<LEFTHALF> EQL HCOLON 
	    THEN (N_ IF LITP(.FUTSYM) THEN LITV(.FUTSYM)
			ELSE CVSIX(FUTSYM[2]); SRUND(#22);
		  FILE_ CVSIX(ACCUM); .N)

	    ELSE (IF .FUTSYM EQL 0 THEN
			(FILE_ CVSIX(ACCUM); SRUND(#12))	!IF THE FILENAME WAS PARSED
								!AS A DELIMETER, THEN UPDATE
								!THE WINDOW
		   ELSE (FILE_ IF LITP(.FUTSYM) THEN LITV(.FUTSYM)
				ELSE CVSIX(FUTSYM[2]));
		SIXBIT 'DSK   ');

IF NOT REQINIT(.DEVICE) THEN RETURN RECOVER(.NDEL,#504);

IF .FUTDEL<LEFTHALF> EQL HDOT THEN (SRUND(#22); EXT_CVSIX(ACCUM));
IF .FUTDEL<LEFTHALF> EQL HSQOPEN
    THEN
      BEGIN
	SKAN(#02);
	IF .CHAR GEQ "0" AND .CHAR LEQ "7" THEN
	   BEGIN
		SRUND(#40); IF .FUTDEL<LEFTHALF> NEQ HCOMMA
				THEN ABORT(#503);
		PPN<18,18>_ .VAL;
		SRUND(#40);
		PPN<0,18>_ .VAL
	   END
	ELSE
	   BEGIN
		HRUND();
		IF .FUTDEL<LEFTHALF> EQL HCOMMA
		  THEN (IF NOT LITP(.FUTSYM) THEN ABORT(#503); PPN<18,18>_LITV(.FUTSYM); HRUND());
		IF LITP(.FUTSYM) THEN PPN_.PPN OR LITV(.FUTSYM) ELSE
		    BEGIN
			N<0,18>_ FUTSYM[2]<0,0>;
			N<18,18>_ PPN<0,0>;
			IF NOT SKIP(CALLI(N,CMUDEC)) THEN ABORT(#501);
		    END;
	   END;

	IF  .FUTDEL<LEFTHALF> NEQ HSQCLO THEN ABORT(#503);
	HRUND()
      END;

IF NOT SKIP(EXECUTE(LOOKUP,(.REQCHN+1),LOOKUPBLOCK<0,0>)) THEN ABORT(#502);
REQCHN_ .REQCHN + 1;

SKAN(1);  HRUND();		!FORCE EOL AND GET NEW LINE FROM REQUIRED FILE

END;   !OF SREQUIRE



!END OF H1REQU.BLI
