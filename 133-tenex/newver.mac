;<133-TENEX>NEWVER.MAC;2     6-JAN-75 14:39:34    EDIT BY LYNCH
; MAD IT COMPATIBLE WITH SRI SCHEME
;<133-TENEX>NEWVER.MAC;1    25-SEP-74 13:30:38    EDIT BY CLEMENTS

	TITLE NEWVER
	SEARCH STENEX

;PROGRAM TO INCREMENT EDIT NUMBER ON EACH SYSTEM LOAD

F=0
T1=1
T2=2
T3=3
T4=4
P1=5
P2=6
P=17
NPDL==100

DEFINE PSTR (A)<
	HRROI 2,[ASCIZ \A\]
	SETZ 3,
	SOUT>

DEFINE PNUM (L,FLG)<
	MOVE 2,L
   IFB <FLG>,<MOVEI 3,^D10>
   IFNB <FLG>,<MOVE 3,[FLG]>
	NOUT
	 JSERR>

DEFINE JSERR (AA)<
	JSP 2,JSERR0>

DEFINE TYPMSG (A)<
	HRROI T1,[ASCIZ \A\]
	PSOUT>
DEFINE ERRMSG (A)<
	HRROI T1,[ASCIZ \A\]
	ESOUT>

OPDEF CALL [PUSHJ P,]
OPDEF RET [POPJ P,]

BGN:	MOVE P,[IOWD NPDL,PDL]
	RESET
	MOVSI 1,(1B2+1B17)	;GET CURRENT VERSION
	HRROI 2,VNAME
	GTJFN
	 JRST NOSLET		;THAT LETTER NOT IN USE
	MOVEM 1,JFN
	HRROI 1,VERTXT
	HRRZ 2,JFN
	MOVE 3,[1B14]		;VERSION ONLY
	JFNS			;GET VERSION NUMBER TO STRING
	MOVE 1,JFN
	RLJFN			;RELEASE CURRENT VERSION JFN
	 JSERR
	HRROI 1,VERTXT
	MOVEI 3,^D10
	NIN			;CONVERT IT TO BINARY
	 JSERR
	ADDI 2,1		;INCREASE VERSION NUMBER BY 1
	MOVEM 2,VERNUM		;SAVE IT
MAKNLT:	HRROI 1,VERTXT		;NEW CONVERT VERSION NUMBER BACK TO TEXT
	MOVE 2,VERNUM
	MOVEI 3,^D10
	NOUT
	 JSERR
	MOVSI 1,(1B0+1B17)
	HRR 1,VERNUM		;USE NEW VERSION NUMBER FOR FILE
	HRROI 2,VNAME
	GTJFN			;CREATE FILE WHICH DEFINES VERSION
	 JSERR
	MOVEM 1,JFN
	MOVE 2,[7B5+1B20]	;OPEN FOR ASCII WRITE
	OPENF
	 JSERR
	; ..

;CONSTRUCT TEXT FOR PARAMETER AND WRITE INTO FILE

	MOVE 1,JFN
	PSTR <
;DEFINE VERSION NUMBER FOR SYSTEM NAME AND FOR EXTENSIONS
; OF DEPENDENT FILES (BUGTABLE,...)

SVNM==:^D>
	HRROI 2,VERTXT		;COMPOSIT VERSION NUMBER
	SOUT
	PSTR<

>
	CLOSF
	 JSERR
	MOVEI 1,101
	PSTR <
BUILDING VERSION >
	HRROI 2,VERTXT		;COMPOSIT NUMBER TO TTY
	SOUT
	PSTR <
>
	HALTF
	JRST BGN

NOSLET:	TYPMSG <
THERE IS NO EXISTING >
	HRROI T1,VNAME
	PSOUT
	TYPMSG < . CREATE ONE? (Y OR N) >
	CALL YESNO
	  JRST BGN
	JUMPGE T1,BGN
NUMPLS:	TYPMSG <
TYPE DESIRED VERSION NUMBER, E.G., 13301 : >
	MOVEI T1,100
	MOVEI T3,12
	NIN
NUMXXX:	  JRST [ERRMSG <>
		JRST NUMPLS]
	MOVE P1,T2		;COPY ANSWER
	BKJFN
	  JSERR
	BIN
	CAIE T2,37
	CAIN T2,40
	SKIPA
	JRST NUMXXX
	MOVEM P1,VERNUM
	CAIL P1,^D13300
	CAILE P1,^D13399
	JRST OHYEAH
	JRST MAKNLT		;OK, DO IT

OHYEAH:	TYPMSG <
 REALLY? >
	HRROI 1,VNAME
	PSOUT
	MOVEI 1,";"
	PBOUT
	MOVEI 1,101
	MOVE 2,VERNUM
	MOVEI 3,12
	NOUT
	  JSERR
	TYPMSG < ? (Y OR N)>
	CALL YESNO
	  JRST BGN
	JUMPGE T1,BGN
	JRST MAKNLT		;OK, HE ASKED FOR IT

YESNO:	CALL GETL
	  RET
	CAIE T1,"Y"
	CAIN T1,"N"
	  SKIPA
	    JRST ERR3
	CAIE T1,"Y"
	TDZA T1,T1
	SETO T1,0
	AOS (P)
	RET

GETL:	PBIN
	CAIN T1,177
	JRST RUB1
	CAILE T1,140
	SUBI T1,40
	MOVE T2,T1
	PBIN
	CAIN T1,177
	JRST RUB1
	EXCH T1,T2
	CAIN T2,37
	AOS 0(P)
	RET

RUB1:	TYPMSG < XXX
>
	RET

ERR1:	ERRMSG < TYPE A LETTER FROM A TO Z FOLLOWED BY EOL
>
	JRST BGN
ERR3:	ERRMSG < TYPE Y OR N FOLLOWED BY EOL
>
	RET

;JSYS ERROR HANDLER

JSERR0:	HRROI 1,[ASCIZ /
JSYS ERROR: /]
	PSOUT
	MOVEI 1,101		;OUTPUT ERROR STRING TO PRIMARY OUTPUT,
	HRLOI 2,400000		;CURRENT FORK ,, LAST ERROR
	SETZ 3,
	ERSTR
	 JFCL
	 JFCL
	HRROI 1,[ASCIZ /
/]
	PSOUT
	HALTF
	JRST BGN

VNAME:	ASCIZ /VERSIO.MAC/
PNAMEL:	POINT 7,VNAME+1,20
;STORAGE

JFN=10

VERTXT:	BLOCK 10
VERNUM:	BLOCK 1			;VERSION NUMBER (BINARY)
NAMEL:	BLOCK 1

PDL:	BLOCK NPDL

	END BGN
