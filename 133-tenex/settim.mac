
	TITLE	SETTIM
	SUBTTL	SYSTEM JOB TO SET TIME FROM NETWORK
	SEARCH	STENEX

;Standard definitions

A=1
B=2
C=3
D=4
E=5
F=7
P=17

OPDEF	CALL	[PUSHJ P,]
OPDEF	RET	[POPJ P,]

NPDL==20
MAXDIF==^D15
TIMOUT==^D90000		;GIVE-UP TIME AFTER START

;Main program

START:	RESET
	MOVE P,[IOWD NPDL,PDL]
	TIME			;SAVE FOR TIME-OUT FUNCTION
	MOVE	F,1
	MOVEI A,400000
	SETOB B,C
	EPCAP
LP0:	MOVSI E,-NHOSTS
LP:	CALL GETTIM
	 AOBJN E,LP
	JUMPGE E,NOTFND		; NOBODY HAS THE TIME
	PUSH P,B
	GTAD			; IS TIME ALREADY SET?
	HLRZ B,A
	HLRZ C,0(P)
	SUB B,C
	HRRZ D,A
	HRRZ C,0(P)
	SUB D,C
	IMULI B,^D<60*60*24>
	ADD B,D
	MOVMS B
	CAIG B,MAXDIF
	JRST GOOD		; TIME IS SET CORRECTLY
	POP P,A
	;test code only
	move	2,1
	movei	1,101
	setz	3,
	odtim

       ;	STAD
	JRST DONE

NOTFND:
	TIME			;CHECK TIME-OUT
	SUB	1,F
	CAMLE	1,[TIMOUT]	;TRY FOR A WHILE
	JRST	[GTAD
		 JUMPGE	1,DONE	;GIVE UP IF SET NOW
		 JRST	.+1]	;ELSE KEEP TRYING (OPR DIED??)
	MOVEI A,^D3000
	DISMS
	JRST LP0

GOOD:	POP P,A
DONE:	GJINF
	SKIPLE	C		; JOB 0? IF SO, DONT LOGOUT
	SKIPL D
	 HALTF
	SETO A,
	LGOUT
	JRST DONE

GETTIM:	HRRO A,HSTNAM(E)
	MOVEI B,15
	MOVEI C,0
	CALL GETJFN
	 RET
	MOVEM A,SJFN
	MOVE B,[40B5+1B19]
	OPENF
	 JRST REL1
	BIN
	MOVEM B,SOCKET
	CLOSF
	 JFCL
	HRRO A,HSTNAM(E)
	MOVEI C,2
	CALL GETJFN
	 RET
	MOVEM A,SJFN
	HRRO A,HSTNAM(E)
	MOVE B,SOCKET
	MOVEI C,2
	CALL GETJFN
	 JRST REL1
	MOVEM A,RJFN
	MOVE B,[7B5+6B9+1B19]
	OPENF
	 JRST REL2
	MOVE A,SJFN
	MOVE B,[7B5+1B20]
	OPENF
	 JRST [	MOVE A,RJFN
		CLOSF
		 JFCL
		JRST REL1]
	MOVE A,RJFN
LDE:	SETZ B,
	IDTIM
	 JRST [	GTSTS
		TLNE B,1000
		 JRST CLZ2
		JRST LDE]
	MOVE A,B
	AOS 0(P)
CLZ2:	MOVE A,RJFN
	CLOSF
	 JFCL
	MOVE A,SJFN
	CLOSF
	 JFCL
	RET

REL2:	MOVE A,RJFN
	RLJFN
	 JFCL
REL1:	MOVE A,SJFN
	RLJFN
	 JFCL
	RET

GETJFN:	PUSH P,B
	PUSH P,A
	PUSH P,C
	MOVE A,[POINT 7,STRING]
	HRROI B,[ASCIZ /NET:/]
	SETZ C,
	SOUT
	POP P,B
	MOVEI C,10
	NOUT
	 JRST 4,.
	MOVEI B,"."
	IDPB B,A
	POP P,B
	SETZ C,
	SOUT
	POP P,B
	MOVNS B
	MOVEI C,10
	NOUT
	 JRST 4,.
	HRROI B,[ASCIZ /;T/]
	SETZ C,
	SOUT
	MOVSI A,(1B17)
	HRROI B,STRING
	GTJFN
	 RET
	AOS 0(P)
	RET

LIT

HSTNAM:
	[ASCIZ/AIC/]
	[ASCIZ/PARC/]
	[ASCIZ/ISI/]
	[ASCIZ/BBN/]
NHOSTS==.-HSTNAM

STRING:	BLOCK 20
PDL:	BLOCK NPDL
SOCKET:	BLOCK 1
SJFN:	BLOCK 1
RJFN:	BLOCK 1

	END START

