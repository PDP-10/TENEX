;<132>TV.MAC;2    12-DEC-74 12:15:55    EDIT BY UNTULIS
; CHANGED DEVIDE CODE NAME FOR DC-10 CONTOLLER TO TVDSK FOR USING RP10 COMPATIBILITY.
;<TENEX-MON>TV.MAC;13     9-JUL-74 02:10:47	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;12     8-JUL-74 20:25:03	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;11     7-JUL-74 23:01:10	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;10     7-JUL-74 22:51:33	EDIT BY KREMERS
;MODIFY FOR NEW TV START STUFF
;<TENEX-MON>TV.MAC;9    10-JUN-74 15:03:56	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;8     6-JUN-74 00:39:58	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;7     6-JUN-74 00:38:48	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;6     6-JUN-74 00:20:03	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;5    11-APR-74 01:34:09	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;4    11-APR-74 01:32:46	EDIT BY KREMERS
;MODIFIED FOR NEW PDP15
;<TENEX-MON>TV.MAC;3     7-MAR-74 00:52:55	EDIT BY KREMERS
;<TENEX-MON>TV.MAC;2     7-MAR-74 00:50:24	EDIT BY KREMERS
;ADDED CODE TO KILL DF10 ON TIME-OUT
;<SRIMON131>TV.MAC;6    31-JUL-73  1:48:22	EDIT BY KREMERS
;<SRIMON131>TV.MAC;5    30-JUL-73 14:24:50	EDIT BY KREMERS
;<SRIMON131>TV.MAC;4    30-JUL-73 13:53:34	EDIT BY KREMERS
;<SRIMON131>TV.MAC;3    25-JUL-73 23:58:53	EDIT BY KREMERS
;<SRIMON131>TV.MAC;2	11 JULY 73	CONVERTED BY KREMERS
;EDITED BY E. POLLACK 21 SEP 72


;	OPERATION OF TV.

;	THE T.V. A/D IS CONTROLLED THROUGH THE DC10 DISK CONTROLLER,
;AND IS TREATE AS THOUGH IT WERE DRIVE 8.  OPERATION IS AFFECTED VIA THE FOLLOWING CONO'S;
;	CONO DSK,(1B23)		;SELECTS THE TV FOR USE
;	CONO DSK,(1B18+1B23)		;SELECTS THE TV AND INITS PICTURE INPUT
;	CONO DSK,0			;DESELCTS THE TV (ENTIRE CONTROLLER)

	SEARCH STENEX,FILEDEF
	TITLE TV

NTVSCN==^D20	;NO OF 36 BIT WORDS IN ONE SCAN LINE
TVCMDP==34	;LOCATION OF DF10 COMMAND
TVWAIT==^D10000	;NO. OF MILISECONDS TO WAIT FOR TV BEFORE TIMEOUT
TVSTBT==1B18		;START BIT IN CONO WORD
TVDVBT==1B23		;SELECT BIT IN CONO WORD
TVDSK==700		;DEVICE CODE FOR DISK CONTROLLER

;TVX1==1		;ILLEGAL TV MODE
;TVX2==2		;PICTURE WON'T FIT IN VIRT ADR SPACE, CROSSES TOP
;TVX3==3		;PICTURE DIDN'T COME IN
;TVX4==4		;PICTURE INCOMPLETE

	INTERN .TVPIC,TVINI

;LINKAGE TO DSKPAK.MAC
EXTERN GTDF10,RLDF10,DISL,EDISMS

;LINKAGE TO PISRV.MAC
	EXTERN TODCLK

;LINKAGE TO PAGEM.MAC
	EXTERN MLKPG,MULKPG,FPTA

;LINKAGE TO FILE SYSTEM
	EXTERN ERRD

LS(TVUSE)	;-1 IF TV FREE, 0 IF IN USE
LS(TVLSTW)	;ADR OF LAST WORD +1 XFERRED INTO,  SHOULD AGREE WITH WORD STORED IN 35 BY DF10
LS(TVCLPT)	;COMMAND LIST POINTER
LS(TVCLST,^D22)	;TV DF10 COMMAND LIST
LS(TVPGBD)	;IF -1, COULDN'T USE FIRST USER PAGE
LS(TVTIME)		;TIME TV IS DECLARED DEAD


;TVPIC JSYS CALLED WITH 1=STARTING MEM ADR, 2=TVMODE [0,7]
;	TVMODE	BIT 33 = 0 FOR CAMERA 1
;		       = 1 FOR CAMERA 2
;		BIT 34 = 0 FOR SINGLE DENSITY
;		       = 1 FOR DOUBLE DENSITY
;		BIT 35 = 0 FOR NON-STEREO
;		       = 1 FOR STEREO







;INIT TV CODE

USE RESPC
TVINI:	SETOM TVUSE		;BY UNLOCKING
	POPJ P,			;RETURN


USE SWAPPC


.TVPIC::JSYS MENTR
TVPIC3:	NOINT
	LOCK TVUSE,<JRST TVPIC4>
	SETZM TVPGBD
	TDNE B,[-10]	;IN RANGE [0,7]?
	JRST TVERR1	;NO
	MOVEI D,TVCLST
	MOVEM D,TVCLPT
	MOVEI D,^D9600
	TRNN B,1B34	;DOUBLE DENSITY
	MOVEI D,^D2400	;NO
	MOVE C,D
	ADD C,A		;ADD STARTING ADR
	CAILE C,777777	;CAN'T CROSS END OF ADR SPACE
	JRST TVERR2
TVPIC1:	MOVE B,A
	ADDI B,1000	;FORM BEGINNING ADR OF NEXT PAGE
	ANDI B,777000
	SUB B,A		;SUBTRACT ORIG ADR, YIELDS ROOM IN THIS PAGE
	IDIVI B,NTVSCN	;NO. OF SCAN LINES THAT WILL FIT
	IMULI B,NTVSCN	;NO. OF WORDS IN THAT MANY SCAN LINES
	JUMPE B,TVPIC6	;IF NO SCAN LINES FIT, THEN DON'T USE THIS PAGE
	CAMLE B,D	;MORE THAN WHATS LEFT TO XFER?
	MOVE B,D	;YES, THEN XFER JUST WHATS LEFT
	PUSHJ P,TVPAGE	;LOCK THE PAGE IN AND SET UP IOWD
	MOVEM C,@TVCLPT	;STORE CHANNEL COMMAND
	AOS TVCLPT	;BUMP CHAN CMD PTR
TVPIC5:	ADDI A,1000	;TO BEGINNING OF NEXT PAGE
	ANDI A,777000	;BECAUSE LOW ADR BITS MAY HAVE BEEN THERE
	SUB D,B
	JUMPG D,TVPIC1
	SETZM @TVCLPT	;END OF CHAN CMD LIST
	MOVEI 1,TVCLST
	SUB 1,TVCLPT	;NEG. NO. OF CHAN COMMANDS
	MOVEM A,TVCLPT	;SAVE FOR UNLOCKING PAGES
	HRRZI A,0(C)	;(REAL ADR -1) OF LAST CHAN CMD
	HLRE C,C	;NEG WORD COUNT
	SUB A,C		;REAL ADR OF LAST WORD XFERRED
	MOVEM A,TVLSTW	;SAVE FOR CHECK AT COMPLETION OF XFER

;	THE FOLLOWING STUFF TAKE PLACE WITH THE SYSTEM RUNNING NOSKED
;SO AS NOT TO HAVE ANYONE ELSE GRABBING THE CONTROLLER AND SCREWING UP THE
;PICTURE.  WHENEVER THE CONTROLLER HARDWARE IS MODIFIED SO THAT THE
;CONTROLLER GOES BUSY WHILE THE PICTURE IS BEING TAKEN THIS SHOULD BE
;MODIFIED TO RUN IN A TIME SHARING MODE.  THE FILE <KREMERS>TVCODE.CTLBSY
;IS INTENDED TO RUN THIS WAY.  ALSO DSKPAK MUST BE MODIFIED SO THAT GTDF10
;DOES NOT RETURN NOSKED.
;				J.H. KREMERS 7/9/74

	PUSHJ P,GTDF10	;GRAB THE DF10
	MOVEI 1,TVCLST	;SETUP COMMAND LISTS
	MOVEM 1,TVCMDP	
	SETZM TVCMDP+1

	CONO TVDSK,TVSTBT+TVDVBT+DSKCHN	;START PICTURE
	MOVE 1,TODCLK	;GET CURRENT TIME
	ADDI 1,TVWAIT	;ADD WAIT TIME
	CAML 1,TODCLK	;TIME UP ?
	SKIPE TVCMDP+1	;OR PICTURE DONE ?
	CAIA		;YES
	JRST .-3	;NO, KEEP TRYING

	CONO TVDSK,DSKCHN	;DONE FOR WHATEVER REASON, TURN OFF THE TV
TVPIC7:	PUSH P,TVCMDP+1		;SAVE DF10 STATUS

	PUSHJ P,RLDF10	;RELEASE THE DF10
	UMOVE B,A
	SKIPE TVPGBD
	ADDI B,1000	;CHECK IF FIRST USER PAGE WAS LOCKED
	HRL B,TVCLPT	;GET NEG COUNT OF CMDS
TVPIC2:	HRRZ A,B
	TLO A,(1B0)
	PUSHJ P,FPTA	;ASSUME B IS LEFT OK
	PUSHJ P,MULKPG
	ADDI B,777
	AOBJN B,TVPIC2	;UNLOCK NEXT PAGE
	POP P,B		;RECOVER DF10 STATUS
	MOVEM B,TVCLPT	;SAVE FOR SYSTEM PROGRAMMER TO LOOK AT
	HRLI B,0	;SHOULD POINT AT LAST+1 WORD
	JUMPE B,TVERR3	;IF 0, THEN PICTURE DIDN'T COME IN
	CAME B,TVLSTW	;IF EQUAL THEN TV PIC IS COMPLETE
	JRST TVERR3	;EITHER INCOMPLETE OR DIDN'T COME IN AT ALL
	UNLOCK TVUSE
	OKINT
	AOS 0(P)
	JRST MRETN

TVPIC6:	SETOM TVPGBD
	JRST TVPIC5
TVPIC4:	OKINT
	MOVEI A,TVUSE
	PUSHJ P,DISL	;WAIT UNTIL .LT. 0
	JRST TVPIC3	;TRY TO LOCK IT NOW

TVERR1:	MOVEI A,TVX1	;ILLEGAL TV MODE
TVERRX:	UNLOCK TVUSE
	OKINT
	JRST ERRD
TVERR2:	MOVEI A,TVX2	;TV PIC OVERFLOWS VIRT ADR SPACE
	JRST TVERRX
TVERR3:	MOVEI A,TVX3	;PICTURE DIDN'T COME IN AT ALL
	SKIPE TVCLPT
	MOVEI A,TVX4	;PICTURE INCOMPLETE
	JRST TVERRX



;USER ADR IN A, WORD COUNT IN B, LOCK PAGE, FORM IOWD IN C
TVPAGE:	PUSH P,A
	PUSH P,B
	PUSH P,D
	UMOVES 0(A)	;WRITE INTO USER PAGE
	TLO A,(1B0)	;IT IS A USER PAGE (NO MONITOR CALLS)
	PUSHJ P,FPTA
	PUSHJ P,MLKPG
	LSH A,^D9	;PAGE NO. TO ADR
	MOVN C,-1(P)	;-WDCNT TO C
	MOVE B,-2(P)	;GET LOW ADR BITS
	ANDI B,777
	ADD A,B		;FORM REAL CORE ADR
	HRLZS C		;C= XWD -WDCNT,0
	ADDI C,-1(A)	;FORM C= XWD -WDCNT,REAL ADR-1
	POP P,D
	POP P,B
	POP P,A
	POPJ P,


USE	RESPC


	END
