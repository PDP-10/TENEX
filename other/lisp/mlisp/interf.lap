(PUTPROP @IOR '434000 @SYM)
(PUTPROP @IORI '435000 @SYM)
(PUTPROP @LSH '242000 @SYM)
(PUTPROP @SOJGE '365000 @SYM)
(PUTPROP @HRL '504000 @SYM)
(PUTPROP @CAMGE '315000 @SYM)

(LAP TTYLINELEN SUBR)  RETURNS LENGTH OF TTY LINE - 3
 (MOVEI 1 -1)
 (JSYS 0 107)  RFMOD
 (LSH 2 13)
 (LSH 2 -35)  ISOLATE PAGE WIDTH
 (MOVE 1 2)
 (SUBI 1 3)  MAKE LESS FOR CONTROL-Y
 (PUSHJ P FIX1A)
 (POPJ P)
 NIL

(LAP INEX SUBR)  RETURNS T IF EXCLAM PT IN TERMINAL AND GOBBLES IT
 (MOVEI A '100)
 (JSYS 0 '102)  SIBE
 (TDZA 1 1)
 (JRST 0 RNIL)  NO CHARS, EXIT NIL
 (JSYS 0 '73)  PBIN
 (CAIN A '41)  SKIP IF NOT !
 (POPJ P)  RETURN NONNIL
 (MOVEI A '100)  TTY
 (JSYS 0 '42)  BKJFN - PUT THE NON! CHAR BACK
 (JFCL 0 0)  ERROR NOP
RNIL
 (MOVEI 1 0)
 (POPJ P)
 NIL

(LAP LREXEC SUBR)
 (MOVE 2 LRFRK)
 (JUMPN 2 LREX1)  JUMP TO EXEC IF ALREADY STARTED
 (PUSH P 1)   SAVE ARG
 (HRLZI 1 0)  CREATE EMPTY FORK
 (JSYS 0 $CFORK)
 (PUSHJ P FERROR)
 (HRRZM 1 LRFRK)
 (HRRZM 1 (SPECIAL LRFORK) S)  SAVE SO LRDONE CAN ACCESS IT
 (HRLZI 1 1)
 (HRROI 2 LRNAME)   USE ORDINARY NAME IF NIL
 (POP P 3)
 (SKIPE 3 3)
 (HRROI 2 LRNAM2)   USE SPECIAL NAME IF NONNIL
 (JSYS 0 $GTJFN)
 (PUSHJ P FERROR)
 (HRRZM 1 LRJFN)
 (HRL 1 LRFRK)
 (HRR 1 LRJFN)
 (JSYS 0 $GET)  GET THE LR CORE IMAGE
 (PUSHJ P LREX2)  GET INFO FOR LRWAIT
 (MOVE 1 LRFRK)
 (SETZ 2 0)
 (JSYS 0 $SFRKV)
 (POPJ P)
LREX1
 (PUSHJ P LREX2)  GET INFO FOR LRWAIT
 (MOVE 1 LRFRK)
 (MOVEI 2 1)
 (JSYS 0 '157)  SFORK
 (POPJ P)
LREX2  SAVE INFO FOR LRDONE
 (MOVE 1 LRFRK)  GET TRAPS AND RUNTM SO WE WILL KNOW IF LR RAN AT ALL
 (JSYS 0 '172)  GTRPI
 (MOVEM 1 (SPECIAL LRTRAPS) S)
 (MOVE 1 LRFRK)
 (JSYS 0 '15)  RUNTM
 (HRLI 1 0)
 (MOVEM 1 (SPECIAL LRTIME) S)
 (POPJ P)
LRFRK (0 0 0)
LRJFN (0 0 0)
LRNAME 
(363414 0 171326)
(647475 0 767174)
(663456 0 327346)
(607540 0 0)
LRNAM2
(363414 0 171344)
(745755 0 471346)
(273474 0 173000)
FERROR
 (HRROI 1 (QUOTE "ERROR: ") S)
 (JSYS 0 '76)  PSOUT
 (551000 1 '101)  HRRZI
 (MOVE 2 (C 400000 0 777777))
 (MOVEI 3 0)
 (JSYS 0 '11)  ERSTR
 (JRST 0 EXIT9)
 (JRST 0 EXIT9)
 (JSYS 0 '76)  PSOUT
 (JRST 0 EXIT9)
EXIT9
 (JSYS 0 $HALTF)
 NIL

(LAP LRDONE SUBR)
 (MOVEI 4 '10)  ONLY 8 SLEEPS
TAG1
 (MOVE 1 (SPECIAL LRFORK) S)
 (JSYS 0 '172)  GTRPI
 (SUB 1 (SPECIAL LRTRAPS) S)
 (JUMPN 1 TAG2)
 (MOVE 1 (SPECIAL LRFORK) S)
 (JSYS 0 '15)  RUNTM
 (HRLI 1 0)
 (SUB 1 (SPECIAL LRTIME) S)
 (JUMPN 1 TAG2)
 (MOVEI 1 '101)
 (JSYS 0 '74)  PBOUT
 (MOVEI 1 '10000)  FIVESECS
 (JSYS 0 '167)  SLEEP DISMS
 (MOVEI 1 '102)
 (JSYS 0 '74)  PBOUT
 (SOJGE 4 TAG1)
TAG2
 (MOVE 1 (SPECIAL LRFORK) S)
 (JSYS 0 '156)  RFSTS
 (HLRZ 2 1)
 (405000 2 2)  ANDI
 (MOVEI 1 (QUOTE NIL))
 (SKIPE 1 2)
 (MOVEI 1 (QUOTE T) S)
 (POPJ P)
 NIL

(LAP LRWAIT SUBR)
 (CALL 0 (E LRDONE) S)
 (MOVE 1 (SPECIAL LRFORK) S)
 (JSYS 0 '163)  WFORK
 (POPJ P)
 NIL

(LAP JOBNO SUBR)
 (JSYS 0 $GJINF)
 (MOVE 1 3)
 (PUSHJ P FIX1A)
 (POPJ P)
 NIL

(LAP PTIME SUBR)
 (MOVEI 1 '400000)
 (JSYS 0 '172)
 (MOVE 1 3)
 (PUSHJ P FIX1A)
 (POPJ P)
 NIL
(LAP LRPTIME SUBR)
 (MOVE 1 (SPECIAL LRFORK) S)
 (JSYS 0 '172)
 (MOVE 1 3)
 (PUSHJ P FIX1A)
 (POPJ P)
 NIL
(LAP GTRPI SUBR)
 (MOVEI 1 '400000)
 (JSYS 0 '172)
 (PUSH P 1)
 (PUSH P 2)
 (MOVE 1 3)
 (PUSHJ P FIX1A)
 (CALL 1 (E NCONS) S)
 (EXCH 1 0 P)
 (PUSHJ P FIX1A)
 (POP P 2)
 (CALL 2 (E CONS) S)
 (EXCH 1 0 P)
 (PUSHJ P FIX1A)
 (POP P 2)
 (CALL 2 (E CONS) S)
 (POPJ P)
 NIL

(LAP NEDIT SUBR)
 (PUSH P 1)
 (MOVEI 2 (QUOTE 0))
 (CALL 2 (E EQ) S)
 (JUMPE 1 TAG1)
 (POP P 1)
 (POPJ P)
TAG1
 (POP P 1)
  (MOVEI 2 (QUOTE '144))
  (CALL 2 (E *TIMES) S)
  (MOVEI 2 (QUOTE .5) S)
  (CALL 2 (E *PLUS) S)
  (CALL 1 (E FIX) S)
 (PUSHJ P NUMVAL)
 (IDIVI 1 '12)
 (ADDI 2 '60)
 (LSH 2 1)
 (MOVE 5 2)
 (IDIVI 1 '12)
 (ADDI 2 '60)
 (LSH 2 '10)
 (IOR 5 2)
 (IDIVI 1 '12)
 (ADDI 2 '60)
 (LSH 2 '26)
 (IOR 5 2)
 (IDIVI 1 '12)
 (ADDI 2 '60)
 (MOVE 3 2)
 (LSH 2 '35)
 (IOR 5 2)
 (MOVEI 2 '56)
 (LSH 2 '17)
 (IOR 5 2)
 (CAIN 3 '60)
 (LSH 5 7)
 (PUSH P 5)
 (PUSHJ P GETFW)
 (POP P 2)
 (MOVEM 2 0 1)
 (CALL 1 (E NCONS) S)
 (CALL 1 (E NCONS) S)
 (MOVEI 2 (QUOTE PNAME) S)
 (CALL 2 (E XCONS) S)
 (MOVEI 2 '777777)
 (CALL 2 (E XCONS) S)
 (POPJ P)
GETFW
 (SKIPN 0 '16)
 (CALL 0 (E GC) S)
 (MOVE 1 '16)
 (HRRZ '16 0 '16)
 (POPJ P)
 NIL
(LAP LAPPLUS SUBR)
 (ADD 1 2)
 (SUBI 1 '577777)
 (POPJ P)
 NIL
(LAP LAPGEQUAL SUBR)
 (CAMGE 1 2)
 (MOVEI 1 0)
 (POPJ P)
 NIL
(LAP FIX1A SUBR)
 (PUSHJ P FIX1A)
 (POPJ P)
 NIL

(LAP PERCINIT SUBR)
 (MOVE 1 TAG1)
 (JUMPN 1 TAG0)
 (MOVE 1 (SPECIAL NUMORG) S)
 (PUSHJ P NUMVAL)
 (MOVE 2 1)
 (ADDI 2 1)
 (HRL 2 1)
 (MOVEM 2 TAG1)
 (MOVE 1 (SPECIAL NUMEND) S)
 (PUSHJ P NUMVAL)
 (MOVEM 1 TAG2)
 (MOVE 1 (SPECIAL VALORG) S)
 (PUSHJ P NUMVAL)
 (MOVE 2 1)
 (ADDI 2 1)
 (HRL 2 1)
 (MOVEM 2 TAG3)
 (MOVE 1 (SPECIAL VALEND) S)
 (PUSHJ P NUMVAL)
 (MOVEM 1 TAG4)
TAG0
 (MOVE 1 TAG1)
 (MOVE 2 TAG2)
 (BLT 1 0 2)
 (MOVE 1 TAG3)
 (MOVE 2 TAG4)
 (BLT 1 0 2)
 (MOVEI 1 0)
 (POPJ P)
TAG1 (0 0 0)
TAG2 (0 0 0)
TAG3 (0 0 0)
TAG4 (0 0 0)
 NIL
(LAP GETDATE SUBR)   RETURNS TIME OR (YR MO DATE . DAY)
     INPUT - 0 TODAY, 1 TOMORROW, -1 YESTERDAY, 2 JUST TIME
 (PUSHJ P NUMVAL)   CONVERT
 (MOVE 5 1)   SAVE
 (JSYS 0 '227)   GTAD
 (CAIN 1 -1)   SKIP IF NOT -1
 (JRST GERROR)   NO SYSTEM TIME
 (HRRZ 2 1)   SAVE TIME
 (HLRZ 3 1)   DATE
 (ADD 3 5)   CHANGE TO YESTERDAY ETC
 (HRL 2 3)
 (MOVEI 4 0)
 (JSYS 0 '222)   ODCNV
 (CAIE 5 2)   SKIP IF 2 - TIME ONLY
 (JRST DATE)
 (HRRZ 1 4)   GET TIME
 (PUSHJ P FIX1A)
 (POPJ P)   RETURN THE TIME
DATE
 (HLRZ 1 2)
 (PUSH P 1)
 (HRRZ 1 2)
 (PUSH P 1)
 (HLRZ 1 3)
 (PUSH P 1)
 (HRRZ 1 3)
 (PUSHJ P FIX1A)
 (EXCH 1 0 P)
 (PUSHJ P FIX1A)
 (POP P 2)
 (CALL 2 (E CONS) S)
 (EXCH 1 0 P)
 (PUSHJ P FIX1A)
 (POP P 2)
 (CALL 2 (E CONS) S)
 (EXCH 1 0 P)
 (PUSHJ P FIX1A)
 (POP P 2)
 (CALL 2 (E CONS) S)
 (POPJ P)
GERROR
 (MOVEI 1 (QUOTE NIL))
 (POPJ P)
 NIL

(LAP RUNTIM SUBR)   RETURN ELAPSED TIME IF INPUT NONNIL, ELSE SET RUNSAV
 (MOVE 5 1)
 (MOVEI 1 -5)   RUNTIME OF WHOLE JOB
 (JSYS 0 '15)   RUNTM
 (IDIV 1 2)   CONVERT TO SECONDS
 (CAIE 5 (QUOTE NIL))   IF NIL THEN SET RUNSAV
 (JRST RN2)   GET CURRENT TIME USED
 (MOVEM 1 RUNSAV)   SAVE BEGIN TIME
 (MOVEI 1 (QUOTE NIL))
 (POPJ P)
RN2
 (SUB 1 RUNSAV)   SUB OLD FROM NEW
 (PUSHJ P FIX1A)   CONVERT
 (POPJ P)   RETURN ELAPSED TIME
RUNSAV
 (0 0 0)
 NIL

