 COMMENT  THIS FILE IS IODEFS;
DEFINE  TAB="'11", LF="'12", FORM="'14", CR="'15", 
 	ALT="'33", DEL="'177", =" &'15&'12 ", CREEP="(CR&DEL&'21)";
DEFINE 	$=" &'15&'12 ";
DEFINE 	RRDIN=" SS_TRIM(INPUT(INCH,1)) ";

DEFINE  TIL="STEP 1 UNTIL", LN="LENGTH",
	SAY="OUTSTR",PROC="PROCEDURE";

INTEGER INCH,OUCH,CNT,BRK,EOF,FLAG;	COMMENT INPUT/OUTPUT GLOBALS;

DEFINE DSKI="OPEN(INCH_GETCHAN,""DSK"",1,4,0,CNT_200,BRK,EOF)",
	BDSKI="OPEN(INCH_GETCHAN,""DSK"",8,40,0,CNT_200,BRK,EOF)",
	DSKO="OPEN(OUCH_GETCHAN,""DSK"",1,0,4,0,0,0)",
	BDSKO="OPEN(OUCH_GETCHAN,""DSK"",8,0,40,0,0,0)",
	FILI(FILE)="LOOKUP(INCH,FILE,FLAG)",
	FILO(FILE)="ENTER(OUCH,FILE,FLAG)",
	LPTO="OPEN(OUCH_GETCHAN,""LPT"",1,0,2,0,0,EOF_0)",
	TTYI="OPEN(INCH_GETCHAN,""TTY"",1,2,0,CNT_200,BRK,EOF)",
	TTYO="OPEN(OUCH_GETCHAN,""TTY"",1,0,2,0,0,0)";

PROC FILIN(STRING F);  BEGIN  DSKI; FILI(F) END;

PROC FILOUT(STRING F);  BEGIN DSKO; FILO(F) END;

PROC FILOUTP(STRING F;INTEGER P);  BEGIN DSKO; FILO(F); RENAME(OUCH,F,P,FLAG) END;

PROC BFILIN(STRING F);  BEGIN  BDSKI; FILI(F) END;

PROC BFILOUT(STRING F);  BEGIN BDSKO; FILO(F) END;


STRING BLANKS,ZEROKS;
DEFINE	INLINE="INPUT(INCH,1)",  TOTAB(S)="SCAN(S,13,BRK)";

STRING PROC LEFT(INTEGER L; STRING S);
	RETURN(IF LN(S)<L THEN S&BLANKS[1 TO L-LN(S)] ELSE S[1 TO L]);

STRING PROC RIGHT(INTEGER L; STRING S);
	RETURN(IF LN(S)<L THEN BLANKS[1 TO L-LN(S)]&S  ELSE S[LN(S)-L+1 TO LN(S)]);

STRING PROC RIGHTZ(INTEGER L; STRING S);
	RETURN(IF LN(S)<L THEN ZEROKS[1 TO L-LN(S)]&S  ELSE S[LN(S)-L+1 TO LN(S)]);

STRING PROC CENTER(INTEGER L; STRING S);
	RETURN(IF LN(S)<L THEN BLANKS[1 TO (L-LN(S))DIV 2]&
		S & BLANKS[1 TO (L-LN(S)+1)DIV 2] ELSE S[1 TO L]);

STRING PROC ASK(STRING S);
	BEGIN  OUTSTR(S);  RETURN(INCHWL)  END;

INTEGER PROC ROUND(REAL R);  RETURN(R + .5);

STRING PROC TRIM(VALUE STRING LINE);
	BEGIN "TRIM" INTEGER POS;
	POS _ LENGTH(LINE);
	WHILE LINE[POS FOR 1] = " " DO POS _ POS - 1;
	RETURN(LINE[1 TO POS]);
	END "TRIM";
STRING PROC INPLINE(INTEGER CHAN);
  BEGIN STRING LINE;
  DO LINE_TRIM(INPUT(CHAN,1)) UNTIL LINE OR EOF;
  RETURN(LINE);
  END;

